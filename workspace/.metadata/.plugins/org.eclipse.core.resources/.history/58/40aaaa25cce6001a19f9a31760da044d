class Y20_CL_ODATA_QUAL_GA definition
  public
  final
  create public .

public section.

  interfaces Y20_IF_ODATA_ENTITY .

  types:
      "!<p class="shorttext synchronized">Task create input parameters</p>
    BEGIN OF tys_task_create,
        update_task TYPE /scwm/rl03averbu,
        commit_work TYPE /scwm/rl03acomit,
        wtcode      TYPE /scwm/de_wtcode,
        rfc_queue   TYPE /scwm/s_rfc_queue,
        create_hu   TYPE /scwm/tt_to_crea_hu,
      END OF   tys_task_create .

  data:
      "!<p class="shorttext synchronized">UserCommand - Trolley entity attribute</p>
    BEGIN OF ms_user_command.
        INCLUDE TYPE YCL_Y20_EWM_QUALITY_GA_MPC=>ts_usercommandent.
    DATA: documentitementset        TYPE YCL_Y20_EWM_QUALITY_GA_MPC=>tt_documentitement,
      END OF ms_user_command .

  methods CONSTRUCTOR
    importing
      !IO_LOG type ref to Y20_CL_LOG .
  methods EXECUTE_USERCOMM
    importing
      !IO_DATA_PROVIDER type ref to /IWBEP/IF_MGW_ENTRY_PROVIDER
    returning
      value(RO_ENTITY) type ref to DATA
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods COMPLETE_HU_PROCESS_STEP
    importing
      !IV_LGNUM type /SCWM/LGNUM
      !IV_WORKSTATION type /SCWM/DE_WORKSTATION
      !IV_SAVE type ABAP_BOOL default ABAP_TRUE
      !IS_HUHDR type /SCWM/S_HUHDR_INT
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
protected section.
private section.

    "!<p class="shorttext synchronized">Message string</p>
  data MV_MSG type STRING .
    "!<p class="shorttext synchronized">Warehouse Number</p>
  data MV_LGNUM type /SCWM/S_T300-LGNUM .
    "!<p class="shorttext synchronized">Navigation key, indicates execution method</p>
  data MV_KEY_NAVIGATION type STRING .
    "!<p class="shorttext synchronized">Global material attribute</p>
  data MT_MATERIAL_GLOBAL type /SCWM/TT_MATERIAL_GLOBAL .
*  data MT_HUITEM type /SCWM/TT_HUITM_INT .
    "!<p class="shorttext synchronized">HU Header</p>
  data MS_HUHDR type /SCWM/S_HUHDR_INT .
    "!<p class="shorttext synchronized">Message log</p>
  data MO_LOG type ref to Y20_CL_LOG .
    "!<p class="shorttext synchronized">Instance of Handling Unit</p>
  data MO_HU type ref to Y20_CL_ODATA_HANDLING_UNIT .
  data MO_DELIVERY_BOM type ref to /SCDL/CL_BO_MANAGEMENT .
  data MT_DLV_ITEMS type YCL_Y20_EWM_QUALITY_GA_MPC=>TT_DOCUMENTITEMENT .
  data MV_EXECUTE_METHOD type STRING .

  methods READ_OPEN_TASK
    changing
      !CS_ITEM type YCL_Y20_EWM_QUALITY_GA_MPC=>TS_DOCUMENTITEMENT
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods READ_PICK_TASKS
    changing
      !CS_ITEM type YCL_Y20_EWM_QUALITY_GA_MPC=>TS_DOCUMENTITEMENT
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods COUNTING_WITHOUT_DIFFERENCES
    returning
      value(RV_RESULT) type BOOLEAN .
  methods COUNTING_WITH_DIFFERENCES
    returning
      value(RV_RESULT) type BOOLEAN .
  methods COMPLETE_HU_PROCESS_STEPS
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods SET_DELIVERY_ITEMS
    importing
      !IT_ITEMS type /SCDL/DL_ITEM_TAB .
  methods GET_EXPANDED_HU
    importing
      !IT_KEY_TAB type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
    returning
      value(RO_RESULT) type ref to DATA
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION
      /IWBEP/CX_MGW_BUSI_EXCEPTION .
  methods GET_EXPANDED_USERCOMMAND
    importing
      !IT_KEY_TAB type /IWBEP/T_MGW_NAME_VALUE_PAIR optional
    returning
      value(RO_RESULT) type ref to DATA
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
        "!<p class="shorttext synchronized">Convert structure to reference data</p>
  methods COPY_DATA_TO_REF
    importing
      !IS_DATA type ANY
    returning
      value(RO_DATA) type ref to DATA .
  methods EXECUTE_CLEARING
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods EXECUTE_COUNTING
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods EXECUTE_REPRINT
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods CREATE_HU_TASK
    importing
      !IS_TASK_CREATE type TYS_TASK_CREATE
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods CREATE_HU_TASKS_TO_CLEARING
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods GET_BO_DOCUMENT_ITEMS
    importing
      !IV_DOCID type /SCWM/S_DOCID_ITMID-DOCID
    returning
      value(RT_RESULT) type /SCDL/DL_ITEM_TAB
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods GET_BO_HEADER
    importing
      !IV_DOCID type /SCWM/S_DOCID_ITMID-DOCID
    returning
      value(RO_BO) type ref to /SCDL/CL_DL_HEADER_PRD
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods READ_HU_FOR_DOCITEM
    importing
      !IS_DOCID type /SCWM/S_DOCID_ITMID
    exporting
      !ET_HUHDR type /SCWM/TT_HUHDR_INT
      !ET_HUITEM type /SCWM/TT_HUITM_INT
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods RAISE_BAPIRET_MESSAGE
    importing
      !IT_BAPIRET type BAPIRETTAB
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods RAISE_EXCEPTION
    raising
      /IWBEP/CX_MGW_TECH_EXCEPTION .
  methods READ_MATERIAL_SAPAPO
    importing
      !IO_ITEM type ref to /SCDL/CL_DL_ITEM_PRD
    returning
      value(RS_RESULT) type /SAPAPO/MATKEY_OUT .
ENDCLASS.



CLASS Y20_CL_ODATA_QUAL_GA IMPLEMENTATION.


  METHOD complete_hu_process_step.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Complete HU process step ZOB4
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-18¦F.Pleinies    ¦Initial
*
************************************************************************
    DATA: lv_message     TYPE string,
          ls_workstation TYPE /scwm/tworkst.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    /scwm/cl_tm=>cleanup( ).
    /scwm/cl_tm=>cleanup( iv_lgnum = mv_lgnum ).
    /scwm/cl_tm=>set_lgnum( mv_lgnum ).

    /scwm/cl_wm_packing=>get_instance( IMPORTING eo_instance = DATA(lo_pack) ).

    "Read workcenter
    CALL FUNCTION '/SCWM/TWORKST_READ_SINGLE'
      EXPORTING
        iv_lgnum       = iv_lgnum
        iv_workstation = iv_workstation
      IMPORTING
        es_workst      = ls_workstation
      EXCEPTIONS
        error          = 1
        not_found      = 2
        OTHERS         = 3.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_message.
      RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
    ENDIF.
    " initialize packing. Set global parameters, log instance
    lo_pack->init_by_workstation(
      EXPORTING
        is_workstation   = VALUE #( lgnum = iv_lgnum
                                    workstation = iv_workstation
                                    procs = ls_workstation-procs )
        ir_huident       = VALUE #( ( sign   = wmegc_sign_inclusive
                                      option = wmegc_option_eq
                                      low    = is_huhdr-huident ) )
      EXCEPTIONS
        error            = 1
        OTHERS           = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_message.
      RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
    ENDIF.

    lo_pack->hu_process_completed(
      EXPORTING
        iv_hu  = is_huhdr-guid_hu
      EXCEPTIONS
        error  = 1
        OTHERS = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_message.
      RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
    ENDIF.

    IF iv_save = abap_true.
      lo_pack->save(
        EXCEPTIONS
          error     = 1
          OTHERS    = 2 ).
      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
           WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_message.
        RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD complete_hu_process_steps.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Complete HU process steps for all delivery HUs.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-18¦F.Pleinies    ¦Initial
*
************************************************************************

    DATA: lv_complete_hu  TYPE boolean VALUE abap_true,
          ls_docid_itemid TYPE /scwm/s_docid_itmid.

    DATA(lv_workst) = zswl_cl_param=>read_const(
                                   iv_lgnum     = mv_lgnum
                                   iv_context   = zswl_if_param_c=>mc_qgate
                                   iv_parameter = zswl_if_param_c=>mc_workst ).


*          LOOP AT mt_dlv_items INTO DATA(ls_item).

    ls_docid_itemid-docid = mt_dlv_items[ 1 ]-docid.
*            ls_docid_itemid-itmid = ls_item-itemid.
    ls_docid_itemid-doccat = 'PDO'.

    read_hu_for_docitem(
    EXPORTING
      is_docid = ls_docid_itemid
    IMPORTING
      et_huhdr = DATA(lt_huhdr) ).

*            lt_huhdr[ 1 ]-guid_hu.

    LOOP AT lt_huhdr INTO DATA(ls_huhdr).

      complete_hu_process_step(
        EXPORTING
          iv_lgnum = mv_lgnum
          is_huhdr = ls_huhdr
          iv_workstation = CONV #( lv_workst )
           ).

    ENDLOOP.


  ENDMETHOD.


  method CONSTRUCTOR.


mo_hu = y20_cl_odata_handling_unit=>get_instance( ).

************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Create instance
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-18¦F.Pleinies    ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_huident TYPE /scwm/de_huident.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    "mo_log = io_log.

    mo_hu = y20_cl_odata_handling_unit=>get_instance( ).


endmethod.


  METHOD copy_data_to_ref.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-18¦F.Pleinies    ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    FIELD-SYMBOLS: <ls_data> TYPE any.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CREATE DATA ro_data LIKE is_data.
    ASSIGN ro_data->* TO <ls_data>.
    <ls_data> = is_data.
  ENDMETHOD.


  METHOD counting_without_differences.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Check if all items were counted without difference.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-18¦F.Pleinies    ¦Initial
*
************************************************************************

    rv_result = abap_true.

        LOOP AT mt_dlv_items INTO DATA(ls_item).
          IF ls_item-counted = abap_false OR ls_item-result = '2'. "Okay min. one Item is not okay
            rv_result = abap_false.
            EXIT.
          ENDIF.
        ENDLOOP.


  ENDMETHOD.


  METHOD COUNTING_WITH_DIFFERENCES.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Check if all delivery items were counted with min. one difference.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-18¦F.Pleinies    ¦Initial
*
************************************************************************

    rv_result = abap_true.

        LOOP AT mt_dlv_items INTO DATA(ls_item).
          IF ls_item-counted = abap_false OR
              ( ls_item-result = '2' AND ls_item-countedtimes < 2 ). "Okay min. one Item is not okay
            rv_result = abap_false.
            EXIT.
          ENDIF.
        ENDLOOP.


  ENDMETHOD.


  METHOD CREATE_HU_TASK.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Create HU movement task.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-18¦F.Pleinies    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_bapiret  TYPE bapirettab,
          lv_severity TYPE bapi_mtype.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/TO_CREATE_MOVE_HU'
      EXPORTING
        iv_lgnum       = mv_lgnum
        iv_bname       = sy-uname
        iv_update_task = is_task_create-update_task
        iv_commit_work = is_task_create-commit_work
        iv_wtcode      = is_task_create-wtcode
        it_create_hu   = is_task_create-create_hu
      IMPORTING
        et_bapiret     = lt_bapiret
        ev_severity    = lv_severity.
    IF lv_severity CA wmegc_severity_ea.
      raise_bapiret_message( lt_bapiret ).
    ENDIF.
  ENDMETHOD.


  METHOD create_hu_tasks_to_clearing.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Create HU movement tasks for all HUs to the clearing zone.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-18¦F.Pleinies    ¦Initial
*
************************************************************************

    DATA: lt_create       TYPE /scwm/tt_to_crea_hu,
          ls_create       LIKE LINE OF lt_create,
          ls_docid_itemid TYPE /scwm/s_docid_itmid.

    DATA(lv_clearing_bin) = zswl_cl_param=>read_const(
                                   iv_lgnum     = mv_lgnum
                                   iv_context   = zswl_if_param_c=>mc_qgate
                                   iv_parameter = zswl_if_param_c=>mc_clearing_bin ).
    DATA(lv_procty) = zswl_cl_param=>read_const(
                                   iv_lgnum     = mv_lgnum
                                   iv_context   = zswl_if_param_c=>mc_qgate
                                   iv_parameter = zswl_if_param_c=>mc_procty_clearing ).

    ls_docid_itemid-docid = mt_dlv_items[ 1 ]-docid.
*            ls_docid_itemid-itmid = ls_item-itemid.
    ls_docid_itemid-doccat = 'PDO'.

    read_hu_for_docitem(
    EXPORTING
      is_docid = ls_docid_itemid
    IMPORTING
      et_huhdr = DATA(lt_huhdr) ).

*            lt_huhdr[ 1 ]-guid_hu.

    LOOP AT lt_huhdr INTO DATA(ls_huhdr).
*            lt_huhdr[ 1 ]-guid_hu.
      ls_create-guid_hu = ls_huhdr-guid_hu.
      ls_create-huident = ls_huhdr-huident.
      ls_create-procty = CONV #( lv_procty ).
      ls_create-squit  = abap_true.
      ls_create-norou  = wmegc_norou_yes.
*            nltyp = ls_storage_type-lgtyp
      ls_create-nlpla = lv_clearing_bin.
      ls_create-rdocrelevant = abap_true.
      APPEND ls_create TO lt_create.
    ENDLOOP.

    " build create task data
    create_hu_task( VALUE #( commit_work = abap_true
                             update_task = abap_false
                             create_hu = lt_create ) ).

  ENDMETHOD.


  METHOD execute_clearing.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Handle Clearing command on UI. Move all HUs immediately
*               to the clearing zone.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-28¦F.Pleinies    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( ms_user_command-documentitementset ) = 0.
      ##NEEDED
      MESSAGE e003(y20_ui5) INTO DATA(lv_msg).
*      mo_entity->raise_exception( ).
    ENDIF.

    mt_dlv_items = ms_user_command-documentitementset.

    READ TABLE mt_dlv_items ASSIGNING FIELD-SYMBOL(<ls_dlvitem>)
          INDEX 1.
    IF <ls_dlvitem> IS ASSIGNED.
      mv_lgnum = <ls_dlvitem>-lgnum.
    ENDIF.

    "Create HU-WTs to clearing
    create_hu_tasks_to_clearing( ).

    ms_user_command-completed = abap_true.
    ms_user_command-showmsg = '03'. "Show final msg with differences

    ms_user_command-documentitementset = mt_dlv_items.
  ENDMETHOD.


  METHOD execute_counting.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Handle Counting command on UI. Count the selected item
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-28¦F.Pleinies    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( ms_user_command-documentitementset ) = 0.
      ##NEEDED
      MESSAGE e003(y20_ui5) INTO DATA(lv_msg).
*      mo_entity->raise_exception( ).
    ENDIF.

    mt_dlv_items = ms_user_command-documentitementset.

    DATA(ls_item) = REF #( mt_dlv_items[ 1 ] ).

    clear ms_user_command-showMsg.

    READ TABLE mt_dlv_items ASSIGNING FIELD-SYMBOL(<ls_dlvitem>)
      with key  itemid = ms_user_command-itemid.

    IF <ls_dlvitem> is ASSIGNED.

       mv_lgnum = <ls_dlvitem>-lgnum.
      "Set and check the counted quantity
       <ls_dlvitem>-counted = abap_true.
       <ls_dlvitem>-countedtimes = <ls_dlvitem>-countedtimes + 1.

      <ls_dlvitem>-qtycounted-quan = ms_user_command-qtycounted.

      if  <ls_dlvitem>-qtycounted-quan <> <ls_dlvitem>-qty-quan.
        <ls_dlvitem>-result = '2'. "Fail
      else.
        <ls_dlvitem>-result = '3'. "Okay
      endif.

      IF <ls_dlvitem>-result = '2' AND <ls_dlvitem>-countedtimes = 1 .
        ms_user_command-showmsg = '01'. "Show first wrong quantity msg
      ELSEIF <ls_dlvitem>-result = '2' AND <ls_dlvitem>-countedtimes >= 2 .
        ms_user_command-showmsg = '02'. "Show second wrong quantity msg

      ENDIF.
    ENDIF.


**********************************************************************
*Check if all delivery items are counted
    IF counting_without_differences( ).
        ms_user_command-completed = abap_true.
        ms_user_command-showmsg = '04'. "Show final msg without differences
        "Close all HU's if counting were successful,
        complete_hu_process_steps( ).
    ELSEIF counting_with_differences( ).
        ms_user_command-completed = abap_true.
        ms_user_command-showmsg = '03'. "Show final msg with differences
      "else move to clearing
        create_hu_tasks_to_clearing( ).
    ENDIF.


    ms_user_command-documentitementset = mt_dlv_items.
  ENDMETHOD.


  METHOD execute_reprint.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Handle REPRINT command on UI. Print the counting report
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-28¦F.Pleinies    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    DATA:
          ls_qgate_report TYPE y20_s_qgate_report.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( ms_user_command-documentitementset ) = 0.
      ##NEEDED
      MESSAGE e003(y20_ui5) INTO DATA(lv_msg).
*      mo_entity->raise_exception( ).
    ENDIF.

    mt_dlv_items = ms_user_command-documentitementset.

    DATA(ls_item) = REF #( mt_dlv_items[ 1 ] ).

*    clear ms_user_command-showMsg.

*    READ TABLE mt_dlv_items ASSIGNING FIELD-SYMBOL(<ls_dlvitem>)
*      with key  itemid = ms_user_command-itemid.

    IF ls_item IS NOT INITIAL.
      mv_lgnum = ls_item->lgnum.
      "Read the delivery header
      DATA(lr_dlv_header) = get_bo_header( ls_item->docid ).

      "Fill header
      ls_qgate_report-date = sy-datum.
      ls_qgate_report-time = sy-uzeit.
      ls_qgate_report-uname = sy-uname.
      ls_qgate_report-lgnum = ls_item->lgnum.
      DATA(lt_refdoc) = lr_dlv_header->get_refdoc( EXPORTING iv_refdoccat = 'ERP' ).
      ls_qgate_report-delivery = lt_refdoc[ 1 ]-refdocno.
      ls_qgate_report-ewm_document = lr_dlv_header->get_docno( ).
      SHIFT ls_qgate_report-ewm_document LEFT DELETING LEADING '0'.
      lt_refdoc = lr_dlv_header->get_refdoc( EXPORTING iv_refdoccat = 'ERP' ).
      "Read all delivery items
      DATA(lt_items) = get_bo_document_items( ls_item->docid ).
      lt_refdoc = lt_items[ 1 ]-item->get_refdoc( EXPORTING iv_refdoccat = 'SO' ).
      ls_qgate_report-po_so = lt_refdoc[ 1 ]-refdocno.
      ls_qgate_report-pool_name = lr_dlv_header->get_eew( )-zzpoolname.
      ls_qgate_report-pool_date = lr_dlv_header->get_eew( )-zzpooldate.

      "Fill items
      "Read all Dlv HUs
      read_hu_for_docitem(
      EXPORTING
        is_docid = VALUE #( docid = ls_item->docid doccat = 'PDO' )
      IMPORTING
        et_huhdr  = DATA(lt_huhdr)
        et_huitem = DATA(lt_huitem) ).

      LOOP AT mt_dlv_items ASSIGNING FIELD-SYMBOL(<fs_item>).

        LOOP AT lt_huitem INTO DATA(ls_huitem).
          IF ls_huitem-qitmid = <fs_item>-itemid.
            READ TABLE lt_huhdr INTO DATA(ls_huhdr) WITH KEY guid_hu = ls_huitem-guid_parent.
            IF ls_huhdr IS NOT INITIAL.
              APPEND VALUE #( item = <fs_item>-itemno
                              huident = ls_huhdr-huident
                              "wt = '333'
                              product = <fs_item>-product-matnr
                              product_desc = <fs_item>-product-maktx
                              item_qty = <fs_item>-qty-quan
                              counted_qty = <fs_item>-qtycounted-quan
                              uom = <fs_item>-qty-unit
                              "task_quant = <fs_item>-qty-quan
                             ) TO ls_qgate_report-items.

            ENDIF.
          ENDIF.
        ENDLOOP.

      ENDLOOP.


      "Call the reprint function.
      y20_cl_print_pdf_lbl=>print_qgate_report( ls_qgate_report ).
      "TODO - to be implemented
    ENDIF.


    ms_user_command-documentitementset = mt_dlv_items.
  ENDMETHOD.


  METHOD EXECUTE_USERCOMM.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Handle user command on UI. Dispatch command to the
*                correct execution method
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data(
      IMPORTING es_data = ms_user_command ).

    CASE ms_user_command-usercommand.
      WHEN '01'. "Count
        execute_counting( ).
      WHEN '02'. "Reprint
        execute_reprint( ).
      WHEN '03'. "Clearing
        execute_clearing( ).
    ENDCASE.

    ro_entity = copy_data_to_ref( is_data = ms_user_command ).
  ENDMETHOD.


  METHOD GET_BO_DOCUMENT_ITEMS.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Return instance of all delivery items
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lo_bo_delivery) = mo_delivery_bom->get_bo_by_id( iv_docid = iv_docid ).
    IF lo_bo_delivery IS NOT BOUND.
      "raise_exception( ).
    ENDIF.

    lo_bo_delivery->get_item_tab(
      IMPORTING
        et_item = rt_result ).
  ENDMETHOD.


  METHOD GET_BO_HEADER.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Return instance of delivery header
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_delivery_bom = /scdl/cl_bo_management=>get_instance( ).
    IF mo_delivery_bom IS NOT BOUND.
      RETURN.
    ENDIF.

    DATA(lo_dl_query) = NEW /scdl/cl_dl_query( iv_doccat = /scdl/if_dl_doc_c=>sc_doccat_out_prd ).
*    LOOP AT mt_docid ASSIGNING FIELD-SYMBOL(<ls_docid>).
      lo_dl_query->add_docid( iv_docid = iv_docid ).
*    ENDLOOP.
    " execute query to load the BOs
    mo_delivery_bom->query(
      EXPORTING
        io_query            = lo_dl_query
        iv_lock_mode        = /scdl/if_dl_object_c=>sc_lock_none
        iv_data_source      = /scdl/if_dl_query_c=>sc_source_db ).

    DATA(lo_bo_delivery) = mo_delivery_bom->get_bo_by_id( iv_docid = iv_docid ).
    IF lo_bo_delivery IS NOT BOUND.
*      raise_exception( ).
    ENDIF.

    ro_bo = CAST /scdl/cl_dl_header_prd( lo_bo_delivery->get_header( ) ).
  ENDMETHOD.


  METHOD get_expanded_hu.
***********************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Get the relevant UI data after scanning a HU.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-28¦F.Pleinies    ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    "DATA(ls_entity_keys) = get_hu_entity_key( it_key_tab ).

    "validate_set_hu_enitity( ls_entity_keys ).
    DATA: lv_huident TYPE /scwm/de_huident,
          lv_msg     TYPE string.
    DATA:
      BEGIN OF ls_entity.
        INCLUDE TYPE ycl_y20_ewm_quality_ga_mpc=>ts_scanhandlingunitent.
    DATA: documentheaderent  TYPE ycl_y20_ewm_quality_ga_mpc=>ts_documentheaderent,
        documentitementset TYPE ycl_y20_ewm_quality_ga_mpc=>tt_documentitement,
      END OF ls_entity,
      ls_who TYPE /scwm/s_who_int.

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA: ls_entity_key TYPE ycl_y20_ewm_quality_ga_mpc_ext=>tys_entity_keys.

    LOOP AT it_key_tab ASSIGNING FIELD-SYMBOL(<ls_key>).
      CASE <ls_key>-name.
        WHEN ycl_y20_ewm_quality_ga_mpc_ext=>ms_field_name-lgnum.
          ls_entity_key-lgnum = <ls_key>-value.
          mv_lgnum = ls_entity_key-lgnum.
        WHEN ycl_y20_ewm_quality_ga_mpc_ext=>ms_field_name-scanhu.
          ls_entity_key-huident = <ls_key>-value.
      ENDCASE.
    ENDLOOP.

    IF ls_entity_key IS NOT INITIAL.
      "validate_set_warehouse_number( is_entity_key-lgnum ).

      IF ls_entity_key-huident IS NOT INITIAL.
        lv_huident = ls_entity_key-huident.

        mo_hu->read_hu_single_by_huident(
         EXPORTING
           iv_huident = lv_huident
         IMPORTING
           es_huhdr   = DATA(ls_huhdr)
           et_huitem  = DATA(lt_huitm) ).
        IF lines( lt_huitm ) IS INITIAL.
          MESSAGE e017(y20_ui5) INTO lv_msg.
          RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.

        ENDIF.

        if  ls_huhdr-lgtyp is NOT INITIAL AND ls_huhdr-lgtyp NE 'QGTT'.
          MESSAGE e112(y20_ui5)  with lv_huident ls_huhdr-lgtyp INTO lv_msg.
          RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
        endif.

        DATA(ls_item) = REF #( lt_huitm[ 1 ] ).
        IF ls_item->qdocid IS INITIAL OR ls_item->qitmid IS INITIAL.
          MESSAGE e145(/scwm/shp_rcv) WITH lv_huident INTO lv_msg.
          RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
        ENDIF.

        "ms_entity_key = is_entity_key.
      ENDIF.

      "Read the delivery header
      DATA(lr_dlv_header) = get_bo_header( ls_item->qdocid ).

      "Read all delivery items
      DATA(lt_items) = get_bo_document_items( ls_item->qdocid ).

      set_delivery_items( lt_items ).
    ENDIF.

    ls_entity-lgnum = ls_entity_key-lgnum.
    ls_entity-documentheaderent-docno = lr_dlv_header->get_docno( ).
    SHIFT ls_entity-documentheaderent-docno LEFT DELETING LEADING '0'.
    ls_entity-documentheaderent-docid = lr_dlv_header->get_docid( ).
    ls_entity-documentitementset = mt_dlv_items.
*    ls_entity-scanhu_hu-warehousetask = ls_ordim_o->tanum.

    ro_result = copy_data_to_ref( ls_entity ).
  ENDMETHOD.


  METHOD GET_EXPANDED_USERCOMMAND.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Get HU entity after HU is scanned
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    "validate_set_hu_enitity( ls_entity_keys ).
    DATA: lv_huident TYPE /scwm/de_huident,
          lv_msg TYPE string.
    DATA:
      BEGIN OF ls_entity.
        INCLUDE TYPE ycl_y20_ewm_quality_ga_mpc=>ts_scanhandlingunitent.
        DATA: documentitementset type ycl_y20_ewm_quality_ga_mpc=>tt_documentitement,
      END OF ls_entity,
      ls_who TYPE /scwm/s_who_int.

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA: ls_entity_key TYPE ycl_y20_ewm_quality_ga_mpc_ext=>tys_entity_keys.

    LOOP AT it_key_tab ASSIGNING FIELD-SYMBOL(<ls_key>).
      CASE <ls_key>-name.
        WHEN ycl_y20_ewm_quality_ga_mpc_ext=>ms_field_name-lgnum.
         ls_entity_key-lgnum = <ls_key>-value.
        WHEN ycl_y20_ewm_quality_ga_mpc_ext=>ms_field_name-scanhu.
          ls_entity_key-huident = <ls_key>-value.
      ENDCASE.
    ENDLOOP.

    IF ls_entity_key IS NOT INITIAL.
      "validate_set_warehouse_number( is_entity_key-lgnum ).

      IF ls_entity_key-huident IS NOT INITIAL.
        lv_huident = ls_entity_key-huident.

         mo_hu->read_hu_single_by_huident(
          EXPORTING
            iv_huident = lv_huident
          IMPORTING
            es_huhdr   = DATA(ls_huhdr)
            et_huitem  = DATA(lt_huitm) ).
        IF lines( lt_huitm ) IS INITIAL.
          MESSAGE e063(y20_ui5) WITH lv_huident INTO lv_msg.
          RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
        ENDIF.

        DATA(ls_item) = REF #( lt_huitm[ 1 ] ).
        IF ls_item->qdocid IS INITIAL OR ls_item->qitmid IS INITIAL.
          MESSAGE e145(/scwm/shp_rcv) WITH lv_huident INTO lv_msg.
          RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
        ENDIF.

      "ms_entity_key = is_entity_key.
    ENDIF.

      "Read the delivery header
      DATA(lr__dlv_header) = get_bo_header( ls_item->qdocid ).

      "Read all delivery items
      DATA(lt_items) = get_bo_document_items( ls_item->qdocid ).

      set_delivery_items( lt_items ).
ENDIF.

    ls_entity-lgnum = ls_entity_key-lgnum.
    ls_entity-documentitementset = mt_dlv_items.
*    ls_entity-scanhu_hu-warehousetask = ls_ordim_o->tanum.

    ro_result = copy_data_to_ref( ls_entity ).
  ENDMETHOD.


  METHOD RAISE_BAPIRET_MESSAGE.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Raise message from BAPIRET
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    LOOP AT it_bapiret ASSIGNING FIELD-SYMBOL(<ls_bapiret>) WHERE type CA wmegc_severity_ea.
      MESSAGE ID <ls_bapiret>-id TYPE <ls_bapiret>-type NUMBER <ls_bapiret>-number
         WITH <ls_bapiret>-message_v1 <ls_bapiret>-message_v2 <ls_bapiret>-message_v3 <ls_bapiret>-message_v4
         INTO mv_msg.
      raise_exception( ).
    ENDLOOP.
  ENDMETHOD.


  METHOD RAISE_EXCEPTION.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Raise exception
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF mo_log IS BOUND.
      mo_log->add_message( ).
    ENDIF.
    RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception(
        textid = VALUE #( msgno = sy-msgno
                          msgid = sy-msgid
                          attr1 = sy-msgv1
                          attr2 = sy-msgv2
                          attr3 = sy-msgv3
                          attr4 = sy-msgv4 ) ).
  ENDMETHOD.


  METHOD READ_HU_FOR_DOCITEM.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Read HUs for document item
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " read HUs items
    CALL FUNCTION '/SCWM/HU_SELECT_GEN'
      EXPORTING
        iv_lgnum     = mv_lgnum
        ir_qdoccat   = VALUE rseloption( ( sign = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low    = is_docid-doccat ) )
        it_qdocid    = VALUE /scwm/tt_docid( ( docid = is_docid-docid ) )
*        it_qitmid    = VALUE /scwm/tt_itemid( ( is_docid-itmid ) )
        ir_vhi       = VALUE rseloption( ( sign = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low = wmegc_vhi_real ) )
      IMPORTING
        et_huhdr     = et_huhdr
        et_huitm     = et_huitem
      EXCEPTIONS
        wrong_input  = 1
        not_possible = 2
        error        = 3
        OTHERS       = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
*      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD READ_MATERIAL_SAPAPO.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Read SAPAPO material data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_item->get_sapext( IMPORTING es_sapext_o = DATA(ls_sapext) ).

    DATA(lo_prod) = NEW /scwm/cl_mon_prod( ).

    lo_prod->get_prod_node_data(
      EXPORTING
        iv_lgnum            = mv_lgnum
        it_matnr_range      = VALUE #( ( sign = wmegc_sign_inclusive
                                         option = wmegc_option_eq
                                         low = io_item->get_product( )-productno ) )
        it_entit_range      = VALUE #( ( sign = wmegc_sign_inclusive
                                         option = wmegc_option_eq
                                         low = ls_sapext-entitled ) )
      IMPORTING
        et_material         = DATA(lt_material) ).
    IF lines( lt_material ) = 0.
      RETURN.
    ENDIF.

    rs_result = lt_material[ 1 ].
  ENDMETHOD.


  method READ_OPEN_TASK.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Determine WHO number.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_tasks TYPE /scwm/tt_to_det_mon,
          lv_message type string.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF cs_item IS INITIAL.
      MESSAGE e015(y20_ui5) INTO lv_message.
      RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
    ENDIF.

    DATA(ls_selcrit) = VALUE /scwm/s_to_selcrit_mon(
      r_rdocid = VALUE #( ( sign   = wmegc_sign_inclusive
                             option = wmegc_option_eq
                             low    = cs_item-docid  )  )
                          ).
    CALL FUNCTION '/SCWM/TO_GET_WIP'
      EXPORTING
        iv_lgnum   = mv_lgnum
        iv_open    = abap_true
        is_selcrit = ls_selcrit
      IMPORTING
        et_to      = lt_tasks.
    IF lines( lt_tasks ) = 0.
*      MESSAGE e001(y20_ui5) WITH iv_huident INTO lv_message.
*      RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
    ENDIF.

    delete lt_tasks WHERE ritmid <> cs_item-itemid.
*    APPEND LINES OF lt_tasks TO mt_task_open.
    IF lines( lt_tasks ) > 0.
      cs_item-who = lt_tasks[ 1 ]-who.
    ENDIF.

endmethod.


  method READ_PICK_TASKS.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Determine all confirmed pick tasks for delivery item
*               and return the amount of entries.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_tasks TYPE /scwm/tt_to_det_mon,
          lv_message type string.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF cs_item IS INITIAL.
      MESSAGE e015(y20_ui5) INTO lv_message.
      RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
    ENDIF.

    DATA(ls_selcrit) = VALUE /scwm/s_to_selcrit_mon(
      r_tostat = VALUE #( ( sign   = wmegc_sign_inclusive
                          option = wmegc_option_eq
                          low    = 'C'  )  )
      r_trart = VALUE #( ( sign   = wmegc_sign_inclusive
                          option = wmegc_option_eq
                          low    = '2'  )  )
      r_flghuto = VALUE #( ( sign   = wmegc_sign_inclusive
                             option = wmegc_option_eq
                             low    = abap_false  )  )
      r_rdocid = VALUE #( ( sign   = wmegc_sign_inclusive
                             option = wmegc_option_eq
                             low    = cs_item-docid  )  )
                          ).
    CALL FUNCTION '/SCWM/TO_GET_WIP'
      EXPORTING
        iv_lgnum   = mv_lgnum
        iv_conf    = abap_true
        is_selcrit = ls_selcrit
      IMPORTING
        et_to      = lt_tasks.
    IF lines( lt_tasks ) = 0.
*      MESSAGE e001(y20_ui5) WITH iv_huident INTO lv_message.
*      RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
    ENDIF.

    delete lt_tasks WHERE ritmid <> cs_item-itemid.
*    APPEND LINES OF lt_tasks TO mt_task_open.

    cs_item-taskcount = lines( lt_tasks ).

endmethod.


  METHOD set_delivery_items.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Fill the response structure for delivery items.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************

    DATA: ls_dlv_item LIKE LINE OF mt_dlv_items,
          lv_matnr    TYPE /sapapo/matnr.
    LOOP AT it_items ASSIGNING FIELD-SYMBOL(<ls_dlvitm>).
*           TRY.
*               DATA(ls_huhdr) = REF #( it_huhdr[ guid_hu = <ls_huitm>-guid_parent ] ).
*             CATCH cx_sy_itab_line_not_found.
*               CONTINUE.
*           ENDTRY.

*           " get material for current HU
*           DATA(ls_material_global) = get_material_global( <ls_huitm>-matid ).
      ls_dlv_item-lgnum = mv_lgnum.
      IF <ls_dlvitm>-item->get_product( )-productno IS INITIAL .
        CONTINUE.
      ENDIF.

      CALL FUNCTION 'CONVERSION_EXIT_PRODU_OUTPUT'
        EXPORTING
          input        = <ls_dlvitm>-item->get_product( )-productno
        IMPORTING
          output       = lv_matnr
        EXCEPTIONS
          length_error = 1
          OTHERS       = 2.
      IF sy-subrc <> 0.
*              RETURN.
      ENDIF.

      ls_dlv_item-product-matnr = lv_matnr.
      ls_dlv_item-product-matid = <ls_dlvitm>-item->get_product( )-productid.
      ls_dlv_item-product-maktx = <ls_dlvitm>-item->get_product( )-product_text.
      "Read the handlingIndicator(SensivityCode)
      ls_dlv_item-product-hndlcode = read_material_sapapo( CAST /scdl/cl_dl_item_prd( <ls_dlvitm>-item ) )-hndlcode.
      ls_dlv_item-docid = <ls_dlvitm>-item->get_docid( ).
      ls_dlv_item-itemid = <ls_dlvitm>-itemid.
      ls_dlv_item-itemno = <ls_dlvitm>-itemno.
      ls_dlv_item-itemno = <ls_dlvitm>-itemno.
      ls_dlv_item-qty-quan = <ls_dlvitm>-item->get_qty( )-qty.
      ls_dlv_item-qty-unit = <ls_dlvitm>-item->get_qty( )-uom.
      ls_dlv_item-qtycounted-quan = 0.
      ls_dlv_item-qtycounted-unit = <ls_dlvitm>-item->get_qty( )-uom.

      read_pick_tasks( CHANGING cs_item = ls_dlv_item ).
*           ls_dlv_item-taskcount = '2'.
      read_open_task( CHANGING cs_item = ls_dlv_item ).

      APPEND ls_dlv_item TO mt_dlv_items.
    ENDLOOP.
  ENDMETHOD.


  method Y20_IF_ODATA_ENTITY~CREATE_DEEP_ENTITY.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Execute POST request for userCommand. Dispatch to the correct
*               function.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
*    CLEAR: et_expanded_tech_clauses, er_entity.

    CASE mv_execute_method.
      WHEN ycl_Y20_ewm_quality_ga_mpc_ext=>ms_method_key-usercommand.
        ro_deep_entity = execute_usercomm( io_data_provider = io_data_provider ).
*        APPEND 'DOCUMENTHEADERENT' TO et_expanded_tech_clauses.
*        APPEND 'DOCUMENTITEMENTSET' TO et_expanded_tech_clauses.

*      WHEN ycl_Y20_ewm_quality_ga_mpc_ext=>ms_method_key-usercommand.
*        er_entity = get_expanded_userCommand( it_key_tab = it_key_tab ).
*        APPEND 'DOCUMENTITEMENTSET' TO et_expanded_tech_clauses.
  ENDCASE.

  endmethod.


  method Y20_IF_ODATA_ENTITY~GET_EXPANDED_ENTITY.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_QGTE
* Description : Get request, expanded entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-06-25¦F. Pleinies     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: et_expanded_tech_clauses, er_entity.

    CASE mv_execute_method.
      WHEN ycl_Y20_ewm_quality_ga_mpc_ext=>ms_method_key-scanhu_huent.
        er_entity = get_expanded_hu( it_key_tab = it_key_tab ).
        APPEND 'DOCUMENTHEADERENT' TO et_expanded_tech_clauses.
        APPEND 'DOCUMENTITEMENTSET' TO et_expanded_tech_clauses.

      WHEN ycl_Y20_ewm_quality_ga_mpc_ext=>ms_method_key-usercommand.
        er_entity = get_expanded_userCommand( it_key_tab = it_key_tab ).
        APPEND 'DOCUMENTITEMENTSET' TO et_expanded_tech_clauses.
  ENDCASE.
endmethod.


  method Y20_IF_ODATA_ENTITY~SET_EXECUTE_METHOD.
    mv_execute_method = iv_key.
  endmethod.
ENDCLASS.