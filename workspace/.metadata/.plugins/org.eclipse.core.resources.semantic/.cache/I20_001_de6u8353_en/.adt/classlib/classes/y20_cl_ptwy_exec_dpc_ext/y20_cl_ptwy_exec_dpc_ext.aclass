CLASS y20_cl_ptwy_exec_dpc_ext DEFINITION
  PUBLIC
  INHERITING FROM y20_cl_ptwy_exec_dpc
  CREATE PUBLIC .

  PUBLIC SECTION.

    METHODS /iwbep/if_mgw_appl_srv_runtime~get_entity
        REDEFINITION .
    METHODS /iwbep/if_mgw_core_srv_runtime~create_entity
        REDEFINITION .
    METHODS /iwbep/if_mgw_appl_srv_runtime~get_entityset
        REDEFINITION .
  PROTECTED SECTION.
  PRIVATE SECTION.

    DATA mo_container TYPE REF TO /iwbep/if_message_container .
    DATA mv_lgnum TYPE /scwm/lgnum .
    DATA mv_msg_dummy TYPE string  ##NEEDED.
    CONSTANTS mc_user_comm_appl_1 TYPE y20_de_user_comm_appl VALUE '01' ##NO_TEXT.
    CONSTANTS mc_user_comm_appl_2 TYPE y20_de_user_comm_appl VALUE '02' ##NO_TEXT.
    CONSTANTS mc_user_comm_appl_3 TYPE y20_de_user_comm_appl VALUE '03' ##NO_TEXT.
    CONSTANTS mc_user_logged_on TYPE /scwm/de_rsrc_sts VALUE 'A' ##NO_TEXT.

    METHODS add_message_odata_container .
    METHODS get_user_rsrc_chain
      IMPORTING
        !it_key_tab               TYPE /iwbep/t_mgw_name_value_pair OPTIONAL
        !it_filter_select_options TYPE /iwbep/t_mgw_select_option OPTIONAL
      RETURNING
        VALUE(er_entityset)       TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_busi_exception
        /iwbep/cx_mgw_tech_exception .
    METHODS check_lgnum
      IMPORTING
        !iv_lgnum TYPE /scwm/lgnum OPTIONAL
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS scan_trolley
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(er_entity)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS get_trolley_number
      IMPORTING
        !is_scantrolleyent TYPE y20_cl_ptwy_exec_mpc=>ts_scantrolleyent
      EXPORTING
        !es_trolley_data   TYPE /scwm/rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS load_lgnum
      IMPORTING
        !it_key_tab TYPE /iwbep/t_mgw_name_value_pair
      EXPORTING
        !ev_lgnum   TYPE /scwm/lgnum
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS read_trolley_single
      IMPORTING
        !iv_trolley       TYPE /scwm/de_rsrc
      RETURNING
        VALUE(rs_trolley) TYPE /scwm/rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS read_table_user_rsrc
      IMPORTING
        !iv_lgnum              TYPE /scwm/lgnum
        !iv_uname              TYPE syst_uname OPTIONAL
        !iv_trolley            TYPE /scwm/de_rsrc OPTIONAL
        !iv_seqno              TYPE y20_de_trolley_seqno_chain OPTIONAL
      RETURNING
        VALUE(rt_t_trolleyusr) TYPE y20_tt_trolleyusr .
    METHODS set_table_user_rsrc
      IMPORTING
        !iv_lgnum      TYPE /scwm/lgnum
        !iv_trolley    TYPE /scwm/de_rsrc
        !it_trolleyusr TYPE y20_tt_trolleyusr OPTIONAL
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS select_who_from_rsrc
      IMPORTING
        !is_trolley TYPE /scwm/rsrc
      EXPORTING
        !et_who_int TYPE /scwm/tt_who_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS clear_chain_rsrc_user
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(er_entity)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS rsrc_user_mon
      RETURNING
        VALUE(rs_rsrc) TYPE /scwm/rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS get_trolley_wt_conf
      IMPORTING
        !it_key_tab      TYPE /iwbep/t_mgw_name_value_pair
      RETURNING
        VALUE(er_entity) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS get_best_task
      IMPORTING
        !it_rsrc_r  TYPE rseloption
      EXPORTING
        !es_ordim_o TYPE /scwm/ordim_o
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS conf_process
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(er_entity)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS validate_source_hu
      IMPORTING
        !is_trol_task TYPE y20_s_trol_task_conf_odata
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS validate_dest_bin_conf
      IMPORTING
        !is_trol_task TYPE y20_s_trol_task_conf_odata
        !iv_exc_flag  TYPE abap_bool OPTIONAL
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS confirm_tasks
      IMPORTING
        !is_ordim_o  TYPE /scwm/ordim_o
        !iv_exc_flag TYPE abap_bool OPTIONAL
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS rsrc_wo_rsrc_assign
      IMPORTING
        !iv_rsrc TYPE /scwm/de_rsrc
        !iv_who  TYPE /scwm/de_who
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS remove_trolley
      IMPORTING
        !iv_rsrc TYPE /scwm/de_rsrc OPTIONAL
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS rsrc_wo_rsrc_unasin
      IMPORTING
        iv_rsrc TYPE /scwm/de_rsrc
        iv_who  TYPE /scwm/de_who
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS print_hu
      IMPORTING
        iv_guid_hu TYPE /scwm/ordim_o-sguid_hu.
ENDCLASS.



CLASS Y20_CL_PTWY_EXEC_DPC_EXT IMPLEMENTATION.


  METHOD /iwbep/if_mgw_appl_srv_runtime~get_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : ODATA: Get data of single entity structure
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    LOG-POINT ID y20_inb_putaway_trolley
                 SUBKEY sy-uzeit
                 FIELDS it_key_tab.

    TRY.
        CASE iv_entity_name.
          WHEN y20_cl_ptwy_exec_mpc=>gc_ptwywtconftrol.
            " get WT which we should confirm
            get_trolley_wt_conf( EXPORTING it_key_tab = it_key_tab
                                 RECEIVING er_entity  = er_entity ).

        ENDCASE.
      CATCH /iwbep/cx_mgw_tech_exception.
        add_message_odata_container( ).
    ENDTRY.

  ENDMETHOD.


  METHOD /iwbep/if_mgw_appl_srv_runtime~get_entityset.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : ODATA: Get data of single entity structure
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    BREAK-POINT ID y20_inb_putaway_trolley.

    TRY.
        CASE iv_entity_name.

          WHEN y20_cl_ptwy_exec_mpc=>gc_usertrolleychain.
            LOG-POINT ID y20_inb_putaway_trolley
               SUBKEY sy-uzeit
              FIELDS it_key_tab.
            get_user_rsrc_chain( EXPORTING it_key_tab               = it_key_tab
                                           it_filter_select_options = it_filter_select_options
                                 RECEIVING er_entityset             = er_entityset ).

        ENDCASE.
      CATCH /iwbep/cx_mgw_tech_exception.
        add_message_odata_container( ).
    ENDTRY.

  ENDMETHOD.


  METHOD /iwbep/if_mgw_core_srv_runtime~create_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : ODATA: Create data of single entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    BREAK-POINT ID y20_inb_putaway_trolley.

    TRY.
        CASE iv_entity_name.
          WHEN y20_cl_ptwy_exec_mpc=>gc_scantrolleyent.
            " scan new trolley and set it to the user
            scan_trolley(  EXPORTING io_data_provider = io_data_provider
                           RECEIVING er_entity        = cr_entity ).
          WHEN y20_cl_ptwy_exec_mpc=>gc_clearuserchain.
            " clean whole chain trolley assigment to this user
            clear_chain_rsrc_user(  EXPORTING io_data_provider = io_data_provider
                                    RECEIVING er_entity        = cr_entity ).
          WHEN y20_cl_ptwy_exec_mpc=>gc_ptwywtconftrol.
            " WT processing started
            conf_process(  EXPORTING io_data_provider = io_data_provider
                           RECEIVING er_entity        = cr_entity ).
        ENDCASE.
      CATCH /iwbep/cx_mgw_tech_exception.
        add_message_odata_container( ).
    ENDTRY.

  ENDMETHOD.


  METHOD add_message_odata_container.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Add messages log to service container
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    IF mo_container IS NOT BOUND.
      mo_container = mo_context->get_message_container( ).
    ENDIF.

    IF mo_container IS NOT BOUND.
      RETURN.
    ENDIF.
    mo_container->add_message(
      EXPORTING
        iv_msg_type    = sy-msgty
        iv_msg_id      = sy-msgid
        iv_msg_number  = sy-msgno
        iv_msg_v1      = sy-msgv1
        iv_msg_v2      = sy-msgv2
        iv_msg_v3      = sy-msgv3
        iv_msg_v4      = sy-msgv4
        iv_add_to_response_header = abap_true ).

  ENDMETHOD.


  METHOD check_lgnum.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Get LGNUM from key and set it to the object
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_t300  TYPE /scwm/s_t300.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " check lgnum
    CALL FUNCTION '/SCWM/T300_READ_SINGLE'
      EXPORTING
        iv_lgnum  = iv_lgnum
      IMPORTING
        es_t300   = ls_t300
      EXCEPTIONS
        not_found = 1
        OTHERS    = 2.

    IF sy-subrc <> 0 OR
       ls_t300-lgnum IS INITIAL.
      MESSAGE e011(/scwm/ccheck) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSE.
      mv_lgnum  = ls_t300-lgnum.
    ENDIF.

  ENDMETHOD.


  METHOD clear_chain_rsrc_user.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Remove all trolleys from the user. Clear button
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_odata_clear_trolley TYPE y20_cl_ptwy_exec_mpc=>ts_clearuserchain.
****          ls_usr_st_log          TYPE /scwm/usr_st_log,
***          ls_s_rsrc              TYPE /scwm/s_rsrc.

***    CONSTANTS lc_user_logged_off TYPE /scwm/de_rsrc_sts VALUE 'B'.

    " ------------------------------------------ "
    "  Processing Logic.
    " ------------------------------------------ "

    " get data for POST request
    io_data_provider->read_entry_data( IMPORTING es_data = ls_odata_clear_trolley ).

    IF ls_odata_clear_trolley IS INITIAL.
      MESSAGE e003(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check lgnum
    check_lgnum( EXPORTING iv_lgnum = ls_odata_clear_trolley-lgnum ).

    " get resource which is assigned in the monitor

    remove_trolley( ).
****    DATA(ls_trolley_mon) =  rsrc_user_mon( ).
****
****    IF ls_trolley_mon IS NOT INITIAL.
****
****      IF ls_trolley_mon-rfind IS NOT INITIAL.
****        MOVE-CORRESPONDING ls_trolley_mon TO ls_s_rsrc.
****        "  Unassign warehouse orders from resource
****        CALL FUNCTION '/SCWM/RSRC_WHO_UNASSIGN'
****          EXPORTING
****            is_rsrc       = ls_s_rsrc
****          EXCEPTIONS
****            error_message = 1
****            OTHERS        = 2.
****
****        IF sy-subrc <> 0.
****          CLEAR ls_s_rsrc.
****        ENDIF.
****        CLEAR:
****         ls_trolley_mon-uname,
****         ls_trolley_mon-rfind,
****         ls_trolley_mon-ltrans,
****         ls_trolley_mon-ltranschg_at,
****         ls_trolley_mon-act_queue,
****         ls_trolley_mon-lgtyp,
****         ls_trolley_mon-semi_lgpla,
****         ls_trolley_mon-semi_queue,
****         ls_trolley_mon-semi_queue_chg,
****         ls_trolley_mon-rec_grp,
****         ls_trolley_mon-grp_leave_time,
****         ls_trolley_mon-act_xcoord,
****         ls_trolley_mon-act_ycoord,
****         ls_trolley_mon-curr_act_type,
****         ls_trolley_mon-recgrp_entrytime,
****         ls_trolley_mon-work_in_recgrp,
****         ls_trolley_mon-recactive.
****
****      ELSE.
****        " Clear user name from resource
****        CLEAR ls_trolley_mon-uname.
****      ENDIF.
****
****      " Update resource record in DB
****      CALL FUNCTION '/SCWM/RSRC_RESOURCE_SET'
****        EXPORTING
****          iv_action = wmegc_update
****          is_rsrc   = ls_trolley_mon.
****
****      " Set log with status, timestamp, reason
****      MOVE-CORRESPONDING ls_trolley_mon TO ls_usr_st_log  ##ENH_OK.
****      ls_usr_st_log-uname = sy-uname.
****      GET TIME STAMP FIELD ls_usr_st_log-tstmp.
****      ls_usr_st_log-status = lc_user_logged_off.
****      CLEAR ls_usr_st_log-reason.
****      " Update user status log table
****      CALL FUNCTION '/SCWM/RSRC_USER_LOG_SET'
****        EXPORTING
****          iv_action   = wmegc_insert
****          is_user_log = ls_usr_st_log.
****    ENDIF.
****
****    " delete all assigments of the trolleis for this user
****    DELETE FROM y20_t_trolleyusr
****    WHERE lgnum    = @mv_lgnum AND
****          uname    = @sy-uname.
****
****    IF sy-subrc <> 0 AND sy-subrc <> 4.
****      MESSAGE e041(y20_ui5) INTO mv_msg_dummy.
****      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
****    ELSE.
****      COMMIT WORK AND WAIT.
****    ENDIF.

    " prepare data to ref
    copy_data_to_ref( EXPORTING is_data = ls_odata_clear_trolley
                      CHANGING  cr_data = er_entity ).


  ENDMETHOD.


  METHOD confirm_tasks.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_AFPI
* Description : Confirm tasks.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-19¦A.Yordanov     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_severity TYPE bapi_mtype,
          lt_bapiret  TYPE bapirettab,
          lt_to_conf  TYPE /scwm/to_conf_tt,
          lt_conf_exc TYPE /scwm/tt_conf_exc.
    " ------------------------------------------ "
    "  Processing logic..
    " ------------------------------------------ "

    lt_to_conf =  VALUE #( ( tanum = is_ordim_o-tanum
                             nista = is_ordim_o-vsolm
                             altme = is_ordim_o-altme
                             wdatu = is_ordim_o-wdatu
                             coo   = is_ordim_o-coo
                             nlpla = is_ordim_o-nlpla
                             guid_to = is_ordim_o-guid_to ) ).

    IF iv_exc_flag = abap_true.
      " get exception code
      DATA(lv_exc_code) =  zswl_cl_param=>read_const( iv_lgnum     = mv_lgnum
                                                      iv_context   = zswl_if_param_c=>mc_ptwy_appl_trol
                                                      iv_parameter = zswl_if_param_c=>mc_excep_code ).

      IF lv_exc_code IS INITIAL.
        MESSAGE e051(y20_ui5) INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

      lt_conf_exc = VALUE #( ( tanum     = is_ordim_o-tanum
                               buscon    = wmegc_buscon_tpt
                               exec_step = wmegc_execstep_04
                               exccode   = lv_exc_code ) ).

    ENDIF.

    " do real WT confirmation
    CALL FUNCTION '/SCWM/TO_CONFIRM'
      EXPORTING
        iv_lgnum       = mv_lgnum
        iv_update_task = abap_true
        iv_commit_work = abap_false
        it_conf        = lt_to_conf
        it_conf_exc    = lt_conf_exc
      IMPORTING
        et_bapiret     = lt_bapiret
        ev_severity    = lv_severity.

    IF lv_severity CA wmegc_severity_ea.
      LOOP AT lt_bapiret ASSIGNING FIELD-SYMBOL(<ls_bapiret>) WHERE type = wmegc_severity_err.
        MESSAGE ID <ls_bapiret>-id TYPE <ls_bapiret>-type NUMBER <ls_bapiret>-number
           WITH <ls_bapiret>-message_v1
                <ls_bapiret>-message_v2
                <ls_bapiret>-message_v3
                <ls_bapiret>-message_v4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDLOOP.

    ELSE.
      COMMIT WORK AND WAIT.

*      IF iv_exc_flag = abap_true OR is_ordim_o-nlpla IS INITIAL.
      print_hu( iv_guid_hu = is_ordim_o-sguid_hu ).
*      ENDIF.

      " if there are no more tasks for this trolley remove it from the chain
      DATA(ltr_ptwy_queue) =  zswl_cl_param=>read_const_range( iv_lgnum     = mv_lgnum
                                                         iv_context   = zswl_if_param_c=>mc_ptwy_appl_trol
                                                         iv_parameter = zswl_if_param_c=>mc_ptwy_queue_trol ).

      " read all open tasks from this trolley
      SELECT COUNT(*) UP TO 1 ROWS
        FROM /scwm/ordim_o
        WHERE lgnum = @mv_lgnum   AND
              srsrc = @is_ordim_o-srsrc AND
              queue IN @ltr_ptwy_queue AND
              trart = 1. " putaway

      IF sy-subrc <> 0.
        " first unassigne from the standard
        rsrc_wo_rsrc_unasin( iv_who = is_ordim_o-who
                             iv_rsrc = is_ordim_o-srsrc ).
        " second unassigne from the user - cust logic
        remove_trolley( iv_rsrc = is_ordim_o-srsrc ).
      ENDIF.

    ENDIF.

  ENDMETHOD.


  METHOD conf_process.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : WT Confirmation Process
* to Chain Data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-18¦A.Yordanov    ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA ls_odata_trolley_conf TYPE y20_cl_ptwy_exec_mpc=>ts_ptwywtconftrol.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " get data for POST request
    io_data_provider->read_entry_data( IMPORTING es_data = ls_odata_trolley_conf ).

    IF ls_odata_trolley_conf IS INITIAL.
      MESSAGE e046(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check lgnum
    check_lgnum( EXPORTING iv_lgnum = ls_odata_trolley_conf-lgnum ).

    CASE ls_odata_trolley_conf-user_comm.
      WHEN mc_user_comm_appl_1.
        " User just scanned the source HU
        validate_source_hu( is_trol_task = ls_odata_trolley_conf ).
      WHEN mc_user_comm_appl_2.
        " User just scanned the destination BIN - no exception code
        " and we will confirm the WT
        validate_source_hu( is_trol_task = ls_odata_trolley_conf ).
        validate_dest_bin_conf( is_trol_task = ls_odata_trolley_conf ).
      WHEN mc_user_comm_appl_3.
        " User just scanned the destination BIN - exception code should be added
        " and we will confirm the WT
        validate_source_hu( is_trol_task = ls_odata_trolley_conf ).
        validate_dest_bin_conf( iv_exc_flag  = abap_true
                                is_trol_task = ls_odata_trolley_conf ).
      WHEN OTHERS.
    ENDCASE.

    " prepare data to ref
    copy_data_to_ref( EXPORTING is_data = ls_odata_trolley_conf
                      CHANGING  cr_data = er_entity ).

  ENDMETHOD.


  METHOD get_best_task.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Get WT from the Trolley which should be conf.  first
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-16¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_aarea        TYPE /scwm/de_aarea,
          lt_ordim_o_sort TYPE /scwm/tt_ordim_o,
          lt_taaread      TYPE /scwm/tt_taaread.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR es_ordim_o.
    " get from param. Queues which should be relevant for putaway trolley logic
    DATA(ltr_ptwy_queue) =  zswl_cl_param=>read_const_range( iv_lgnum     = mv_lgnum
                                                             iv_context   = zswl_if_param_c=>mc_ptwy_appl_trol
                                                             iv_parameter = zswl_if_param_c=>mc_ptwy_queue_trol ).

    " read all open tasks from this trolley chain
    SELECT * FROM /scwm/ordim_o
      WHERE lgnum = @mv_lgnum   AND
            srsrc IN @it_rsrc_r AND
            queue IN @ltr_ptwy_queue AND
            trart = 1
      INTO TABLE @DATA(lt_ordim_o).

    IF sy-subrc <> 0.
      MESSAGE e045(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    "" get the best task for confirmation
    DATA(lv_aarea_param) =  zswl_cl_param=>read_const( iv_lgnum     = mv_lgnum
                                                       iv_context   = zswl_if_param_c=>mc_ptwy_appl_trol
                                                       iv_parameter = zswl_if_param_c=>mc_aare ).
    " get sorting for the storage type
    IF lv_aarea_param IS NOT INITIAL.
      lv_aarea  = lv_aarea_param.

      CALL FUNCTION '/SCWM/TAAREAD_READ_SINGLE'
        EXPORTING
          iv_lgnum    = mv_lgnum
          iv_aarea    = lv_aarea
        IMPORTING
          et_taaread  = lt_taaread
        EXCEPTIONS
          wrong_input = 1
          not_found   = 2
          OTHERS      = 3.

      IF sy-subrc <> 0.
        DATA(lv_no_sort) = abap_true.
      ENDIF.
    ELSE.
      lv_no_sort = abap_true.
    ENDIF.

    IF lt_taaread IS NOT INITIAL.

      SORT lt_taaread BY rank.

      LOOP AT lt_taaread ASSIGNING FIELD-SYMBOL(<ls_taaread>).
        LOOP AT lt_ordim_o ASSIGNING FIELD-SYMBOL(<ls_ordim_o_nlgty>)
                                         WHERE nltyp = <ls_taaread>-lgtyp.
          APPEND <ls_ordim_o_nlgty> TO lt_ordim_o_sort.
          DATA(lv_find_lgtyp) = abap_true.

        ENDLOOP.
        CHECK lv_find_lgtyp = abap_true.
        EXIT.
      ENDLOOP.

      DATA(lv_line_ordim) = lines( lt_ordim_o_sort ).

      IF lv_line_ordim > 1.
        " check if we have more then one open task for the best storage type
        SORT lt_ordim_o_sort BY pathseq.
      ELSEIF lv_line_ordim = 0.
        lv_no_sort = abap_true.
      ENDIF.

      ASSIGN lt_ordim_o_sort[ 1 ] TO FIELD-SYMBOL(<ls_ordim_o_best>).
      IF sy-subrc = 0.
        es_ordim_o = <ls_ordim_o_best>.
        RETURN.
      ENDIF.

    ENDIF.

    " if there are not settings for sorting then just take first task
    IF lv_no_sort = abap_true.

      SORT lt_ordim_o BY tanum.
      ASSIGN lt_ordim_o[ 1 ] TO <ls_ordim_o_best>.

      IF sy-subrc <> 0.
        MESSAGE e045(y20_ui5) INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ELSE.
        es_ordim_o = <ls_ordim_o_best>.
      ENDIF.

    ENDIF.

  ENDMETHOD.


  METHOD get_trolley_number.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Get trolly after scanning and do a check if this is
* really a trolley
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    CONSTANTS lc_position_length TYPE i VALUE 2.

    DATA: lv_rsrc TYPE /scwm/de_rsrc.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR es_trolley_data.

    IF is_scantrolleyent IS INITIAL.
      RETURN.
    ENDIF.
    " check lgnum
    check_lgnum( EXPORTING iv_lgnum = is_scantrolleyent-lgnum ).

    DATA(lv_number_length) = strlen( is_scantrolleyent-scantrolley ).
    lv_number_length = lv_number_length - lc_position_length.

    " get trolley from the scanned data
    lv_rsrc =  is_scantrolleyent-scantrolley(lv_number_length).

    " check if this resource exist
    es_trolley_data = read_trolley_single( EXPORTING iv_trolley = lv_rsrc ).

  ENDMETHOD.


  METHOD get_trolley_wt_conf.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Get WT from the Trolley which we should conf. Odata load
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-16¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_lgpla              TYPE /scwm/lgpla,
          lv_guid_hu            TYPE /scwm/guid_hu,
          ls_odata_ptwy_trolley TYPE y20_cl_ptwy_exec_mpc=>ts_ptwywtconftrol,
          ls_lagp               TYPE /scwm/lagp,
          ls_huhdr              TYPE /scwm/s_huhdr_int,
          ltr_rsrc              TYPE rseloption.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " load lgnum from url key value
    load_lgnum( EXPORTING it_key_tab = it_key_tab
                IMPORTING ev_lgnum   = DATA(lv_lgnum) ).

    " check lgnum
    check_lgnum( EXPORTING iv_lgnum = lv_lgnum ).

    " get all trolleys for this user
    DATA(lt_trolleyusr) = read_table_user_rsrc( iv_lgnum = mv_lgnum
                                                iv_uname = sy-uname ).

    IF lt_trolleyusr IS INITIAL.
      MESSAGE e044(y20_ui5) WITH sy-uname INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    ltr_rsrc = VALUE #( FOR <ls_trolley> IN lt_trolleyusr
                                          ( sign   = wmegc_sign_inclusive
                                            option = wmegc_option_eq
                                            low    = <ls_trolley>-trolley ) ).
    " get the best task
    get_best_task( EXPORTING
                       it_rsrc_r  =  ltr_rsrc
                   IMPORTING
                       es_ordim_o = DATA(ls_ordim_o) ).

    " set odata info to the screen
    IF ls_ordim_o IS INITIAL.
      MESSAGE e045(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    ASSIGN lt_trolleyusr[ trolley = ls_ordim_o-srsrc ] TO FIELD-SYMBOL(<ls_trolleyusr>).
    IF sy-subrc <> 0.
      " this will not happen
      MESSAGE e026(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check if this is general storage type
    IF ls_ordim_o-nlpla IS NOT INITIAL.
      lv_lgpla = ls_ordim_o-nlpla.

      CALL FUNCTION '/SCWM/LAGP_READ_SINGLE'
        EXPORTING
          iv_lgnum      = mv_lgnum
          iv_lgpla      = lv_lgpla
        IMPORTING
          es_lagp       = ls_lagp
        EXCEPTIONS
          wrong_input   = 1
          not_found     = 2
          enqueue_error = 3
          OTHERS        = 4.

      IF sy-subrc <> 0.
        MESSAGE e045(/scwm/rf_en) WITH lv_lgpla.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

    ENDIF.

    IF ls_ordim_o-sguid_hu IS NOT INITIAL.

      lv_guid_hu = ls_ordim_o-sguid_hu.

      CALL FUNCTION '/SCWM/HU_READ'
        EXPORTING
          iv_appl    = wmegc_huappl_wme
          iv_lgnum   = mv_lgnum
          iv_guid_hu = lv_guid_hu
        IMPORTING
          es_huhdr   = ls_huhdr
        EXCEPTIONS
          deleted    = 1
          not_found  = 2
          error      = 3
          OTHERS     = 4.

      IF sy-subrc <> 0.
        LOG-POINT ID y20_inb_putaway_trolley
              SUBKEY sy-uzeit
             FIELDS ls_huhdr.
      ENDIF.

    ENDIF.

    NEW /scwm/cl_ui_stock_fields( )->get_matkey_by_id( EXPORTING
                                          iv_matid = ls_ordim_o-matid
                                       IMPORTING
                                          ev_matnr = DATA(lv_matnr)
                                          ev_maktx = DATA(lv_maktx) ).

    " build odata information to the structure
    ls_odata_ptwy_trolley = VALUE #( lgnum   = mv_lgnum
                                     uname   = sy-uname
                                     seqno   = <ls_trolleyusr>-seqno
                                     trolley = <ls_trolleyusr>-trolley
                                     tanum   = ls_ordim_o-tanum
                                     who     = ls_ordim_o-who
                                     procty  = ls_ordim_o-procty
                                     aarea   = ls_ordim_o-aarea
                                     nltyp   = ls_ordim_o-nltyp
                                     nlber   = ls_ordim_o-nlber
                                     nlpla   =  COND #( WHEN   ls_ordim_o-nlpla IS INITIAL
                                                        THEN   ls_ordim_o-nltyp
                                                        ELSE ls_ordim_o-nlpla )
                                     aisle   = ls_lagp-aisle
                                     stack   = ls_lagp-stack
                                     lvl_v   = ls_lagp-lvl_v
                                     vlenr   = |{ ls_ordim_o-vlenr ALPHA = OUT }|
                                     hu_trolley_position = ls_huhdr-logpos+0(1) && '-' && ls_huhdr-logpos+1(1)
                                     matnr   = lv_matnr
                                     maktx   = lv_maktx
                                     vsolm   = ls_ordim_o-vsolm
                                     meins   = ls_ordim_o-meins ).

    " assigne WHO to the RSRC
    rsrc_wo_rsrc_assign( EXPORTING
                            iv_rsrc = ls_odata_ptwy_trolley-trolley
                            iv_who  = ls_odata_ptwy_trolley-who ).

    " prepare data to ref
    copy_data_to_ref( EXPORTING is_data = ls_odata_ptwy_trolley
                      CHANGING  cr_data = er_entity ).

  ENDMETHOD.


  METHOD get_user_rsrc_chain.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Get Resource Chain Data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ltr_rsrc               TYPE rseloption,
          lt_key_tab             TYPE /iwbep/t_mgw_name_value_pair,
          lt_odata_trolley_chain TYPE  y20_cl_ptwy_exec_mpc=>tt_usertrolleychain.

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    " load lgnum
    IF it_key_tab IS NOT INITIAL.
      lt_key_tab  = it_key_tab.
    ELSE.
      lt_key_tab = VALUE #( FOR <ls_lgnum_sel_opt> IN it_filter_select_options
                                   WHERE (  property = y20_cl_ptwy_exec_mpc_ext=>mc_property_name-lgnum )
                                          ( name = y20_cl_ptwy_exec_mpc_ext=>mc_property_name-lgnum
                                            value = VALUE #( <ls_lgnum_sel_opt>-select_options[ 1 ]-low OPTIONAL )  ) ).
    ENDIF.

    load_lgnum( EXPORTING it_key_tab = lt_key_tab
                IMPORTING ev_lgnum   = DATA(lv_lgnum) ).

    " check lgnum
    check_lgnum( EXPORTING iv_lgnum = lv_lgnum ).

    DATA(lt_trolley_user) =  read_table_user_rsrc( EXPORTING
                                                      iv_lgnum = lv_lgnum
                                                      iv_uname = sy-uname ).

    IF lt_trolley_user IS INITIAL.
      " No Trolley assigned to current user
      copy_data_to_ref( EXPORTING is_data = lt_odata_trolley_chain
                        CHANGING  cr_data = er_entityset ).
      RETURN.
    ENDIF.

    ltr_rsrc = VALUE #( FOR <ls_trolley> IN lt_trolley_user
                          ( sign   = wmegc_sign_inclusive
                            option = wmegc_option_eq
                            low    = <ls_trolley>-trolley ) ).

    SELECT FROM /scwm/ordim_o
      FIELDS srsrc,
             aarea,
            COUNT( vlenr ) AS count_hu
      WHERE lgnum = @mv_lgnum AND
            srsrc IN @ltr_rsrc AND
            trart = 1
      GROUP BY srsrc, aarea
      INTO TABLE @DATA(lt_ordim_o).

    IF sy-subrc <> 0.
      copy_data_to_ref( EXPORTING is_data = lt_odata_trolley_chain
                        CHANGING  cr_data = er_entityset ).
      RETURN.
    ENDIF.

    lt_odata_trolley_chain = VALUE #(
                         FOR <ls_troll_rsrc> IN lt_trolley_user
                         LET lv_aarea = VALUE #( lt_ordim_o[ srsrc = <ls_troll_rsrc>-trolley ]-aarea OPTIONAL )
                             lv_count_hu =  VALUE #( lt_ordim_o[ srsrc = <ls_troll_rsrc>-trolley ]-count_hu OPTIONAL )
                               IN aarea = lv_aarea
                                  count_hu = lv_count_hu
                            ( lgnum   = <ls_troll_rsrc>-lgnum
                              uname   = sy-uname
                              seqno   = <ls_troll_rsrc>-seqno
                              trolley = <ls_troll_rsrc>-trolley ) ).

    " prepare data to ref
    copy_data_to_ref( EXPORTING is_data = lt_odata_trolley_chain
                      CHANGING  cr_data = er_entityset ).
  ENDMETHOD.


  METHOD load_lgnum.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Load Lgnum from Odata table key
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-16¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    CLEAR ev_lgnum.

    ASSIGN it_key_tab[ name = y20_cl_ptwy_exec_mpc_ext=>mc_property_name-lgnum ]
     TO FIELD-SYMBOL(<ls_filter_sel_lgnum>).

    IF sy-subrc <> 0.
      MESSAGE e011(/scwm/ccheck) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSE.
      ev_lgnum = <ls_filter_sel_lgnum>-value.
    ENDIF.

  ENDMETHOD.


  METHOD print_hu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA ls_huhdr    TYPE /scwm/s_huhdr_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/HU_READ'
      EXPORTING
        iv_appl    = wmegc_huappl_wme
        iv_guid_hu = iv_guid_hu
        iv_lgnum   = mv_lgnum
      IMPORTING
        es_huhdr   = ls_huhdr
      EXCEPTIONS
        deleted    = 1
        not_found  = 2
        error      = 3
        OTHERS     = 4.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    y20_cl_odata_facade=>print_hu( it_huhdr = VALUE #( ( ls_huhdr ) )
                                   iv_fcode = y20_if_printing_c=>ms_odata_fcode-ptwy01 ).

  ENDMETHOD.


  METHOD read_table_user_rsrc.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Read table for trolleis assigned to the user
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    DATA: ltr_rsrc  TYPE rseloption,
          ltr_seqno TYPE rseloption,
          ltr_uname TYPE rseloption.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    IF iv_trolley IS SUPPLIED.
      ltr_rsrc =  VALUE #( ( sign   = wmegc_sign_inclusive
                             option = wmegc_option_eq
                             low    = iv_trolley ) ).

    ENDIF.

    IF iv_seqno IS SUPPLIED.
      ltr_seqno =  VALUE #( ( sign   = wmegc_sign_inclusive
                              option = wmegc_option_eq
                              low    = iv_seqno ) ).

    ENDIF.

    IF iv_uname IS SUPPLIED.
      ltr_uname =  VALUE #( ( sign   = wmegc_sign_inclusive
                              option = wmegc_option_eq
                              low    = iv_uname ) ).

    ENDIF.

    SELECT *
    FROM y20_t_trolleyusr
    WHERE lgnum = @iv_lgnum AND
          uname   IN @ltr_uname AND
          trolley IN @ltr_rsrc  AND
          seqno   IN @ltr_seqno
    INTO TABLE @DATA(lt_trolley_user).

    IF sy-subrc =  0.
      rt_t_trolleyusr = lt_trolley_user.
    ELSE.
      LOG-POINT ID y20_inb_putaway_trolley
          FIELDS iv_lgnum
                 ltr_uname
                 ltr_rsrc
                 ltr_seqno
                 lt_trolley_user.
    ENDIF.

  ENDMETHOD.


  METHOD read_trolley_single.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Check if the scanned value is a trolley
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    DATA: ls_usr_st_log TYPE /scwm/usr_st_log,
          ls_trolley    TYPE /scwm/rsrc.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    IF iv_trolley IS INITIAL OR mv_lgnum IS INITIAL.
      MESSAGE e009(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    CALL FUNCTION '/SCWM/RSRC_READ_SINGLE'
      EXPORTING
        iv_lgnum    = mv_lgnum
        iv_rsrc     = iv_trolley
      IMPORTING
        es_rsrc     = ls_trolley
      EXCEPTIONS
        wrong_input = 1
        not_found   = 2
        OTHERS      = 3.

    IF sy-subrc <> 0 OR ls_trolley IS INITIAL.
      MESSAGE e033(y20_ui5) WITH iv_trolley  INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check if scanned resource is trolley
    DATA(lt_rsrc_type_trol) =  zswl_cl_param=>read_const_list( iv_lgnum     = mv_lgnum
                                                             iv_context   = zswl_if_param_c=>mc_ptwy_appl_trol
                                                             iv_parameter = zswl_if_param_c=>mc_trol_type ).

    IF NOT line_exists( lt_rsrc_type_trol[ table_line = ls_trolley-rsrc_type ] ).
      MESSAGE e033(y20_ui5) WITH iv_trolley  INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    ls_trolley-uname = sy-uname.

    " Update Resource record in DB
    CALL FUNCTION '/SCWM/RSRC_RESOURCE_SET'
      EXPORTING
        iv_action = wmegc_update
        is_rsrc   = ls_trolley.

    " Set log with status, timestamp, reason
    MOVE-CORRESPONDING ls_trolley TO ls_usr_st_log  ##ENH_OK.
    GET TIME STAMP FIELD ls_usr_st_log-tstmp.
    ls_usr_st_log-status = mc_user_logged_on.
    CLEAR ls_usr_st_log-reason.

    " Update user status log table
    CALL FUNCTION '/SCWM/RSRC_USER_LOG_SET'
      EXPORTING
        iv_action   = wmegc_insert
        is_user_log = ls_usr_st_log.

    rs_trolley = ls_trolley.

  ENDMETHOD.


  METHOD remove_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Remove all trolleys from the user. Clear button
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_usr_st_log TYPE /scwm/usr_st_log,
          ls_s_rsrc     TYPE /scwm/s_rsrc,
          ltr_rsrc      TYPE rseloption.

    CONSTANTS lc_user_logged_off TYPE /scwm/de_rsrc_sts VALUE 'B'.

    " get resource which is assigned in the monitor
    DATA(ls_trolley_mon) =  rsrc_user_mon( ).
    IF iv_rsrc IS NOT INITIAL AND
       ls_trolley_mon-rsrc <> iv_rsrc.
      DATA(lv_no_clear_mon) = abap_true.
    ENDIF.

    IF ls_trolley_mon IS NOT INITIAL AND
       lv_no_clear_mon = abap_false.

      IF ls_trolley_mon-rfind IS NOT INITIAL.
        MOVE-CORRESPONDING ls_trolley_mon TO ls_s_rsrc.
        "  Unassign warehouse orders from resource
        CALL FUNCTION '/SCWM/RSRC_WHO_UNASSIGN'
          EXPORTING
            is_rsrc       = ls_s_rsrc
          EXCEPTIONS
            error_message = 1
            OTHERS        = 2.

        IF sy-subrc <> 0.
          CLEAR ls_s_rsrc.
        ENDIF.
        CLEAR:
         ls_trolley_mon-uname,
         ls_trolley_mon-rfind,
         ls_trolley_mon-ltrans,
         ls_trolley_mon-ltranschg_at,
         ls_trolley_mon-act_queue,
         ls_trolley_mon-lgtyp,
         ls_trolley_mon-semi_lgpla,
         ls_trolley_mon-semi_queue,
         ls_trolley_mon-semi_queue_chg,
         ls_trolley_mon-rec_grp,
         ls_trolley_mon-grp_leave_time,
         ls_trolley_mon-act_xcoord,
         ls_trolley_mon-act_ycoord,
         ls_trolley_mon-curr_act_type,
         ls_trolley_mon-recgrp_entrytime,
         ls_trolley_mon-work_in_recgrp,
         ls_trolley_mon-recactive.

      ELSE.
        " Clear user name from resource
        CLEAR ls_trolley_mon-uname.
      ENDIF.

      " Update resource record in DB
      CALL FUNCTION '/SCWM/RSRC_RESOURCE_SET'
        EXPORTING
          iv_action = wmegc_update
          is_rsrc   = ls_trolley_mon.

      " Set log with status, timestamp, reason
      MOVE-CORRESPONDING ls_trolley_mon TO ls_usr_st_log  ##ENH_OK.
      ls_usr_st_log-uname = sy-uname.
      GET TIME STAMP FIELD ls_usr_st_log-tstmp.
      ls_usr_st_log-status = lc_user_logged_off.
      CLEAR ls_usr_st_log-reason.
      " Update user status log table
      CALL FUNCTION '/SCWM/RSRC_USER_LOG_SET'
        EXPORTING
          iv_action   = wmegc_insert
          is_user_log = ls_usr_st_log.
    ENDIF.

    IF iv_rsrc IS NOT INITIAL.
      ltr_rsrc = VALUE #( ( sign   = wmegc_sign_inclusive
                            option = wmegc_option_eq
                            low    = iv_rsrc ) ).

    ENDIF.

    " delete all assigments of the trolleis for this user
    DELETE FROM y20_t_trolleyusr
    WHERE lgnum    = @mv_lgnum AND
          uname    = @sy-uname AND
          trolley IN @ltr_rsrc.

    IF sy-subrc <> 0 AND sy-subrc <> 4.
      MESSAGE e041(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSE.
      COMMIT WORK AND WAIT.
    ENDIF.

  ENDMETHOD.


  METHOD rsrc_user_mon.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : If user is assigned to some trolley in the monitor
* we will do logoff there as well
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_trolley_mon TYPE /scwm/rsrc.
    " ------------------------------------------ "
    "  Processing Logic.
    " ------------------------------------------ "

    IF mv_lgnum IS INITIAL.
      RETURN.
    ENDIF.

    SELECT SINGLE FROM /scwm/user
        FIELDS rsrc
        WHERE lgnum      = @mv_lgnum  AND
              uname      = @sy-uname AND
              data_entry = @space
      INTO @DATA(lv_rsrc).

    IF sy-subrc <> 0.
      " Nothing to do here. In the EWM user is not assigned to RSRC
      LOG-POINT ID y20_inb_putaway_trolley
            FIELDS sy-subrc
                   mv_lgnum
                   sy-uname.
      RETURN.
    ENDIF.

    CALL FUNCTION '/SCWM/RSRC_READ_SINGLE'
      EXPORTING
        iv_lgnum    = mv_lgnum
        iv_rsrc     = lv_rsrc
        iv_db_lock  = abap_true
      IMPORTING
        es_rsrc     = ls_trolley_mon
      EXCEPTIONS
        wrong_input = 1
        not_found   = 2
        OTHERS      = 3.

    IF sy-subrc <> 0.
      " this should not happen. Standard whoulc take care about this
      LOG-POINT ID y20_inb_putaway_trolley
            FIELDS sy-subrc
                   mv_lgnum
                   lv_rsrc.
      RETURN.
    ENDIF.

    " check if scanned resource is trolley
    DATA(lt_rsrc_type_trol) =  zswl_cl_param=>read_const_list( iv_lgnum     = mv_lgnum
                                                               iv_context   = zswl_if_param_c=>mc_ptwy_appl_trol
                                                               iv_parameter = zswl_if_param_c=>mc_trol_type ).

    IF NOT line_exists( lt_rsrc_type_trol[ table_line = ls_trolley_mon-rsrc_type ] ).
      RETURN.
    ENDIF.

    rs_rsrc = ls_trolley_mon.

  ENDMETHOD.


  METHOD rsrc_wo_rsrc_assign.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : assign WHO to the RSRC
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      ls_wo_rsrc_ty TYPE /scwm/wo_rsrc_ty,
      ls_rsrc       TYPE /scwm/rsrc,
      ls_t346       TYPE /scwm/t346,
      ls_usr_st_log TYPE /scwm/usr_st_log,
      lt_ordim_o    TYPE /scwm/tt_ordim_o,
      lt_whoid      TYPE  /scwm/tt_whoid,
      lt_wo_rsrc_ty TYPE /scwm/tt_wo_rsrc_ty,
      lt_who        TYPE /scwm/tt_who_int.

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    CALL FUNCTION '/SCWM/RSRC_READ_SINGLE'
      EXPORTING
        iv_lgnum    = mv_lgnum
        iv_rsrc     = iv_rsrc
      IMPORTING
        es_rsrc     = ls_rsrc
      EXCEPTIONS
        wrong_input = 1
        not_found   = 2
        OTHERS      = 3.

    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    IF  ls_rsrc-uname <> sy-uname AND
        ls_rsrc-uname IS NOT INITIAL ##USER_OK.
      " this should not be happen
      MESSAGE e052(y20_ui5) WITH ls_rsrc-rsrc
                                 sy-uname
                                 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSEIF ls_rsrc-uname IS INITIAL.
      "" Add logon user to this resource - EWM standard
      " Add user name to resource
      ls_rsrc-uname = sy-uname.

      " Update Resource record in DB
      CALL FUNCTION '/SCWM/RSRC_RESOURCE_SET'
        EXPORTING
          iv_action = wmegc_update
          is_rsrc   = ls_rsrc.

      " Set log with status, timestamp, reason
      MOVE-CORRESPONDING ls_rsrc TO ls_usr_st_log  ##ENH_OK.
      GET TIME STAMP FIELD ls_usr_st_log-tstmp.
      ls_usr_st_log-status = mc_user_logged_on.
      CLEAR ls_usr_st_log-reason.

      " Update user status log table
      CALL FUNCTION '/SCWM/RSRC_USER_LOG_SET'
        EXPORTING
          iv_action   = wmegc_insert
          is_user_log = ls_usr_st_log.

      COMMIT WORK AND WAIT.
    ENDIF.

    lt_whoid = VALUE #( (  who   = iv_who ) ).

    " Select WO to check if already locked
    TRY.
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_to       = 'X'
            iv_lgnum    = mv_lgnum
            iv_lock_who = 'X'
            it_who      = lt_whoid
          IMPORTING
            et_who      = lt_who
            et_ordim_o  = lt_ordim_o.

      CATCH /scwm/cx_core.
        CALL FUNCTION 'DEQUEUE_ALL'.

        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
           WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDTRY.

    SORT lt_ordim_o BY lgnum who tanum.

    LOOP AT lt_ordim_o ASSIGNING FIELD-SYMBOL(<ls_ordim_o>).
      ASSIGN lt_who[ who = <ls_ordim_o>-who ] TO  FIELD-SYMBOL(<ls_who>) .
      CHECK  sy-subrc = 0.

      IF <ls_ordim_o>-srsrc IS NOT INITIAL AND
        ( <ls_who>-status = wmegc_wo_in_process AND
          iv_rsrc <> <ls_ordim_o>-srsrc ).
        CALL FUNCTION 'DEQUEUE_ALL'.

        MESSAGE e140(/scwm/rf_en) WITH <ls_ordim_o>-srsrc
                                       <ls_ordim_o>-who INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.
    ENDLOOP.

    LOOP AT lt_who ASSIGNING <ls_who>.
      CLEAR: lt_wo_rsrc_ty,
             ls_wo_rsrc_ty.

      MOVE-CORRESPONDING <ls_who> TO ls_wo_rsrc_ty  ##ENH_OK.
      APPEND ls_wo_rsrc_ty TO lt_wo_rsrc_ty.

      "   Read queue to get operating environment
      CALL FUNCTION '/SCWM/T346_READ_SINGLE'
        EXPORTING
          iv_lgnum  = ls_wo_rsrc_ty-lgnum
          iv_queue  = ls_wo_rsrc_ty-queue
        IMPORTING
          es_t346   = ls_t346
        EXCEPTIONS
          not_found = 1
          OTHERS    = 2.

      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

      "   Select/assign warehouse order for a resource
      CALL FUNCTION '/SCWM/RSRC_WHO_SELECT'
        EXPORTING
          iv_rfrsrc         = ls_t346-rfrsrc
          iv_norsrc_upd     = abap_true
          iv_man_wo_sel     = abap_true
          iv_no_rec_call    = abap_true "NO REC-Control by call from NON RF-Environment
        CHANGING
          cs_rsrc           = ls_rsrc
          ct_wo_rsrc_ty     = lt_wo_rsrc_ty
        EXCEPTIONS
          no_rstyp_attached = 1
          OTHERS            = 2.

      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

    ENDLOOP.

    " commit work and wait to display new data after auto-refresh
    COMMIT WORK AND WAIT.

    CALL FUNCTION 'DEQUEUE_ALL'.

  ENDMETHOD.


  METHOD rsrc_wo_rsrc_unasin.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : This logic should unassigne WHO from the trolley
* if there are no more open tasks relevant for this trolley
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov     ¦Initial
*
************************************************************************

    DATA: ls_rsrc    TYPE /scwm/s_rsrc,
          lt_whoid   TYPE  /scwm/tt_whoid,
          lt_who     TYPE /scwm/tt_who_int,
          lt_ordim_o TYPE /scwm/tt_ordim_o.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    lt_whoid = VALUE #( ( who = iv_who ) ).

    " Select WO to check if already locked
    TRY.
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_to       = 'X'
            iv_lgnum    = mv_lgnum
            iv_lock_who = 'X'
            it_who      = lt_whoid
          IMPORTING
            et_who      = lt_who
            et_ordim_o  = lt_ordim_o.

      CATCH /scwm/cx_core.
        CALL FUNCTION 'DEQUEUE_ALL'.

        MESSAGE ID sy-msgid TYPE wmegc_severity_suc NUMBER sy-msgno
          WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RETURN.
    ENDTRY.

    SORT lt_ordim_o BY lgnum who tanum.

    LOOP AT lt_ordim_o ASSIGNING FIELD-SYMBOL(<ls_ordim_o>).
      READ TABLE lt_who ASSIGNING FIELD-SYMBOL(<ls_who>)
                       WITH KEY who = <ls_ordim_o>-who.
      IF sy-subrc NE 0.
        EXIT.
      ENDIF.
      IF <ls_ordim_o>-srsrc = iv_rsrc AND
         <ls_who>-status = wmegc_wo_in_process.
        CALL FUNCTION 'DEQUEUE_ALL'.

        MESSAGE s140(/scwm/rf_en)
          WITH <ls_ordim_o>-srsrc <ls_ordim_o>-who INTO mv_msg_dummy.
        RETURN.
      ENDIF.
    ENDLOOP.

    ls_rsrc-lgnum = mv_lgnum.

    " Unassign warehouse order from a resource
    CALL FUNCTION '/SCWM/RSRC_WHO_UNASSIGN'
      EXPORTING
        it_who        = lt_who
        is_rsrc       = ls_rsrc
      EXCEPTIONS
        error_message = 1
        OTHERS        = 2.

    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE wmegc_severity_suc NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.
    "commit work and wait to display new data after auto-refresh
    COMMIT WORK AND WAIT.

    CALL FUNCTION 'DEQUEUE_ALL'.

  ENDMETHOD.


  METHOD scan_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Scan Resource - validate the scanned data and add
* to Chain Data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    DATA ls_odata_trolley_scan TYPE y20_cl_ptwy_exec_mpc=>ts_scantrolleyent.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " get data for POST request
    io_data_provider->read_entry_data( IMPORTING es_data = ls_odata_trolley_scan ).

    IF ls_odata_trolley_scan-scantrolley IS INITIAL OR
       ls_odata_trolley_scan-lgnum IS INITIAL.
      MESSAGE e009(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " get trolley after scaning and set lgnum
    get_trolley_number( EXPORTING
                           is_scantrolleyent = ls_odata_trolley_scan
                        IMPORTING
                           es_trolley_data   = DATA(ls_rsrc) ).

    DATA(lt_user_rsrc) =  read_table_user_rsrc( EXPORTING
                                                   iv_lgnum   = mv_lgnum ).

    " check if this torlly was assigned to other user
    LOOP AT lt_user_rsrc ASSIGNING FIELD-SYMBOL(<ls_user_rsrc_check>)
                                             WHERE trolley = ls_rsrc-rsrc.
      IF <ls_user_rsrc_check>-uname <> sy-uname ##USER_OK.
        CALL FUNCTION 'DB_ROLLBACK'.
        MESSAGE e034(y20_ui5) INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ELSE.
        MESSAGE w054(y20_ui5) INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.
    ENDLOOP.

    " check the whos assigned to this resource
    select_who_from_rsrc( EXPORTING
                             is_trolley = ls_rsrc
                          IMPORTING
                             et_who_int = DATA(lt_who_trolley) ).

    IF lines( lt_who_trolley ) = 0 .
      ROLLBACK WORK.
      MESSAGE e038(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " set resource to the user
    set_table_user_rsrc( EXPORTING
                            iv_lgnum      = mv_lgnum
                            iv_trolley    = ls_rsrc-rsrc
                            it_trolleyusr = lt_user_rsrc ).

    " prepare data to ref
    copy_data_to_ref( EXPORTING is_data = ls_odata_trolley_scan
                      CHANGING  cr_data = er_entity ).

  ENDMETHOD.


  METHOD select_who_from_rsrc.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Select WHO from the resource. We do here standard call
* selection
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    DATA: lt_rsrt_ty TYPE /scwm/tt_wo_rsrc_ty,
          lt_whoid   TYPE /scwm/tt_whoid,
          lt_who_int TYPE /scwm/tt_who_int,
          ls_rsrc    TYPE /scwm/rsrc.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF is_trolley IS INITIAL.
      CALL FUNCTION 'DB_ROLLBACK'.
      MESSAGE e035(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSE.
      ls_rsrc       = is_trolley.
      ls_rsrc-rfind = abap_true.
    ENDIF.

    " get all who assigned to this trolley
    CALL FUNCTION '/SCWM/RSRC_WHO_SELECT'
      CHANGING
        ct_wo_rsrc_ty     = lt_rsrt_ty
        cs_rsrc           = ls_rsrc
      EXCEPTIONS
        no_rstyp_attached = 1
        OTHERS            = 2.

    IF sy-subrc <> 0 OR lt_rsrt_ty IS INITIAL.
      ROLLBACK WORK.
      MESSAGE e036(y20_ui5) WITH is_trolley-rsrc INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    lt_whoid = VALUE #( FOR <ls_who_rsrc> IN lt_rsrt_ty
                             ( who = <ls_who_rsrc>-who ) ).

    TRY.
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_lgnum    = mv_lgnum
            iv_lock_who = abap_true
            it_who      = lt_whoid
          IMPORTING
            et_who      = lt_who_int.

      CATCH /scwm/cx_core.
        ROLLBACK WORK.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDTRY.

    DATA(ltr_ptwy_queue) =  zswl_cl_param=>read_const_range( iv_lgnum     = mv_lgnum
                                                             iv_context   = zswl_if_param_c=>mc_ptwy_appl_trol
                                                             iv_parameter = zswl_if_param_c=>mc_ptwy_queue_trol ).

    DATA(ltr_who_status) =  zswl_cl_param=>read_const_range( iv_lgnum     = mv_lgnum
                                                             iv_context   = zswl_if_param_c=>mc_ptwy_appl_trol
                                                             iv_parameter = zswl_if_param_c=>mc_wo_open ).

    " who should be with status open or in process and should be with a proper queue
    et_who_int = VALUE #( FOR <ls_who_int> IN lt_who_int
                                WHERE ( queue IN ltr_ptwy_queue AND
                                        ( status IN ltr_who_status ) )
                                       ( <ls_who_int> ) ).
  ENDMETHOD.


  METHOD set_table_user_rsrc.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Insert record to user-trolleys table assigment
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_max_seqno         TYPE y20_de_trolley_seqno_chain,
          ls_trolley_user_insr TYPE y20_t_trolleyusr,
          lt_trolleyusr        TYPE y20_tt_trolleyusr.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    IF iv_lgnum IS INITIAL OR
       iv_trolley IS INITIAL.
      ROLLBACK WORK.
      MESSAGE e003(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    IF it_trolleyusr IS NOT INITIAL.
      " check if we have all selected trolleis up to now
      lt_trolleyusr =  VALUE y20_tt_trolleyusr( FOR <ls_trolluser_sort> IN it_trolleyusr
                                                        WHERE ( lgnum = iv_lgnum AND
                                                                uname = sy-uname )
                                                               ( <ls_trolluser_sort> ) ).

    ELSE.
      " if we did not selecte rsrc up to now then do selection
      lt_trolleyusr = read_table_user_rsrc( EXPORTING
                                  iv_lgnum = mv_lgnum
                                  iv_uname = sy-uname ).

    ENDIF.

    ASSIGN  lt_trolleyusr[ lines(  lt_trolleyusr ) ] TO FIELD-SYMBOL(<ls_last_trolley>).

    IF sy-subrc = 0.
      lv_max_seqno = <ls_last_trolley>-seqno + 1.
    ELSE.
      lv_max_seqno = 1.
    ENDIF.

    ls_trolley_user_insr = VALUE #( lgnum   = iv_lgnum
                                    uname   = sy-uname
                                    seqno   = lv_max_seqno
                                    trolley = iv_trolley ).
    " insert to DB
    INSERT INTO y20_t_trolleyusr VALUES ls_trolley_user_insr.

    IF sy-subrc <> 0.
      CALL FUNCTION 'DB_ROLLBACK'.
      MESSAGE e003(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSE.
      COMMIT WORK.
    ENDIF.

  ENDMETHOD.


  METHOD validate_dest_bin_conf.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : This Method should do validation of destination BIN
* and then should confirm WT
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-19¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_dest_bin     TYPE /scwm/ltap_nlpla,
          lv_val_dest_bin TYPE /scwm/ltap_nlpla,
          lv_lgpla        TYPE /scwm/lgpla,
          lv_exc_code     TYPE abap_bool,
          ls_lagp         TYPE /scwm/lagp,
          ls_ordim_o      TYPE /scwm/ordim_o.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " first get the task and lock the current task
    CALL FUNCTION '/SCWM/TO_READ_SINGLE'
      EXPORTING
        iv_lgnum     = mv_lgnum
        iv_tanum     = is_trol_task-tanum
        iv_flglock   = abap_true
      IMPORTING
        es_ordim_o   = ls_ordim_o
      EXCEPTIONS
        wrong_input  = 1
        not_found    = 2
        foreign_lock = 3
        error        = 4
        OTHERS       = 5.

    IF sy-subrc <> 0.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    IF is_trol_task-huident_val IS INITIAL.
      MESSAGE e053(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    lv_exc_code = iv_exc_flag.

    IF is_trol_task-lgpla_val IS INITIAL.
      " Enter a destination storage bin and proceed.
      MESSAGE e049(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    lv_dest_bin     = COND #( WHEN iv_exc_flag IS INITIAL
                              THEN  |{ is_trol_task-nlpla ALPHA = OUT }|
                              ELSE space ).
    lv_val_dest_bin = |{ is_trol_task-lgpla_val ALPHA = OUT }|.

    " if destination is not general storage type, then validate
    "destination bin
    IF lv_dest_bin IS NOT INITIAL AND
       ls_ordim_o-nlpla IS NOT INITIAL AND
       lv_dest_bin <> lv_val_dest_bin.

      " Scanned destination storage bin &1 does not match WT data.
      MESSAGE e048(y20_ui5) WITH lv_val_dest_bin INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.

    ENDIF.

    IF ls_ordim_o-nlpla IS INITIAL AND
       is_trol_task-nltyp IS NOT INITIAL AND
       iv_exc_flag = abap_false.

      " get destination bin in a case of general storage type
      lv_lgpla = is_trol_task-lgpla_val.

      CALL FUNCTION '/SCWM/LAGP_READ_SINGLE'
        EXPORTING
          iv_lgnum      = mv_lgnum
          iv_lgpla      = lv_lgpla
        IMPORTING
          es_lagp       = ls_lagp
        EXCEPTIONS
          wrong_input   = 1
          not_found     = 2
          enqueue_error = 3
          OTHERS        = 4.

      IF sy-subrc <> 0.
        MESSAGE e045(/scwm/rf_en) WITH lv_lgpla INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

      IF ls_lagp-lgtyp <> is_trol_task-nltyp AND
         lv_exc_code = abap_false.
        " check if the scanned Bin is in the correct storage type
        " Scanned dest. bin &1 is not in correct storage type &2.
        MESSAGE e050(y20_ui5) WITH lv_val_dest_bin
                                   is_trol_task-nltyp INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ELSE.
        ls_ordim_o-nltyp = ls_lagp-lgtyp.
      ENDIF.

      ls_ordim_o-nlpla = ls_lagp-lgpla.

    ENDIF.

    IF iv_exc_flag = abap_true.
      IF  ls_ordim_o-nlpla IS INITIAL.
        lv_exc_code = abap_false.
      ENDIF.

      ls_ordim_o-nlpla = is_trol_task-lgpla_val.
      ls_ordim_o-nlber = space.
    ENDIF.

    " confirm WT
    confirm_tasks( iv_exc_flag = lv_exc_code
                   is_ordim_o  = ls_ordim_o ).

  ENDMETHOD.


  METHOD validate_source_hu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : This Method should do validation of source HU
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-19¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_val_hu TYPE /scwm/ltap_vlenr.
    " ------------------------------------------ "
    "  Processing Logic.
    " ------------------------------------------ "

    lv_val_hu = |{ is_trol_task-huident_val ALPHA = OUT }|.

    IF is_trol_task-vlenr <> lv_val_hu.
      MESSAGE e047(y20_ui5) WITH lv_val_hu INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

  ENDMETHOD.
ENDCLASS.