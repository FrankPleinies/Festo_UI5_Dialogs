CLASS /iwfnd/cl_sodata_processor DEFINITION
  PUBLIC
  INHERITING FROM /iwcor/cl_ds_proc_single
  FINAL
  CREATE PRIVATE

  GLOBAL FRIENDS /iwfnd/cl_sodata_post_proc .

  PUBLIC SECTION.
    TYPE-POOLS abap .

    CONSTANTS co_codeployment_proxy_impl TYPE seoclsname VALUE '/IWFND/CL_MGW_CO_DEPLOYM_PROXY'. "#EC NOTEXT

    CLASS-METHODS get_processor
      IMPORTING
        !io_service         TYPE REF TO /iwfnd/if_med_mdl_service_grp
      RETURNING
        VALUE(ro_processor) TYPE REF TO /iwcor/cl_ds_proc_single .
    METHODS read_opensearch_description
      IMPORTING
        !iv_entity_set     TYPE string
      RETURNING
        VALUE(ro_provider) TYPE REF TO /iwcor/if_rest_entity_provider
      RAISING
        /iwcor/cx_ds_error .
    METHODS session_is_terminated
      IMPORTING
        !iv_reason TYPE i .

    METHODS /iwcor/if_ds_proc_batch~execute            REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_cprop~read        REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_links~count       REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_links~create_link REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_links~read        REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_link~delete       REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_link~exists       REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_link~read         REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_media~delete      REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_media~read        REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_media~update      REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_set~count         REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_set~create_entity REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_set~read          REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_sprop~read        REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity_vprop~read        REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity~delete            REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity~exists            REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity~read              REDEFINITION.
    METHODS /iwcor/if_ds_proc_entity~update            REDEFINITION.
    METHODS /iwcor/if_ds_proc_func_import~execute      REDEFINITION.
    METHODS /iwcor/if_ds_proc_metadata~read            REDEFINITION.
    METHODS /iwcor/if_ds_proc_service~read             REDEFINITION.


  PROTECTED SECTION.
    DATA mt_entity_headers TYPE tihttpnvp .

    METHODS get_resource_uri
      IMPORTING
        !iv_skiptoken          TYPE string
        !iv_deltatoken         TYPE string
        !ir_data               TYPE data
        !io_context            TYPE REF TO /iwcor/if_ds_cntxt OPTIONAL
      RETURNING
        VALUE(rv_resource_uri) TYPE string
      RAISING
        /iwcor/cx_ds_error .


  PRIVATE SECTION.

    TYPES:
      BEGIN OF ty_name_mapping,
        internal_name   TYPE string,
        external_name   TYPE string,
        internal_type   TYPE inttype,
        length          TYPE i,
        edm_simple_type TYPE REF TO /iwcor/if_ds_edm_simple_type,
      END OF ty_name_mapping .
    TYPES:
      tt_name_mapping TYPE SORTED TABLE OF ty_name_mapping WITH UNIQUE KEY internal_name .

    CONSTANTS co_filter_endswith TYPE string VALUE 'endswith' ##NO_TEXT.
    CONSTANTS co_filter_startswith TYPE string VALUE 'startswith' ##NO_TEXT.
    CONSTANTS co_filter_substringof TYPE string VALUE 'substringof' ##NO_TEXT.
    CONSTANTS gc_param_filter TYPE string VALUE '$filter' ##NO_TEXT.
    "! Request header name for preferred handling
    CONSTANTS gc_prefer_header TYPE string VALUE 'prefer' ##NO_TEXT.
    "! Message Scope header provided by HTTP request
    CONSTANTS gc_message_scope TYPE string VALUE 'sap-message-scope' ##NO_TEXT.
    "! Request header name for preferred handling
    CONSTANTS gc_strict_handling TYPE string VALUE 'handling=strict' ##NO_TEXT.
    CONSTANTS mc_agent TYPE /iwfnd/sup_iw_agent VALUE 'LIB 1.0 INT' ##NO_TEXT.
    CONSTANTS mc_consumer_type TYPE /iwfnd/cor_consumer_id VALUE 'ODATA' ##NO_TEXT.
    CONSTANTS mc_message_class TYPE symsgid VALUE '/IWFND/CM_MGW' ##NO_TEXT.
    CONSTANTS:
      BEGIN OF mcs_cardinality,
        cardinality_0_1 TYPE /iwfnd/med_mdl_cardinality VALUE '0',
        cardinality_1   TYPE /iwfnd/med_mdl_cardinality VALUE '1',
        cardinality_0_n TYPE /iwfnd/med_mdl_cardinality VALUE 'N',
        cardinality_1_n TYPE /iwfnd/med_mdl_cardinality VALUE 'M',
      END OF mcs_cardinality .
    CONSTANTS:
      BEGIN OF mcs_formats,
        atom TYPE string VALUE 'atom',
        json TYPE string VALUE 'json',
        xml  TYPE string VALUE 'xml',
      END OF mcs_formats .
    CONSTANTS:
      BEGIN OF mcs_messages,
        operation_start    TYPE symsgno VALUE 031,
        operation_finished TYPE symsgno VALUE 032,
        operation_failed   TYPE symsgno VALUE 033,
        general_failure    TYPE symsgno VALUE 034,
      END OF mcs_messages .
    CONSTANTS:
      BEGIN OF mcs_odata_method,
        entity_vprop_read        TYPE string VALUE 'ENTITY_VPROP~READ',
        func_import_execute      TYPE string VALUE 'FUNC_IMPORT~EXECUTE',
        entity_sprop_read        TYPE string VALUE 'ENTITY_SPROP~READ',    " same handling as complex property
        entity_cprop_read        TYPE string VALUE 'ENTITY_CPROP~READ',
        entity_media_read        TYPE string VALUE 'ENTITY_MEDIA~READ',
        entity_media_update      TYPE string VALUE 'ENTITY_MEDIA~UPDATE',
        entity_media_delete      TYPE string VALUE 'ENTITY_MEDIA~DELETE',
        entity_link_read         TYPE string VALUE 'ENTITY_LINK~READ',
        entity_link_exists       TYPE string VALUE 'ENTITY_LINK~EXISTS',
        entity_link_delete       TYPE string VALUE 'ENTITY_LINK~DELETE',       " No batch
        entity_read              TYPE string VALUE 'ENTITY~READ',
        entity_exists            TYPE string VALUE 'ENTITY~EXISTS',
        entity_update            TYPE string VALUE 'ENTITY~UPDATE',
        entity_delete            TYPE string VALUE 'ENTITY~DELETE',
        entity_links_read        TYPE string VALUE 'ENTITY_LINKS~READ',
        entity_links_count       TYPE string VALUE 'ENTITY_LINKS~COUNT',
        entity_links_create_link TYPE string VALUE 'ENTITY_LINKS~CREATE_LINK', " No batch
        entity_set_read          TYPE string VALUE 'ENTITY_SET~READ',
        entity_set_count         TYPE string VALUE 'ENTITY_SET~COUNT',
        entity_set_create_entity TYPE string VALUE 'ENTITY_SET~CREATE_ENTITY',
        entity_batch_execute     TYPE string VALUE 'BATCH~EXECUTE',
        service_read             TYPE string VALUE 'SERVICE~READ',
        metadata_read            TYPE string VALUE 'METADATA~READ',
      END OF mcs_odata_method .
    CONSTANTS:
      BEGIN OF mcs_operations,
        read        TYPE string VALUE 'read',
        create      TYPE string VALUE 'create',
        update      TYPE string VALUE 'update',
        delete      TYPE string VALUE 'delete',
        search      TYPE string VALUE 'search',
        operation   TYPE string VALUE 'service operation',
        create_link TYPE string VALUE 'create_link',
        delete_link TYPE string VALUE 'delete_link',
        batch       TYPE string VALUE 'batch',
        metadata    TYPE string VALUE 'metadata',
        document    TYPE string VALUE 'document',
      END OF mcs_operations .
    CONSTANTS:
      BEGIN OF mcs_operation_types,
        feed     TYPE string VALUE 'feed',
        entry    TYPE string VALUE 'entry',
        stream   TYPE string VALUE 'stream',
        count    TYPE string VALUE '$count',
        value    TYPE string VALUE '$value',
        links    TYPE string VALUE '$links',
        metadata TYPE string VALUE '$metadata',
        service  TYPE string VALUE 'service',
        property TYPE string VALUE 'property',
      END OF mcs_operation_types .
    CONSTANTS:
      "! <p class="shorttext synchronized" lang="en">http cache control response header values</p>
      BEGIN OF gcs_cache_header_value,
        max_age_zero     TYPE string VALUE 'max-age=0',
        max_age_one_year TYPE string VALUE 'max-age=31536000, public', "One Year 'publicly cachable'
        icm_cache_agent  TYPE string VALUE '0',
        icm_cache_ctrl   TYPE string VALUE '+864000',
      END OF gcs_cache_header_value .
    CLASS-DATA mo_batch_helper TYPE REF TO /iwfnd/cl_mgw_batch_helper .
    CLASS-DATA mo_post_processor TYPE REF TO /iwfnd/if_sodata_post_proc .
    DATA mo_co_deploym_proxy TYPE REF TO /iwfnd/if_mgw_co_deploym_proxy .
    DATA mo_logger TYPE REF TO /iwfnd/cl_logger .
    DATA mo_mgw_context TYPE REF TO /iwfnd/cl_mgw_context .
    DATA mo_mgw_runtime TYPE REF TO /iwfnd/bd_mgw_runtime .
    DATA mo_root_handler TYPE REF TO /iwfnd/cl_sodata_root_handler .
    DATA mo_service TYPE REF TO /iwfnd/if_med_mdl_service_grp .
    DATA mo_sutil_runtime TYPE REF TO /iwfnd/cl_sutil_runtime .
    DATA mo_transaction_handler TYPE REF TO /iwfnd/cl_transaction_handler .
    DATA ms_name_mapping TYPE ty_name_mapping .
    DATA mt_expressions TYPE /iwfnd/if_mgw_core_types=>tt_expressions .
    DATA mt_functions TYPE /iwfnd/if_mgw_core_types=>tt_functions .
    DATA mt_name_mapping TYPE tt_name_mapping .
    DATA mt_select_params TYPE string_table .
    DATA mv_base_url TYPE string .
    DATA mv_batch TYPE abap_bool .
    DATA mv_changeset TYPE abap_bool .
    DATA mv_conditional_read_processed TYPE abap_bool .
    DATA mv_content_format TYPE /iwfnd/mgw_content_format .
    DATA mv_is_stream TYPE abap_bool .
    DATA mv_msg_handle TYPE balmsghndl .
    DATA mv_namespace TYPE /iwfnd/med_mdl_namespace .
    DATA mv_service_name TYPE string .
    DATA mv_service_version TYPE /iwfnd/med_mdl_version .
    DATA mv_source_entity TYPE string .
    DATA mv_source_entity_set TYPE string .
    DATA mv_target_entity TYPE string .
    DATA mv_target_entity_set TYPE string .

    METHODS handle_query_result_cnt_id_ref
      IMPORTING
        !it_batch_operation TYPE /iwfnd/cl_mgw_batch_helper=>ty_t_batch_operation
      CHANGING
        !cs_batch_operation TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_batch_operation .
    METHODS add_collection_element
      IMPORTING
        !io_entity_set TYPE REF TO /iwfnd/if_med_mdl_reference
      CHANGING
        !cs_collection TYPE /iwcor/if_app_types=>collection_s .
    METHODS add_entity_set_annotation
      IMPORTING
        !io_entity_set TYPE REF TO /iwfnd/if_med_mdl_reference
      CHANGING
        !cs_collection TYPE /iwcor/if_app_types=>collection_s
      RAISING
        /iwcor/cx_ds_error .
    METHODS add_service_version_links
      IMPORTING
        !ir_service TYPE REF TO /iwcor/if_app_types=>service_s .
    METHODS batch_return_error
      IMPORTING
        !is_error_operation TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_batch_operation .
    METHODS batch_return_exception
      IMPORTING
        !iv_exception_type TYPE seoclsname
        !io_rest_request   TYPE REF TO /iwcor/if_rest_request
        !io_rest_response  TYPE REF TO /iwcor/if_rest_response .
    METHODS batch_return_result
      CHANGING
        !cs_operation TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_batch_operation
      RAISING
        /iwcor/cx_ds_error .
    METHODS changeset_return_error
      IMPORTING
        !is_error_operation TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_batch_operation
        !it_operation       TYPE /iwfnd/cl_mgw_batch_helper=>ty_t_batch_operation .
    METHODS changeset_return_result
      CHANGING
        !cs_operation TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_batch_operation
      RAISING
        /iwcor/cx_ds_error .
    METHODS check_etag_processing
      IMPORTING
        !io_request     TYPE REF TO /iwcor/if_rest_request
      RETURNING
        VALUE(rv_value) TYPE xsdboolean .
    METHODS constructor
      IMPORTING
        !io_service TYPE REF TO /iwfnd/if_med_mdl_service_grp .
    METHODS convert_datetime
      IMPORTING
        !io_simple_type            TYPE REF TO /iwcor/if_ds_edm_simple_type
        !io_edm_instance           TYPE REF TO object OPTIONAL
        !io_gw_property            TYPE REF TO /iwfnd/if_med_mdl_property OPTIONAL
        !iv_value                  TYPE string
        !iv_use_abap_default_value TYPE abap_bool DEFAULT abap_false
      RETURNING
        VALUE(rv_value)            TYPE string
      RAISING
        /iwcor/cx_ds_error .
    METHODS convert_value
      IMPORTING
        !io_simple_type            TYPE REF TO /iwcor/if_ds_edm_simple_type
        !io_edm_instance           TYPE REF TO object OPTIONAL
        !io_gw_property            TYPE REF TO /iwfnd/if_med_mdl_property OPTIONAL
        !iv_value                  TYPE string OPTIONAL
        !iv_use_abap_default_value TYPE abap_bool DEFAULT abap_false
      RETURNING
        VALUE(rv_value)            TYPE string
      RAISING
        /iwcor/cx_ds_error .
    METHODS convert_value_by_parameter
      IMPORTING
        !ir_parameter_value TYPE REF TO /iwcor/if_ds_uri=>parameter_value_s
      RETURNING
        VALUE(rv_value)     TYPE string
      RAISING
        /iwcor/cx_ds_error .
    METHODS convert_value_by_predicate
      IMPORTING
        !ir_key_predicate TYPE REF TO /iwcor/if_ds_uri=>key_predicate_s
      RETURNING
        VALUE(rv_value)   TYPE string
      RAISING
        /iwcor/cx_ds_error .
    METHODS correct_boolean_value
      IMPORTING
        !io_simple_type TYPE REF TO /iwcor/if_ds_edm_simple_type
        !iv_value       TYPE string
      RETURNING
        VALUE(rv_value) TYPE string
      RAISING
        /iwcor/cx_ds_error .
    METHODS correct_datetime_value
      IMPORTING
        !iv_internal_type          TYPE inttype
        !iv_length                 TYPE i
        !iv_value                  TYPE string
        !io_simple_type            TYPE REF TO /iwcor/if_ds_edm_simple_type
        !iv_use_abap_default_value TYPE abap_bool DEFAULT abap_false
      RETURNING
        VALUE(rv_value)            TYPE string
      RAISING
        /iwcor/cx_ds_error .
    METHODS correct_guid_value
      IMPORTING
        !io_simple_type TYPE REF TO /iwcor/if_ds_edm_simple_type
        !iv_value       TYPE string
      RETURNING
        VALUE(rv_value) TYPE string
      RAISING
        /iwcor/cx_ds_error .
    METHODS create_filter_tree
      IMPORTING
        !io_filter TYPE REF TO /iwcor/if_ds_expr_node
      EXPORTING
        !ev_index  TYPE sysuuid_c
      RAISING
        /iwcor/cx_ds_internal_error
        /iwcor/cx_ds_proc_unsupported
        /iwcor/cx_ds_edm_error
        /iwcor/cx_ds_error .
    METHODS expr_node_to_property_path
      IMPORTING
        !io_node          TYPE REF TO /iwcor/if_ds_expr_node
      EXPORTING
        !et_property      TYPE /iwcor/if_ds_uri=>property_t
        !ev_navi_path_ext TYPE string
        !ev_navi_path_int TYPE string
      RAISING
        /iwcor/cx_ds_internal_error
        /iwcor/cx_ds_proc_unsupported
        /iwcor/cx_ds_edm_error
        /iwcor/cx_ds_error .
    METHODS get_conditions
      IMPORTING
        !iv_http_method       TYPE string
        !io_target_entity_set TYPE REF TO /iwcor/if_ds_edm_entity_set
        !io_function_import   TYPE REF TO /iwcor/if_ds_edm_func_import
      EXPORTING
        !es_conditions        TYPE /iwfnd/if_mgw_core_types=>ty_s_conditions
      RAISING
        /iwcor/cx_ds_error .
    METHODS get_entry_data
      IMPORTING
        !io_entity_set           TYPE REF TO /iwcor/if_ds_edm_entity_set OPTIONAL
        !is_function_import_info TYPE /iwcor/if_ds_proc_func_import=>function_import_info_s OPTIONAL
        !it_key                  TYPE /iwcor/if_ds_uri=>key_predicate_t OPTIONAL
        !it_navigation_path      TYPE /iwcor/if_ds_uri=>navigation_path_segment_t OPTIONAL
        !it_select               TYPE /iwcor/if_ds_uri=>select_item_t OPTIONAL
        !io_filter               TYPE REF TO /iwcor/if_ds_expr_node OPTIONAL
        !iv_format               TYPE string OPTIONAL
        !iv_for_ds_operation     TYPE string OPTIONAL
      EXPORTING
        !er_data                 TYPE REF TO data
        !es_request_details      TYPE /iwfnd/if_mgw_core_types=>request_s
        !es_response_context     TYPE /iwfnd/if_mgw_core_types=>response_s
      RAISING
        /iwcor/cx_ds_error .
    METHODS get_etags
      IMPORTING
        !io_target_entity_type  TYPE REF TO /iwcor/if_ds_edm_entity_type OPTIONAL
        !it_rest_etag           TYPE /iwcor/rest_etag_t
        !io_gateway_entity_type TYPE REF TO /iwfnd/if_med_mdl_node OPTIONAL
      EXPORTING
        !et_etag                TYPE /iwfnd/if_mgw_core_types=>ty_t_etag
      RAISING
        /iwcor/cx_ds_error .
    METHODS get_expand_string
      IMPORTING
        !it_expand          TYPE /iwcor/if_ds_uri=>expand_clause_t
      EXPORTING
        !ev_expand_external TYPE string
        !ev_expand_internal TYPE string
      RAISING
        /iwcor/cx_ds_error .
    METHODS get_filter_string
      IMPORTING
        !io_filter         TYPE REF TO /iwcor/if_ds_expr_node
      EXPORTING
        !ev_value_external TYPE string
        !ev_value_internal TYPE string
      RAISING
        /iwcor/cx_ds_error .
    METHODS get_fi_data_by_read
      IMPORTING
        !io_entity_set           TYPE REF TO /iwcor/if_ds_edm_entity_set
        !is_function_import_info TYPE /iwcor/if_ds_proc_func_import=>function_import_info_s
        !it_expand               TYPE /iwcor/if_ds_uri=>expand_clause_t OPTIONAL
        !it_select               TYPE /iwcor/if_ds_uri=>select_item_t OPTIONAL
        !iv_format               TYPE string
        !iv_for_ds_operation     TYPE string OPTIONAL
      EXPORTING
        !eo_target_entity_set    TYPE REF TO /iwcor/if_ds_edm_entity_set
      CHANGING
        VALUE(cr_entityset)      TYPE REF TO data OPTIONAL
        !ct_inline_info          TYPE /iwfnd/if_mgw_core_runtime=>ty_t_inline_info OPTIONAL
        !cs_response_context     TYPE /iwfnd/if_mgw_core_runtime=>ty_s_mgw_response_context OPTIONAL
        !cs_odata_request        TYPE /iwfnd/if_mgw_core_types=>request_s
      RAISING
        /iwcor/cx_ds_error .
    METHODS get_guid
      RETURNING
        VALUE(rv_guid) TYPE sysuuid_c .
    METHODS get_mgw_context
      RETURNING
        VALUE(ro_context) TYPE REF TO /iwfnd/if_mgw_context .
    METHODS get_mgw_runtime
      IMPORTING
        !iv_function_name TYPE string OPTIONAL
      RETURNING
        VALUE(ro_badi)    TYPE REF TO /iwfnd/bd_mgw_runtime .
    METHODS get_name_from_property_member
      IMPORTING
        !io_node           TYPE REF TO /iwcor/if_ds_expr_node
      EXPORTING
        !ev_value_external TYPE string
        !ev_value_internal TYPE string
      RAISING
        /iwcor/cx_ds_internal_error
        /iwcor/cx_ds_proc_unsupported
        /iwcor/cx_ds_edm_error
        /iwcor/cx_ds_error .
    METHODS get_name_from_property_path
      IMPORTING
        !it_property_path    TYPE /iwcor/if_ds_uri=>property_t
        !iv_is_internal_name TYPE xsdboolean OPTIONAL
      RETURNING
        VALUE(rv_value)      TYPE string
      RAISING
        /iwcor/cx_ds_error .
    "! <p class="shorttext synchronized" lang="en">Get navigation path info of Message Scope header</p>
    "!
    "! @parameter iv_message_scope_value | <p class="shorttext synchronized" lang="en">Header value of Message Scope</p>
    "! E.g. sap-message-scope = /Employees(Id='0005')/My_Expanders('0001')
    "! @parameter et_message_scope       | <p class="shorttext synchronized" lang="en">Message Scope info as navigation table</p>
    "! @parameter ev_message_scope_error | <p class="shorttext synchronized" lang="en">Errors occurred calculating Message Scope</p>
    "! If calculation of the message scope fails, no exception is raised to avoid an incompatible change to applications. Instead,
    "! the error is written into MESSAGE_SCOPE_ERROR<br/>
    METHODS get_nav_path_for_message_scope
      IMPORTING
        !iv_message_scope_value TYPE string
      EXPORTING
        !et_message_scope       TYPE /iwfnd/if_sodata_types=>ty_t_nav_message_scope
        !ev_message_scope_error TYPE string .
    METHODS get_orderby_parameters
      IMPORTING
        !io_orderby        TYPE REF TO /iwcor/if_ds_expr_orderby
      EXPORTING
        !et_order_external TYPE /iwfnd/if_mgw_core_types=>order_t
        !et_order_internal TYPE /iwfnd/if_mgw_core_types=>ty_t_technical_order
      RAISING
        /iwcor/cx_ds_error .
    METHODS init_model
      RAISING
        /iwcor/cx_ds_error .
    METHODS init_request
      IMPORTING
        !iv_http_method         TYPE string
        !iv_operation           TYPE string
        !iv_operation_type      TYPE string OPTIONAL
        !io_entity_set          TYPE REF TO /iwcor/if_ds_edm_entity_set
        !it_key                 TYPE /iwcor/if_ds_uri=>key_predicate_t OPTIONAL
        !it_navigation_path     TYPE /iwcor/if_ds_uri=>navigation_path_segment_t OPTIONAL
        !io_navigation_property TYPE REF TO /iwcor/if_ds_edm_nav_property OPTIONAL
        !it_expand              TYPE /iwcor/if_ds_uri=>expand_clause_t OPTIONAL
        !it_select              TYPE /iwcor/if_ds_uri=>select_item_t OPTIONAL
        !io_filter              TYPE REF TO /iwcor/if_ds_expr_node OPTIONAL
        !io_orderby             TYPE REF TO /iwcor/if_ds_expr_orderby OPTIONAL
        !iv_skip                TYPE i OPTIONAL
        !iv_top                 TYPE string OPTIONAL
        !iv_format              TYPE string OPTIONAL
        !iv_for_ds_operation    TYPE string OPTIONAL
        !iv_skiptoken           TYPE string OPTIONAL
        !iv_inlinecount         TYPE abap_bool OPTIONAL
        !iv_count               TYPE abap_bool DEFAULT abap_false
        !iv_edm_multiplicity    TYPE /iwcor/if_ds_edm=>edm_multiplicity OPTIONAL
        !iv_mdl_cardinality     TYPE /iwfnd/med_mdl_cardinality OPTIONAL
        !io_function_import     TYPE REF TO /iwcor/if_ds_edm_func_import OPTIONAL
      EXPORTING
        !es_request             TYPE /iwfnd/if_mgw_core_types=>request_s
        !et_parameter           TYPE /iwfnd/if_mgw_core_types=>parameter_values_t
        !eo_target_entity_set   TYPE REF TO /iwcor/if_ds_edm_entity_set
      RAISING
        /iwcor/cx_ds_error .
    METHODS raise_business_error
      IMPORTING
        !ix_business_exception TYPE REF TO /iwfnd/cx_mgw_busi_exception
      RAISING
        /iwcor/cx_ds_error .
    METHODS raise_technical_error
      IMPORTING
        !ix_technical_exception TYPE REF TO /iwfnd/cx_mgw_tech_exception
      RAISING
        /iwcor/cx_ds_error .
    METHODS read_entityset_post_pro_excel
      IMPORTING
        !is_request_info   TYPE /iwfnd/if_mgw_core_types=>request_s OPTIONAL
        !ir_data           TYPE REF TO data
      RETURNING
        VALUE(ro_provider) TYPE REF TO /iwcor/if_rest_entity_provider
      RAISING
        /iwcor/cx_ds_error .
    METHODS serialize_batch_opdata
      IMPORTING
        !io_provider      TYPE REF TO /iwcor/if_rest_entity_provider
        !io_rest_response TYPE REF TO /iwcor/if_rest_response .
    "! <p class="shorttext synchronized" lang="en">Sets cache control header for context token requests</p>
    "!
    "! @parameter io_lib_provider               | <p class="shorttext synchronized" lang="en">Lib Rest Entity Provider</p>
    "! @parameter iv_is_resource_lang_dependent | <p class="shorttext synchronized" lang="en">Is the requested resource language dependent?</p>
    "! @parameter rv_have_been_set              | <p class="shorttext synchronized" lang="en">TRUE if there is a valid token and the header have been set</p>
    "! Otherwise the cache control header have not been set
    METHODS set_cache_ctrl_for_ctxt_token
      IMPORTING
        !io_lib_provider               TYPE REF TO /iwcor/if_rest_entity_provider
        !iv_is_resource_lang_dependent TYPE abap_bool
      RETURNING
        VALUE(rv_have_been_set)        TYPE abap_bool
      RAISING
        /iwcor/cx_ds_http_error .
    METHODS set_cache_hit_info
      IMPORTING
        !is_response_context TYPE /iwfnd/if_mgw_core_types=>response_s .
    METHODS set_common_response_headers
      IMPORTING
        !io_provider            TYPE REF TO /iwcor/if_rest_entity_provider OPTIONAL
        !is_response_context    TYPE /iwfnd/if_mgw_core_types=>response_s
        !iv_do_add_cache_header TYPE abap_bool DEFAULT abap_true
      CHANGING
        !ct_headers             TYPE tihttpnvp OPTIONAL .
    METHODS set_key_info
      IMPORTING
        !is_key_external        TYPE /iwfnd/if_mgw_core_types=>nvp_s
        !io_edm_instance        TYPE REF TO object
      CHANGING
        !cv_key_string_external TYPE string
        !cv_key_string_internal TYPE string
        !ct_key_tab_external    TYPE /iwfnd/if_mgw_core_types=>nvp_t
        !ct_key_tab_internal    TYPE /iwfnd/if_mgw_core_types=>ty_t_technical_pair
      RAISING
        /iwcor/cx_ds_error .
    "! <p class="shorttext synchronized" lang="en">Sets the processing option as defined in the prefer header</p>
    "! Supports only strict handling at the moment
    "! @parameter iv_prefer_header | <p class="shorttext synchronized" lang="en">Content of the prefer header, e.g. handling=strict</p>
    "! @parameter cs_technical_request | <p class="shorttext synchronized" lang="en">Technical request</p>
    METHODS set_requested_preferences
      IMPORTING
        !iv_prefer_header     TYPE ihttpval
      CHANGING
        !cs_technical_request TYPE /iwfnd/if_mgw_core_types=>technical_request_s .
    METHODS set_st_metering_info
      IMPORTING
        !iv_num_entries   TYPE i
        !iv_response_size TYPE i
        !is_request_info  TYPE /iwfnd/if_mgw_core_types=>request_s
        !io_ds_context    TYPE REF TO /iwcor/if_ds_cntxt .
ENDCLASS.



CLASS /IWFND/CL_SODATA_PROCESSOR IMPLEMENTATION.


  METHOD /iwcor/if_ds_proc_batch~execute.

    DATA: lv_packet_no              TYPE i,
          lv_operation_no           TYPE i,
          lv_changeset_optotal      TYPE i,
          lv_changeset_error        TYPE xsdboolean,
          lv_packet_error           TYPE xsdboolean,
          lv_service_name           TYPE string,
          ls_http_header            TYPE ihttpnvp,
          lt_http_header            TYPE tihttpnvp,
          ls_metering               TYPE /iwfnd/s_metering_col,
          lr_batch                  TYPE REF TO /iwcor/if_ds_proc_batch=>batch_s,
          lr_parameter              TYPE REF TO /iwcor/if_ds_proc_batch=>handler_parameter_s,
          lo_svc_factory            TYPE REF TO /iwcor/if_ds_svc_factory,
          ls_cs_operation           TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_batch_operation,
          lt_cs_operation           TYPE /iwfnd/cl_mgw_batch_helper=>ty_t_batch_operation,
          ls_batch_operation        TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_batch_operation,
          lt_batch_operation        TYPE /iwfnd/cl_mgw_batch_helper=>ty_t_batch_operation,
          lt_packet_operation       TYPE /iwfnd/cl_mgw_batch_helper=>ty_t_batch_operation,
          lv_is_independent_read_op TYPE abap_bool VALUE abap_true,
          lv_is_co_deployment       TYPE abap_bool,
          lv_batch_operations_count TYPE i,
          ls_model_features         TYPE /iwfnd/s_med_mdl_rw_features,
          lv_use_deferred_resp_crea TYPE abap_bool,
          lx_busi_exception         TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception         TYPE REF TO /iwfnd/cx_mgw_tech_exception,
          lv_gwbep_version          TYPE numc3,
          lt_model                  TYPE /iwfnd/if_med_mdl_service_grp=>ty_med_mdl_meta_model_table,
          ls_model                  LIKE LINE OF lt_model.


* Enable Batch Mode
    mo_batch_helper->set_batch_mode( abap_true ).

    " Check for modifying operation and X-CSRF token
    LOOP AT it_batch REFERENCE INTO lr_batch.
      IF lr_batch->change_set IS BOUND AND mo_batch_helper->is_only_read_allowed( ) = abap_true.

        "For modifying operations a X-CSRF token is needed. Exception: $batch only with get operations
        CREATE OBJECT lx_tech_exception
          EXPORTING
            textid           = /iwfnd/cx_mgw_tech_exception=>no_xcsrf_token_for_modify_op
            http_status_code = '403'.

        /iwfnd/cx_sodata=>raise_cor_exception(
          EXPORTING
            ix_previous         = lx_tech_exception
            iv_http_status_code = '403'
            iv_exception_type   = /iwfnd/cx_sodata=>gcs_cor_exceptions-client_forbidden  ).

      ENDIF.
    ENDLOOP.

    mo_service->get_meta_models( IMPORTING et_meta_models = lt_model ).
    LOOP AT lt_model INTO ls_model.

      "If RAL is enabled, X-CSRF token is required
      IF mo_batch_helper->is_only_read_allowed( ) = abap_true.
        IF ls_model-model->is_ral_enabled( ) = abap_true.
          CREATE OBJECT lx_tech_exception
            EXPORTING
              textid           = /iwfnd/cx_mgw_tech_exception=>xcsrf_token_needed_for_ral
              http_status_code = '406'.

          /iwfnd/cx_sodata=>raise_cor_exception(
            EXPORTING
              ix_previous         = lx_tech_exception
              iv_http_status_code = '406'
              iv_exception_type   = /iwfnd/cx_sodata=>gcs_cor_exceptions-client_not_acceptable  ).
        ENDIF.
      ENDIF.

      " check for deferred batch response creation (batch-at-once)
      ls_model-model->get_model_features(
        IMPORTING
          es_model_features = ls_model_features ).
      IF ls_model_features-use_deferred_batch_resp_crea EQ abap_true.
        " at least one model wants to use deferred batch response creation
        lv_use_deferred_resp_crea = abap_true.
      ENDIF.

    ENDLOOP.


    IF lv_use_deferred_resp_crea EQ abap_true.
      " direct delegation of independent reads does not make sense
      " if the data provider wants to handle all operations at once
      lv_is_independent_read_op = abap_false.

      mo_batch_helper->set_use_deferred_resp_crea( abap_true ).

    ENDIF.

* Get and save HTTP headers for all inner operations
    mo_context->get_parameter(
      EXPORTING
        iv_name  = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-all_parameters
      IMPORTING
        ev_value = lt_http_header ).


    LOOP AT lt_http_header INTO ls_http_header.
      IF ls_http_header-name IS INITIAL           OR
         ls_http_header-name = 'sap-dtrace'       OR
         ls_http_header-name = 'sap-passport'     OR
         ls_http_header-name = 'x-csrf-token'     OR
         ls_http_header-name = 'x-requested-with' OR
         ls_http_header-name = 'content-type'     OR
         ls_http_header-name = 'content-length'   OR
         ls_http_header-name = 'requestid'        OR
         ( ls_http_header-name(1) = '~' AND ls_http_header-name <> if_http_header_fields_sap=>request_uri ).
        DELETE lt_http_header.
      ENDIF.
    ENDLOOP.
    mo_batch_helper->save_batch_http_headers( lt_http_header ).

* Get the IWF service factory responsible to return the FND business data and edm provider
    lo_svc_factory = /iwfnd/cl_sodata_svc_factory=>get_svc_factory( ).

* Get the IWF root handler responsible to do the dispatching (via SUPER) and to initialize and close the IWF transaction
    mo_root_handler = /iwfnd/cl_sodata_root_handler=>get_root_handler( lo_svc_factory ).

* Segment_1_and_2 is resolved in namespace and service document name
* URI sample: /odata/iwfnd/sampleflight -> iwfnd=namespace svc. name= sampleflight
    mo_root_handler->set_resolution_type(
        iv_svc_resolution_type = /iwcor/cl_ds_hdlr_root=>gc_svc_by_segment_1_and_2
        iv_svc_extension       = /iwfnd/cl_sodata_root_handler=>gc_svc_extension_none ).

    "Set start authorization to TRUE
    mo_batch_helper->set_start_auth_in_bep_needed( iv_start_auth_in_bep_needed = abap_true ).

    " Get Service Name for CATALOGSERVICE
    lv_service_name = mv_service_name.
    TRANSLATE lv_service_name TO UPPER CASE.

    " Query or Changeset
    LOOP AT it_batch REFERENCE INTO lr_batch.

      " Query Operations
      IF lr_batch->query_operation IS BOUND.
        lr_parameter = lr_batch->query_operation.

        " Always add operation even if it is not clear at this time whether Batch_All is active
        " They will be deleted later if not active
        lv_packet_no = lv_packet_no + 1.
        lv_operation_no = 1.
        IF lv_service_name <> 'CATALOGSERVICE'.
          mo_batch_helper->batch_add_operation(
            iv_packet_no    = lv_packet_no
            iv_operation_no = lv_operation_no
            iv_changeset    = abap_false
            io_request      = lr_parameter->request
            io_response     = lr_parameter->response
            io_context      = lr_parameter->context
            iv_is_independent_read = lv_is_independent_read_op ).
        ENDIF.

        " Check etag Processing
        lr_parameter = lr_batch->query_operation.
        lv_gwbep_version = /iwfnd/cl_mgw_util_versions=>get_gwbep_version( ).
        IF lv_gwbep_version < '009'.
          check_etag_processing( lr_parameter->*-request ).
        ENDIF.

        mo_root_handler->/iwcor/if_rest_handler~handle(
          io_request  = lr_parameter->request
          io_response = lr_parameter->response
          io_context  = lr_parameter->context ).

        " Changeset Operations
      ELSEIF lr_batch->change_set IS BOUND.

        lv_is_independent_read_op = abap_false. "Only for Co-Deployed


        " Batch All
        lv_packet_no = lv_packet_no + 1.
        CLEAR lv_operation_no.
        mo_batch_helper->batch_changeset_start( ).

        " Set total number of operations (OLD Batch Handling: Changeset only)
        DESCRIBE TABLE lr_batch->change_set->* LINES lv_changeset_optotal.
        mo_batch_helper->changeset_set_optotal( lv_changeset_optotal ).

        LOOP AT lr_batch->change_set->* REFERENCE INTO lr_parameter.

          " Batch All
          lv_operation_no = lv_operation_no + 1.
          IF lv_service_name <> 'CATALOGSERVICE'.
            mo_batch_helper->batch_add_operation(
              iv_packet_no    = lv_packet_no
              iv_operation_no = lv_operation_no
              iv_changeset    = abap_true
              io_request      = lr_parameter->request
              io_response     = lr_parameter->response
              io_context      = lr_parameter->context ).
          ENDIF.

          " Check etag Processing if eTag handling in backend
          lv_gwbep_version = /iwfnd/cl_mgw_util_versions=>get_gwbep_version( ).
          IF lv_gwbep_version < '009'.
            check_etag_processing( lr_parameter->*-request ).
          ENDIF.

          " Call operation in Changeset
          mo_root_handler->/iwcor/if_rest_handler~handle(
            io_request  = lr_parameter->request
            io_response = lr_parameter->response
            io_context  = lr_parameter->context ).
        ENDLOOP.

        "  OLD Batch Handling: Changeset only
        IF mo_batch_helper->mv_batch_all <> abap_true.

          " Get Changeset Result
          mo_batch_helper->changeset_get_operations(
            IMPORTING
              et_operation = lt_cs_operation ).

          " Process Changeset Result
          IF lt_cs_operation IS NOT INITIAL.

            " In error case only one operation error to Odata lib
            CLEAR lv_changeset_error.
            LOOP AT lt_cs_operation INTO ls_cs_operation
              WHERE operation_state = /iwfnd/cl_mgw_batch_helper=>mcs_operation_state-error.
              me->changeset_return_error(
                is_error_operation = ls_cs_operation
                it_operation       = lt_cs_operation ).

              lv_changeset_error = abap_true.
            ENDLOOP.
            IF lv_changeset_error IS NOT INITIAL.
              mo_sutil_runtime->set_new_operation( ).

              " In success case create result for each operation
            ELSE.
              LOOP AT lt_cs_operation INTO ls_cs_operation
                WHERE odata_method IS NOT INITIAL.
                me->changeset_return_result(
                  CHANGING
                    cs_operation = ls_cs_operation
                ).
              ENDLOOP.
            ENDIF.
          ENDIF.
        ENDIF.

        " FINISH Current Changeset
        mo_batch_helper->batch_changeset_finish( ).
      ENDIF.
    ENDLOOP.

* Execute Batch Request
    lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
    lv_batch_operations_count = mo_batch_helper->batch_count_operations( ).

    IF lv_batch_operations_count > 0 OR
       lv_is_co_deployment EQ abap_false.
      TRY.
          mo_batch_helper->batch_execute( ).

        CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
          raise_business_error( lx_busi_exception ).

        CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
          raise_technical_error( lx_tech_exception ).
      ENDTRY.
    ENDIF.

* Batch All: Return result
    mo_batch_helper->batch_get_operations(
      EXPORTING
        iv_error_included = abap_true
      IMPORTING
        et_operation      = lt_batch_operation
    ).

* Process Single Result
    LOOP AT lt_batch_operation INTO ls_batch_operation.
      APPEND ls_batch_operation TO lt_packet_operation.
      AT END OF packet_no.

        " Handle single packet
        CLEAR lv_packet_error.
        LOOP AT lt_packet_operation INTO ls_batch_operation
          WHERE operation_state = /iwfnd/cl_mgw_batch_helper=>mcs_operation_state-error.
          IF ls_batch_operation-odata_method IS NOT INITIAL.
            batch_return_error( is_error_operation = ls_batch_operation ).
          ENDIF.
          lv_packet_error = abap_true.
        ENDLOOP.
        IF lv_packet_error IS NOT INITIAL.
          mo_sutil_runtime->set_new_operation( ).

          " In success case create result for each operation
        ELSE.
          LOOP AT lt_packet_operation INTO ls_batch_operation
            WHERE odata_method IS NOT INITIAL.

            IF  ls_batch_operation-content_id_ref IS NOT INITIAL
            AND ls_batch_operation-changeset IS INITIAL.
              " note: this call is required!
              " it handle read operation's result having a content-id-ref
              " and sets the correct resulting query resource path
              " [this is of importance with containment navigation in query path]
              handle_query_result_cnt_id_ref(
                EXPORTING it_batch_operation = lt_batch_operation
                CHANGING  cs_batch_operation = ls_batch_operation ).
            ENDIF.

            batch_return_result(
              CHANGING
                cs_operation = ls_batch_operation ).

            IF ls_batch_operation-response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral( ).
            ENDIF.

          ENDLOOP.
        ENDIF.
        CLEAR lt_packet_operation.
      ENDAT.
    ENDLOOP.

* Metering
    ls_metering-access_type = /iwfnd/cl_sodata_processor=>mc_consumer_type.
    ls_metering-operation   = /iwfnd/cl_sodata_processor=>mcs_operations-batch.
    ls_metering-odc_used    = abap_true.
    ls_metering-format_type = 'mixed'.                      "#EC NOTEXT
    ls_metering-num_entries = mo_batch_helper->get_num_entries( ).
    mo_context->set_parameter(
      iv_name  = /iwfnd/cl_metering_api=>gc_metering
      iv_value = ls_metering
    ).

* Finish Batch Processing
    mo_batch_helper->set_batch_mode( abap_false ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_batch~execute


  METHOD /iwcor/if_ds_proc_entity_cprop~read.

    DATA: lr_data             TYPE REF TO data,
          lv_operation_name   TYPE char30,
          lv_operation        TYPE char10,
          ls_odata_request    TYPE /iwfnd/if_mgw_core_types=>request_s,
          ls_response_context TYPE /iwfnd/if_mgw_core_types=>response_s.

    DATA: lv_perf_handle     TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method(
        iv_odata_method  = mcs_odata_method-entity_cprop_read
        it_property_path = it_property_path
      ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-property
      INTO lv_operation_name SEPARATED BY space.

*-initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = lv_operation_name
        iv_agent         = mc_agent
    ).

* Please note: This method is called also for simple properties!
    get_entry_data(
      EXPORTING
        io_entity_set           = io_entity_set
        it_key                  = it_key
        is_function_import_info = is_function_import_info
        it_navigation_path      = it_navigation_path
        iv_format               = iv_format
        iv_for_ds_operation     = iv_for
      IMPORTING
        er_data                 = lr_data
        es_request_details      = ls_odata_request
        es_response_context     = ls_response_context ).

    IF ls_response_context-is_ral_relevant EQ abap_true.
      mo_transaction_handler->set_is_ral(  ).
    ENDIF.

* Batch: Create a dummy provider, business data will be written later
    IF mo_batch_helper->mv_batch_all = abap_true
    AND mo_batch_helper->mv_etag_processing = abap_false.
      CREATE OBJECT ro_provider TYPE /iwfnd/cl_sodata_dummy_prov.

* No Batch
    ELSE.
      mo_post_processor->entity_cprop_read(
        EXPORTING
          iv_odata_method      = mcs_odata_method-entity_cprop_read
          is_request_details   = ls_odata_request
          io_ds_context        = mo_context
          iv_format            = iv_format
          iv_for_ds_operation  = iv_for
          iv_source_entity_set = mv_source_entity_set
          it_property_path     = it_property_path
          io_mgw_context       = mo_mgw_context
          ir_entity            = lr_data
        IMPORTING
          eo_provider          = ro_provider
        CHANGING
          ct_headers           = mt_entity_headers
      ).

*---setting common http response headers
      set_common_response_headers(
        EXPORTING
          io_provider         = ro_provider
          is_response_context = ls_response_context ).

    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/IWCOR/IF_DS_PROC_ENTITY_CPROP~READ


  METHOD /iwcor/if_ds_proc_entity_links~count.

    DATA: lv_operation_name         TYPE char30,
          lv_operation              TYPE char10,
          lv_is_target_format       TYPE abap_bool,
          ls_odata_request          TYPE /iwfnd/if_mgw_core_types=>request_s,
          lt_dollar_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lr_data                   TYPE REF TO data,
          lo_mgw_runtime            TYPE REF TO /iwfnd/bd_mgw_runtime,
          lv_is_co_deployment       TYPE abap_bool,
          lv_is_independent_read_op TYPE abap_bool,
          lx_busi_exception         TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception         TYPE REF TO /iwfnd/cx_mgw_tech_exception,
          ls_response_context       TYPE /iwfnd/if_mgw_core_types=>response_s,
          lv_format                 TYPE string.

    DATA: lv_perf_handle        TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_links_count ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-feed mcs_operation_types-links mcs_operation_types-count
      INTO lv_operation_name SEPARATED BY space.

*-initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = lv_operation_name
        iv_agent         = mc_agent
    ).

*-function import
    IF is_function_import_info IS NOT INITIAL.
      me->get_fi_data_by_read(
            EXPORTING
              io_entity_set           = io_entity_set
              is_function_import_info = is_function_import_info
              iv_format               = lv_format
            CHANGING
              cr_entityset            = lr_data
              cs_odata_request        = ls_odata_request
          ).
    ELSE.

*---initialization
      me->init_request(
        EXPORTING
          iv_http_method     = /iwcor/if_rest_request=>gc_method_get
          iv_operation       = mcs_operations-read
          iv_operation_type  = mcs_operation_types-feed
          io_entity_set      = io_entity_set
          it_key             = it_key
          it_navigation_path = it_navigation_path
          io_filter          = io_filter
          iv_skip            = iv_skip
          iv_top             = iv_top
        IMPORTING
          es_request         = ls_odata_request
          et_parameter       = lt_dollar_parameter
      ).

      lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
      IF mv_batch EQ abap_true.
        lv_is_independent_read_op = mo_batch_helper->is_independent_read( ).
      ENDIF.

      IF lv_is_co_deployment EQ abap_true AND
         ( mv_batch EQ abap_false OR
          lv_is_independent_read_op EQ abap_true ).

        mo_co_deploym_proxy = mo_transaction_handler->get_co_deploym_proxy( ).

        TRY.
            mo_co_deploym_proxy->init(
              EXPORTING
                iv_service_document_name = mv_service_name
                iv_namespace             = mv_namespace
                iv_version               = mv_service_version
                iv_base_url              = mv_base_url
                iv_entity_name           = mv_target_entity
                io_context               = mo_mgw_context
                io_batch_helper          = mo_batch_helper ).

            mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~read_entityset(
               EXPORTING
                 iv_entity_name       = mv_target_entity
                 iv_entity_set_name   = mv_target_entity_set
                 iv_source_name       = mv_source_entity
                 is_request_details   = ls_odata_request
                 iv_content_format    = mv_content_format
                 it_parameter         = lt_dollar_parameter
               CHANGING
                 ct_headers           = mt_entity_headers
                 cr_entityset         = lr_data
                 cv_is_target_format  = lv_is_target_format
                 cs_response_context = ls_response_context ).

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.
      ELSE.
* Get MGW Runtime
        lo_mgw_runtime = get_mgw_runtime(  ).

* Call Provider Delegator
        TRY.
            CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~read_entityset
              EXPORTING
                iv_entity_name      = mv_target_entity
                iv_entity_set_name  = mv_target_entity_set
                iv_source_name      = mv_source_entity
                is_request_details  = ls_odata_request
                iv_content_format   = mv_content_format
                it_parameter        = lt_dollar_parameter
              CHANGING
                ct_headers          = mt_entity_headers
                cr_entityset        = lr_data
                cv_is_target_format = lv_is_target_format
                cs_response_context = ls_response_context.

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).

        ENDTRY.
      ENDIF.
    ENDIF.

* Batch: Set RV_COUNT temporarily to "1", right value later
    IF mo_batch_helper->mv_batch_all = abap_true
    AND mo_batch_helper->mv_etag_processing = abap_false.
      rv_count = 1.

* No Batch
    ELSE.
      mo_post_processor->entity_links_count(
        EXPORTING
          iv_odata_method      = mcs_odata_method-entity_link_read
          is_request_details   = ls_odata_request
          io_ds_context        = mo_context
          iv_source_entity_set = mv_source_entity_set
          io_mgw_context       = mo_mgw_context
          ir_entity            = lr_data
        IMPORTING
          ev_count             = rv_count
        CHANGING
          ct_headers           = mt_entity_headers
      ).
    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_links~count


  METHOD /iwcor/if_ds_proc_entity_links~create_link.

    DATA: lv_operation         TYPE char10,
          ls_odata_request     TYPE /iwfnd/if_mgw_core_types=>request_s,
          lo_target_entity_set TYPE REF TO /iwcor/if_ds_edm_entity_set,
          lo_mgw_runtime       TYPE REF TO /iwfnd/bd_mgw_runtime,
          lx_busi_exception    TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception    TYPE REF TO /iwfnd/cx_mgw_tech_exception.
    DATA: lt_parameter          TYPE /iwfnd/if_mgw_core_types=>parameter_values_t.
    DATA: lv_perf_handle        TYPE i.
    DATA: lo_entry_provider     TYPE REF TO /iwfnd/cl_sodata_entry_prov.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* initialize logger header
    lv_operation  = mcs_operations-create_link.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = mcs_operations-create_link
                      iv_agent         = mc_agent
                    ).

* Initialization
    me->init_request(
      EXPORTING
        iv_http_method          = /iwcor/if_rest_request=>gc_method_post
        iv_operation            = mcs_operations-create_link
        io_entity_set           = io_entity_set
        io_navigation_property  = io_navigation_property
        it_key                  = it_key
      IMPORTING
        es_request              = ls_odata_request
        eo_target_entity_set    = lo_target_entity_set
        et_parameter            = lt_parameter
    ).

* Get MGW Runtime
    lo_mgw_runtime = get_mgw_runtime(  ).

    CREATE OBJECT lo_entry_provider
      EXPORTING
        io_request_entity = io_request_entity
        io_entity_set     = lo_target_entity_set
        iv_is_stream      = mv_is_stream.


* Call Provider Delegator
    TRY.
        CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~create_link
          EXPORTING
            iv_targent_entity_name = mv_target_entity
            iv_entity_set_name     = mv_target_entity_set
            iv_source_name         = mv_source_entity
            is_request_details     = ls_odata_request
            io_data_provider       = lo_entry_provider
            iv_content_format      = mv_content_format
            it_parameter           = lt_parameter
          CHANGING
            ct_headers             = mt_entity_headers.

* Set the HTTP Headers
        mo_context->set_parameter(
          EXPORTING
            iv_name  = /iwfnd/if_sodata_types=>gcs_cor_request_parameters-http_header
            iv_value = mt_entity_headers ).

      CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
        raise_business_error( lx_busi_exception ).

      CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
        raise_technical_error( lx_tech_exception ).

    ENDTRY.


* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type    = /iwfnd/cl_logger=>success
      iv_msg_number  = mcs_messages-operation_finished
      iv_msg_id      = mc_message_class
      iv_agent       = mc_agent
      iv_msg_handle  = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.


  METHOD /iwcor/if_ds_proc_entity_links~read.

    DATA: lv_operation_name         TYPE char30,
          lv_operation              TYPE char10,
          lv_is_target_format       TYPE abap_bool,
          ls_odata_request          TYPE /iwfnd/if_mgw_core_types=>request_s,
          lt_dollar_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lr_data                   TYPE REF TO data,
          lo_mgw_runtime            TYPE REF TO /iwfnd/bd_mgw_runtime,
          lx_busi_exception         TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception         TYPE REF TO /iwfnd/cx_mgw_tech_exception,
          ls_response_context       TYPE /iwfnd/if_mgw_core_types=>response_s,
          lv_is_co_deployment       TYPE abap_bool,
          lv_is_independent_read_op TYPE abap_bool.

    DATA: lv_perf_handle        TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_links_read ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-feed mcs_operation_types-links
      INTO lv_operation_name SEPARATED BY space.

*-initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = lv_operation_name
        iv_agent         = mc_agent
    ).

*-function import
    IF is_function_import_info IS NOT INITIAL.
      me->get_fi_data_by_read(
            EXPORTING
              io_entity_set           = io_entity_set
              is_function_import_info = is_function_import_info
              iv_format               = iv_format
              iv_for_ds_operation     = iv_for
            CHANGING
              cr_entityset            = lr_data
              cs_odata_request        = ls_odata_request
          ).
    ELSE.

*---initialization
      me->init_request(
        EXPORTING
          iv_http_method      = /iwcor/if_rest_request=>gc_method_get
          iv_operation        = mcs_operations-read
          iv_operation_type   = mcs_operation_types-feed
          io_entity_set       = io_entity_set
          it_key              = it_key
          it_navigation_path  = it_navigation_path
          io_filter           = io_filter
          iv_skip             = iv_skip
          iv_top              = iv_top
          iv_format           = iv_format
          iv_for_ds_operation = iv_for
        IMPORTING
          es_request          = ls_odata_request
          et_parameter        = lt_dollar_parameter
      ).

      lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
      IF mv_batch EQ abap_true.
        lv_is_independent_read_op = mo_batch_helper->is_independent_read( ).
      ENDIF.

      IF lv_is_co_deployment EQ abap_true AND
         ( mv_batch EQ abap_false OR
          lv_is_independent_read_op EQ abap_true ).

        mo_co_deploym_proxy = mo_transaction_handler->get_co_deploym_proxy( ).

        TRY.
            mo_co_deploym_proxy->init(
              EXPORTING
                iv_service_document_name = mv_service_name
                iv_namespace             = mv_namespace
                iv_version               = mv_service_version
                iv_base_url              = mv_base_url
                iv_entity_name           = mv_target_entity
                io_context               = mo_mgw_context
                io_batch_helper          = mo_batch_helper ).

            mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~read_entityset(
               EXPORTING
                 iv_entity_name       = mv_target_entity
                 iv_entity_set_name   = mv_target_entity_set
                 iv_source_name       = mv_source_entity
                 is_request_details   = ls_odata_request
                 iv_content_format    = mv_content_format
                 it_parameter         = lt_dollar_parameter
               CHANGING
                 ct_headers           = mt_entity_headers
                 cr_entityset         = lr_data
                 cs_response_context  = ls_response_context
                 cv_is_target_format  = lv_is_target_format ).

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.
      ELSE.

* Get MGW Runtime
        lo_mgw_runtime = get_mgw_runtime(  ).

* Call Provider Delegator
        TRY.
            CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~read_entityset
              EXPORTING
                iv_entity_name      = mv_target_entity
                iv_entity_set_name  = mv_target_entity_set
                iv_source_name      = mv_source_entity
                is_request_details  = ls_odata_request
                iv_content_format   = mv_content_format
                it_parameter        = lt_dollar_parameter
              CHANGING
                ct_headers          = mt_entity_headers
                cr_entityset        = lr_data
                cv_is_target_format = lv_is_target_format
                cs_response_context = ls_response_context.

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).

        ENDTRY.
      ENDIF.
    ENDIF.

* Batch: Create a dummy provider, business data will be written later
    IF mo_batch_helper->mv_batch_all = abap_true
    AND mo_batch_helper->mv_etag_processing = abap_false.
      CREATE OBJECT ro_provider TYPE /iwfnd/cl_sodata_dummy_prov.

* No Batch
    ELSE.
      mo_post_processor->entity_links_read(
        EXPORTING
          iv_odata_method      = mcs_odata_method-entity_link_read
          is_request_details   = ls_odata_request
          io_entity_set        = io_entity_set
          io_ds_context        = mo_context
          iv_format            = iv_format
          iv_for_ds_operation  = iv_for
          iv_source_entity_set = mv_source_entity_set
          it_navigation_path   = it_navigation_path
          io_mgw_context       = mo_mgw_context
          ir_entity            = lr_data
        IMPORTING
          eo_provider          = ro_provider
        CHANGING
          ct_headers            = mt_entity_headers
      ).

*---setting common http response headers
      set_common_response_headers(
        EXPORTING
          io_provider         = ro_provider
          is_response_context = ls_response_context ).

    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_links~read


  METHOD /iwcor/if_ds_proc_entity_link~delete.


    DATA: lv_operation      TYPE char10,
          ls_odata_request  TYPE /iwfnd/if_mgw_core_types=>request_s,
          lo_mgw_runtime    TYPE REF TO /iwfnd/bd_mgw_runtime,
          lx_busi_exception TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception TYPE REF TO /iwfnd/cx_mgw_tech_exception.
    DATA: lt_parameter        TYPE /iwfnd/if_mgw_core_types=>parameter_values_t.
    DATA: lv_perf_handle      TYPE i.
    DATA: lt_navigation_paths TYPE /iwcor/if_ds_uri=>navigation_path_segment_t.


    APPEND is_navigation_path_segment TO lt_navigation_paths.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

    " initialize logger header
    lv_operation  = mcs_operations-delete_link.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = mcs_operations-delete_link
                      iv_agent         = mc_agent
                    ).

* Initialization
    me->init_request(
      EXPORTING
        iv_http_method      = /iwcor/if_rest_request=>gc_method_delete
        iv_operation        = mcs_operations-delete_link
        io_entity_set       = io_entity_set
        it_navigation_path  = lt_navigation_paths
        it_key              = it_key
      IMPORTING
        es_request          = ls_odata_request
        et_parameter        = lt_parameter
    ).

* Get MGW Runtime
    lo_mgw_runtime = get_mgw_runtime(  ).

* Call Provider Delegator
    TRY.
        CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~delete_link
          EXPORTING
            iv_targent_entity_name = mv_target_entity
            iv_entity_set_name     = mv_target_entity_set
            iv_source_name         = mv_source_entity
            is_request_details     = ls_odata_request
            it_parameter           = lt_parameter
          CHANGING
            ct_headers             = mt_entity_headers.

* Set the HTTP Headers
        mo_context->set_parameter(
          EXPORTING
            iv_name  = /iwfnd/if_sodata_types=>gcs_cor_request_parameters-http_header
            iv_value = mt_entity_headers
        ).

      CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
        raise_business_error( lx_busi_exception ).

      CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
        raise_technical_error( lx_tech_exception ).

    ENDTRY.


* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type    = /iwfnd/cl_logger=>success
      iv_msg_number  = mcs_messages-operation_finished
      iv_msg_id      = mc_message_class
      iv_agent       = mc_agent
      iv_msg_handle  = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.


  METHOD /iwcor/if_ds_proc_entity_link~exists.

    DATA: lr_data             TYPE REF TO data,
          lv_operation_name   TYPE char30,
          lv_operation        TYPE char10,
          ls_odata_request    TYPE /iwfnd/if_mgw_core_types=>request_s,
          ls_response_context TYPE /iwfnd/if_mgw_core_types=>response_s.

    DATA: lv_perf_handle     TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_link_exists ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-entry mcs_operation_types-links mcs_operation_types-count
      INTO lv_operation_name SEPARATED BY space.

*-initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = lv_operation_name
        iv_agent         = mc_agent
    ).

    get_entry_data(
      EXPORTING
        io_entity_set           = io_entity_set
        it_key                  = it_key
        is_function_import_info = is_function_import_info
        it_navigation_path      = it_navigation_path
      IMPORTING
        er_data                 = lr_data
        es_request_details      = ls_odata_request
        es_response_context     = ls_response_context ).

    IF ls_response_context-is_ral_relevant EQ abap_true.
      mo_transaction_handler->set_is_ral(  ).
    ENDIF.

* Batch: Set RV_EXISTS temporarily to "abap_true", right value later
    IF mo_batch_helper->mv_batch_all = abap_true
    AND mo_batch_helper->mv_etag_processing = abap_false.
      rv_exists = abap_true.

* No Batch
    ELSE.
      mo_post_processor->entity_link_exists(
        EXPORTING
          iv_odata_method    = mcs_odata_method-entity_link_exists
          is_request_details = ls_odata_request
          io_ds_context      = mo_context
          ir_entity          = lr_data
        IMPORTING
          ev_exists          = rv_exists
        CHANGING
          ct_headers         = mt_entity_headers
      ).
    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_link~exists


  METHOD /iwcor/if_ds_proc_entity_link~read.

    DATA: lr_data             TYPE REF TO data,
          lv_operation_name   TYPE char30,
          lv_operation        TYPE char10,
          ls_odata_request    TYPE /iwfnd/if_mgw_core_types=>request_s,
          ls_response_context TYPE /iwfnd/if_mgw_core_types=>response_s.

    DATA: lv_perf_handle     TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_link_read ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-entry mcs_operation_types-links
      INTO lv_operation_name SEPARATED BY space.

*-initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = lv_operation_name
        iv_agent         = mc_agent
    ).

* Please note: This method is called also for simple properties!
    get_entry_data(
      EXPORTING
        io_entity_set           = io_entity_set
        it_key                  = it_key
        is_function_import_info = is_function_import_info
        it_navigation_path      = it_navigation_path
        iv_format               = iv_format
        iv_for_ds_operation     = iv_for
      IMPORTING
        er_data                 = lr_data
        es_request_details      = ls_odata_request
        es_response_context     = ls_response_context ).

    IF ls_response_context-is_ral_relevant EQ abap_true.
      mo_transaction_handler->set_is_ral(  ).
    ENDIF.

* Batch: Create a dummy provider, business data will be written later
    IF mo_batch_helper->mv_batch_all = abap_true
    AND mo_batch_helper->mv_etag_processing = abap_false.
      CREATE OBJECT ro_provider TYPE /iwfnd/cl_sodata_dummy_prov.

* No Batch
    ELSE.
      mo_post_processor->entity_link_read(
        EXPORTING
          iv_odata_method      = mcs_odata_method-entity_link_read
          is_request_details   = ls_odata_request
          io_entity_set        = io_entity_set
          io_ds_context        = mo_context
          iv_format            = iv_format
          iv_for_ds_operation  = iv_for
          iv_source_entity_set = mv_source_entity_set
          it_navigation_path   = it_navigation_path
          io_mgw_context       = mo_mgw_context
          ir_entity            = lr_data
        IMPORTING
          eo_provider          = ro_provider
        CHANGING
          ct_headers            = mt_entity_headers
      ).

*---setting common http response headers
      set_common_response_headers(
        EXPORTING
          io_provider         = ro_provider
          is_response_context = ls_response_context ).

    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_link~read


  METHOD /iwcor/if_ds_proc_entity_media~delete.

    mv_is_stream = abap_true.

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_media_delete ).
    ENDIF.

    me->/iwcor/if_ds_proc_entity~delete(
      EXPORTING
        io_entity_set      = io_entity_set
        it_key             = it_key
    ).

    CLEAR mv_is_stream.

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_media~delete


  METHOD /iwcor/if_ds_proc_entity_media~read.

    DATA:
      lo_lib_binary_provider        TYPE REF TO /iwcor/cl_ds_ep_binary,
      lo_entity_set                 TYPE REF TO /iwfnd/if_med_mdl_entity_set,
      lo_mgw_runtime                TYPE REF TO /iwfnd/bd_mgw_runtime,
      lr_lib_conditions             TYPE REF TO /iwcor/if_ds_cntxt=>conditional_info_s,
      lr_data                       TYPE REF TO data,
      ls_entity_header              LIKE LINE OF mt_entity_headers,
      ls_odata_request              TYPE /iwfnd/if_mgw_core_types=>request_s,
      ls_response_context           TYPE /iwfnd/if_mgw_core_types=>response_s,
      lt_dollar_parameter           TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
      lv_extension                  TYPE c LENGTH 50,
      lv_filename                   TYPE string,
      lv_if_modified_since          TYPE string,
      lv_is_cache_active            TYPE abap_bool,
      lv_is_co_deployment           TYPE abap_bool,
      "! TRUE if the requested resource is in the entity set Annotations of the catalogue service v2
      "! /sap/opu/odata/IWFND/CATALOGSERVICE;v=2/Annotations(TechnicalName='%2FIWBEP%2FTEA_TEST_ANNOTATION_FILE',Version='0001')/$value
      lv_is_catalog_serv_annotation TYPE abap_bool,
      lv_is_ctxt_cache_header_set   TYPE abap_bool,
      lv_is_independent_read_op     TYPE abap_bool,
      lv_is_target_format           TYPE abap_bool,
      lv_last_modified              TYPE tzntstmps,
      lv_last_modified_formatted    TYPE string,
      lv_max_val                    TYPE string,
      lv_max_age                    TYPE string,
      lv_mime_type                  TYPE mimetypes-type,
      lv_operation                  TYPE char10,
      lv_operation_name             TYPE char30,
      lv_perf_handle                TYPE i,
      lx_busi_exception             TYPE REF TO /iwfnd/cx_mgw_busi_exception,
      lx_tech_exception             TYPE REF TO /iwfnd/cx_mgw_tech_exception.

    FIELD-SYMBOLS:
          <ls_stream>            TYPE /iwfnd/if_mgw_core_runtime=>ty_s_media_resource.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing and not etag processing
    IF mo_batch_helper->mv_batch = abap_true AND mo_batch_helper->mv_etag_processing = abap_false.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_media_read ).
    ENDIF.

    mv_is_stream = abap_true.

    CONCATENATE mcs_operations-read mcs_operation_types-stream
      INTO lv_operation_name SEPARATED BY space.

    " initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = lv_operation_name
                      iv_agent         = mc_agent
                    ).

* With Function Import
    IF is_function_import_info IS NOT INITIAL.
      me->get_fi_data_by_read(
            EXPORTING
              io_entity_set           = io_entity_set
              is_function_import_info = is_function_import_info
              iv_format               = iv_format
              iv_for_ds_operation     = iv_for
            CHANGING
              cr_entityset            = lr_data
              cs_odata_request        = ls_odata_request
          ).

* Without Function Import
    ELSE.

* Initialization
      me->init_request(
        EXPORTING
          iv_http_method      = /iwcor/if_rest_request=>gc_method_get
          iv_operation        = mcs_operations-read
          iv_operation_type   = mcs_operation_types-entry
          io_entity_set       = io_entity_set
          it_key              = it_key
          it_navigation_path  = it_navigation_path
          io_filter           = io_filter
          iv_format           = iv_format
          iv_for_ds_operation = iv_for
        IMPORTING
          es_request          = ls_odata_request
          et_parameter        = lt_dollar_parameter
      ).

      lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
      IF mv_batch EQ abap_true.
        lv_is_independent_read_op = mo_batch_helper->is_independent_read( ).
      ENDIF.

      IF lv_is_co_deployment EQ abap_true AND
              ( mv_batch EQ abap_false OR
               lv_is_independent_read_op EQ abap_true ).

        mo_co_deploym_proxy = mo_transaction_handler->get_co_deploym_proxy( ).

        TRY.
            mo_co_deploym_proxy->init(
              EXPORTING
                iv_service_document_name = mv_service_name
                iv_namespace             = mv_namespace
                iv_version               = mv_service_version
                iv_base_url              = mv_base_url
                iv_entity_name           = mv_target_entity
                io_context               = mo_mgw_context
                io_batch_helper          = mo_batch_helper ).

            mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~read_stream(
               EXPORTING
                 iv_entity_name       = mv_target_entity
                 iv_entity_set_name   = mv_target_entity_set
                 iv_source_name       = mv_source_entity
                 is_request_details   = ls_odata_request
                 iv_content_format    = mv_content_format
                 it_parameter         = lt_dollar_parameter
               CHANGING
                 cr_stream            = lr_data
                 cs_response_context  = ls_response_context
                 ct_headers           = mt_entity_headers
                 cv_is_target_format  = lv_is_target_format ).

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.
      ELSE.


* Get MGW Runtime
        lo_mgw_runtime = get_mgw_runtime(  ).

        IF  mo_transaction_handler->get_service_identifier( ) = /iwfnd/cl_mgw_inst_man=>gcs_service_ids-catalog_service_2
        AND mv_target_entity_set = /iwfnd/if_sodata_types=>gcs_catalog_service-target_entity_set.
          lv_is_catalog_serv_annotation = abap_true.
        ENDIF.


* Call Provider Delegator
        TRY.
            CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~read_stream
              EXPORTING
                iv_entity_name      = mv_target_entity
                iv_entity_set_name  = mv_target_entity_set
                iv_source_name      = mv_source_entity
                is_request_details  = ls_odata_request
                iv_content_format   = mv_content_format
                it_parameter        = lt_dollar_parameter
              CHANGING
                cr_stream           = lr_data
                cs_response_context = ls_response_context
                ct_headers          = mt_entity_headers
                cv_is_target_format = lv_is_target_format.

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).

        ENDTRY.
      ENDIF.
    ENDIF.


    IF lv_is_catalog_serv_annotation = abap_true.
      " Conditional GET (i.e. if-modified-since)
      lr_lib_conditions = mo_context->get_conditional_info( ).
      IF lr_lib_conditions IS BOUND.
        lv_if_modified_since = lr_lib_conditions->request->get_header_field( iv_name = 'if-modified-since' ).

        IF lv_if_modified_since IS NOT INITIAL.
          READ TABLE mt_entity_headers INTO ls_entity_header WITH KEY name = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-last_modified.
          IF sy-subrc EQ 0.
            IF lv_if_modified_since EQ ls_entity_header-value.
              ls_response_context-is_not_modified = /iwfnd/if_mgw_core_types=>gcs_modification_status-is_not_modified.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.


* Batch: Create a dummy provider, business data will be written later
    IF mo_batch_helper->mv_batch_all = abap_true
    AND mo_batch_helper->mv_etag_processing = abap_false.
      CREATE OBJECT ro_provider TYPE /iwfnd/cl_sodata_dummy_prov.

* No Batch
    ELSE.
      " If not modified, then send 304 response (Not Modified)
      IF  ls_response_context-is_not_modified EQ /iwfnd/if_mgw_core_types=>gcs_modification_status-is_not_modified
      AND iv_for EQ /iwcor/if_ds_svc_proc=>gc_operation_read.
        "populate response header information
        CREATE OBJECT ro_provider TYPE /iwcor/cl_ds_ep_conditional.
        lv_last_modified_formatted = /iwcor/cl_rest_http_utils=>format_http_date( lv_last_modified ).

        ro_provider->set_header(
          EXPORTING
            iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-last_modified
            iv_value =  lv_last_modified_formatted ).

        mv_conditional_read_processed = abap_true.

      ENDIF.

      IF ls_response_context-is_not_modified NE /iwfnd/if_mgw_core_types=>gcs_modification_status-is_not_modified.
      mo_post_processor->entity_media_read(
        EXPORTING
          iv_odata_method      = mcs_odata_method-entity_media_read
          is_request_details   = ls_odata_request
          io_ds_context        = mo_context
          iv_format            = iv_format
          iv_for_ds_operation  = iv_for
          iv_source_entity_set = mv_source_entity_set
          io_mgw_context       = mo_mgw_context
          ir_entity            = lr_data
        IMPORTING
          eo_provider          = ro_provider
        CHANGING
          ct_headers           = mt_entity_headers
      ).

      ASSIGN lr_data->* TO <ls_stream>.

      lo_lib_binary_provider ?= ro_provider.
      lo_lib_binary_provider->set_binary_data( <ls_stream>-value ).
      lo_lib_binary_provider->set_content_type( <ls_stream>-mime_type ).
      ENDIF.


*     Context token cache header
      lo_entity_set ?= mo_service->get_entity_set( iv_namespace = ''
                                                   iv_name      = mv_target_entity_set ).

      IF lo_entity_set->does_support_context_token( ).
        lv_is_ctxt_cache_header_set = set_cache_ctrl_for_ctxt_token(
            io_lib_provider               = ro_provider
            iv_is_resource_lang_dependent = lo_entity_set->is_language_dependent( ) ).
      ENDIF.


*     Common http response headers
      IF lv_is_catalog_serv_annotation = abap_true.
        IF lv_is_ctxt_cache_header_set = abap_false.
          ro_provider->set_header(
              iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-cache_control
              iv_value = 'max-age=0' ). "Max Age in seconds
        ENDIF.

        set_common_response_headers(
                EXPORTING
                  io_provider            = ro_provider
                  is_response_context    = ls_response_context
                  iv_do_add_cache_header = abap_false ).

      ELSE.
        set_common_response_headers(
          EXPORTING
            io_provider            = ro_provider
            is_response_context    = ls_response_context
            iv_do_add_cache_header = xsdbool( NOT lv_is_ctxt_cache_header_set = abap_true ) ).

        IF  ls_response_context-is_not_modified EQ /iwfnd/if_mgw_core_types=>gcs_modification_status-is_not_modified
        AND iv_for EQ /iwcor/if_ds_svc_proc=>gc_operation_read.

          lv_is_cache_active = /iwfnd/cl_med_mdl_cache_persis=>is_caching_enabled( ).
          IF lv_is_cache_active = abap_true.
            lv_max_val = ls_response_context-max_age.
            CONCATENATE 'max-age=' lv_max_val INTO lv_max_age.

            ro_provider->set_header(
              EXPORTING
                iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-cache_control
                iv_value = lv_max_age  ). "Max Age in seconds
          ENDIF.
        ENDIF.
      ENDIF.


      IF ls_response_context-is_not_modified NE /iwfnd/if_mgw_core_types=>gcs_modification_status-is_not_modified.
*       No sniff option for browser
      ro_provider->set_header( EXPORTING
           iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-content_type_options
           iv_value = /iwfnd/if_sodata_types=>gcs_cor_response_parameters_va-nosniff ).



*       Open "Save" dialog in browser instead of display
      lv_filename = 'entity'.
      IF <ls_stream> IS ASSIGNED AND <ls_stream> IS NOT INITIAL.
        lv_mime_type = <ls_stream>-mime_type.

        CALL FUNCTION 'SDOK_FILE_NAME_EXTENSION_GET'
          EXPORTING
            mimetype  = lv_mime_type
          IMPORTING
            extension = lv_extension.

        IF lv_extension IS NOT INITIAL.
          CONCATENATE lv_filename '.' lv_extension INTO lv_filename.
        ENDIF.
      ENDIF.
      CONCATENATE 'attachment; filename=' lv_filename INTO lv_filename.

      ro_provider->set_header( EXPORTING
           iv_name  = 'Content-disposition'
           iv_value = lv_filename ).
      ENDIF.
    ENDIF.


* Batch: Reset etag processing if set
    IF mo_batch_helper->mv_etag_processing = abap_true.
      mo_batch_helper->batch_set_etag_processing( abap_false ).
    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_media~read


  METHOD /iwcor/if_ds_proc_entity_media~update.

    mv_is_stream = abap_true.

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_media_update ).
    ENDIF.

    me->/iwcor/if_ds_proc_entity~update(
      EXPORTING
        io_request_entity  = io_request_entity
        io_entity_set      = io_entity_set
        it_key             = it_key
    ).

    CLEAR mv_is_stream.

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_media~update


  METHOD /iwcor/if_ds_proc_entity_set~count.

    DATA: lv_operation_name         TYPE char30,
          lv_operation              TYPE char10,
          lv_is_target_format       TYPE abap_bool,
          ls_odata_request          TYPE /iwfnd/if_mgw_core_types=>request_s,
          lt_dollar_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lr_data                   TYPE REF TO data,
          lo_mgw_runtime            TYPE REF TO /iwfnd/bd_mgw_runtime,
          lx_busi_exception         TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception         TYPE REF TO /iwfnd/cx_mgw_tech_exception,
          lv_format                 TYPE        string,
          ls_response_context       TYPE        /iwfnd/if_mgw_core_types=>response_s,
          lv_is_co_deployment       TYPE abap_bool,
          lv_is_independent_read_op TYPE abap_bool.

    DATA: lv_perf_handle        TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_set_count ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-count
      INTO lv_operation_name SEPARATED BY space.

*-initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = lv_operation_name
        iv_agent         = mc_agent
    ).

*-function import
    IF is_function_import_info IS NOT INITIAL.
      me->get_fi_data_by_read(
            EXPORTING
              io_entity_set           = io_entity_set
              is_function_import_info = is_function_import_info
              iv_format               = lv_format
            CHANGING
              cr_entityset            = lr_data
              cs_odata_request        = ls_odata_request
          ).
    ELSE.

*---initialization
      me->init_request(
        EXPORTING
          iv_http_method       = /iwcor/if_rest_request=>gc_method_get
          iv_operation         = mcs_operations-read
          iv_operation_type    = mcs_operation_types-feed
          io_entity_set        = io_entity_set
          it_key               = it_key
          it_navigation_path   = it_navigation_path
          io_filter            = io_filter
          iv_skip              = iv_skip
          iv_top               = iv_top
          iv_count             = abap_true
        IMPORTING
          es_request           = ls_odata_request
          et_parameter         = lt_dollar_parameter
      ).

      lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
      IF mv_batch EQ abap_true.
        lv_is_independent_read_op = mo_batch_helper->is_independent_read( ).
      ENDIF.

      IF lv_is_co_deployment EQ abap_true AND
               ( mv_batch EQ abap_false OR
                lv_is_independent_read_op EQ abap_true ).

        mo_co_deploym_proxy = mo_transaction_handler->get_co_deploym_proxy( ).

        TRY.

            mo_co_deploym_proxy->init(
              EXPORTING
                iv_service_document_name = mv_service_name
                iv_namespace             = mv_namespace
                iv_version               = mv_service_version
                iv_base_url              = mv_base_url
                iv_entity_name           = mv_target_entity
                io_context               = mo_mgw_context
                io_batch_helper          = mo_batch_helper ).

            mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~read_entityset(
               EXPORTING
                 iv_entity_name       = mv_target_entity
                 iv_entity_set_name   = mv_target_entity_set
                 iv_source_name       = mv_source_entity
                 is_request_details   = ls_odata_request
                 iv_content_format    = mv_content_format
                 it_parameter         = lt_dollar_parameter
               CHANGING
                 ct_headers           = mt_entity_headers
                 cr_entityset         = lr_data
                 cs_response_context  = ls_response_context
                 cv_is_target_format  = lv_is_target_format ).

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).
          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.

      ELSE.

* Get MGW Runtime
        lo_mgw_runtime = get_mgw_runtime(  ).

* Call Provider Delegator
        TRY.
            CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~read_entityset
              EXPORTING
                iv_entity_name      = mv_target_entity
                iv_entity_set_name  = mv_target_entity_set
                iv_source_name      = mv_source_entity
                is_request_details  = ls_odata_request
                iv_content_format   = mv_content_format
                it_parameter        = lt_dollar_parameter
              CHANGING
                ct_headers          = mt_entity_headers
                cr_entityset        = lr_data
                cv_is_target_format = lv_is_target_format
                cs_response_context = ls_response_context.

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).

        ENDTRY.
      ENDIF.
    ENDIF.


* Batch: Set RV_COUNT temporarily to "1", right value later
    IF mo_batch_helper->mv_batch_all = abap_true.
      rv_count = 1.

* No Batch
    ELSE.

      " If not modified, then send 304 response (Not Modified)
      IF ls_response_context-is_not_modified EQ /iwfnd/if_mgw_core_types=>gcs_modification_status-is_not_modified.
        RAISE EXCEPTION TYPE /iwcor/cx_ds_not_modified.
      ENDIF.

*---setting common http response headers
      set_common_response_headers(
        EXPORTING
          is_response_context = ls_response_context
        CHANGING
          ct_headers = mt_entity_headers ).

      mo_post_processor->entity_set_count(
        EXPORTING
          iv_odata_method       = mcs_odata_method-entity_set_read
          is_request_details    = ls_odata_request
          io_ds_context         = mo_context
          iv_source_entity_set  = mv_source_entity_set
          io_mgw_context        = mo_mgw_context
          ir_entity_set         = lr_data
          is_response_context   = ls_response_context
        IMPORTING
          ev_count              = rv_count
        CHANGING
          ct_headers            = mt_entity_headers
      ).

    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_set~count


  METHOD /iwcor/if_ds_proc_entity_set~create_entity.

    DATA: lv_operation         TYPE char10,
          lv_is_target_format  TYPE abap_bool,
          ls_odata_request     TYPE /iwfnd/if_mgw_core_types=>request_s,
          lo_target_entity_set TYPE REF TO /iwcor/if_ds_edm_entity_set,
          lt_inline_info       TYPE /iwfnd/if_mgw_core_runtime=>ty_t_inline_info,
          lr_data              TYPE REF TO data,
          lo_mgw_runtime       TYPE REF TO /iwfnd/bd_mgw_runtime,
          lx_busi_exception    TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception    TYPE REF TO /iwfnd/cx_mgw_tech_exception.

    DATA: lv_inlines        TYPE string,
          lv_tech_inlines   TYPE string,
          ls_parameter      TYPE /iwfnd/if_mgw_core_types=>parameter_values_s,
          lt_parameter      TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lo_entry_provider TYPE REF TO /iwfnd/cl_sodata_entry_prov.

    DATA: lv_key_value_default TYPE string,
          lt_property          TYPE /iwcor/if_ds_edm_entity_type=>key_property_t,
          lo_entity_type       TYPE REF TO /iwcor/if_ds_edm_entity_type,
          lo_property          TYPE REF TO /iwcor/if_ds_edm_property,
          lo_simple_type       TYPE REF TO /iwcor/if_ds_edm_simple_type.

    DATA: lv_perf_handle      TYPE i,
          ls_response_context TYPE /iwfnd/if_mgw_core_types=>response_s.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_set_create_entity ).
    ENDIF.

    " initialize logger header
    lv_operation  = mcs_operations-create.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = mcs_operations-create
                      iv_agent         = mc_agent
                    ).

* Initialization
    me->init_request(
      EXPORTING
        iv_http_method         = /iwcor/if_rest_request=>gc_method_post
        iv_operation           = mcs_operations-create
        io_entity_set          = io_entity_set
        it_key                 = it_key
        io_navigation_property = io_navigation_property
        iv_format              = iv_format
      IMPORTING
        es_request             = ls_odata_request
        eo_target_entity_set   = lo_target_entity_set
        et_parameter           = lt_parameter
    ).

* Get MGW Runtime
    lo_mgw_runtime = get_mgw_runtime(  ).

    CREATE OBJECT lo_entry_provider
      EXPORTING
        io_request_entity = io_request_entity
        io_entity_set     = lo_target_entity_set
        iv_is_stream      = mv_is_stream
        iv_format         = iv_format.

* Don't get inlines if Changeset is required but not supported because of unexpected error
    IF mv_changeset = abap_false OR mo_batch_helper->mv_changeset_not_supported = abap_false.
      TRY.
          lo_entry_provider->get_inlines(
            IMPORTING
              ev_inlines      = lv_inlines
              ev_tech_inlines = lv_tech_inlines
          ).
          IF lv_inlines IS NOT INITIAL.
            ls_parameter-name  = /iwfnd/if_mgw_core_types=>gc_param_expand.
            ls_parameter-value = lv_inlines.
            INSERT ls_parameter INTO TABLE lt_parameter.
            ls_odata_request-technical_request-expand = lv_tech_inlines.

            " Save Inlines in case of Changeset Processing
            IF mv_changeset = abap_true.
              mo_batch_helper->changeset_set_attributes( iv_inlines = lv_inlines ).
            ENDIF.
          ENDIF.
        CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
          raise_technical_error( lx_tech_exception ).
      ENDTRY.
    ENDIF.

* Call Provider Delegator
    TRY.
        CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~create_entity
          EXPORTING
            iv_entity_name      = mv_target_entity
            iv_entity_set_name  = mv_target_entity_set
            iv_source_name      = mv_source_entity
            is_request_details  = ls_odata_request
            io_data_provider    = lo_entry_provider
            iv_content_format   = mv_content_format
            it_parameter        = lt_parameter
          CHANGING
            cr_entity           = lr_data
            ct_headers          = mt_entity_headers
            cv_is_target_format = lv_is_target_format
            cs_response_context = ls_response_context
            ct_inline_info      = lt_inline_info.

        IF ls_response_context-is_ral_relevant EQ abap_true.
          mo_transaction_handler->set_is_ral(  ).
        ENDIF.

      CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
        raise_business_error( lx_busi_exception ).

      CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
        raise_technical_error( lx_tech_exception ).
    ENDTRY.


* Batch - Changeset: Create a dummy provider and dummy keys, business data will be written later
    IF mv_changeset = abap_true.
      CREATE OBJECT eo_provider TYPE /iwfnd/cl_sodata_dummy_prov.
      lo_entity_type = lo_target_entity_set->get_entity_type( ).
      lo_entity_type->get_key_properties( IMPORTING et_key_property = lt_property ).
      LOOP AT lt_property INTO lo_property.
        lo_simple_type ?= lo_property->get_type( ).
        lv_key_value_default = lo_simple_type->default.
        INSERT lv_key_value_default INTO TABLE et_key.
      ENDLOOP.

* No Batch
    ELSE.
      ls_odata_request-parameters = lt_parameter.
      mo_post_processor->entity_set_create_entity(
        EXPORTING
          iv_odata_method    = mcs_odata_method-entity_set_create_entity
          io_entity_set        = lo_target_entity_set
          is_request_details   = ls_odata_request
          io_ds_context        = mo_context
          iv_format            = iv_format
          iv_target_entity     = mv_target_entity
          iv_service_name      = mv_service_name
          iv_service_version   = mv_service_version
          iv_namespace         = mv_namespace
          iv_base_url          = mv_base_url
          iv_source_entity_set = mv_source_entity_set
          iv_is_target_format  = lv_is_target_format
          iv_inlines           = lv_inlines
          io_mgw_context       = mo_mgw_context
          io_mdl_service       = mo_service
          ir_entity            = lr_data
          it_inline_info       = lt_inline_info
        IMPORTING
          eo_provider          = eo_provider
          et_key               = et_key
        CHANGING
          ct_headers           = mt_entity_headers
      ).
    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_set~create_entity


  METHOD /iwcor/if_ds_proc_entity_set~read.

    DATA: lv_operation_name         TYPE char30,
          lv_operation              TYPE char10,
          lv_is_target_format       TYPE abap_bool,
          ls_odata_request          TYPE /iwfnd/if_mgw_core_types=>request_s,
          lt_dollar_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lo_target_entity_set      TYPE REF TO /iwcor/if_ds_edm_entity_set,
          lt_inline_info            TYPE /iwfnd/if_mgw_core_runtime=>ty_t_inline_info,
          lr_data                   TYPE REF TO data,
          lr_deleted_data           TYPE REF TO data,
          lo_mgw_runtime            TYPE REF TO /iwfnd/bd_mgw_runtime,
          lx_busi_exception         TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception         TYPE REF TO /iwfnd/cx_mgw_tech_exception,
          ls_response_context       TYPE /iwfnd/if_mgw_core_types=>response_s,
          lv_is_co_deployment       TYPE abap_bool,
          lv_is_independent_read_op TYPE abap_bool,
          lv_perf_handle            TYPE i,
          ls_entity_header          TYPE ihttpnvp.
    DATA: lv_response_body       TYPE xstring.
    DATA: lo_binary_provider     TYPE REF TO /iwfnd/cl_sodata_json_provider.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing and not etag processing
    IF mo_batch_helper->mv_batch = abap_true AND mo_batch_helper->mv_etag_processing = abap_false.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_set_read ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-feed
      INTO lv_operation_name SEPARATED BY space.

    " initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = lv_operation_name
        iv_agent         = mc_agent
    ).

* With Function Import
    IF is_function_import_info IS NOT INITIAL.
      me->get_fi_data_by_read(
            EXPORTING
              io_entity_set           = io_entity_set
              is_function_import_info = is_function_import_info
              it_expand               = it_expand
              it_select               = it_select
              iv_format               = iv_format
              iv_for_ds_operation     = iv_for
            IMPORTING
              eo_target_entity_set    = lo_target_entity_set
            CHANGING
              cr_entityset            = lr_data
              ct_inline_info          = lt_inline_info
              cs_response_context     = ls_response_context
              cs_odata_request        = ls_odata_request ).

      IF ls_response_context-is_ral_relevant EQ abap_true.
        mo_transaction_handler->set_is_ral(  ).
      ENDIF.

* Without Function Import
    ELSE.

* Initialization
      me->init_request(
        EXPORTING
          iv_http_method       = /iwcor/if_rest_request=>gc_method_get
          iv_operation         = mcs_operations-read
          iv_operation_type    = mcs_operation_types-feed
          io_entity_set        = io_entity_set
          it_key               = it_key
          it_navigation_path   = it_navigation_path
          it_expand            = it_expand
          it_select            = it_select
          io_filter            = io_filter
          io_orderby           = io_orderby
          iv_skip              = iv_skip
          iv_top               = iv_top
          iv_format            = iv_format
          iv_for_ds_operation  = iv_for
          iv_skiptoken         = iv_skiptoken
          iv_inlinecount       = iv_inlinecount
        IMPORTING
          es_request           = ls_odata_request
          et_parameter         = lt_dollar_parameter
          eo_target_entity_set = lo_target_entity_set
      ).

      lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
      IF mv_batch EQ abap_true.
        lv_is_independent_read_op = mo_batch_helper->is_independent_read( ).
      ENDIF.

      IF lv_is_co_deployment EQ abap_true AND
         ( mv_batch EQ abap_false OR
          lv_is_independent_read_op EQ abap_true ).

        mo_co_deploym_proxy = mo_transaction_handler->get_co_deploym_proxy( ).

        TRY.
            mo_co_deploym_proxy->init(
              EXPORTING
                iv_service_document_name = mv_service_name
                iv_namespace             = mv_namespace
                iv_version               = mv_service_version
                iv_base_url              = mv_base_url
                iv_entity_name           = mv_target_entity
                io_context               = mo_mgw_context
                io_batch_helper          = mo_batch_helper ).

            mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~read_entityset(
               EXPORTING
                 iv_entity_name       = mv_target_entity
                 iv_entity_set_name   = mv_target_entity_set
                 iv_source_name       = mv_source_entity
                 is_request_details   = ls_odata_request
                 iv_content_format    = mv_content_format
                 it_parameter         = lt_dollar_parameter
               CHANGING
                 ct_headers           = mt_entity_headers
                 cr_entityset         = lr_data
                 cr_deleted_entityset = lr_deleted_data
                 cs_response_context  = ls_response_context
                 cv_is_target_format  = lv_is_target_format
                 ct_inline_info       = lt_inline_info
                 cv_response_body     = lv_response_body ).

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.
      ELSE.
* Get MGW Runtime
        lo_mgw_runtime = get_mgw_runtime(  ).

* Call Provider Delegator
        TRY.
            CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~read_entityset
              EXPORTING
                iv_entity_name       = mv_target_entity
                iv_entity_set_name   = mv_target_entity_set
                iv_source_name       = mv_source_entity
                is_request_details   = ls_odata_request
                iv_content_format    = mv_content_format
                it_parameter         = lt_dollar_parameter
              CHANGING
                ct_headers           = mt_entity_headers
                cr_entityset         = lr_data
                cr_deleted_entityset = lr_deleted_data
                cs_response_context  = ls_response_context
                cv_is_target_format  = lv_is_target_format
                ct_inline_info       = lt_inline_info.

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).
          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.
        "todo: place for static transformation
      ENDIF.

    ENDIF.

    set_cache_hit_info( ls_response_context ).

* Batch: Create a dummy provider, business data will be written later
    IF mo_batch_helper->mv_batch_all = abap_true AND mo_batch_helper->mv_etag_processing = abap_false.

      CREATE OBJECT ro_provider TYPE /iwfnd/cl_sodata_dummy_prov.

* No Batch
    ELSE.

      " If not modified, then send 304 response (Not Modified)
      IF ls_response_context-is_not_modified EQ /iwfnd/if_mgw_core_types=>gcs_modification_status-is_not_modified AND
         iv_for EQ /iwcor/if_ds_svc_proc=>gc_operation_read.
        RAISE EXCEPTION TYPE /iwcor/cx_ds_not_modified.
      ENDIF.

      ls_odata_request-parameters = lt_dollar_parameter.


      IF ( lv_is_target_format = abap_true AND lv_response_body IS NOT INITIAL ). "Response is already in target format
        " Create the Response Provider
        CREATE OBJECT lo_binary_provider.
        lo_binary_provider->set_binary_data( lv_response_body ).
        ro_provider = lo_binary_provider.

        "fill the response headers - needed as no post processing is done in this case
        LOOP AT mt_entity_headers INTO ls_entity_header.
          ro_provider->set_header( EXPORTING iv_name = ls_entity_header-name
                                             iv_value = ls_entity_header-value ).
        ENDLOOP.

        " @todo: this code is the same in  mo_post_processor->entity_set_read()
        IF NOT ls_response_context-crp_provider_request IS INITIAL.
          "@todo - could also be a class with static methods
          DATA lo_cr_handler      TYPE REF TO /iwfnd/cl_sodata_cr_handler.
          CREATE OBJECT lo_cr_handler.
          lo_cr_handler->write_request(
            EXPORTING
              is_odata_request  = ls_odata_request
              is_odata_response = ls_response_context ).
        ENDIF.

        " Set ST Metering Info
        set_st_metering_info(
          iv_num_entries   = ls_response_context-st_entityset_count
          iv_response_size = ls_response_context-st_response_size
          is_request_info  = ls_odata_request
          io_ds_context    = mo_context
        ).

      ELSEIF ( iv_format = /iwfnd/if_sodata_types=>gcs_formats-xlsx_format ). " Format xlsx has been requested
        me->read_entityset_post_pro_excel(
          EXPORTING
            is_request_info         = ls_odata_request
            ir_data                 = lr_data
          RECEIVING
            ro_provider             = ro_provider    " Entity Provider
        ).

      ELSE. "Default: Build response in format xml or json
        mo_post_processor->entity_set_read(
          EXPORTING
            iv_odata_method       = mcs_odata_method-entity_set_read
            io_entity_set         = lo_target_entity_set
            is_request_details    = ls_odata_request
            io_ds_context         = mo_context
            iv_format             = iv_format
            iv_for_ds_operation   = iv_for
            iv_target_entity      = mv_target_entity
            iv_service_name       = mv_service_name
            iv_service_version    = mv_service_version
            iv_namespace          = mv_namespace
            iv_base_url           = mv_base_url
            iv_source_entity_set  = mv_source_entity_set
            iv_is_target_format   = lv_is_target_format
            it_select             = it_select
            it_expand             = it_expand
            io_mgw_context        = mo_mgw_context
            io_mdl_service        = mo_service
            ir_entity_set         = lr_data
            ir_deleted_entity_set = lr_deleted_data
            is_response_context   = ls_response_context
            it_inline_info        = lt_inline_info
          IMPORTING
            eo_provider           = ro_provider
          CHANGING
            ct_headers            = mt_entity_headers
        ).

      ENDIF.

*---setting common http response headers
      set_common_response_headers(
        EXPORTING
          io_provider         = ro_provider
          is_response_context = ls_response_context ).

    ENDIF.

* Batch: Reset etag processing if set
    IF mo_batch_helper->mv_etag_processing = abap_true.
      mo_batch_helper->batch_set_etag_processing( abap_false ).
    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_set~read


  METHOD /iwcor/if_ds_proc_entity_sprop~read.

*-simple property can be handled by processing of complex property
    ro_provider = /iwcor/if_ds_proc_entity_cprop~read(
                    io_entity_set           = io_entity_set
                    is_function_import_info = is_function_import_info
                    it_key                  = it_key
                    it_property_path        = it_property_path
                    it_navigation_path      = it_navigation_path
                    iv_format               = iv_format
                    iv_for                  = iv_for
                  ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_sprop~read


  METHOD /iwcor/if_ds_proc_entity_vprop~read.

    DATA: lr_data             TYPE REF TO data,
          lv_operation_name   TYPE char30,
          lv_operation        TYPE char10,
          ls_odata_request    TYPE /iwfnd/if_mgw_core_types=>request_s,
          ls_response_context TYPE /iwfnd/if_mgw_core_types=>response_s.

    DATA: lv_perf_handle     TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method(
        iv_odata_method  = mcs_odata_method-entity_vprop_read
        it_property_path = it_property_path
      ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-property
      INTO lv_operation_name SEPARATED BY space.

*-initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = lv_operation_name
        iv_agent         = mc_agent
    ).

    get_entry_data(
      EXPORTING
        io_entity_set           = io_entity_set
        it_key                  = it_key
        is_function_import_info = is_function_import_info
        it_navigation_path      = it_navigation_path
        iv_format               = iv_format
        iv_for_ds_operation     = iv_for
      IMPORTING
        er_data                 = lr_data
        es_request_details      = ls_odata_request
        es_response_context     = ls_response_context ).

    IF ls_response_context-is_ral_relevant EQ abap_true.
      mo_transaction_handler->set_is_ral(  ).
    ENDIF.

* Batch: Create a dummy provider, business data will be written later
    IF mo_batch_helper->mv_batch_all = abap_true
    AND mo_batch_helper->mv_etag_processing = abap_false.
      CREATE OBJECT ro_provider TYPE /iwfnd/cl_sodata_dummy_prov.

* No Batch
    ELSE.
      mo_post_processor->entity_vprop_read(
        EXPORTING
          iv_odata_method      = mcs_odata_method-entity_vprop_read
          is_request_details   = ls_odata_request
          io_ds_context        = mo_context
          iv_format            = iv_format
          iv_for_ds_operation  = iv_for
          iv_source_entity_set = mv_source_entity_set
          it_property_path     = it_property_path
          io_mgw_context       = mo_mgw_context
          ir_entity            = lr_data
        IMPORTING
          eo_provider          = ro_provider
        CHANGING
         ct_headers            = mt_entity_headers
       ).

*---setting common http response headers
      set_common_response_headers(
        EXPORTING
          io_provider         = ro_provider
          is_response_context = ls_response_context ).

    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity_vprop~read


  METHOD /iwcor/if_ds_proc_entity~delete.

    DATA: lv_operation        TYPE char10,
          ls_odata_request    TYPE /iwfnd/if_mgw_core_types=>request_s,
          lo_mgw_runtime      TYPE REF TO /iwfnd/bd_mgw_runtime,
          lx_busi_exception   TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception   TYPE REF TO /iwfnd/cx_mgw_tech_exception,
          lt_parameter        TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lv_perf_handle      TYPE i,
          ls_response_context TYPE /iwfnd/if_mgw_core_runtime=>ty_s_mgw_response_context.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_delete ).
    ENDIF.

    " initialize logger header
    lv_operation  = mcs_operations-delete.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = mcs_operations-delete
                      iv_agent         = mc_agent
                    ).

* Initialization
    me->init_request(
      EXPORTING
        iv_http_method = /iwcor/if_rest_request=>gc_method_delete
        iv_operation   = mcs_operations-delete
        io_entity_set  = io_entity_set
        it_key         = it_key
      IMPORTING
        es_request     = ls_odata_request
        et_parameter   = lt_parameter
    ).

* Get MGW Runtime
    lo_mgw_runtime = get_mgw_runtime(  ).

* Call Provider Delegator
    TRY.
        CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~delete_entity
          EXPORTING
            iv_entity_name      = mv_target_entity
            iv_entity_set_name  = mv_target_entity_set
            iv_source_name      = mv_source_entity
            is_request_details  = ls_odata_request
            it_parameter        = lt_parameter
          CHANGING
            ct_headers          = mt_entity_headers
            cs_response_context = ls_response_context.

        IF ls_response_context-is_ral_relevant EQ abap_true.
          mo_transaction_handler->set_is_ral(  ).
        ENDIF.

      CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
        raise_business_error( lx_busi_exception ).

      CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
        raise_technical_error( lx_tech_exception ).

    ENDTRY.


* Post Processing for no batch
    IF mv_changeset = abap_false.
      mo_post_processor->entity_delete(
        EXPORTING
          iv_odata_method    = mcs_odata_method-entity_delete
          is_request_details = ls_odata_request
          io_ds_context      = mo_context
          iv_target_entity   = mv_target_entity
        CHANGING
          ct_headers         = mt_entity_headers
      ).
    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type    = /iwfnd/cl_logger=>success
      iv_msg_number  = mcs_messages-operation_finished
      iv_msg_id      = mc_message_class
      iv_agent       = mc_agent
      iv_msg_handle  = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity~delete


  METHOD /iwcor/if_ds_proc_entity~exists.

    DATA: lr_data             TYPE REF TO data,
          lv_operation_name   TYPE char30,
          lv_operation        TYPE char10,
          ls_odata_request    TYPE /iwfnd/if_mgw_core_types=>request_s,
          ls_response_context TYPE /iwfnd/if_mgw_core_types=>response_s.

    DATA: lv_perf_handle     TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_exists ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-count
      INTO lv_operation_name SEPARATED BY space.

*-initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = lv_operation_name
        iv_agent         = mc_agent
    ).

*-suppress business exception
    get_entry_data(
      EXPORTING
        io_entity_set           = io_entity_set
        it_key                  = it_key
        is_function_import_info = is_function_import_info
        it_navigation_path      = it_navigation_path
        io_filter               = io_filter
      IMPORTING
        er_data                 = lr_data
        es_request_details      = ls_odata_request
        es_response_context     = ls_response_context ).

    IF ls_response_context-is_ral_relevant EQ abap_true.
      mo_transaction_handler->set_is_ral(  ).
    ENDIF.

* Batch: Set RV_EXISTS temporarily to "abap_true", right value later
    IF mo_batch_helper->mv_batch_all = abap_true
    AND mo_batch_helper->mv_etag_processing = abap_false.
      rv_exists = abap_true.

* No Batch
    ELSE.
      mo_post_processor->entity_link_exists(
        EXPORTING
          iv_odata_method    = mcs_odata_method-entity_exists
          is_request_details = ls_odata_request
          io_ds_context      = mo_context
          ir_entity          = lr_data
        IMPORTING
          ev_exists          = rv_exists
        CHANGING
          ct_headers         = mt_entity_headers
      ).
    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity~exists


  METHOD /iwcor/if_ds_proc_entity~read.

    DATA: lv_operation_name         TYPE char30,
          lv_operation              TYPE char10,
          lv_is_target_format       TYPE abap_bool,
          ls_odata_request          TYPE /iwfnd/if_mgw_core_types=>request_s,
          lt_dollar_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lo_target_entity_set      TYPE REF TO /iwcor/if_ds_edm_entity_set,
          lt_expand_skiptoken       TYPE string_table,
          lt_inline_info            TYPE /iwfnd/if_mgw_core_runtime=>ty_t_inline_info,
          lr_data                   TYPE REF TO data,
          lo_mgw_runtime            TYPE REF TO /iwfnd/bd_mgw_runtime,
          ls_response_context       TYPE /iwfnd/if_mgw_core_types=>response_s,
          lv_is_co_deployment       TYPE abap_bool,
          lv_is_independent_read_op TYPE abap_bool,
          lx_busi_exception         TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception         TYPE REF TO /iwfnd/cx_mgw_tech_exception,
          lv_perf_handle            TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing and not etag processing
    IF mo_batch_helper->mv_batch = abap_true AND mo_batch_helper->mv_etag_processing = abap_false.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_read ).
    ENDIF.

    CONCATENATE mcs_operations-read mcs_operation_types-entry
      INTO lv_operation_name SEPARATED BY space.

    " initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = lv_operation_name
                      iv_agent         = mc_agent
                    ).

* With Function Import
    IF is_function_import_info IS NOT INITIAL.
      me->get_fi_data_by_read(
            EXPORTING
              io_entity_set           = io_entity_set
              is_function_import_info = is_function_import_info
              it_expand               = it_expand
              iv_format               = iv_format
              iv_for_ds_operation     = iv_for
            IMPORTING
              eo_target_entity_set    = lo_target_entity_set
            CHANGING
              cr_entityset            = lr_data
              ct_inline_info          = lt_inline_info
              cs_odata_request        = ls_odata_request
          ).

* Without Function Import
    ELSE.

* Initialization
      me->init_request(
        EXPORTING
          iv_http_method       = /iwcor/if_rest_request=>gc_method_get
          iv_operation         = mcs_operations-read
          iv_operation_type    = mcs_operation_types-entry
          io_entity_set        = io_entity_set
          it_key               = it_key
          it_navigation_path   = it_navigation_path
          it_expand            = it_expand
          it_select            = it_select
          io_filter            = io_filter
          iv_format            = iv_format
          iv_for_ds_operation  = iv_for
        IMPORTING
          es_request           = ls_odata_request
          et_parameter         = lt_dollar_parameter
          eo_target_entity_set = lo_target_entity_set
      ).

      lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
      IF mv_batch EQ abap_true.
        lv_is_independent_read_op = mo_batch_helper->is_independent_read( ).
      ENDIF.

      IF lv_is_co_deployment EQ abap_true AND
               ( mv_batch EQ abap_false OR
                lv_is_independent_read_op EQ abap_true ).

        mo_co_deploym_proxy = mo_transaction_handler->get_co_deploym_proxy( ).

        TRY.

            mo_co_deploym_proxy->init(
              EXPORTING
                iv_service_document_name = mv_service_name
                iv_namespace             = mv_namespace
                iv_version               = mv_service_version
                iv_base_url              = mv_base_url
                iv_entity_name           = mv_target_entity
                io_context               = mo_mgw_context
                io_batch_helper          = mo_batch_helper ).

            mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~read_entity(
               EXPORTING
                 iv_entity_name       = mv_target_entity
                 iv_entity_set_name   = mv_target_entity_set
                 iv_source_name       = mv_source_entity
                 is_request_details   = ls_odata_request
                 iv_content_format    = mv_content_format
                 it_parameter         = lt_dollar_parameter
               CHANGING
                 ct_headers           = mt_entity_headers
                 cr_entity            = lr_data
                 cs_response_context  = ls_response_context
                 cv_is_target_format  = lv_is_target_format
                 ct_expand_skiptoken  = lt_expand_skiptoken
                 ct_inline_info       = lt_inline_info ).

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.
      ELSE.

* Get MGW Runtime
        lo_mgw_runtime = get_mgw_runtime(  ).

* Call Provider Delegator
        TRY.
            CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~read_entity
              EXPORTING
                iv_entity_name      = mv_target_entity
                iv_entity_set_name  = mv_target_entity_set
                iv_source_name      = mv_source_entity
                is_request_details  = ls_odata_request
                iv_content_format   = mv_content_format
                it_parameter        = lt_dollar_parameter
              CHANGING
                ct_headers          = mt_entity_headers
                cr_entity           = lr_data
                cs_response_context = ls_response_context
                cv_is_target_format = lv_is_target_format
                ct_expand_skiptoken = lt_expand_skiptoken
                ct_inline_info      = lt_inline_info.

            IF ls_response_context-is_ral_relevant EQ abap_true.
              mo_transaction_handler->set_is_ral(  ).
            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.
      ENDIF.
    ENDIF.


* Batch: Create a dummy provider, business data will be written later
* Or used for navigation with multiplicity 0..1  as no content was provided
    IF ( mo_batch_helper->mv_batch_all = abap_true AND mo_batch_helper->mv_etag_processing = abap_false )
    OR ( ls_response_context-no_content = abap_true ).
      CREATE OBJECT ro_provider TYPE /iwfnd/cl_sodata_dummy_prov.

* No Batch
    ELSE.

      " If not modified, then send 304 response (Not Modified)
      IF ls_response_context-is_not_modified EQ /iwfnd/if_mgw_core_types=>gcs_modification_status-is_not_modified AND
         iv_for EQ /iwcor/if_ds_svc_proc=>gc_operation_read.
        RAISE EXCEPTION TYPE /iwcor/cx_ds_not_modified.
      ENDIF.


      ls_odata_request-parameters = lt_dollar_parameter.
      mo_post_processor->entity_read(
        EXPORTING
          iv_odata_method       = mcs_odata_method-entity_read
          io_entity_set         = lo_target_entity_set
          is_request_details    = ls_odata_request
          io_ds_context         = mo_context
          iv_format             = iv_format
          iv_for_ds_operation   = iv_for
          iv_service_name       = mv_service_name
          iv_service_version    = mv_service_version
          iv_namespace          = mv_namespace
          iv_base_url           = mv_base_url
          iv_source_entity_set  = mv_source_entity_set
          iv_is_target_format   = lv_is_target_format
          it_select             = it_select
          it_expand             = it_expand
          io_mgw_context        = mo_mgw_context
          io_mdl_service        = mo_service
          ir_entity             = lr_data
          it_expand_skiptoken   = lt_expand_skiptoken
          it_inline_info        = lt_inline_info
        IMPORTING
          eo_provider           = ro_provider
        CHANGING
          ct_headers            = mt_entity_headers
      ).

*---setting common http response headers
      set_common_response_headers(
        EXPORTING
          io_provider         = ro_provider
          is_response_context = ls_response_context ).

    ENDIF.

* Batch: Reset etag processing if set
    IF mo_batch_helper->mv_etag_processing = abap_true.
      mo_batch_helper->batch_set_etag_processing( abap_false ).
    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity~read


  METHOD /iwcor/if_ds_proc_entity~update.

    DATA: lv_http_method      TYPE string,
          lv_operation        TYPE char10,
          lv_is_target_format TYPE abap_bool,
          ls_odata_request    TYPE /iwfnd/if_mgw_core_types=>request_s,
          lr_data             TYPE REF TO data,
          lo_mgw_runtime      TYPE REF TO /iwfnd/bd_mgw_runtime,
          lx_busi_exception   TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception   TYPE REF TO /iwfnd/cx_mgw_tech_exception.
    DATA: lt_parameter          TYPE /iwfnd/if_mgw_core_types=>parameter_values_t.
    DATA: lo_entry_provider     TYPE REF TO /iwfnd/cl_sodata_entry_prov.
    DATA: lv_perf_handle      TYPE i,
          ls_response_context TYPE /iwfnd/if_mgw_core_types=>response_s.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-entity_update ).
    ENDIF.

    " initialize logger header
    lv_operation  = mcs_operations-update.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = mcs_operations-update
                      iv_agent         = mc_agent
                    ).

* Initialization
    IF iv_merge_semantics = abap_false.
      lv_http_method = /iwcor/if_rest_request=>gc_method_put.
    ELSE.
      lv_http_method = /iwcor/if_rest_request=>gc_method_patch.
    ENDIF.

    me->init_request(
      EXPORTING
        iv_http_method  = lv_http_method
        iv_operation    = mcs_operations-update
        io_entity_set   = io_entity_set
        it_key          = it_key
        io_filter       = io_filter
      IMPORTING
        es_request    = ls_odata_request
        et_parameter  = lt_parameter
    ).

* Get MGW Runtime
    lo_mgw_runtime = get_mgw_runtime(  ).

    CREATE OBJECT lo_entry_provider
      EXPORTING
        io_request_entity = io_request_entity
        io_entity_set     = io_entity_set
        iv_is_stream      = mv_is_stream.

* Call Provider Delegator
    TRY.
        IF iv_merge_semantics EQ abap_true.
          lo_entry_provider->get_components(
            IMPORTING
              et_components   = ls_odata_request-technical_request-components " components contained in partial update
              et_components_h = ls_odata_request-technical_request-components_h ).

          IF ls_odata_request-technical_request-components IS INITIAL.
            RAISE EXCEPTION TYPE /iwfnd/cx_mgw_tech_exception
              EXPORTING
                textid           = /iwfnd/cx_mgw_tech_exception=>empty_patch_not_supported
                http_status_code = /iwfnd/cx_mgw_tech_exception=>gc_status_not_implemented.
          ENDIF.
        ENDIF.

        CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~update_entity
          EXPORTING
            iv_entity_name      = mv_target_entity
            iv_entity_set_name  = mv_target_entity_set
            iv_source_name      = mv_source_entity
            is_request_details  = ls_odata_request
            io_data_provider    = lo_entry_provider
            iv_content_format   = mv_content_format
            it_parameter        = lt_parameter
          CHANGING
            cr_entity           = lr_data
            ct_headers          = mt_entity_headers
            cs_response_context = ls_response_context
            cv_is_target_format = lv_is_target_format.

        IF ls_response_context-is_ral_relevant EQ abap_true.
          mo_transaction_handler->set_is_ral(  ).
        ENDIF.

      CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
        raise_business_error( lx_busi_exception ).

      CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
        raise_technical_error( lx_tech_exception ).
    ENDTRY.




* Post Processing for no batch
    IF mv_changeset = abap_false.
      mo_post_processor->entity_update(
        EXPORTING
          iv_odata_method      = mcs_odata_method-entity_update
          io_entity_set        = io_entity_set
          is_request_details   = ls_odata_request
          io_ds_context        = mo_context
          iv_source_entity_set = mv_source_entity_set
          iv_is_target_format  = lv_is_target_format
          io_mgw_context       = mo_mgw_context
          ir_entity            = lr_data
        CHANGING
          ct_headers           = mt_entity_headers

      ).
    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_entity~update


  METHOD /iwcor/if_ds_proc_func_import~execute.

    DATA: lv_http_method            TYPE string VALUE /iwcor/if_rest_request=>gc_method_get,
          lv_http_method_fi         TYPE string,
          lv_operation              TYPE char10,
          lv_action_name            TYPE string,
          lv_is_target_format       TYPE abap_bool,
          ls_odata_request          TYPE /iwfnd/if_mgw_core_types=>request_s,
          ls_action_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_s,
          lt_action_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lr_parameter_value        TYPE REF TO /iwcor/if_ds_uri=>parameter_value_s,
          lv_return_type            TYPE string,
          lt_expand_skiptoken       TYPE string_table,
          lt_inline_info            TYPE /iwfnd/if_mgw_core_runtime=>ty_t_inline_info,
          lr_data                   TYPE REF TO data,
          lo_mgw_runtime            TYPE REF TO /iwfnd/bd_mgw_runtime,
          lv_is_co_deployment       TYPE abap_bool,
          lv_is_independent_read_op TYPE abap_bool,
          lo_edm_return_type        TYPE REF TO /iwcor/if_ds_edm_typed,
          lo_edm_type               TYPE REF TO /iwcor/if_ds_edm_type,
          lo_entity_set             TYPE REF TO /iwcor/if_ds_edm_entity_set,
          lv_last_mod_formatted     TYPE string,
          lv_last_modified          TYPE tzntstmps,
          lt_parameters             TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lv_lib_type_kind          TYPE /iwcor/if_ds_edm_type=>type_kind,
          lv_sodata_multipli        TYPE /iwcor/if_ds_edm=>edm_multiplicity,
          lv_multiplicity           TYPE /iwfnd/med_mdl_cardinality,
          lx_busi_exception         TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception         TYPE REF TO /iwfnd/cx_mgw_tech_exception,
          lv_perf_handle            TYPE i,
          ls_response_context       TYPE /iwfnd/if_mgw_core_types=>response_s.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

* Set Odata Method if batch processing
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->batch_set_odata_method( mcs_odata_method-func_import_execute ).
    ENDIF.

    " initialize logger header
    lv_operation  = mcs_operations-operation.
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
        iv_msg_number    = mcs_messages-operation_start
        iv_msg_id        = mc_message_class
        iv_msg_v1        = mcs_operations-operation
        iv_agent         = mc_agent
    ).

* Initialization
    lv_action_name = io_function_import->get_name( ).
    lo_entity_set  = io_function_import->get_entity_set( ).

* Return Type
    lo_edm_return_type = io_function_import->get_return_type( ).
    lo_edm_type        = lo_edm_return_type->get_type( ).
    lv_return_type     = lo_edm_type->/iwcor/if_ds_edm_named~get_name( ).

    lv_lib_type_kind  = lo_edm_type->kind.
    IF lv_lib_type_kind = /iwcor/if_ds_edm_type=>kind_entity. "The new name mapping currently only exists for entity types
      ls_odata_request-technical_request-action_return_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_edm_type ).
    ELSE.
      ls_odata_request-technical_request-action_return_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name( lo_edm_type ).
    ENDIF.


* Multiplicity
    lv_sodata_multipli = lo_edm_return_type->get_multiplicity( ).
    CASE lv_sodata_multipli.
      WHEN /iwcor/if_ds_edm=>gc_multiplicity_zero_one.
        lv_multiplicity = mcs_cardinality-cardinality_0_1.
      WHEN /iwcor/if_ds_edm=>gc_multiplicity_one.
        lv_multiplicity = mcs_cardinality-cardinality_1.
      WHEN /iwcor/if_ds_edm=>gc_multiplicity_many.
        lv_multiplicity = mcs_cardinality-cardinality_0_n.
    ENDCASE.

    lv_http_method_fi = io_function_import->get_http_method( ).
    IF lv_http_method_fi IS NOT INITIAL.
      lv_http_method = lv_http_method_fi.
    ENDIF.

    me->init_request(
      EXPORTING
        iv_http_method      = lv_http_method
        iv_operation        = mcs_operations-operation
        io_function_import  = io_function_import
        io_entity_set       = lo_entity_set
        iv_format           = iv_format
        iv_edm_multiplicity = lv_sodata_multipli
        iv_mdl_cardinality  = lv_multiplicity
      IMPORTING
        es_request          = ls_odata_request
        et_parameter        = lt_parameters
    ).

* Function Import Parameters
    LOOP AT it_parameter_value REFERENCE INTO lr_parameter_value.
      ls_action_parameter-name  = lr_parameter_value->parameter->get_name( ).
      ls_action_parameter-value = me->convert_value_by_parameter( lr_parameter_value ).
      APPEND ls_action_parameter TO lt_action_parameter.
      ls_action_parameter-name = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lr_parameter_value->parameter ).
      APPEND ls_action_parameter TO ls_odata_request-technical_request-action_parameters.
    ENDLOOP.

* Function Import External Names
    ls_odata_request-function-name = lv_action_name.
    ls_odata_request-function-return_type = lv_return_type.
    IF lo_entity_set IS BOUND.
      ls_odata_request-function-entity_set = lo_entity_set->get_name( ).
    ENDIF.
    ls_odata_request-function-http_method = io_function_import->get_http_method( ).

* "Normal" Parameters, e.g. request paramters
* For all other operations these are transferred via it_parameter, but that one is being misused here for the action parameters
    ls_odata_request-technical_request-parameters = lt_parameters.


    lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
    IF mv_batch EQ abap_true.
      lv_is_independent_read_op = mo_batch_helper->is_independent_read( ).
    ENDIF.

    IF lv_is_co_deployment EQ abap_true AND
                   ( mv_batch EQ abap_false OR
                    lv_is_independent_read_op EQ abap_true ).

      mo_co_deploym_proxy = mo_transaction_handler->get_co_deploym_proxy( ).

      TRY.

          mo_co_deploym_proxy->init(
            EXPORTING
              iv_service_document_name = mv_service_name
              iv_namespace             = mv_namespace
              iv_version               = mv_service_version
              iv_base_url              = mv_base_url
              iv_entity_name           = mv_target_entity
              iv_function_name         = lv_action_name
              io_context               = mo_mgw_context
              io_batch_helper          = mo_batch_helper ).

          mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~exec_service_operation(
             EXPORTING
               iv_name             = lv_action_name
               iv_return_type      = lv_return_type
               iv_multiplicity     = lv_multiplicity
               is_request_details  = ls_odata_request
               iv_content_format   = mv_content_format
               it_parameter        = lt_action_parameter
             CHANGING
               ct_headers          = mt_entity_headers
               cr_data             = lr_data
               cv_is_target_format = lv_is_target_format
               ct_expand_skiptoken = lt_expand_skiptoken
               cs_response_context = ls_response_context
               ct_inline_info      = lt_inline_info ).

        CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
          raise_business_error( lx_busi_exception ).

        CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
          raise_technical_error( lx_tech_exception ).
      ENDTRY.


    ELSE.
* Get MGW Runtime
      lo_mgw_runtime = get_mgw_runtime( lv_action_name ).
* Call Provider Delegator
      TRY.
          CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~exec_service_operation
            EXPORTING
              iv_name             = lv_action_name
              iv_return_type      = lv_return_type
              iv_multiplicity     = lv_multiplicity    " Single-Character Flag
              iv_content_format   = mv_content_format
              it_parameter        = lt_action_parameter
              is_request_details  = ls_odata_request
            CHANGING
              cr_data             = lr_data
              ct_headers          = mt_entity_headers
              cv_is_target_format = lv_is_target_format
              ct_expand_skiptoken = lt_expand_skiptoken
              cs_response_context = ls_response_context
              ct_inline_info      = lt_inline_info.

        CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
          raise_business_error( lx_busi_exception ).

        CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
          raise_technical_error( lx_tech_exception ).
      ENDTRY.
    ENDIF.


* Batch - Changeset: Create a dummy provider, business data will be written later
    IF mo_batch_helper->mv_batch_all = abap_true
      OR   mv_changeset = abap_true.

      CREATE OBJECT ro_provider TYPE /iwfnd/cl_sodata_dummy_prov.

* No Batch
    ELSE.
      ls_odata_request-parameters = lt_parameters.
      mo_post_processor->func_import_execute(
        EXPORTING
          iv_odata_method      = mcs_odata_method-func_import_execute
          io_function_import   = io_function_import
          io_entity_set        = lo_entity_set
          is_request_details   = ls_odata_request
          iv_edm_multiplicity  = lv_sodata_multipli
          iv_mdl_cardinality   = lv_multiplicity
          iv_content_format    = mv_content_format
          io_ds_context        = mo_context
          iv_format            = iv_format
          iv_service_name      = mv_service_name
          iv_service_version   = mv_service_version
          iv_namespace         = mv_namespace
          iv_base_url          = mv_base_url
          iv_source_entity_set = mv_source_entity_set
          iv_is_target_format  = lv_is_target_format
          io_mgw_context       = mo_mgw_context
          io_mdl_service       = mo_service
          ir_entity            = lr_data
          it_inline_info       = lt_inline_info
          it_expand_skiptoken  = lt_expand_skiptoken
        IMPORTING
          eo_provider          = ro_provider
        CHANGING
          ct_headers           = mt_entity_headers
      ).

*---setting http header x_sap_last_modified (only in case of GET - batch processing is assumed as PUT)
      IF io_function_import->get_http_method( ) = 'GET'.

        mo_service->get_last_modified( IMPORTING
                                          ev_timestamp = lv_last_modified ).

        lv_last_mod_formatted = /iwcor/cl_rest_http_utils=>format_http_date( lv_last_modified ).
        ro_provider->set_header(
          EXPORTING
            iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-x_sap_last_modified
            iv_value = lv_last_mod_formatted ).
      ENDIF.

    ENDIF.

* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_func_import~execute


  METHOD /iwcor/if_ds_proc_metadata~read.

    DATA:
      lo_ds_service               TYPE REF TO /iwcor/if_ds_svc,
      lo_edm_impl_base            TYPE REF TO /iwcor/cl_ds_edm_impl_base,
      lo_edm_provider             TYPE REF TO /iwcor/if_ds_edm_provider,
      lo_edm_provider_gw          TYPE REF TO /iwfnd/cl_sodata_edm_provider,
      lo_med_mdl_access           TYPE REF TO /iwfnd/cx_med_mdl_access,
      lo_metadata_prov            TYPE REF TO /iwcor/cl_ds_ext_ep_metadata,
      lo_move_cast_error          TYPE REF TO cx_sy_move_cast_error,
      lo_service_metadata         TYPE REF TO /iwfnd/if_med_mdl_service_grp,
      lo_transaction_handler      TYPE REF TO /iwfnd/cl_transaction_handler,
      lr_conditions               TYPE REF TO /iwcor/if_ds_cntxt=>conditional_info_s,
      lr_namespace                TYPE REF TO /iwfnd/if_med_mdl_service_grp=>ty_s_med_mdl_namespace,
      ls_metadata                 TYPE /iwcor/cl_ds_ep_metadata=>metadata_s,
      ls_metering                 TYPE /iwfnd/s_metering_col,
      ls_namespace_predeclaration TYPE /iwcor/if_atom_types=>namespace_s,
      lt_namespace_predeclaration TYPE /iwcor/if_atom_types=>namespace_t,
      lt_namespaces               TYPE /iwfnd/if_med_mdl_service_grp=>ty_t_med_mdl_namespaces,
      lt_schema_vocab_annotations TYPE /iwcor/if_ds_ext_types=>schema_annotations_t,
      lt_schema_vocab_terms       TYPE /iwcor/if_ds_ext_types=>schema_term_t,
      lt_vocab_references         TYPE /iwcor/if_ds_ext_types=>reference_t,
      lv_base_url                 TYPE string,
      lv_is_cache_active          TYPE abap_bool,
      lv_is_ct_cache_header_set   TYPE abap_bool,
      lv_last_modified            TYPE tzntstmps,
      lv_last_modified_formatted  TYPE string,
      lv_operation                TYPE char10,
      lv_operation_name           TYPE char30,
      lv_perf_handle              TYPE i.


    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

    CONCATENATE mcs_operations-read mcs_operation_types-metadata INTO lv_operation_name SEPARATED BY space.

    " initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = lv_operation_name
                      iv_agent         = mc_agent
                    ).


*   For the conditional check we do not create the complete answer (the $metadata document
*   but only set the last_modified header.
*   Lib will compare and call this method again in case the answer has chanbed
*   Otherwise a 304 (not modified) is returned after the first time this method was called
    lr_conditions = mo_context->get_conditional_info( ).
    IF lr_conditions IS BOUND. "Conditional GET (i.e. if-modified-since)
      lr_conditions->incomplete = abap_true. "We do not return the data but only the last modified

      CREATE OBJECT ro_provider TYPE /iwcor/cl_ds_ep_conditional.
      mo_service->get_last_modified(
        IMPORTING
          ev_timestamp = lv_last_modified ).
      lv_last_modified_formatted = /iwcor/cl_rest_http_utils=>format_http_date( lv_last_modified ).
      ro_provider->set_header(
        EXPORTING
          iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-last_modified
          iv_value =  lv_last_modified_formatted ).
      mv_conditional_read_processed = abap_true.

*   Enforce the last_modfied cache handshake between client and GW - new with SP14
*  *---set Max Age to 0
      lv_is_cache_active = /iwfnd/cl_med_mdl_cache_persis=>is_caching_enabled( ).
      IF ( lv_is_cache_active = abap_true ).
        ro_provider->set_header(
          EXPORTING
            iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-cache_control
            iv_value = 'max-age=0' "Max Age in seconds
        ).
      ENDIF.

    ELSE. "GET without condition
      IF mv_conditional_read_processed EQ abap_true.
        "Former conditional read failed - metadata has to be loaded again as currently only the timestamp was loaded
        mo_transaction_handler->set_metadata_access_info(
          iv_is_busi_data_request    = abap_false
          iv_load_last_modified_only = abap_false
          iv_do_cache_handshake      = abap_true
        ).

        TRY.
            lo_service_metadata = mo_transaction_handler->get_service_group_metadata( ).

          CATCH /iwfnd/cx_med_mdl_access INTO lo_med_mdl_access.
            RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error
              EXPORTING
                previous = lo_med_mdl_access.
        ENDTRY.

        lv_base_url = mo_transaction_handler->get_base_url( ).
        lo_edm_provider = /iwfnd/cl_sodata_edm_provider=>get_edm_provider(
          io_service  = lo_service_metadata
          iv_base_url = lv_base_url ).

        lo_ds_service = mo_context->get_service( ).

      ELSE.
        lo_ds_service = mo_context->get_service( ).
        lo_edm_impl_base ?= lo_ds_service->get_entity_data_model( ).
        lo_edm_provider = lo_edm_impl_base->get_provider( ).

      ENDIF.

      TRY.
          " configure EDM provider for metadata document
          lo_edm_provider_gw ?= lo_edm_provider.
          lo_edm_provider_gw->m_hide_nullable_default_value = abap_true.
          lo_edm_provider_gw->m_hide_stream_default_value   = abap_true.

          mo_context->get_parameter(
            EXPORTING
              iv_name  = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-value_list
            IMPORTING
              ev_value = lo_edm_provider_gw->m_value_list ).
          IF lo_edm_provider_gw->m_value_list CS '%'.
            lo_edm_provider_gw->m_value_list = cl_http_utility=>unescape_url( lo_edm_provider_gw->m_value_list ).
          ENDIF.


        CATCH cx_sy_move_cast_error INTO lo_move_cast_error.
          RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error
            EXPORTING
              previous = lo_move_cast_error.
      ENDTRY.

      lo_edm_provider->get_schemas(
            IMPORTING et_schema = ls_metadata-schemas ).
      lo_edm_provider_gw->get_schema_vocab_annotations(
            IMPORTING et_schema_vocab_annotations = lt_schema_vocab_annotations ).
      lo_edm_provider_gw->get_schema_vocab_terms(
            IMPORTING et_schema_vocab_terms = lt_schema_vocab_terms ).
      lo_edm_provider_gw->get_vocab_references(
            IMPORTING et_vocab_references = lt_vocab_references ).

*---get namespaces from of service
      lt_namespaces = mo_service->get_namespaces( ).

      LOOP AT lt_namespaces REFERENCE INTO lr_namespace.
        ls_namespace_predeclaration-prefix    = lr_namespace->*-namespace.
        ls_namespace_predeclaration-namespace = lr_namespace->*-namespace_name.
        INSERT ls_namespace_predeclaration INTO TABLE lt_namespace_predeclaration.
      ENDLOOP.

      CREATE OBJECT lo_metadata_prov
        EXPORTING
          it_namespace_predeclaration = lt_namespace_predeclaration.


      ls_metadata-data_service_version = lo_ds_service->get_version( ).
      lo_metadata_prov->set_metadata( ls_metadata ).
      lo_metadata_prov->set_annotations( lt_schema_vocab_annotations ).
      lo_metadata_prov->set_terms( lt_schema_vocab_terms ).
      lo_metadata_prov->set_references( lt_vocab_references ).


      mo_service->get_last_modified(
        IMPORTING
          ev_timestamp = lv_last_modified
      ).

*---setting http header last modified
      lv_last_modified_formatted = /iwcor/cl_rest_http_utils=>format_http_date( lv_last_modified ).
      lo_metadata_prov->set_entity_header(
        EXPORTING
          iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-last_modified
          iv_value =  lv_last_modified_formatted
      ).


      ro_provider = lo_metadata_prov.


*---Cache header
      IF /iwfnd/cl_med_mdl_cache_persis=>is_caching_enabled( ).
        lv_is_ct_cache_header_set = set_cache_ctrl_for_ctxt_token(
            io_lib_provider               = ro_provider
            iv_is_resource_lang_dependent = abap_true ).

        IF lv_is_ct_cache_header_set = abap_false.
          lo_metadata_prov->set_entity_header(
              iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-cache_control
              iv_value = 'max-age=0' ). "Max Age in seconds
        ENDIF.
      ENDIF.


* Metering
      ls_metering-access_type = /iwfnd/cl_sodata_processor=>mc_consumer_type.

      ls_metering-operation = /iwfnd/cl_sodata_processor=>mcs_operations-metadata.
      IF mo_batch_helper->mv_batch = abap_true.
        CONCATENATE /iwfnd/cl_sodata_processor=>mcs_operations-batch
                    ls_metering-operation
          INTO ls_metering-operation SEPARATED BY '-'.
      ENDIF.

      ls_metering-odc_used    = abap_true.
      ls_metering-format_type = 'xml'.                      "#EC NOTEXT
      ls_metering-num_entries = 1.
      mo_context->set_parameter(
        iv_name  = /iwfnd/cl_metering_api=>gc_metering
        iv_value = ls_metering
      ).
    ENDIF. "GET without condition


    lo_transaction_handler = /iwfnd/cl_transaction_handler=>get_transaction_handler( ).
    IF  ( lo_transaction_handler->get_extend_of_metadata( ) = /iwfnd/if_mgw_core_types=>gcs_verbose_metadata-all_as_markdown )
    AND ( /iwfnd/cl_mgw_util_versions=>get_gwbep_version( ) < '019' ).
      /iwfnd/cl_mgw_msg_container=>get_mgw_msg_container( )->add_message_text_only(
        EXPORTING
          iv_msg_type               = /iwfnd/if_mgw_msg_container=>gcs_message_type-warning
          iv_msg_text               = 'Markdown can not be provided on backend side'
          iv_message_creator        = 'Processor'
          iv_entity_type            = '$metadata'
          iv_system_alias           = lo_transaction_handler->get_system_alias( )
          iv_is_relevant_for_client = abap_true ).
    ENDIF.

*---Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_metadata~read


  METHOD /iwcor/if_ds_proc_service~read.

    DATA:
      ld_service            TYPE REF TO /iwcor/if_app_types=>service_s,
      ls_workspace          TYPE /iwcor/if_app_types=>workspace_s,
      ls_collection         TYPE /iwcor/if_app_types=>collection_s,
      lv_entity_set         TYPE string,
      ld_entity_set         TYPE REF TO /iwcor/if_ds_edm_provider=>entity_set_s,
      ld_entity_container   TYPE REF TO /iwcor/if_ds_edm_provider=>entity_container_s,
      lv_entity_container   TYPE string,
      ld_schemas            TYPE REF TO /iwcor/if_ds_edm_provider=>schema_t,
      lt_schema             TYPE /iwcor/if_ds_edm_provider=>schema_t,
      ld_schema             TYPE REF TO /iwcor/if_ds_edm_provider=>schema_s,
      lo_service_prov       TYPE REF TO /iwcor/cl_app_service_prov,
      lv_last_mod_formatted TYPE string,
      lv_last_modified      TYPE tzntstmps,
      lo_edm_impl_base      TYPE REF TO /iwcor/cl_ds_edm_impl_base,
      lo_edm_provider       TYPE REF TO /iwfnd/cl_sodata_edm_provider,
      lo_move_cast_error    TYPE REF TO cx_sy_move_cast_error,
      lt_namespaces         TYPE /iwfnd/if_med_mdl_service_grp=>ty_t_med_mdl_namespaces,
      lr_namespace          TYPE REF TO /iwfnd/if_med_mdl_service_grp=>ty_s_med_mdl_namespace,
      lt_ns                 TYPE /iwcor/if_atom_types=>namespace_t,
      ls_ns                 TYPE /iwcor/if_atom_types=>namespace_s,
      lv_language           TYPE laiso,
      lo_entity_set         TYPE REF TO /iwfnd/if_med_mdl_reference,
      lt_annotations        TYPE /iwfnd/t_med_mdl_annotations,
      lv_operation_name     TYPE char30,
      lv_operation          TYPE char10,
      lo_ds_service         TYPE REF TO /iwcor/if_ds_svc,
      lv_service_root       TYPE string,
      lv_perf_handle        TYPE i,
      ls_metering           TYPE /iwfnd/s_metering_col.

    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

    CONCATENATE mcs_operations-read mcs_operation_types-service INTO lv_operation_name SEPARATED BY space.

    " initialize logger header
    lv_operation  = mcs_operations-read.
    mo_logger->set_header_operation( lv_operation ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = lv_operation_name
                      iv_agent         = mc_agent
                    ).

    TRY.
        lo_ds_service = mo_context->get_service( ).
        lo_edm_impl_base ?= lo_ds_service->get_entity_data_model( ).
        lo_edm_provider ?= lo_edm_impl_base->get_provider( ).
      CATCH cx_sy_move_cast_error INTO lo_move_cast_error.
        RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error
          EXPORTING
            previous = lo_move_cast_error.
    ENDTRY.

    " get Gateway namespaces
    lt_namespaces = mo_service->get_namespaces( ).

    IF iv_format EQ /iwcor/if_ds_uri=>gc_format_json.
      lo_edm_provider->set_build_service_doc_only( abap_true ).
      lv_service_root = mo_context->get_service_root_absolute( ).
      ro_provider = /iwcor/cl_ds_ep_facade=>create_service_provider(
        io_edm          = lo_edm_impl_base
        iv_service_root = lv_service_root
        iv_format       = iv_format ).
      lo_edm_provider->set_build_service_doc_only( abap_false ). "In case of a BATCH with a service doc and another request

    ELSE.
      lo_edm_provider->set_build_service_doc_only( abap_true ).
      lo_edm_provider->/iwcor/if_ds_edm_provider~get_schemas( IMPORTING et_schema = lt_schema ).
      lo_edm_provider->set_build_service_doc_only( abap_false ). "In case of a BATCH with a service doc and another request

      GET REFERENCE OF lt_schema INTO ld_schemas.

      IF ld_schemas IS NOT INITIAL.
        CREATE DATA ld_service.

        " convert current language to ISO
        CALL FUNCTION 'CONVERSION_EXIT_ISOLA_OUTPUT'
          EXPORTING
            input  = sy-langu
          IMPORTING
            output = lv_language.

        TRANSLATE lv_language TO LOWER CASE.
        ld_service->common_attributes-xml_lang = lv_language.

        " get entitysets from model
        LOOP AT ld_schemas->* REFERENCE INTO ld_schema.
          LOOP AT ld_schema->entity_containers REFERENCE INTO ld_entity_container.
            LOOP AT ld_entity_container->entity_sets REFERENCE INTO ld_entity_set.
              lv_entity_set = ld_entity_set->name.
              CLEAR ls_collection.

              " if this leads to a syntax error in your system please apply SAP note 1582870 "ABAP XSS Escaping Support"
              ls_collection-href = cl_abap_dyn_prg=>escape_xss_url( val = lv_entity_set ).

              IF ld_entity_container->is_default_entity_container <> /iwcor/if_ds_edm=>gc_bool_true.
                lv_entity_container = cl_abap_dyn_prg=>escape_xss_url( val = ld_entity_container->name ).

*-------------<app:collection - atom:href />
                CONCATENATE lv_entity_container ls_collection-href INTO ls_collection-href SEPARATED BY '.'.
              ENDIF.

*-----------copy all annotations from entity set to collection - basically no difference
*            append lines of ld_entity_set->*-annotation-attributes to ls_collection-common_attributes-undefined_attributes.

*ld_entity_set->annotation-attributes

              lo_entity_set = mo_service->get_entity_set( iv_namespace = '' iv_name = lv_entity_set ).


              IF lo_entity_set IS BOUND.

*-----------<app:collection - atom:title />
                ls_collection-title-text = lo_entity_set->/iwfnd/if_med_mdl_element~get_label( ).
                ls_collection-title-type = /iwcor/if_atom_types=>gc_content_text.
                IF ls_collection-title-text IS INITIAL.
*-------------defaulting if label is not set
                  ls_collection-title-text = lo_entity_set->get_name( ).
                ENDIF.

                " other EntitySet annotations and extensions (gp ...) from metadata
                lo_entity_set->/iwfnd/if_med_mdl_element~get_annotations( IMPORTING et_annotations = lt_annotations ).

                /iwfnd/cl_sodata_edm_provider=>get_annotations(
                  EXPORTING
                    it_annotations    = lt_annotations
                    it_namespaces     = lt_namespaces
                    iv_mapping_type   = /iwfnd/if_med_mdl_element=>gcs_anno_mapping_types-collection
                  CHANGING
                    cs_anno_attributes  = ls_collection-common_attributes-undefined_attributes
                    cs_anno_elements    = ls_collection-extension_elements
                ).

                " SAP data extensions
                me->add_collection_element(
                  EXPORTING
                    io_entity_set = lo_entity_set
                  CHANGING
                    cs_collection = ls_collection
                       ).

                "SAP data annotations (of entity)
                me->add_entity_set_annotation(
                  EXPORTING
                    io_entity_set = lo_entity_set
                  CHANGING
                    cs_collection = ls_collection
                       ).

              ENDIF.

              APPEND ls_collection TO ls_workspace-collections.
            ENDLOOP.
          ENDLOOP.
        ENDLOOP.


        ls_workspace-title-text = 'Data'.                   "#EC NOTEXT
        ls_workspace-title-type = /iwcor/if_atom_types=>gc_content_text.
*      me->add_workspace_annotation( CHANGING cs_workspace = ls_workspace )."Workspace annotation sap:semantics no longer required because only one workspace supported

        APPEND ls_workspace TO ld_service->workspaces.

        " Get base URL from context
        ld_service->common_attributes-xml_base = mo_context->get_service_root_absolute( ).

        " add versioning - first the self link
        add_service_version_links( ld_service ).

        " add Gateway namespaces
        LOOP AT lt_namespaces REFERENCE INTO lr_namespace.
          ls_ns-prefix    = lr_namespace->*-namespace.
          ls_ns-namespace = lr_namespace->*-namespace_name.
          APPEND ls_ns TO lt_ns.
        ENDLOOP.

        " add atom namespace
        ls_ns-prefix    = 'atom'.                           "#EC NOTEXT
        ls_ns-namespace = 'http://www.w3.org/2005/Atom'.    "#EC NOTEXT
        APPEND ls_ns TO lt_ns.

        CREATE OBJECT lo_service_prov
          EXPORTING
            it_namespace_predeclaration = lt_ns.

        lo_service_prov->set_service( ld_service->* ).
        IF iv_format EQ /iwcor/if_ds_uri=>gc_format_xml.
          lo_service_prov->set_content_type( 'application/xml' ). "#EC NOTEXT
        ENDIF.

        ro_provider = lo_service_prov.
      ENDIF.
    ENDIF.

*---setting http header x_sap_last_modified
    mo_service->get_last_modified(
                   IMPORTING
                     ev_timestamp = lv_last_modified ).

    lv_last_mod_formatted = /iwcor/cl_rest_http_utils=>format_http_date( lv_last_modified ).
    ro_provider->set_header(
      EXPORTING
        iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-x_sap_last_modified
        iv_value = lv_last_mod_formatted ).
    ro_provider->set_header(
      EXPORTING
        iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-last_modified
        iv_value = lv_last_mod_formatted ).

* Metering
    ls_metering-access_type = /iwfnd/cl_sodata_processor=>mc_consumer_type.

    ls_metering-operation = /iwfnd/cl_sodata_processor=>mcs_operations-document.
    IF mo_batch_helper->mv_batch = abap_true.
      CONCATENATE /iwfnd/cl_sodata_processor=>mcs_operations-batch
                  ls_metering-operation
        INTO ls_metering-operation SEPARATED BY '-'.
    ENDIF.

    ls_metering-odc_used    = abap_true.
    ls_metering-format_type = iv_format.
    ls_metering-num_entries = 1.
    mo_context->set_parameter(
      iv_name  = /iwfnd/cl_metering_api=>gc_metering
      iv_value = ls_metering
    ).


* Log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "/iwcor/if_ds_proc_service~read


  METHOD add_collection_element.
*	Collection
*
* Name              Required    Default   Type / Values
* ----------------------------------------------------------------------------------------
* member-title      No (0..1)                       singular form of title
* icon              No (0..n)                       similar to atom:icon
* the size is specified using the Attribute height: pixel size [1:1] of the referenced image
*
* Optional sub-element atom:link @rel=search: reference to an OpenSearch description
* Optional sub-element atom:link @rel=subscribe: reference to the Subscriptions Collection
*
* Extensions to SAPData may add attributes and elements, e.g. Element gp:collectionLayout

    DATA: ls_ex_elem     TYPE /iwcor/if_atom_types=>ext_element_s,
          lv_xml         TYPE string,
          lo_target_node TYPE REF TO /iwfnd/if_med_mdl_node.

    " icon
    lo_target_node = io_entity_set->get_target_node( ).
    lv_xml = lo_target_node->get_icon( ).
    IF lv_xml IS NOT INITIAL.
      CONCATENATE '<atom:icon'
                  ' xmlns:atom="http://www.w3.org/2005/Atom" '
                  ' href="'
                  lv_xml
                  '"  />'
        INTO lv_xml.                                        "#EC NOTEXT
      ls_ex_elem-xmldata = cl_http_utility=>encode_utf8( lv_xml ).
      APPEND ls_ex_elem TO cs_collection-extension_elements.
    ENDIF.


    " add member title
    " sap:member-title xmlns:sap="http://www.sap.com/sdata">Flight</sap:member-title>'
    lv_xml = io_entity_set->get_member_title( ).

    IF lv_xml IS INITIAL.
      lv_xml = lo_target_node->/iwfnd/if_med_mdl_element~get_label( ).
      IF lv_xml IS INITIAL.
        lv_xml = lo_target_node->get_name( ).
      ENDIF.
    ENDIF.

    IF lv_xml IS NOT INITIAL.
      lv_xml = escape( val = lv_xml format = cl_abap_format=>e_xml_text ).

      CONCATENATE '<'
                  /iwfnd/if_sodata_annotation=>gc_prefix_sap
                  ':'
                  /iwfnd/if_sodata_annotation=>gc_anno_elem_member_title
                  ' xmlns:sap="'
                  /iwcor/if_ds_edm=>gc_namespace_sap
                  '">'
                  lv_xml
                  '</'
                    /iwfnd/if_sodata_annotation=>gc_prefix_sap
                  ':'
                  /iwfnd/if_sodata_annotation=>gc_anno_elem_member_title
                  '>'
        INTO lv_xml.
      ls_ex_elem-xmldata = cl_http_utility=>encode_utf8( lv_xml ).

      APPEND  ls_ex_elem TO cs_collection-extension_elements.
    ENDIF.


    " add subscription
    " <atom:link href="SubscriptionCollection" rel="http://www.sap.com/Protocols/SAPData/rel#subscribe" />
    IF lo_target_node->is_subscribable( ) = abap_true.
      lv_xml = 'SubscriptionCollection'.                    "#EC NOTEXT
      CONCATENATE '<atom:link'
                  ' xmlns:atom="http://www.w3.org/2005/Atom" '
                  ' href="'
                  lv_xml
                  '" rel="http://www.sap.com/Protocols/SAPData/rel#subscribe" />'
        INTO lv_xml.                                        "#EC NOTEXT
      ls_ex_elem-xmldata = cl_http_utility=>encode_utf8( lv_xml ).
      APPEND  ls_ex_elem TO cs_collection-extension_elements.
    ENDIF.


    " add open search
    " <atom:link href="TravelagencyCollection/OpenSearchDescription.xml" rel="search" type="application/opensearchdescription+xml" title="search TravelagencyCollection" />
    IF lo_target_node->is_searchable( ) = abap_true.
      lv_xml = lo_target_node->get_entity_set_name( ).
      CONCATENATE '<atom:link'
                  ' xmlns:atom="http://www.w3.org/2005/Atom" '
                  ' href="'
                  lv_xml
                  '/OpenSearchDescription.xml" rel="search" type="application/opensearchdescription+xml" title="search '
                  lv_xml
                  '" />'
        INTO lv_xml.
      ls_ex_elem-xmldata = cl_http_utility=>encode_utf8( lv_xml ).
      APPEND  ls_ex_elem TO cs_collection-extension_elements.
    ENDIF.

  ENDMETHOD.                    "add_collection_element


  METHOD add_entity_set_annotation.
    " add SAP annotions of Entity as well because the metadata dodument is optional

    DATA:
      lo_ds_service      TYPE REF TO /iwcor/if_ds_svc,
      lo_edm_impl_base   TYPE REF TO /iwcor/cl_ds_edm_impl_base,
      lo_edm_provider    TYPE REF TO /iwfnd/cl_sodata_edm_provider,
      lo_move_cast_error TYPE REF TO cx_sy_move_cast_error,
      ls_anno_attributes TYPE /iwcor/if_ds_edm_provider=>annotation_s-annotation-attributes,
      ls_anno_elements   TYPE /iwcor/if_ds_edm_provider=>annotation_s-annotation-elements.


    TRY.
        lo_ds_service = mo_context->get_service( ).
        lo_edm_impl_base ?= lo_ds_service->get_entity_data_model( ).
        lo_edm_provider ?= lo_edm_impl_base->get_provider( ).

        lo_edm_provider->add_entity_set_annotation_sap(
          EXPORTING
            io_node            = io_entity_set
          CHANGING
            cs_anno_attributes = ls_anno_attributes
            cs_anno_elements   = ls_anno_elements
               ).

        APPEND LINES OF ls_anno_attributes TO cs_collection-common_attributes-undefined_attributes.

      CATCH cx_sy_move_cast_error INTO lo_move_cast_error.
        RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error
          EXPORTING
            previous = lo_move_cast_error.
    ENDTRY.

  ENDMETHOD.                    "add_entity_set_annotation


  METHOD add_service_version_links.

    DATA:  ls_ext_element    TYPE /iwcor/if_atom_types=>ext_element_s
          ,lv_xml            TYPE string
          ,lv_latest_version TYPE string
          ,lv_base_url       TYPE string
          .

    " set self url - base url currently only required here - otherwise move to member variable
    lv_base_url = mo_context->get_service_root_absolute( ).

    CONCATENATE '<atom:link xmlns:atom="http://www.w3.org/2005/Atom" rel="self" href="' lv_base_url '" />' INTO lv_xml. "#EC NOTEXT
    ls_ext_element-xmldata = cl_http_utility=>encode_utf8( lv_xml ).
    INSERT ls_ext_element INTO TABLE ir_service->extension_elements.

    " set latest-version link
    CLEAR: lv_xml, ls_ext_element.

    lv_latest_version = mo_service->get_latest_version( ).
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
      EXPORTING
        input  = lv_latest_version
      IMPORTING
        output = lv_latest_version.
    CONDENSE lv_latest_version.

    IF lv_latest_version = '1'.

      lv_xml = lv_base_url.

    ELSE.

      lv_xml = /iwfnd/cl_sodata_utils=>put_version_into_url(
          iv_url     = lv_base_url
          iv_version = lv_latest_version ).

    ENDIF.

    CONCATENATE '<atom:link xmlns:atom="http://www.w3.org/2005/Atom" rel="latest-version" href="' lv_xml '" />' INTO lv_xml. "#EC NOTEXT
    ls_ext_element-xmldata = cl_http_utility=>encode_utf8( lv_xml ).
    INSERT ls_ext_element INTO TABLE ir_service->extension_elements.

  ENDMETHOD.                    "add_service_version_links


  METHOD batch_return_error.

    DATA: lv_http_status TYPE /iwfnd/mgw_http_status_code,
          lx_ds_error    TYPE REF TO /iwcor/cx_ds_http_error.


* Add context object to exception
    IF is_error_operation-mgw_base_exception IS BOUND.
      is_error_operation-mgw_base_exception->mgw_context = is_error_operation-mgw_context.
    ENDIF.

* Create an IWCOR Exception
    TRY.
        lv_http_status = is_error_operation-mgw_base_exception->get_http_status_code( ).
        /iwfnd/cx_sodata=>raise_cor_exception(
          EXPORTING
            ix_previous         = is_error_operation-mgw_base_exception
            iv_exception_type   = /iwfnd/cx_sodata=>gcs_cor_exceptions-client_bad_request
            iv_http_status_code = lv_http_status
        ).
      CATCH /iwcor/cx_ds_http_error INTO lx_ds_error.   "#EC NO_HANDLER
    ENDTRY.

* Call Root Handler to create Error XML as response to consumer
    mo_root_handler->handle_error(
      io_error    = lx_ds_error
      io_request  = is_error_operation-rest_request
      io_response = is_error_operation-rest_response
    ).

  ENDMETHOD.                    "batch_return_error


  METHOD batch_return_exception.

    DATA: lx_ds_error     TYPE REF TO /iwcor/cx_ds_http_error.


* Create an IWCOR Exception
    TRY.
        CASE iv_exception_type.
          WHEN '/IWCOR/CX_DS_NOT_FOUND'.
            RAISE EXCEPTION TYPE /iwcor/cx_ds_not_found.
          WHEN OTHERS.
            RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
        ENDCASE.
      CATCH /iwcor/cx_ds_http_error INTO lx_ds_error.   "#EC NO_HANDLER
    ENDTRY.

* Call Root Handler to create Error XML as response to consumer
    mo_root_handler->handle_error(
      io_error    = lx_ds_error
      io_request  = io_rest_request
      io_response = io_rest_response
    ).

  ENDMETHOD.                    "batch_return_error


  METHOD batch_return_result.

    DATA: lv_count           TYPE i,
          lv_count_string    TYPE string,
          lv_location        TYPE string,
          lv_exists          TYPE abap_bool,
          lt_key             TYPE string_table,
          lo_post_processor  TYPE REF TO /iwfnd/if_sodata_post_proc,
          lo_provider        TYPE REF TO /iwcor/if_rest_entity_provider,
          lo_rest_entity     TYPE REF TO /iwcor/if_rest_entity,
          lo_service         TYPE REF TO /iwcor/if_ds_svc,
          lo_binary_provider TYPE REF TO /iwcor/cl_ds_ep_binary,
          lo_proc_dispatcher TYPE REF TO /iwfnd/cl_sodata_proc_disptchr.

    FIELD-SYMBOLS:
          <ls_stream>         TYPE /iwfnd/if_mgw_core_runtime=>ty_s_media_resource.


    lo_post_processor ?= cs_operation-post_processor.

    CASE cs_operation-odata_method.


* Read Property Value
      WHEN mcs_odata_method-entity_vprop_read.
        lo_post_processor->entity_vprop_read(
          EXPORTING
            iv_odata_method       = cs_operation-odata_method
            iv_app_time           = cs_operation-app_time
            is_request_details    = cs_operation-request_details
            io_ds_context         = cs_operation-ds_context
            iv_format             = cs_operation-format
            iv_for_ds_operation   = cs_operation-for_ds_operation
            iv_source_entity_set  = cs_operation-source_entity_set
            it_property_path      = cs_operation-property_path
            io_mgw_context        = cs_operation-mgw_context
            io_rest_request       = cs_operation-rest_request
            io_rest_response      = cs_operation-rest_response
            ir_entity             = cs_operation-business_data
            io_msg_container      = cs_operation-msg_container
          IMPORTING
            eo_provider           = lo_provider
          CHANGING
            ct_headers            = cs_operation-headers
        ).
        IF lo_provider IS BOUND.
          set_common_response_headers(
            EXPORTING
              io_provider         = lo_provider
              is_response_context = cs_operation-response_context
          ).
          IF cs_operation-response_context-is_ral_relevant EQ abap_true.
            mo_transaction_handler->set_is_ral( ).
          ENDIF.
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
          cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).
        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Function Import
      WHEN mcs_odata_method-func_import_execute.
        CLEAR: lo_provider, lt_key.
        lo_post_processor ?= cs_operation-post_processor.
        lo_post_processor->func_import_execute(
          EXPORTING
            iv_odata_method      = cs_operation-odata_method
            iv_app_time          = cs_operation-app_time
            io_function_import   = cs_operation-function_import
            io_entity_set        = cs_operation-entity_set
            is_request_details   = cs_operation-request_details
            iv_edm_multiplicity  = cs_operation-edm_multiplicity
            iv_mdl_cardinality   = cs_operation-mdl_cardinality
            iv_content_format    = cs_operation-content_format
            io_ds_context        = cs_operation-ds_context
            iv_format            = cs_operation-format
            iv_service_name      = cs_operation-service_name
            iv_service_version   = cs_operation-service_version
            iv_namespace         = cs_operation-namespace
            iv_base_url          = cs_operation-base_url
            iv_source_entity_set = cs_operation-source_entity_set
            iv_is_target_format  = space
            io_mgw_context       = cs_operation-mgw_context
            io_mdl_service       = cs_operation-mdl_service
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
            ir_entity            = cs_operation-business_data
            it_inline_info       = cs_operation-inline_info
            it_expand_skiptoken  = cs_operation-expand_skiptokens
            io_msg_container     = cs_operation-msg_container
          IMPORTING
            eo_provider          = lo_provider
            et_key               = lt_key
          CHANGING
            ct_headers           = cs_operation-headers
        ).
        IF lo_provider IS BOUND.
          " check whether the return type of the function import is a complex type or an entity set
          " -> in case of a complex type a location header cannot be set. Location header is only required for entity set
          IF cs_operation-entity_set IS BOUND.
            lo_service ?= cs_operation-ds_context->get_object( iv_name = /iwfnd/cl_sodata_root_handler=>gc_object_service ).
            lo_proc_dispatcher ?= lo_service->get_processor( cs_operation-ds_context ).
            lv_location = lo_proc_dispatcher->get_entry_location(
                            io_entity_set = cs_operation-entity_set
                            it_key        = lt_key
                            io_context    = cs_operation-ds_context
                          ).
            cs_operation-rest_response->set_header_field(
              iv_name  = if_http_header_fields=>location
              iv_value = lv_location
            ).
          ENDIF.
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Read Property
      WHEN mcs_odata_method-entity_cprop_read.
        lo_post_processor->entity_cprop_read(
          EXPORTING
            iv_odata_method       = cs_operation-odata_method
            iv_app_time           = cs_operation-app_time
            is_request_details    = cs_operation-request_details
            io_ds_context         = cs_operation-ds_context
            iv_format             = cs_operation-format
            iv_for_ds_operation   = cs_operation-for_ds_operation
            iv_source_entity_set  = cs_operation-source_entity_set
            it_property_path      = cs_operation-property_path
            io_mgw_context        = cs_operation-mgw_context
            io_rest_request       = cs_operation-rest_request
            io_rest_response      = cs_operation-rest_response
            ir_entity             = cs_operation-business_data
            io_msg_container      = cs_operation-msg_container
          IMPORTING
            eo_provider           = lo_provider
          CHANGING
            ct_headers           = cs_operation-headers
        ).
        IF lo_provider IS BOUND.
          set_common_response_headers(
            EXPORTING
              io_provider         = lo_provider
              is_response_context = cs_operation-response_context
          ).
          IF cs_operation-response_context-is_ral_relevant EQ abap_true.
            mo_transaction_handler->set_is_ral( ).
          ENDIF.
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
          cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).
        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Read Media
      WHEN mcs_odata_method-entity_media_read.
        lo_post_processor->entity_media_read(
          EXPORTING
            iv_odata_method      = mcs_odata_method-entity_media_read
            iv_app_time          = cs_operation-app_time
            is_request_details   = cs_operation-request_details
            io_ds_context        = cs_operation-ds_context
            iv_format            = cs_operation-format
            iv_for_ds_operation  = cs_operation-for_ds_operation
            iv_source_entity_set = cs_operation-source_entity_set
            io_mgw_context       = cs_operation-mgw_context
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
            ir_entity            = cs_operation-business_data
            io_msg_container     = cs_operation-msg_container
          IMPORTING
            eo_provider          = lo_provider
          CHANGING
            ct_headers           = cs_operation-headers
        ).
        IF lo_provider IS BOUND.
          lo_binary_provider ?= lo_provider.
          ASSIGN cs_operation-business_data->* TO <ls_stream>.
          lo_binary_provider->set_binary_data( <ls_stream>-value ).
          lo_binary_provider->set_content_type( <ls_stream>-mime_type ).
          set_common_response_headers(
            EXPORTING
              io_provider         = lo_provider
              is_response_context = cs_operation-response_context
          ).
          IF cs_operation-response_context-is_ral_relevant EQ abap_true.
            mo_transaction_handler->set_is_ral( ).
          ENDIF.
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
          cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).

        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Update Media / Update Entity
      WHEN mcs_odata_method-entity_media_update OR mcs_odata_method-entity_update.
        lo_post_processor->entity_update(
          EXPORTING
            iv_odata_method      = mcs_odata_method-entity_update
            iv_app_time          = cs_operation-app_time
            io_entity_set        = cs_operation-entity_set
            is_request_details   = cs_operation-request_details
            io_ds_context        = cs_operation-ds_context
            iv_source_entity_set = cs_operation-source_entity_set
            iv_is_target_format  = space
            io_mgw_context       = cs_operation-mgw_context
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
            ir_entity            = cs_operation-business_data
            io_msg_container     = cs_operation-msg_container
          CHANGING
            ct_headers           = cs_operation-headers
        ).


* Delete Media / Delete Entity
      WHEN mcs_odata_method-entity_media_delete OR mcs_odata_method-entity_delete.
        lo_post_processor->entity_delete(
          EXPORTING
            iv_odata_method    = mcs_odata_method-entity_delete
            iv_app_time        = cs_operation-app_time
            is_request_details = cs_operation-request_details
            io_ds_context      = cs_operation-ds_context
            iv_target_entity   = cs_operation-target_entity
            io_rest_request    = cs_operation-rest_request
            io_rest_response   = cs_operation-rest_response
            io_msg_container   = cs_operation-msg_container
          CHANGING
            ct_headers         = cs_operation-headers
        ).


* Read Entity Link
      WHEN mcs_odata_method-entity_link_read.
        lo_post_processor->entity_link_read(
          EXPORTING
            iv_odata_method       = cs_operation-odata_method
            iv_app_time           = cs_operation-app_time
            is_request_details    = cs_operation-request_details
            io_entity_set         = cs_operation-entity_set
            io_ds_context         = cs_operation-ds_context
            iv_format             = cs_operation-format
            iv_for_ds_operation   = cs_operation-for_ds_operation
            iv_source_entity_set  = cs_operation-source_entity_set
            it_navigation_path    = cs_operation-navigation_path
            io_mgw_context        = cs_operation-mgw_context
            io_rest_request       = cs_operation-rest_request
            io_rest_response      = cs_operation-rest_response
            ir_entity             = cs_operation-business_data
            io_msg_container      = cs_operation-msg_container
          IMPORTING
            eo_provider           = lo_provider
          CHANGING
            ct_headers           = cs_operation-headers
        ).
        IF lo_provider IS BOUND.
          set_common_response_headers(
            EXPORTING
              io_provider         = lo_provider
              is_response_context = cs_operation-response_context
          ).
          IF cs_operation-response_context-is_ral_relevant EQ abap_true.
            mo_transaction_handler->set_is_ral( ).
          ENDIF.
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
          cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).
        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Exist Entity or Entity Link
      WHEN mcs_odata_method-entity_exists OR mcs_odata_method-entity_link_exists.
        lo_post_processor->entity_link_exists(
          EXPORTING
            iv_odata_method       = cs_operation-odata_method
            iv_app_time           = cs_operation-app_time
            is_request_details    = cs_operation-request_details
            io_ds_context         = cs_operation-ds_context
            io_rest_request       = cs_operation-rest_request
            io_rest_response      = cs_operation-rest_response
            ir_entity             = cs_operation-business_data
            io_msg_container      = cs_operation-msg_container
          IMPORTING
            ev_exists             = lv_exists
          CHANGING
            ct_headers           = cs_operation-headers
        ).
        lo_rest_entity = cs_operation-rest_response->create_entity( ).
        IF lv_exists = abap_true.
          lo_rest_entity->set_string_data( '1' ).
          lo_rest_entity->set_content_type( /iwcor/if_rest_media_type=>gc_text_plain ).
          cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).
        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_NOT_FOUND'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Read Entity
      WHEN mcs_odata_method-entity_read.
        IF cs_operation-response_context-no_content = abap_true.
          CREATE OBJECT lo_provider TYPE /iwfnd/cl_sodata_dummy_prov.
        ELSE.
          lo_post_processor->entity_read(
            EXPORTING
              iv_odata_method       = cs_operation-odata_method
              iv_app_time           = cs_operation-app_time
              io_entity_set         = cs_operation-entity_set
              is_request_details    = cs_operation-request_details
              io_ds_context         = cs_operation-ds_context
              iv_format             = cs_operation-format
              iv_for_ds_operation   = cs_operation-for_ds_operation
              iv_service_name       = cs_operation-service_name
              iv_service_version    = cs_operation-service_version
              iv_namespace          = cs_operation-namespace
              iv_base_url           = cs_operation-base_url
              iv_source_entity_set  = cs_operation-source_entity_set
              iv_is_target_format   = space
              it_select             = cs_operation-selects
              it_expand             = cs_operation-expands
              io_mgw_context        = cs_operation-mgw_context
              io_mdl_service        = cs_operation-mdl_service
              io_rest_request       = cs_operation-rest_request
              io_rest_response      = cs_operation-rest_response
              ir_entity             = cs_operation-business_data
              it_expand_skiptoken   = cs_operation-expand_skiptokens
              it_inline_info        = cs_operation-inline_info
              io_msg_container      = cs_operation-msg_container
            IMPORTING
              eo_provider           = lo_provider
            CHANGING
              ct_headers            = cs_operation-headers ).
        ENDIF.

        IF lo_provider IS BOUND.
          set_common_response_headers(
            EXPORTING
              io_provider         = lo_provider
              is_response_context = cs_operation-response_context
          ).
          IF cs_operation-response_context-is_ral_relevant EQ abap_true.
            mo_transaction_handler->set_is_ral( ).
          ENDIF.
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
          IF cs_operation-response_context-no_content = abap_true.
            cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_no_content ).
          ELSE.
            cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).
          ENDIF.
        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Read Entity Links
      WHEN mcs_odata_method-entity_links_read.
        lo_post_processor->entity_links_read(
          EXPORTING
            iv_odata_method      = cs_operation-odata_method
            iv_app_time          = cs_operation-app_time
            is_request_details   = cs_operation-request_details
            io_entity_set        = cs_operation-entity_set
            io_ds_context        = cs_operation-ds_context
            iv_format            = cs_operation-format
            iv_for_ds_operation  = cs_operation-for_ds_operation
            iv_source_entity_set = cs_operation-source_entity_set
            it_navigation_path   = cs_operation-navigation_path
            io_mgw_context       = cs_operation-mgw_context
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
            ir_entity            = cs_operation-business_data
            io_msg_container     = cs_operation-msg_container
          IMPORTING
            eo_provider           = lo_provider
          CHANGING
            ct_headers           = cs_operation-headers
        ).
        IF lo_provider IS BOUND.
          set_common_response_headers(
            EXPORTING
              io_provider         = lo_provider
              is_response_context = cs_operation-response_context
          ).
          IF cs_operation-response_context-is_ral_relevant EQ abap_true.
            mo_transaction_handler->set_is_ral( ).
          ENDIF.
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
          cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).
        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Count Entity Links
      WHEN mcs_odata_method-entity_links_count.
        mo_post_processor->entity_links_count(
          EXPORTING
            iv_odata_method      = cs_operation-odata_method
            iv_app_time          = cs_operation-app_time
            is_request_details   = cs_operation-request_details
            io_ds_context        = cs_operation-ds_context
            iv_source_entity_set = cs_operation-source_entity_set
            io_mgw_context       = cs_operation-mgw_context
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
            ir_entity            = cs_operation-business_data
            io_msg_container     = cs_operation-msg_container
          IMPORTING
            ev_count             = lv_count
          CHANGING
            ct_headers           = cs_operation-headers
        ).
        lv_count_string = lv_count.
        CONDENSE lv_count_string.
        lo_rest_entity = cs_operation-rest_response->create_entity( ).
        lo_rest_entity->set_string_data( lv_count_string ).
        lo_rest_entity->set_content_type( /iwcor/if_rest_media_type=>gc_text_plain ).
        cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).


* Create Entity
      WHEN mcs_odata_method-entity_set_create_entity.
        CLEAR: lo_provider, lt_key.
        lo_post_processor->entity_set_create_entity(
          EXPORTING
            iv_odata_method      = mcs_odata_method-entity_set_create_entity
            iv_app_time          = cs_operation-app_time
            io_entity_set        = cs_operation-entity_set
            is_request_details   = cs_operation-request_details
            io_ds_context        = cs_operation-ds_context
            iv_format            = cs_operation-format
            iv_target_entity     = cs_operation-target_entity
            iv_service_name      = cs_operation-service_name
            iv_service_version   = cs_operation-service_version
            iv_namespace         = cs_operation-namespace
            iv_base_url          = cs_operation-base_url
            iv_source_entity_set = cs_operation-source_entity_set
            iv_is_target_format  = space
            iv_inlines           = cs_operation-inlines
            io_mgw_context       = cs_operation-mgw_context
            io_mdl_service       = cs_operation-mdl_service
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
            ir_entity            = cs_operation-business_data
            it_inline_info       = cs_operation-inline_info
            io_msg_container     = cs_operation-msg_container
          IMPORTING
            eo_provider          = lo_provider
            et_key               = lt_key
          CHANGING
            ct_headers           = cs_operation-headers
        ).
        IF lo_provider IS BOUND.
          lo_service ?= cs_operation-ds_context->get_object( iv_name = /iwfnd/cl_sodata_root_handler=>gc_object_service ).
          lo_proc_dispatcher ?= lo_service->get_processor( cs_operation-ds_context ).
          lv_location = lo_proc_dispatcher->get_entry_location(
                          io_entity_set = cs_operation-entity_set
                          it_key        = lt_key
                          io_context    = cs_operation-ds_context
                        ).
          cs_operation-rest_response->set_header_field(
            iv_name  = if_http_header_fields=>location
            iv_value = lv_location
          ).
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
          cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_created ).
        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Read Entity Set
      WHEN mcs_odata_method-entity_set_read.
        lo_post_processor->entity_set_read(
          EXPORTING
            iv_odata_method       = cs_operation-odata_method
            iv_app_time           = cs_operation-app_time
            io_entity_set         = cs_operation-entity_set
            is_request_details    = cs_operation-request_details
            io_ds_context         = cs_operation-ds_context
            iv_format             = cs_operation-format
            iv_for_ds_operation   = cs_operation-for_ds_operation
            iv_target_entity      = cs_operation-target_entity
            iv_service_name       = cs_operation-service_name
            iv_service_version    = cs_operation-service_version
            iv_namespace          = cs_operation-namespace
            iv_base_url           = cs_operation-base_url
            iv_source_entity_set  = cs_operation-source_entity_set
            iv_is_target_format   = space
            it_select             = cs_operation-selects
            it_expand             = cs_operation-expands
            io_mgw_context        = cs_operation-mgw_context
            io_mdl_service        = cs_operation-mdl_service
            io_rest_request       = cs_operation-rest_request
            io_rest_response      = cs_operation-rest_response
            ir_entity_set         = cs_operation-business_data
            ir_deleted_entity_set = cs_operation-deleted_data
            is_response_context   = cs_operation-response_context
            it_inline_info        = cs_operation-inline_info
            io_msg_container      = cs_operation-msg_container
          IMPORTING
            eo_provider           = lo_provider
          CHANGING
            ct_headers            = cs_operation-headers
        ).
        IF lo_provider IS BOUND.
          set_common_response_headers(
            EXPORTING
              io_provider         = lo_provider
              is_response_context = cs_operation-response_context
          ).
          IF cs_operation-response_context-is_ral_relevant EQ abap_true.
            mo_transaction_handler->set_is_ral( ).
          ENDIF.
          serialize_batch_opdata(
            io_provider = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
          cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).
        ELSE.
          batch_return_exception(
            EXPORTING
              iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
              io_rest_request   = cs_operation-rest_request
              io_rest_response  = cs_operation-rest_response
          ).
        ENDIF.


* Entity Set Count
      WHEN mcs_odata_method-entity_set_count.
        lo_post_processor->entity_set_count(
          EXPORTING
            iv_odata_method       = mcs_odata_method-entity_set_count
            iv_app_time           = cs_operation-app_time
            is_request_details    = cs_operation-request_details
            io_ds_context         = cs_operation-ds_context
            iv_source_entity_set  = cs_operation-source_entity_set
            io_mgw_context        = cs_operation-mgw_context
            io_rest_request       = cs_operation-rest_request
            io_rest_response      = cs_operation-rest_response
            ir_entity_set         = cs_operation-business_data
            is_response_context   = cs_operation-response_context
            io_msg_container      = cs_operation-msg_container
          IMPORTING
            ev_count              = lv_count
          CHANGING
            ct_headers            = cs_operation-headers
        ).
        lo_rest_entity = cs_operation-rest_response->create_entity( ).
        lv_count_string = lv_count.
        CONDENSE lv_count_string.
        lo_rest_entity->set_string_data( lv_count_string ).
        lo_rest_entity->set_content_type( /iwcor/if_rest_media_type=>gc_text_plain ).
        set_common_response_headers(
          EXPORTING
            is_response_context = cs_operation-response_context
          CHANGING
            ct_headers          = cs_operation-headers
        ).
        IF cs_operation-response_context-is_ral_relevant EQ abap_true.
          mo_transaction_handler->set_is_ral( ).
        ENDIF.
        cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_ok ).


* Otherwise
      WHEN OTHERS.
        batch_return_exception(
          EXPORTING
            iv_exception_type = '/IWCOR/CX_DS_INTERNAL_ERROR'
            io_rest_request   = cs_operation-rest_request
            io_rest_response  = cs_operation-rest_response
        ).

    ENDCASE.

  ENDMETHOD.                    "batch_return_result


  METHOD changeset_return_error.

    DATA: ls_changeset_error TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_changeset_error,
          lt_changeset_error TYPE /iwfnd/cl_mgw_batch_helper=>ty_t_changeset_error,
          ls_operation       TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_batch_operation,
          lx_ds_error        TYPE REF TO /iwcor/cx_ds_http_error.
    DATA: lv_http_status       TYPE /iwfnd/mgw_http_status_code.



* Log Exception for error only
    IF is_error_operation-operation_state = /iwfnd/cl_mgw_batch_helper=>mcs_operation_state-error.
      mo_logger->log_step_completion_exception(
        io_exception        = is_error_operation-mgw_base_exception
        iv_msg_id           = mc_message_class
        iv_msg_number       = mcs_messages-operation_failed
        iv_agent            = mc_agent
        iv_msg_handle       = mv_msg_handle
      ).
    ENDIF.

* Error during Changeset Processing: IWCOR Exception
    IF mo_batch_helper->mv_changeset_error = abap_false.

      " Add context object to exception
      IF is_error_operation-mgw_base_exception IS BOUND.
        is_error_operation-mgw_base_exception->mgw_context = is_error_operation-mgw_context.
      ENDIF.

      " Create an IWCOR Exception
      TRY.
          lv_http_status = is_error_operation-mgw_base_exception->get_http_status_code( ).
          /iwfnd/cx_sodata=>raise_cor_exception(
            EXPORTING
              ix_previous         = is_error_operation-mgw_base_exception
              iv_exception_type   = /iwfnd/cx_sodata=>gcs_cor_exceptions-client_bad_request
              iv_http_status_code = lv_http_status
          ).
        CATCH /iwcor/cx_ds_http_error INTO lx_ds_error. "#EC NO_HANDLER
      ENDTRY.

      " Call Root Handler to create Error XML as response to consumer
      mo_root_handler->handle_error(
        io_error    = lx_ds_error
        io_request  = is_error_operation-rest_request
        io_response = is_error_operation-rest_response
      ).

* Error before Changeset Processing:
*    - Error already written to Odata Lib
*    - Write an Error Log for Changeset Operations
    ELSE.
      LOOP AT it_operation INTO ls_operation.
        ls_changeset_error-operation_number = sy-tabix.
        CASE ls_operation-operation_state.
          WHEN /iwfnd/cl_mgw_batch_helper=>mcs_operation_state-error.
            IF is_error_operation-mgw_base_exception IS BOUND.
              ls_changeset_error-error_text = is_error_operation-mgw_base_exception->get_text( ).
            ELSE.
              mo_sutil_runtime->log_get_last_error(
                IMPORTING
                  ev_error_text = ls_changeset_error-error_text
              ).
            ENDIF.
          WHEN /iwfnd/cl_mgw_batch_helper=>mcs_operation_state-rollback.
            ls_changeset_error-error_text = 'Rollbacked because of error during changeset processing'. "#EC NOTEXT
          WHEN /iwfnd/cl_mgw_batch_helper=>mcs_operation_state-reject.
            ls_changeset_error-error_text = 'Rejected because of error during changeset processing'. "#EC NOTEXT
        ENDCASE.
        mo_batch_helper->changeset_get_operation_info(
          EXPORTING
            iv_operation_number = ls_changeset_error-operation_number
          IMPORTING
            ev_packet_number    = ls_changeset_error-packet_number
            ev_request_line     = ls_changeset_error-request_line
        ).
        APPEND ls_changeset_error TO lt_changeset_error.
      ENDLOOP.

      mo_sutil_runtime->log_write(
        EXPORTING
          iv_main_entry = abap_false
          iv_error_info = lt_changeset_error
          iv_msgid      = '/IWFND/CM_MGW'
          iv_msgno      = '050'
      ).
    ENDIF.

  ENDMETHOD.                    "changeset_return_error


  METHOD changeset_return_result.

    DATA: lv_location        TYPE string,
          lt_key             TYPE string_table,
          lo_post_processor  TYPE REF TO /iwfnd/if_sodata_post_proc,
          lo_provider        TYPE REF TO /iwcor/if_rest_entity_provider,
          lo_service         TYPE REF TO /iwcor/if_ds_svc,
          lo_proc_dispatcher TYPE REF TO /iwfnd/cl_sodata_proc_disptchr.

    CASE cs_operation-odata_method.

        " Create Entity
      WHEN mcs_odata_method-entity_set_create_entity.
        CLEAR: lo_provider, lt_key.
        lo_post_processor ?= cs_operation-post_processor.
        lo_post_processor->entity_set_create_entity(
          EXPORTING
            iv_odata_method      = mcs_odata_method-entity_set_create_entity
            io_entity_set        = cs_operation-entity_set
            is_request_details   = cs_operation-request_details
            io_ds_context        = cs_operation-ds_context
            iv_format            = cs_operation-format
            iv_target_entity     = cs_operation-target_entity
            iv_service_name      = cs_operation-service_name
            iv_service_version   = cs_operation-service_version
            iv_namespace         = cs_operation-namespace
            iv_base_url          = cs_operation-base_url
            iv_source_entity_set = cs_operation-source_entity_set
            iv_is_target_format  = space
            iv_inlines           = cs_operation-inlines
            io_mgw_context       = cs_operation-mgw_context
            io_mdl_service       = cs_operation-mdl_service
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
            ir_entity            = cs_operation-business_data
            it_inline_info       = cs_operation-inline_info
          IMPORTING
            eo_provider          = lo_provider
            et_key               = lt_key
          CHANGING
            ct_headers           = cs_operation-headers
        ).

        " Write Returned Data to lib:
        "  - Get own Dispatcher, get and set entry location in payload
        IF lo_provider IS BOUND.
          lo_service ?= cs_operation-ds_context->get_object( iv_name = /iwfnd/cl_sodata_root_handler=>gc_object_service ).
          lo_proc_dispatcher ?= lo_service->get_processor( cs_operation-ds_context ).
          lv_location = lo_proc_dispatcher->get_entry_location(
                          io_entity_set = cs_operation-entity_set
                          it_key        = lt_key
                          io_context    = cs_operation-ds_context
                        ).
          cs_operation-rest_response->set_header_field(
            iv_name  = if_http_header_fields=>location
            iv_value = lv_location
          ).
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
          cs_operation-rest_response->set_status( /iwcor/cl_rest_status_code=>gc_success_created ).
        ELSE.
          RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
        ENDIF.

        " Update Entity
      WHEN mcs_odata_method-entity_update.
        mo_post_processor->entity_update(
          EXPORTING
            iv_odata_method      = mcs_odata_method-entity_update
            io_entity_set        = cs_operation-entity_set
            is_request_details   = cs_operation-request_details
            io_ds_context        = cs_operation-ds_context
            iv_source_entity_set = cs_operation-source_entity_set
            iv_is_target_format  = space
            io_mgw_context       = cs_operation-mgw_context
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
            ir_entity            = cs_operation-business_data
          CHANGING
            ct_headers           = cs_operation-headers
        ).

        " Delete Entity
      WHEN mcs_odata_method-entity_delete.
        mo_post_processor->entity_delete(
          EXPORTING
            iv_odata_method      = mcs_odata_method-entity_delete
            is_request_details   = cs_operation-request_details
            io_ds_context        = cs_operation-ds_context
            iv_target_entity     = cs_operation-target_entity
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
          CHANGING
            ct_headers           = cs_operation-headers
        ).

        " Function Import
      WHEN mcs_odata_method-func_import_execute.
        CLEAR: lo_provider, lt_key.
        lo_post_processor ?= cs_operation-post_processor.
        lo_post_processor->func_import_execute(
          EXPORTING
            iv_odata_method      = mcs_odata_method-func_import_execute
            io_function_import   = cs_operation-function_import
            io_entity_set        = cs_operation-entity_set
            is_request_details   = cs_operation-request_details
            iv_edm_multiplicity  = cs_operation-edm_multiplicity
            iv_mdl_cardinality   = cs_operation-mdl_cardinality
            iv_content_format    = cs_operation-content_format
            io_ds_context        = cs_operation-ds_context
            iv_format            = cs_operation-format
            iv_service_name      = cs_operation-service_name
            iv_service_version   = cs_operation-service_version
            iv_namespace         = cs_operation-namespace
            iv_base_url          = cs_operation-base_url
            iv_source_entity_set = cs_operation-source_entity_set
            iv_is_target_format  = space
            io_mgw_context       = cs_operation-mgw_context
            io_mdl_service       = cs_operation-mdl_service
            io_rest_request      = cs_operation-rest_request
            io_rest_response     = cs_operation-rest_response
            ir_entity            = cs_operation-business_data
            it_inline_info       = cs_operation-inline_info
            it_expand_skiptoken  = cs_operation-expand_skiptokens
          IMPORTING
            eo_provider          = lo_provider
            et_key               = lt_key
          CHANGING
            ct_headers           = cs_operation-headers
        ).

        " Write Returned Data to lib:
        "  - Get own Dispatcher, get and set entry location in payload
        IF lo_provider IS BOUND.

          " check whether the return type of the function import is a complex type or an entity set
          " -> in case of a complex type a location header cannot be set. location header is only required for entity set
          IF cs_operation-entity_set IS BOUND.
            lo_service ?= cs_operation-ds_context->get_object( iv_name = /iwfnd/cl_sodata_root_handler=>gc_object_service ).
            lo_proc_dispatcher ?= lo_service->get_processor( cs_operation-ds_context ).
            lv_location = lo_proc_dispatcher->get_entry_location(
                            io_entity_set = cs_operation-entity_set
                            it_key        = lt_key
                            io_context    = cs_operation-ds_context
                          ).
            cs_operation-rest_response->set_header_field(
              iv_name  = if_http_header_fields=>location
              iv_value = lv_location
            ).
          ENDIF.
          serialize_batch_opdata(
            io_provider      = lo_provider
            io_rest_response = cs_operation-rest_response
          ).
        ELSE.
          RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
        ENDIF.

        " Others
      WHEN OTHERS.
        ASSERT 0 = 1.

    ENDCASE.

  ENDMETHOD.                    "changeset_return_result


  METHOD check_etag_processing.

    DATA: ls_request_header TYPE ihttpnvp,                  "#EC NEEDED
          lt_request_header TYPE tihttpnvp.


    rv_value = abap_false.

    lt_request_header = io_request->get_header_fields( ).

    LOOP AT lt_request_header INTO ls_request_header
      WHERE name = 'if-match'
         OR name = 'if-none-match'
         OR name = 'if-modified-since'
         OR name = 'if-unmodified-since'.
      mo_batch_helper->batch_set_etag_processing( abap_true ).
      rv_value = abap_true.
      EXIT.
    ENDLOOP.

  ENDMETHOD.


  METHOD constructor.

    super->constructor( ).

* Save Service Instance
    mo_service = io_service.

* Get Logger Instance
    mo_logger = /iwfnd/cl_logger=>get_logger( ).

* Get SUTIL Runtime
    mo_sutil_runtime = /iwfnd/cl_sutil_runtime=>get_instance( ).

* Get Transaction Handler
    mo_transaction_handler = /iwfnd/cl_transaction_handler=>get_transaction_handler( ).

* Get Service Information
    mv_namespace        = mo_transaction_handler->get_service_namespace( ).
    mv_service_name     = mo_transaction_handler->get_service_name( ).
    mv_service_version  = mo_transaction_handler->get_service_version( ).

  ENDMETHOD.                    "constructor


  METHOD convert_datetime.

    DATA: lv_length        TYPE i,
          lv_internal_type TYPE inttype.



* Get ABAP Internal Type Info
    IF ( NOT io_edm_instance IS INITIAL ).
      /iwfnd/cl_sodata_utils=>get_edm_type_abap_info_via_map(
        EXPORTING
          io_edm_instance = io_edm_instance
        IMPORTING
          ev_internal_type = lv_internal_type
          ev_length_char   = lv_length
        ).

    ELSE. "IO_EDM_INSTANCE is INITIAL, hence IO_GW_PROPERTY must not be INITIAL
      lv_length         = io_gw_property->get_length( ).
      io_gw_property->get_internal_type_info(
        IMPORTING
          ev_internal_type        = lv_internal_type    " ABAP data type (C,D,N,...)
      ).
    ENDIF.


* Correct Datetime value
    rv_value = correct_datetime_value(
                 iv_internal_type          = lv_internal_type
                 iv_length                 = lv_length
                 io_simple_type            = io_simple_type
                 iv_value                  = iv_value
                 iv_use_abap_default_value = iv_use_abap_default_value
               ).

  ENDMETHOD.                    "convert_datetime


  METHOD convert_value.

    DATA: lr_string   TYPE REF TO string,
          lr_guid     TYPE REF TO guid_32,
          lr_integer  TYPE REF TO i,
          lr_boolean  TYPE REF TO abap_bool,
          lr_binary   TYPE REF TO xstring,
          ld_facets   TYPE REF TO /iwcor/if_ds_edm_element=>facets_s,
          lo_property TYPE REF TO /iwcor/if_ds_edm_property.
    DATA: ls_facets    TYPE /iwcor/if_ds_edm_element=>facets_s.



* Get facets from IO_EDM_INSTANCE
    IF ( NOT io_edm_instance IS INITIAL ).
      TRY.
          lo_property ?= io_edm_instance.
          ld_facets = lo_property->get_facets( ).
        CATCH cx_root.                                   "#EC CATCH_ALL
          CLEAR ld_facets.
      ENDTRY.

    ELSE. "IO_EDM_INSTANCE is INITIAL, hence IO_GW_PROPERTY must not be INITIAL
* Build facets from IO_GW_PROPERTY
*    Coding copied from /IWFND/CL_SODATA_EDM_PROVIDER->BUILD_PROPERTY

      CASE io_gw_property->get_core_type( ).
        WHEN /iwfnd/if_sodata_types=>gc_edm_type_string.
          ls_facets-max_length = io_gw_property->get_length( ).

        WHEN /iwfnd/if_sodata_types=>gc_edm_type_datetime OR
             /iwfnd/if_sodata_types=>gc_edm_type_datetimeoffset OR
             /iwfnd/if_sodata_types=>gc_edm_type_time.
*       The Precision facet indicates the degree of granularity of the DateTime in fractions of a second, based on the number of decimal places supported
          ls_facets-precision = io_gw_property->get_decimals( ).

        WHEN /iwfnd/if_sodata_types=>gc_edm_type_decimal.
*       For a decimal property the value of the precision attribute specifies the maximum number of digits allowed in the property's value
          ls_facets-precision = io_gw_property->get_length( ).
          ls_facets-scale = io_gw_property->get_decimals( ).

      ENDCASE.

      GET REFERENCE OF ls_facets INTO ld_facets.
    ENDIF.

    TRY.
* Conversion
        CASE io_simple_type->/iwcor/if_ds_edm_named~get_name( ).

          WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_string.
            CREATE DATA lr_string.
            IF iv_use_abap_default_value = abap_false.
              io_simple_type->value_of(
                EXPORTING
                  iv_value  = iv_value
                  id_facets = ld_facets
                IMPORTING
                  ev_value  = lr_string->*
              ).
            ENDIF.
            rv_value = lr_string->*.

          WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_boolean.
            CREATE DATA lr_boolean.
            IF iv_use_abap_default_value = abap_false.
              io_simple_type->value_of(
                EXPORTING
                  iv_value  = iv_value
                  id_facets = ld_facets
                IMPORTING
                  ev_value  = lr_boolean->*
              ).
            ENDIF.
            rv_value = lr_boolean->*.

          WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_byte  OR
               /iwcor/cl_ds_edm_simple_type=>gc_name_int16 OR
               /iwcor/cl_ds_edm_simple_type=>gc_name_int32 OR
               /iwcor/cl_ds_edm_simple_type=>gc_name_sbyte.
            CREATE DATA lr_integer.
            IF iv_use_abap_default_value = abap_false.
              io_simple_type->value_of(
                EXPORTING
                  iv_value  = iv_value
                  id_facets = ld_facets
                IMPORTING
                  ev_value  = lr_integer->*
              ).
            ENDIF.
            rv_value = lr_integer->*.

          WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_guid.
            CREATE DATA lr_guid.
            IF iv_use_abap_default_value = abap_false.
              io_simple_type->value_of(
                EXPORTING
                  iv_value  = iv_value
                  id_facets = ld_facets
                IMPORTING
                  ev_value  = lr_guid->*
              ).
            ENDIF.
            rv_value = lr_guid->*.

          WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_time     OR
               /iwcor/cl_ds_edm_simple_type=>gc_name_datetime OR
               /iwcor/cl_ds_edm_simple_type=>gc_name_datetimeoffset.
            rv_value = me->convert_datetime(
                         io_simple_type            = io_simple_type
                         io_edm_instance           = io_edm_instance
                         io_gw_property            = io_gw_property
                         iv_value                  = iv_value
                         iv_use_abap_default_value = iv_use_abap_default_value
                       ).

          WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_binary.
            CREATE DATA lr_binary.
            IF iv_use_abap_default_value = abap_false.
              io_simple_type->value_of(
                EXPORTING
                  iv_value  = iv_value
                  id_facets = ld_facets
                IMPORTING
                  ev_value  = lr_binary->*
              ).
            ENDIF.
            rv_value = lr_binary->*.

          WHEN OTHERS.
            CREATE DATA lr_string.
            IF iv_use_abap_default_value = abap_false.
              io_simple_type->value_of(
                EXPORTING
                  iv_value  = iv_value
                  id_facets = ld_facets
                IMPORTING
                  ev_value  = lr_string->*
              ).
            ENDIF.
            rv_value = lr_string->*.

        ENDCASE.

      CATCH cx_sy_conversion_error INTO DATA(lx_sy_conversion_error).

        RAISE EXCEPTION TYPE /iwcor/cx_ds_edm_type_error
          EXPORTING
            textid   = /iwcor/cx_ds_edm_type_error=>uri_literal_syntax_error
            previous = lx_sy_conversion_error.

      CATCH cx_parameter_invalid_type INTO DATA(lx_parameter_invalid_type).

        RAISE EXCEPTION TYPE /iwcor/cx_ds_edm_type_error
          EXPORTING
            textid   = /iwcor/cx_ds_edm_type_error=>uri_literal_syntax_error
            previous = lx_parameter_invalid_type.

      CATCH cx_parameter_invalid INTO DATA(lx_parameter_invalid).

        RAISE EXCEPTION TYPE /iwcor/cx_ds_edm_type_error
          EXPORTING
            textid   = /iwcor/cx_ds_edm_type_error=>uri_literal_syntax_error
            previous = lx_parameter_invalid.

    ENDTRY.

  ENDMETHOD.                    "convert_to_string


  METHOD convert_value_by_parameter.

    DATA: lv_value          TYPE string,
          lo_simple_type    TYPE REF TO /iwcor/if_ds_edm_simple_type,
          lx_root           TYPE REF TO cx_root,
          lx_tech_exception TYPE REF TO /iwfnd/cx_mgw_tech_exception.

    TRY.
        lo_simple_type ?= ir_parameter_value->parameter->get_type( ).
        lv_value = ir_parameter_value->value.

        rv_value = me->convert_value(
                     io_simple_type  = lo_simple_type
                     io_edm_instance = ir_parameter_value->parameter
                     iv_value        = lv_value
                   ).

      CATCH cx_sy_conversion_error cx_parameter_invalid INTO lx_root.
        CREATE OBJECT lx_tech_exception
          EXPORTING
            previous         = lx_root
            http_status_code = '400'.

        raise_technical_error( lx_tech_exception ).
    ENDTRY.

  ENDMETHOD.                    "convert_to_string_by_parameter


  METHOD convert_value_by_predicate.

    DATA: lv_value          TYPE string,
          lo_property       TYPE REF TO /iwcor/if_ds_edm_property,
          lo_simple_type    TYPE REF TO /iwcor/if_ds_edm_simple_type,
          lx_root           TYPE REF TO cx_root,
          lx_tech_exception TYPE REF TO /iwfnd/cx_mgw_tech_exception.

    TRY.
        lo_property = ir_key_predicate->property.
        lo_simple_type ?= lo_property->get_type( ).
        lv_value = ir_key_predicate->value.

        rv_value = me->convert_value(
                     io_simple_type  = lo_simple_type
                     io_edm_instance = lo_property
                     iv_value        = lv_value
                   ).

      CATCH cx_sy_conversion_error cx_parameter_invalid INTO lx_root.
        CREATE OBJECT lx_tech_exception
          EXPORTING
            previous         = lx_root
            http_status_code = '400'.

        raise_technical_error( lx_tech_exception ).
    ENDTRY.

  ENDMETHOD.                    "convert_to_string_by_predicate


  METHOD correct_boolean_value.

    DATA: lv_type_name TYPE string,
          lr_boolean   TYPE REF TO abap_bool.

    "null is a valid value in this case and should not be converted
    IF iv_value = 'null'.
      rv_value = iv_value.
      RETURN.
    ENDIF.

    lv_type_name = io_simple_type->/iwcor/if_ds_edm_named~get_name( ).

    IF lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_boolean.
      CREATE DATA lr_boolean.
      io_simple_type->value_of(
        EXPORTING
          iv_value = iv_value
        IMPORTING
          ev_value = lr_boolean->*
      ).
      rv_value = lr_boolean->*.

    ELSE.
      rv_value = iv_value.
    ENDIF.

  ENDMETHOD.


  METHOD correct_datetime_value.

    DATA: lr_dats       TYPE REF TO dats,
          lr_tims       TYPE REF TO tims,
          lr_timestamp  TYPE REF TO timestamp,
          lr_timestampl TYPE REF TO timestampl,
          lr_utclong    TYPE REF TO data.

    " null is a valid value in this case and should not be converted
    IF iv_value = 'null'.
      rv_value = iv_value.
      RETURN.
    ENDIF.

    CASE iv_internal_type.

        " Date
      WHEN 'D'.
        CREATE DATA lr_dats.
        IF iv_use_abap_default_value = abap_false.
          io_simple_type->value_of(
            EXPORTING
              iv_value = iv_value
            IMPORTING
              ev_value = lr_dats->*
          ).
        ENDIF.
        rv_value = lr_dats->*.

        " Time
      WHEN 'T'.
        CREATE DATA lr_tims.
        IF iv_use_abap_default_value = abap_false.
          io_simple_type->value_of(
            EXPORTING
              iv_value = iv_value
            IMPORTING
              ev_value = lr_tims->*
          ).
        ENDIF.
        rv_value = lr_tims->*.

        " Timestamp
      WHEN 'P'.

        " Short Timestamp
        IF iv_length = 19 OR iv_length = 4.
          CREATE DATA lr_timestamp.
          IF iv_use_abap_default_value = abap_false.
            io_simple_type->value_of(
              EXPORTING
                iv_value = iv_value
              IMPORTING
                ev_value = lr_timestamp->* ).
            rv_value = lr_timestamp->*.
            WHILE strlen( rv_value ) < 15.
              rv_value = '0' && rv_value.
            ENDWHILE.
          ELSE.
            rv_value = lr_timestamp->*.
          ENDIF.

          " Long Timestamp
        ELSE. "iv_length == 27
          CREATE DATA lr_timestampl.
          IF iv_use_abap_default_value = abap_false.
            io_simple_type->value_of(
              EXPORTING
                iv_value = iv_value
              IMPORTING
                ev_value = lr_timestampl->* ).
            rv_value = lr_timestampl->*.
            WHILE strlen( rv_value ) < 23.
              rv_value = '0' && rv_value.
            ENDWHILE.
          ELSE.
            rv_value = lr_timestampl->*.
          ENDIF.
        ENDIF.

      WHEN 'p'. "UTCLONG

        CREATE DATA lr_utclong TYPE ('UTCLONG').

        FIELD-SYMBOLS <lf_utclong> TYPE any.

        ASSIGN lr_utclong->* TO <lf_utclong>.

        io_simple_type->value_of(
          EXPORTING
            iv_value = iv_value
          IMPORTING
            ev_value = <lf_utclong> ).

        rv_value = <lf_utclong>.

        " Others
      WHEN OTHERS.
        rv_value = iv_value.

    ENDCASE.

    CONDENSE rv_value.

  ENDMETHOD.                    "correct_datetime_value


  METHOD correct_guid_value.

    DATA: lv_type_name TYPE string,
          lr_guid      TYPE REF TO guid_32.

    lv_type_name = io_simple_type->/iwcor/if_ds_edm_named~get_name( ).

    IF lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_guid.
      CREATE DATA lr_guid.
      io_simple_type->value_of(
        EXPORTING
          iv_value = iv_value
        IMPORTING
          ev_value = lr_guid->*
      ).
      rv_value = lr_guid->*.

    ELSE.
      rv_value = iv_value.
    ENDIF.

  ENDMETHOD.


  METHOD create_filter_tree.

    DATA: lv_internal_type TYPE inttype,
          lv_length        TYPE i,
          lv_i             TYPE i,
          lo_unary         TYPE REF TO /iwcor/if_ds_expr_unary,
          lo_binary        TYPE REF TO /iwcor/if_ds_expr_binary,
          lo_literal       TYPE REF TO /iwcor/if_ds_expr_literal,
          lo_function      TYPE REF TO /iwcor/if_ds_expr_function,
          lo_simple_type   TYPE REF TO /iwcor/if_ds_edm_simple_type,
          lr_parameter     TYPE REF TO /iwcor/if_ds_expr_node,
          ls_binary        TYPE /iwfnd/if_mgw_core_types=>ty_expression,
          ls_unary         TYPE /iwfnd/if_mgw_core_types=>ty_expression,
          ls_function      TYPE /iwfnd/if_mgw_core_types=>ty_function,
          ls_parameter     TYPE /iwfnd/if_mgw_core_types=>ty_parameter.


    IF io_filter IS BOUND.

      CASE io_filter->kind.
*/ Unary Expression
        WHEN /iwcor/if_ds_expr_node=>kind_unary.
          lo_unary ?= io_filter.

          ls_unary-expression_id = get_guid( ).
          ev_index = ls_unary-expression_id.
          ls_unary-rop_type = lo_unary->operand->kind.
          ls_unary-operator = lo_unary->operator.

*/ Process Operand of the Unary Expression
          CASE lo_unary->operand->kind.

            WHEN /iwcor/if_ds_expr_node=>kind_binary.     " Binary
              create_filter_tree(
                EXPORTING
                  io_filter = lo_unary->operand
                IMPORTING
                  ev_index  = ls_unary-rop_id
              ).

            WHEN /iwcor/if_ds_expr_node=>kind_function.   " Function
              create_filter_tree(
                EXPORTING
                  io_filter = lo_unary->operand
                IMPORTING
                  ev_index  = ls_unary-rop_id
              ).

            WHEN /iwcor/if_ds_expr_node=>kind_property.   " Property/Member

              get_name_from_property_member(
                EXPORTING
                  io_node           = lo_unary->operand
                IMPORTING
                  ev_value_external = ls_unary-r_operand
                  ev_value_internal = ls_unary-r_oprnd_int
              ).

            WHEN /iwcor/if_ds_expr_node=>kind_literal.    " Literal

              get_filter_string(
                EXPORTING
                  io_filter         = lo_unary->operand
                IMPORTING
                  ev_value_external = ls_unary-r_operand
                  ev_value_internal = ls_unary-r_oprnd_int
              ).
              " unquote the literal maintaining any single quotes in between
              lv_i = strlen( ls_unary-r_operand ).
              lv_i = lv_i - 1.
              ls_unary-r_operand = ls_unary-r_operand+0(lv_i).
              REPLACE FIRST OCCURRENCE OF `'` IN ls_unary-r_operand WITH ''.
              REPLACE ALL OCCURRENCES OF `''` IN ls_unary-r_operand WITH `'`.
              CLEAR lv_i.

              lv_i = strlen( ls_unary-r_oprnd_int ).
              lv_i = lv_i - 1.
              ls_unary-r_oprnd_int = ls_unary-r_oprnd_int+0(lv_i).
              REPLACE FIRST OCCURRENCE OF `'` IN ls_unary-r_oprnd_int WITH ''.
              REPLACE ALL OCCURRENCES OF `''` IN ls_unary-r_oprnd_int WITH `'`.
              CLEAR lv_i.

              ls_unary-literal_type = lo_unary->operand->type->/iwcor/if_ds_edm_named~get_name( ).

          ENDCASE.

          APPEND ls_unary TO mt_expressions.
          CLEAR: ls_unary.

*/ Binary Expression
        WHEN /iwcor/if_ds_expr_node=>kind_binary.
          lo_binary ?= io_filter.
*/ Right Operand can't be Prperty/Member
*        IF  lo_binary->right_operand->kind = /iwcor/if_ds_expr_node=>kind_property OR
*            lo_binary->right_operand->kind = /iwcor/if_ds_expr_node=>kind_member.
*          RAISE EXCEPTION TYPE /iwcor/cx_ds_proc_unsupported.
*        ENDIF.

*/ Assign Operator of the binary Expression
          ls_binary-operator = lo_binary->operator.

*/-------------------------- Process Left Operand ------------------------------/*
*/------------------------------------------------------------------------------/*
          CASE lo_binary->left_operand->kind.

            WHEN /iwcor/if_ds_expr_node=>kind_unary.              " Unary

              create_filter_tree(
                EXPORTING
                  io_filter         = lo_binary->left_operand
                IMPORTING
                  ev_index          = ls_binary-lop_id
              ).

              ls_binary-lop_type = /iwcor/if_ds_expr_node=>kind_unary.

            WHEN /iwcor/if_ds_expr_node=>kind_binary.             " Binary

              create_filter_tree(
                EXPORTING
                  io_filter         = lo_binary->left_operand
                IMPORTING
                  ev_index          = ls_binary-lop_id
              ).

              ls_binary-lop_type = /iwcor/if_ds_expr_node=>kind_binary.

            WHEN /iwcor/if_ds_expr_node=>kind_property OR         " Property/Member
              /iwcor/if_ds_expr_node=>kind_member.

              ls_binary-lop_type = lo_binary->left_operand->kind.
              get_name_from_property_member(
                EXPORTING
                  io_node           = lo_binary->left_operand
                IMPORTING
                  ev_value_external = ls_binary-l_operand
                  ev_value_internal = ls_binary-l_oprnd_int
              ).

            WHEN /iwcor/if_ds_expr_node=>kind_function.           " Function

              create_filter_tree(
                EXPORTING
                  io_filter         = lo_binary->left_operand
                IMPORTING
                  ev_index          = ls_binary-lop_id
              ).

              ls_binary-lop_type = /iwcor/if_ds_expr_node=>kind_function.

          ENDCASE.

*/-------------------------- Process Right Operand -----------------------------/*
*/------------------------------------------------------------------------------/*
          CASE lo_binary->right_operand->kind.

            WHEN /iwcor/if_ds_expr_node=>kind_unary.              " Unary

              create_filter_tree(
                EXPORTING
                  io_filter         = lo_binary->right_operand
                IMPORTING
                  ev_index          = ls_binary-rop_id
              ).

              ls_binary-rop_type = /iwcor/if_ds_expr_node=>kind_unary.

            WHEN /iwcor/if_ds_expr_node=>kind_binary.             " Binary

              create_filter_tree(
                EXPORTING
                  io_filter         = lo_binary->right_operand
                IMPORTING
                  ev_index          = ls_binary-rop_id
              ).

              ls_binary-rop_type = /iwcor/if_ds_expr_node=>kind_binary.

            WHEN /iwcor/if_ds_expr_node=>kind_literal.            " Literal
              ls_binary-rop_type = /iwcor/if_ds_expr_node=>kind_literal.
              lo_literal ?= lo_binary->right_operand.

              IF lo_binary->right_operand->type IS NOT BOUND.
                ls_binary-is_null = abap_true.
*              lo_literal ?= lo_binary->right_operand.
                ls_binary-r_operand = ls_binary-r_oprnd_int = lo_literal->literal.
              ELSE.
                ls_binary-literal_type = lo_binary->right_operand->type->/iwcor/if_ds_edm_named~get_name( ).

*/ Value Conversion when Left Operand is a Function - No validation against Property/Member Edm type
                IF lo_binary->left_operand->kind EQ /iwcor/if_ds_expr_node=>kind_function.
*                lo_literal ?= lo_binary->right_operand.

                  IF lo_binary->right_operand->type IS BOUND.
                    lo_simple_type ?= lo_binary->right_operand->type.

                    CASE lo_binary->right_operand->type->/iwcor/if_ds_edm_named~get_name( ).
                      WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_boolean.             " Boolean
                        ls_binary-r_operand = correct_boolean_value(
                                              io_simple_type   = lo_simple_type
                                              iv_value         = lo_literal->literal
                                            ).
                        IF ls_binary-r_operand IS INITIAL.
                          ls_binary-r_operand = ''' '''.
                        ELSE.
                          ls_binary-r_operand = cl_abap_dyn_prg=>quote( ls_binary-r_operand ).
                        ENDIF.

                      WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_guid.                " GUID
                        ls_binary-r_operand = correct_guid_value(
                                              io_simple_type   = lo_simple_type
                                              iv_value         = lo_literal->literal
                                            ).
                        CONDENSE ls_binary-r_operand.
                        ls_binary-r_operand = cl_abap_dyn_prg=>quote( ls_binary-r_operand ).

                      WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_datetime       OR    " Date/Time
                           /iwcor/cl_ds_edm_simple_type=>gc_name_datetimeoffset OR
                           /iwcor/cl_ds_edm_simple_type=>gc_name_time.

                        /iwfnd/cl_sodata_utils=>get_edm_type_abap_info_via_map(
                          EXPORTING
                            io_edm_instance  = lo_simple_type
                          IMPORTING
                            ev_internal_type = lv_internal_type
                            ev_length_char   = lv_length
                        ).

                        ls_binary-r_operand = correct_datetime_value(
                            iv_internal_type = lv_internal_type
                            iv_length        = lv_length
                            io_simple_type   = lo_simple_type
                            iv_value         = lo_literal->literal
                          ).
                        ls_binary-r_operand = cl_abap_dyn_prg=>quote( ls_binary-r_operand ).

                      WHEN OTHERS.
                        ls_binary-r_operand = lo_literal->literal.
                    ENDCASE.
                    ls_binary-r_oprnd_int = ls_binary-r_operand.
                  ENDIF.
                ELSE.
*/ Value Conversion when Left Operand is Property/Member - Validate against property/Member Edm Type
                  get_filter_string(
                    EXPORTING
                      io_filter         = lo_binary->right_operand
                    IMPORTING
                      ev_value_external = ls_binary-r_operand
                      ev_value_internal = ls_binary-r_oprnd_int
                  ).

                  " unquote the literal maintaining any single quotes in between
                  lv_i = strlen( ls_binary-r_operand ).
                  lv_i = lv_i - 1.
                  ls_binary-r_operand = ls_binary-r_operand+0(lv_i).
                  REPLACE FIRST OCCURRENCE OF `'` IN ls_binary-r_operand WITH ''.
                  REPLACE ALL OCCURRENCES OF `''` IN ls_binary-r_operand WITH `'`.
                  CLEAR lv_i.

                  lv_i = strlen( ls_binary-r_oprnd_int ).
                  lv_i = lv_i - 1.
                  ls_binary-r_oprnd_int = ls_binary-r_oprnd_int+0(lv_i).
                  REPLACE FIRST OCCURRENCE OF `'` IN ls_binary-r_oprnd_int WITH ''.
                  REPLACE ALL OCCURRENCES OF `''` IN ls_binary-r_oprnd_int WITH `'`.
                  CLEAR lv_i.


                ENDIF.
              ENDIF.
              " lo_literal already bound after this statement
              ls_binary-variable = lo_literal->variable.

            WHEN /iwcor/if_ds_expr_node=>kind_function.           " Function

              create_filter_tree(
                EXPORTING
                  io_filter         = lo_binary->right_operand
                IMPORTING
                  ev_index          = ls_binary-rop_id
              ).

              ls_binary-rop_type = /iwcor/if_ds_expr_node=>kind_function.

*--- Support for Property or Complex type in the right operand --*
            WHEN /iwcor/if_ds_expr_node=>kind_property OR         " Property/Member
              /iwcor/if_ds_expr_node=>kind_member.

              ls_binary-rop_type = lo_binary->right_operand->kind.
              get_name_from_property_member(
                EXPORTING
                  io_node           = lo_binary->right_operand
                IMPORTING
                  ev_value_external = ls_binary-r_operand
                  ev_value_internal = ls_binary-r_oprnd_int
              ).
*--- End of support for Property or Complex type in the right operand --*
          ENDCASE.

*/ Append to Expressions Table
          ls_binary-expression_id = get_guid( ).
          ev_index = ls_binary-expression_id.
          APPEND ls_binary TO mt_expressions.
          CLEAR ls_binary.

*/ Function with Parameters
        WHEN /iwcor/if_ds_expr_node=>kind_function.

          lo_function ?= io_filter.

          ls_function-function_id = get_guid( ).
          ev_index = ls_function-function_id.
          ls_function-function = lo_function->function.

*/----------------------- Process Function Parameters --------------------------/*
*/------------------------------------------------------------------------------/*
          LOOP AT lo_function->parameters INTO lr_parameter.

            CASE lr_parameter->kind.

              WHEN /iwcor/if_ds_expr_node=>kind_property OR             " Property/Member
                   /iwcor/if_ds_expr_node=>kind_member.

                ls_parameter-param_id = get_guid( ).
                ls_parameter-param_type = lr_parameter->kind.
                get_name_from_property_member(
                  EXPORTING
                    io_node           = lr_parameter
                  IMPORTING
                    ev_value_external = ls_parameter-param_value
                    ev_value_internal = ls_parameter-param_int
                  ).

                APPEND ls_parameter TO ls_function-param_tab.
                CLEAR: ls_parameter.

              WHEN /iwcor/if_ds_expr_node=>kind_literal.                " Literal
                lo_literal ?= lr_parameter.

                ls_parameter-param_id = get_guid( ).
                ls_parameter-param_type = /iwcor/if_ds_expr_node=>kind_literal.
                ls_parameter-literal_type = lr_parameter->type->/iwcor/if_ds_edm_named~get_name( ).
                get_filter_string(
                  EXPORTING
                    io_filter         = lr_parameter
                  IMPORTING
                    ev_value_external = ls_parameter-param_value
                    ev_value_internal = ls_parameter-param_int
                ).

                " unquote the literal maintaining any single quotes in between
                lv_i = strlen( ls_parameter-param_value ).
                lv_i = lv_i - 1.
                ls_parameter-param_value = ls_parameter-param_value+0(lv_i).
                REPLACE FIRST OCCURRENCE OF `'` IN ls_parameter-param_value WITH ''.
                REPLACE ALL OCCURRENCES OF `''` IN ls_parameter-param_value WITH `'`.
                CLEAR lv_i.

                lv_i = strlen( ls_parameter-param_int ).
                lv_i = lv_i - 1.
                ls_parameter-param_int = ls_parameter-param_int+0(lv_i).
                REPLACE FIRST OCCURRENCE OF `'` IN ls_parameter-param_int WITH ''.
                REPLACE ALL OCCURRENCES OF `''` IN ls_parameter-param_int WITH `'`.
                CLEAR lv_i.
                ls_parameter-variable = lo_literal->variable.

                APPEND ls_parameter TO ls_function-param_tab.
                CLEAR: ls_parameter.

              WHEN /iwcor/if_ds_expr_node=>kind_function.               " Function

                create_filter_tree(
                  EXPORTING
                    io_filter         = lr_parameter
                  IMPORTING
                    ev_index          = ls_parameter-function_id
                ).

                ls_parameter-param_id = get_guid( ).
                ls_parameter-param_type = /iwcor/if_ds_expr_node=>kind_function.
                APPEND ls_parameter TO ls_function-param_tab.
                CLEAR: ls_parameter.

              WHEN /iwcor/if_ds_expr_node=>kind_binary.                 " Binary
*/ Need to check if Function can have a Binary Expression parameter
*              create_filter_tree(
*                EXPORTING
*                  io_filter         = lr_parameter
*                IMPORTING
*                  ev_index          = ls_parameter-function_id
*              ).
*
*              ls_parameter-param_id = get_guid( ).
*              ls_parameter-param_type = /iwcor/if_ds_expr_node=>kind_binary.
*              APPEND ls_parameter TO ls_function-param_tab.
*              CLEAR: ls_parameter.

              WHEN /iwcor/if_ds_expr_node=>kind_unary.                  " Unary
*/ Need to check if Function can have a Unary Expression parameter
*              create_filter_tree(
*                EXPORTING
*                  io_filter         = lr_parameter
*                IMPORTING
*                  ev_index          = ls_parameter-function_id
*              ).
*
*              ls_parameter-param_id = get_guid( ).
*              ls_parameter-param_type = /iwcor/if_ds_expr_node=>kind_unary.
*              APPEND ls_parameter TO ls_function-param_tab.
*              CLEAR: ls_parameter.

            ENDCASE.

          ENDLOOP.

          APPEND ls_function TO mt_functions.
          CLEAR: ls_function.

        WHEN OTHERS.
          RAISE EXCEPTION TYPE /iwcor/cx_ds_proc_unsupported.
      ENDCASE.
    ELSE.
      RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
    ENDIF.

  ENDMETHOD.


  METHOD expr_node_to_property_path.

    DATA lo_node         TYPE REF TO /iwcor/if_ds_expr_node.
    DATA lo_property     TYPE REF TO /iwcor/if_ds_expr_property.
    DATA lo_member       TYPE REF TO /iwcor/if_ds_expr_member.
    DATA lo_edm_property TYPE REF TO /iwcor/if_ds_edm_property.
    DATA lo_type         TYPE REF TO /iwcor/if_ds_edm_type.
    DATA lv_navi_name    TYPE string.


    CLEAR: et_property, ev_navi_path_ext, ev_navi_path_int.

    lo_node = io_node.

    WHILE lo_node IS BOUND.
      IF lo_node->kind = /iwcor/if_ds_expr_node=>kind_property.
        lo_property ?= lo_node.
        CLEAR lo_node.
      ELSEIF lo_node->kind = /iwcor/if_ds_expr_node=>kind_member.
        lo_member   ?= lo_node.
        lo_property ?= lo_member->path.
        lo_node      = lo_member->source_object.
      ELSE.
        RAISE EXCEPTION TYPE /iwcor/cx_ds_proc_unsupported.
      ENDIF.
      lo_type = lo_property->property->get_type( ).
      IF lo_type->kind = /iwcor/if_ds_edm_type=>kind_navigation.
        lv_navi_name = lo_property->property->get_name( ).
        IF ev_navi_path_ext IS INITIAL.
          ev_navi_path_ext = lv_navi_name.
        ELSE.
          CONCATENATE lv_navi_name
                      ev_navi_path_ext
            INTO ev_navi_path_ext SEPARATED BY '-'.
        ENDIF.

        lv_navi_name = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_property->property ).
        IF ev_navi_path_int IS INITIAL.
          ev_navi_path_int = lv_navi_name.
        ELSE.
          CONCATENATE lv_navi_name
                      ev_navi_path_int
            INTO ev_navi_path_int SEPARATED BY '-'.
        ENDIF.
      ENDIF.
      IF lo_type->kind <> /iwcor/if_ds_edm_type=>kind_navigation.
        lo_edm_property ?= lo_property->property.
        lo_type = lo_edm_property->get_type( ).
        IF lo_type->kind = /iwcor/if_ds_edm_type=>kind_simple OR
           lo_type->kind = /iwcor/if_ds_edm_type=>kind_complex.
          INSERT lo_edm_property INTO et_property INDEX 1.
        ELSE.
          RAISE EXCEPTION TYPE /iwcor/cx_ds_proc_unsupported.
        ENDIF.
      ENDIF.
    ENDWHILE.

  ENDMETHOD.                    "expr_node_to_property_path


  METHOD get_conditions.

    DATA: ls_conditions         TYPE /iwcor/rest_conditions.
    DATA: lo_target_entity_type TYPE REF TO /iwcor/if_ds_edm_entity_type.
    DATA: lx_tech_exception     TYPE REF TO /iwfnd/cx_mgw_tech_exception.

    DATA: lv_function_name    TYPE string.
    DATA: lo_function         TYPE REF TO /iwfnd/if_med_mdl_operation.
    DATA: lt_all_annotations  TYPE /iwfnd/t_med_mdl_annotations.
    DATA: ls_sap_annotations  TYPE /iwfnd/s_med_mdl_annotation.
    DATA: ls_annotation       TYPE /iwfnd/s_med_mdl_annot_key_val.
    DATA: lo_entity_type      TYPE REF TO /iwfnd/if_med_mdl_node.

    CLEAR es_conditions.

    mo_context->get_parameter( EXPORTING iv_name  = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-conditions
                               IMPORTING ev_value = ls_conditions ).

    IF ls_conditions IS INITIAL.
      RETURN.
    ENDIF.


* As of SP10 eTags are supported for function imports that "belong to an entity" (sap-action-for)
    IF io_function_import IS BOUND.
*   No eTag for GET
      IF iv_http_method = /iwcor/if_rest_request=>gc_method_get.
        "Currently this condition is never met as GET is handled by the lib;
        "See method /IWFND/CL_SODATA_ROOT_HANDLER->HANDLE_CONDITIONS
        CREATE OBJECT lx_tech_exception
          EXPORTING
            textid           = /iwfnd/cx_mgw_tech_exception=>function_import_etag_not_sup
            http_status_code = /iwfnd/cx_mgw_tech_exception=>gc_status_not_implemented.
        raise_technical_error( lx_tech_exception ).
      ENDIF.

*   Check "is-action-for"
      lv_function_name = io_function_import->get_name( ).
      lo_function = mo_service->get_function_import( iv_namespace = '' iv_name = lv_function_name ).
      lo_function->/iwfnd/if_med_mdl_element~get_annotations(
        IMPORTING
          et_annotations = lt_all_annotations ).
      READ TABLE lt_all_annotations
        INTO ls_sap_annotations
        WITH KEY namespace = /iwfnd/if_sodata_annotation=>gc_prefix_sap.  "'sap'

      IF ( sy-subrc = 0 ).
        READ TABLE ls_sap_annotations-valueset
          INTO ls_annotation
          WITH KEY key = /iwfnd/if_sodata_annotation=>gc_anno_action_for. "'action-for'
        IF ( sy-subrc = 0 ).
          lo_entity_type = mo_service->get_entity(
                           iv_namespace = ''
                           iv_name      = ls_annotation-value ).
          IF ( lo_entity_type IS BOUND ).
            get_etags( EXPORTING io_gateway_entity_type = lo_entity_type
                                 it_rest_etag           = ls_conditions-if_match
                       IMPORTING et_etag                = es_conditions-if_match ).

            get_etags( EXPORTING io_gateway_entity_type = lo_entity_type
                                 it_rest_etag           = ls_conditions-if_none_match
                       IMPORTING et_etag                = es_conditions-if_none_match ).
          ELSE.
            CREATE OBJECT lx_tech_exception
              EXPORTING
                textid      = /iwfnd/cx_mgw_tech_exception=>entity_type_unknown
                entity_name = ls_annotation-value.
            raise_technical_error( lx_tech_exception ).
          ENDIF.
        ELSE.
          CREATE OBJECT lx_tech_exception
            EXPORTING
              textid           = /iwfnd/cx_mgw_tech_exception=>function_import_etag_not_sup
              http_status_code = /iwfnd/cx_mgw_tech_exception=>gc_status_not_implemented.
          raise_technical_error( lx_tech_exception ).
        ENDIF.

        " currently only eTag support on FI for POST/PUT
        IF ls_conditions-if_match IS NOT INITIAL AND
           iv_http_method <> /iwcor/if_rest_request=>gc_method_put AND
           iv_http_method <> /iwcor/if_rest_request=>gc_method_post.
          CREATE OBJECT lx_tech_exception
            EXPORTING
              textid           = /iwfnd/cx_mgw_tech_exception=>fi_etag_not_sup_http_meth
              http_method      = iv_http_method
              http_status_code = /iwfnd/cx_mgw_tech_exception=>gc_status_not_implemented.
          raise_technical_error( lx_tech_exception ).
        ENDIF.

      ENDIF.

      RETURN.
    ENDIF.


* eTag for e.g. an update on an entity
    IF io_target_entity_set IS NOT INITIAL.

      lo_target_entity_type = io_target_entity_set->get_entity_type( ).

      "get/convert if_match eTags for backend call
      get_etags( EXPORTING io_target_entity_type = lo_target_entity_type
                           it_rest_etag          = ls_conditions-if_match
                 IMPORTING et_etag               = es_conditions-if_match ).

      "get/convert if_mone_match eTags for backend call
      get_etags( EXPORTING io_target_entity_type = lo_target_entity_type
                           it_rest_etag          = ls_conditions-if_none_match
                 IMPORTING et_etag               = es_conditions-if_none_match ).

    ELSE.
      CREATE OBJECT lx_tech_exception
        EXPORTING
          textid = /iwfnd/cx_mgw_tech_exception=>target_entity_set_not_found.
      raise_technical_error( lx_tech_exception ).
    ENDIF.

    IF iv_http_method = /iwcor/if_rest_request=>gc_method_post AND
       ( es_conditions-if_match IS NOT INITIAL OR es_conditions-if_none_match IS NOT INITIAL ).
      CREATE OBJECT lx_tech_exception
        EXPORTING
          textid           = /iwfnd/cx_mgw_tech_exception=>etag_not_sup_http_meth
          http_method      = iv_http_method
          http_status_code = /iwfnd/cx_mgw_tech_exception=>gc_status_not_implemented.
      raise_technical_error( lx_tech_exception ).
    ENDIF.

  ENDMETHOD.


  METHOD get_entry_data.

    DATA: lv_is_target_format       TYPE abap_bool,
          lt_dollar_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lv_is_co_deployment       TYPE abap_bool,
          lv_is_independent_read_op TYPE abap_bool,
          lo_mgw_runtime            TYPE REF TO /iwfnd/bd_mgw_runtime,
          lx_busi_exception         TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception         TYPE REF TO /iwfnd/cx_mgw_tech_exception.

*-with Function Import
    IF is_function_import_info IS NOT INITIAL.
      me->get_fi_data_by_read(
            EXPORTING
              io_entity_set           = io_entity_set
              is_function_import_info = is_function_import_info
              iv_format               = iv_format
              iv_for_ds_operation     = iv_for_ds_operation
            CHANGING
              cr_entityset            = er_data
              cs_odata_request        = es_request_details
          ).
    ELSE.
*---Initialization
      me->init_request(
        EXPORTING
          iv_http_method      = /iwcor/if_rest_request=>gc_method_get
          iv_operation        = mcs_operations-read
          iv_operation_type   = mcs_operation_types-entry
          io_entity_set       = io_entity_set
          it_key              = it_key
          it_navigation_path  = it_navigation_path
          it_select           = it_select
          io_filter           = io_filter
          iv_format           = iv_format
          iv_for_ds_operation = iv_for_ds_operation
        IMPORTING
          es_request          = es_request_details
          et_parameter        = lt_dollar_parameter
      ).

      lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
      IF mv_batch EQ abap_true.
        lv_is_independent_read_op = mo_batch_helper->is_independent_read( ).
      ENDIF.

      IF lv_is_co_deployment EQ abap_true AND
               ( mv_batch EQ abap_false OR
                lv_is_independent_read_op EQ abap_true ).

        mo_co_deploym_proxy = mo_transaction_handler->get_co_deploym_proxy( ).

        TRY.

            mo_co_deploym_proxy->init(
              EXPORTING
                iv_service_document_name = mv_service_name
                iv_namespace             = mv_namespace
                iv_version               = mv_service_version
                iv_base_url              = mv_base_url
                iv_entity_name           = mv_target_entity
                io_context               = mo_mgw_context
                io_batch_helper          = mo_batch_helper ).

            mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~read_entity(
               EXPORTING
                 iv_entity_name       = mv_target_entity
                 iv_entity_set_name   = mv_target_entity_set
                 iv_source_name       = mv_source_entity
                 is_request_details   = es_request_details
                 iv_content_format    = mv_content_format
                 it_parameter         = lt_dollar_parameter
               CHANGING
                 ct_headers           = mt_entity_headers
                 cr_entity            = er_data
                 cv_is_target_format  = lv_is_target_format
                 cs_response_context  = es_response_context ).

*            IF es_response_context-is_ral_relevant EQ abap_true.
*              mo_transaction_handler->set_is_ral(  ).
*            ENDIF.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).

          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.
      ELSE.

*---get MGW runtime
        lo_mgw_runtime = get_mgw_runtime(  ).

*---Call Provider Delegator
        TRY.
            CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~read_entity
              EXPORTING
                iv_entity_name      = mv_target_entity
                iv_entity_set_name  = mv_target_entity_set
                iv_source_name      = mv_source_entity
                is_request_details  = es_request_details
                iv_content_format   = mv_content_format
                it_parameter        = lt_dollar_parameter
              CHANGING
                ct_headers          = mt_entity_headers
                cr_entity           = er_data
                cv_is_target_format = lv_is_target_format
                cs_response_context = es_response_context.

          CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
            raise_business_error( lx_busi_exception ).
          CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
            raise_technical_error( lx_tech_exception ).
        ENDTRY.
      ENDIF.
    ENDIF.

  ENDMETHOD.                    "get_entry_data


  METHOD get_etags.

    DATA: ls_etag         TYPE /iwfnd/if_mgw_core_types=>ty_s_etag,
          lv_is_null_etag TYPE abap_bool.
    DATA: lt_etag_value TYPE /iwfnd/if_mgw_core_types=>ty_t_etag_value,
          ls_etag_value TYPE /iwfnd/if_mgw_core_types=>ty_s_etag_value.
    DATA: lv_property_name  TYPE string,
          lt_property_names TYPE string_table.
    DATA: ld_facets           TYPE REF TO /iwcor/if_ds_edm_element=>facets_s.
    DATA: lo_property         TYPE REF TO /iwcor/if_ds_edm_property.
    DATA: lv_index            TYPE i.
    DATA: lo_edm_simple_type  TYPE REF TO /iwcor/if_ds_edm_simple_type.
    DATA: lv_tag_literal TYPE string,
          lv_conv_value  TYPE string.
    DATA: ls_rest_etag        TYPE /iwcor/rest_etag_s.
    DATA: lt_tag_value TYPE STANDARD TABLE OF string,
          lv_tag_value TYPE string.
    DATA: lx_ds_edm_null      TYPE REF TO /iwcor/cx_ds_edm_null.
    DATA: lx_ds_edm_type      TYPE REF TO /iwcor/cx_ds_edm_type_error.
    DATA: lx_tech_exception   TYPE REF TO /iwfnd/cx_mgw_tech_exception.

    DATA: lt_gw_properties    TYPE /iwfnd/if_med_mdl_element=>ty_med_mdl_property_table.
    DATA: ls_gw_property      TYPE /iwfnd/if_med_mdl_element=>ty_med_mdl_property.
    DATA: lv_target_path      TYPE string.



    CLEAR et_etag.

    IF it_rest_etag IS INITIAL.
      RETURN.
    ENDIF.

    READ TABLE it_rest_etag INTO ls_rest_etag WITH TABLE KEY tag = '*'. "#EC CI_STDSEQ
    IF sy-subrc EQ 0.
      ls_etag-any_value = abap_true.
      APPEND ls_etag TO et_etag.
    ELSE.

      "check for properties with concurency mode
      IF ( io_target_entity_type IS BOUND ).
        io_target_entity_type->/iwcor/if_ds_edm_struct_type~get_property_names( IMPORTING et_property = lt_property_names ).
      ELSE. "io_gateway_entity_type is bound
        io_gateway_entity_type->get_properties(
          IMPORTING
            et_properties = lt_gw_properties
        ).
      ENDIF.


      LOOP AT it_rest_etag INTO ls_rest_etag.

        SPLIT ls_rest_etag-tag AT ',' INTO TABLE lt_tag_value.
        lv_is_null_etag = xsdbool( ls_rest_etag-tag IS INITIAL AND ls_rest_etag-weak = abap_true ).
        lv_index = 0.

        CLEAR: ls_etag, lt_etag_value.


        IF ( io_target_entity_type IS BOUND ). "Something like update for Entity
          LOOP AT lt_property_names INTO lv_property_name.

            TRY.
                lo_property ?= io_target_entity_type->get_property( lv_property_name ).

                ld_facets = lo_property->get_facets( ).

                IF ld_facets->concurrency_mode = /iwcor/if_ds_edm=>gc_concurrency_mode_fixed.
                  ADD 1 TO lv_index.

                  ls_etag_value-name  = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_property ).

*             get if-match value / here we rely on the property order
                  READ TABLE lt_tag_value INTO lv_tag_value INDEX lv_index.

                  IF sy-subrc EQ 0.
                    TRY.
                        /iwcor/cl_ds_edm_simple_type=>parse_uri_literal( EXPORTING iv_uri_literal = lv_tag_value
                                                                         IMPORTING eo_simple_type = lo_edm_simple_type
                                                                                   ev_literal     = lv_tag_literal ).
                      CATCH /iwcor/cx_ds_edm_null INTO lx_ds_edm_null.
                        CREATE OBJECT lx_tech_exception
                          EXPORTING
                            previous = lx_ds_edm_null.
                        raise_technical_error( lx_tech_exception ).

                      CATCH /iwcor/cx_ds_edm_type_error INTO lx_ds_edm_type.
                        CREATE OBJECT lx_tech_exception
                          EXPORTING
                            textid = /iwfnd/cx_mgw_tech_exception=>etag_format_invalid.

                        raise_technical_error( lx_tech_exception ).

                    ENDTRY.

                    lv_conv_value = convert_value( io_simple_type  = lo_edm_simple_type
                                                   io_edm_instance = lo_property
                                                   iv_value        = lv_tag_literal ).

                    ls_etag_value-value = lv_conv_value.
                    APPEND ls_etag_value TO lt_etag_value.

                  ELSEIF lv_is_null_etag = abap_true.  " Handle null etag
                    ls_etag_value-value = convert_value( io_simple_type            = CAST #( lo_property->get_type( ) )
                                                         io_edm_instance           = lo_property
                                                         iv_use_abap_default_value = abap_true ).
                    APPEND ls_etag_value TO lt_etag_value.

                  ENDIF.

                ENDIF.

              CATCH cx_sy_move_cast_error.              "#EC NO_HANDLER
                CONTINUE.
            ENDTRY.

          ENDLOOP.


        ELSE. "io_gateway_entity_type is bound - modifying function imports
          LOOP AT lt_gw_properties INTO ls_gw_property.
            lv_target_path = ls_gw_property-attribute->get_fc_target_path( ).
            IF lv_target_path = 'etag'.
              ADD 1 TO lv_index.
*             get if-match value / here we rely on the property order
              READ TABLE lt_tag_value INTO lv_tag_value INDEX lv_index.
              IF sy-subrc EQ 0.

                TRY.
                    /iwcor/cl_ds_edm_simple_type=>parse_uri_literal( EXPORTING iv_uri_literal = lv_tag_value
                                                                     IMPORTING eo_simple_type = lo_edm_simple_type
                                                                               ev_literal     = lv_tag_literal ).
                  CATCH /iwcor/cx_ds_edm_null INTO lx_ds_edm_null.
                    CREATE OBJECT lx_tech_exception
                      EXPORTING
                        previous = lx_ds_edm_null.
                    raise_technical_error( lx_tech_exception ).

                  CATCH /iwcor/cx_ds_edm_type_error INTO lx_ds_edm_type.
                    CREATE OBJECT lx_tech_exception
                      EXPORTING
                        textid = /iwfnd/cx_mgw_tech_exception=>etag_format_invalid.
*                     previous = lx_ds_edm_type.
                    raise_technical_error( lx_tech_exception ).

                ENDTRY.

                lv_conv_value = convert_value( io_simple_type  = lo_edm_simple_type
                                               io_gw_property  = ls_gw_property-attribute
                                               iv_value        = lv_tag_literal ).


                ls_etag_value-name  = ls_gw_property-attribute->get_technical_name( ).
                ls_etag_value-value = lv_conv_value.
                APPEND ls_etag_value TO lt_etag_value.

              ELSEIF lv_is_null_etag = abap_true.  " Handle null etag
                lo_edm_simple_type =
                /iwcor/cl_ds_edm_simple_type=>get_instance( substring_after( val = ls_gw_property-attribute->get_core_type( ) sub = 'Edm.' ) ).
                ls_etag_value-value = convert_value( io_simple_type            = lo_edm_simple_type
                                                     io_gw_property            = ls_gw_property-attribute
                                                     iv_use_abap_default_value = abap_true ).
                APPEND ls_etag_value TO lt_etag_value.

              ENDIF.

            ENDIF.
          ENDLOOP.

          IF ( lv_index = 0 ). "The entity does not have a property used as eTag => The entity does not support eTags
            CREATE OBJECT lx_tech_exception
              EXPORTING
                textid           = /iwfnd/cx_mgw_tech_exception=>function_import_etag_not_sup
                http_status_code = /iwfnd/cx_mgw_tech_exception=>gc_status_not_implemented.
            raise_technical_error( lx_tech_exception ).
          ENDIF.

        ENDIF.


        IF lt_etag_value IS NOT INITIAL.
          ls_etag-tag_values = lt_etag_value.
          ls_etag-weak       = ls_rest_etag-weak.
          APPEND ls_etag TO et_etag.
        ENDIF.

      ENDLOOP.

    ENDIF.

  ENDMETHOD.


  METHOD get_expand_string.

    DATA: lv_expand_external        TYPE string,
          lv_expand_internal        TYPE string,
          lv_property_name_external TYPE string,
          lv_property_name_internal TYPE string,
          ls_navi_property          TYPE /iwcor/if_ds_uri=>navigation_property_s,
          lt_navi_property          TYPE /iwcor/if_ds_uri=>navigation_property_t.

    LOOP AT it_expand INTO lt_navi_property.
      IF ev_expand_external IS NOT INITIAL.
        CONCATENATE ev_expand_external ',' INTO ev_expand_external.
        CLEAR lv_expand_external.
        CONCATENATE ev_expand_internal ',' INTO ev_expand_internal.
        CLEAR lv_expand_internal.
      ENDIF.
      LOOP AT lt_navi_property INTO ls_navi_property.
        lv_property_name_external = ls_navi_property-navigation_property->get_name( ).
        lv_property_name_internal = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( ls_navi_property-navigation_property ).

        IF lv_expand_external IS NOT INITIAL.
          CONCATENATE lv_expand_external '/' INTO lv_expand_external.
          CONCATENATE lv_expand_internal '/' INTO lv_expand_internal.
        ENDIF.
        CONCATENATE lv_expand_external lv_property_name_external INTO lv_expand_external.
        CONCATENATE lv_expand_internal lv_property_name_internal INTO lv_expand_internal.
      ENDLOOP.
      CONCATENATE ev_expand_external lv_expand_external INTO ev_expand_external.
      CONCATENATE ev_expand_internal lv_expand_internal INTO ev_expand_internal.
    ENDLOOP.

  ENDMETHOD.                    "get_expand_string


  METHOD get_filter_string.

    CONSTANTS: co_db_wild_card  TYPE char1 VALUE '%'.

    DATA: lv_leng                 TYPE i,
          lv_operator_external    TYPE string,
          lv_operator_internal    TYPE string,
          lv_function             TYPE string,
          lv_value_help           TYPE string,
          lv_value_left_external  TYPE string,
          lv_value_left_internal  TYPE string,
          lv_value_right_external TYPE string,
          lv_value_right_internal TYPE string,
          lo_unary                TYPE REF TO /iwcor/if_ds_expr_unary,
          lo_binary               TYPE REF TO /iwcor/if_ds_expr_binary,
          lo_literal              TYPE REF TO /iwcor/if_ds_expr_literal,
          lo_function             TYPE REF TO /iwcor/if_ds_expr_function.

    FIELD-SYMBOLS: <ls_param_name>  TYPE REF TO /iwcor/if_ds_expr_node,
                   <ls_param_value> TYPE REF TO /iwcor/if_ds_expr_node.

    IF io_filter IS BOUND.

      CASE io_filter->kind.
        WHEN /iwcor/if_ds_expr_node=>kind_unary.
          lo_unary ?= io_filter.
          lv_operator_external = lv_operator_internal = lo_unary->operator.
          IF lv_operator_external = /iwcor/if_ds_expr_types=>gc_operator_not.
            lv_operator_external = lv_operator_internal = 'not '.
          ENDIF.
          get_filter_string(
            EXPORTING
              io_filter         = lo_unary->operand
            IMPORTING
              ev_value_external = ev_value_external
              ev_value_internal = ev_value_internal
          ).
          CONCATENATE '('
                      lv_operator_external
                      ev_value_external
                      ')'
            INTO ev_value_external SEPARATED BY space RESPECTING BLANKS.
          CONCATENATE '('
                      lv_operator_internal
                      ev_value_internal
                      ')'
            INTO ev_value_internal SEPARATED BY space RESPECTING BLANKS.

        WHEN /iwcor/if_ds_expr_node=>kind_binary.
          lo_binary ?= io_filter.
          IF  lo_binary->right_operand->kind = /iwcor/if_ds_expr_node=>kind_property OR
              lo_binary->right_operand->kind = /iwcor/if_ds_expr_node=>kind_member.
            RAISE EXCEPTION TYPE /iwcor/cx_ds_proc_unsupported.
          ENDIF.
          CASE lo_binary->operator.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_add.
              lv_operator_external = lv_operator_internal = '+'.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_sub.
              lv_operator_external = lv_operator_internal = '-'.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_mul.
              lv_operator_external = lv_operator_internal = '*'.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_div.
              lv_operator_external = lv_operator_internal = '/'.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_eq.
              lv_operator_external = lo_binary->operator.
              lv_operator_internal = '='.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_ne.
              lv_operator_external = lo_binary->operator.
              lv_operator_internal = '<>'.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_lt.
              lv_operator_external = lo_binary->operator.
              lv_operator_internal = '<'.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_le.
              lv_operator_external = lo_binary->operator.
              lv_operator_internal = '<='.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_gt.
              lv_operator_external = lo_binary->operator.
              lv_operator_internal = '>'.
            WHEN /iwcor/if_ds_expr_types=>gc_operator_ge.
              lv_operator_external = lo_binary->operator.
              lv_operator_internal = '>='.
            WHEN OTHERS.
              lv_operator_external = lv_operator_internal = lo_binary->operator.
          ENDCASE.
          get_filter_string(
            EXPORTING
              io_filter         = lo_binary->left_operand
            IMPORTING
              ev_value_external = lv_value_left_external
              ev_value_internal = lv_value_left_internal
          ).
          get_filter_string(
            EXPORTING
              io_filter         = lo_binary->right_operand
            IMPORTING
              ev_value_external = lv_value_right_external
              ev_value_internal = lv_value_right_internal
          ).
          CONCATENATE '('
                      lv_value_left_external
                      lv_operator_external
                      lv_value_right_external
                      ')'
            INTO ev_value_external SEPARATED BY space.
          CONCATENATE '('
                      lv_value_left_internal
                      lv_operator_internal
                      lv_value_right_internal
                      ')'
            INTO ev_value_internal SEPARATED BY space.

        WHEN /iwcor/if_ds_expr_node=>kind_property OR
             /iwcor/if_ds_expr_node=>kind_member.
          get_name_from_property_member(
            EXPORTING
              io_node           = io_filter
            IMPORTING
              ev_value_external = ev_value_external
              ev_value_internal = ev_value_internal
          ).

        WHEN /iwcor/if_ds_expr_node=>kind_literal.
          lo_literal ?= io_filter.
          IF io_filter->type IS NOT BOUND.
            ev_value_external = lo_literal->literal.
          ELSE.
            CASE io_filter->type->/iwcor/if_ds_edm_named~get_name( ).
              WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_guid.
                ev_value_external = correct_guid_value(
                                      io_simple_type   = ms_name_mapping-edm_simple_type
                                      iv_value         = lo_literal->literal
                                    ).
                CONDENSE ev_value_external.
                ev_value_external = cl_abap_dyn_prg=>quote( ev_value_external ).
              WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_boolean.
                ev_value_external = correct_boolean_value(
                                      io_simple_type   = ms_name_mapping-edm_simple_type
                                      iv_value         = lo_literal->literal
                                    ).
                IF ev_value_external IS INITIAL.
                  ev_value_external = ''' '''.
                ELSE.
                  ev_value_external = cl_abap_dyn_prg=>quote( ev_value_external ).
                ENDIF.
              WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_datetime       OR
                   /iwcor/cl_ds_edm_simple_type=>gc_name_datetimeoffset OR
                   /iwcor/cl_ds_edm_simple_type=>gc_name_time.
                ev_value_external = correct_datetime_value(
                                      iv_internal_type = ms_name_mapping-internal_type
                                      iv_length        = ms_name_mapping-length
                                      io_simple_type   = ms_name_mapping-edm_simple_type
                                      iv_value         = lo_literal->literal
                                    ).
                ev_value_external = cl_abap_dyn_prg=>quote( ev_value_external ).
              WHEN OTHERS.
                ev_value_external = cl_abap_dyn_prg=>quote( lo_literal->literal ).
            ENDCASE.
          ENDIF.
          ev_value_internal = ev_value_external.
          CLEAR ms_name_mapping.

        WHEN /iwcor/if_ds_expr_node=>kind_function.
          lo_function ?= io_filter.
          lv_function = lo_function->function.
          IF lv_function <> co_filter_startswith AND
             lv_function <> co_filter_endswith   AND
             lv_function <> co_filter_substringof.
            RAISE EXCEPTION TYPE /iwcor/cx_ds_expr_selopt_error
              EXPORTING
                textid   = /iwcor/cx_ds_expr_selopt_error=>invalid_function
                function = lv_function.
          ENDIF.
          READ TABLE lo_function->parameters ASSIGNING <ls_param_name>  INDEX 1.
          READ TABLE lo_function->parameters ASSIGNING <ls_param_value> INDEX 2.
          lv_operator_external = lv_operator_internal = ','.
          get_filter_string(
            EXPORTING
              io_filter         = <ls_param_name>
            IMPORTING
              ev_value_external = lv_value_left_external
              ev_value_internal = lv_value_left_internal
          ).
          get_filter_string(
            EXPORTING
              io_filter         = <ls_param_value>
            IMPORTING
              ev_value_external = lv_value_right_external
              ev_value_internal = lv_value_right_internal
          ).
          CONCATENATE '('
                      lv_function
                      '('
                      lv_value_left_external
                      lv_operator_external
                      lv_value_right_external
                      ') )'
            INTO ev_value_external SEPARATED BY space.

          "Filter Where Clause for string operations
          IF lv_function = co_filter_substringof.
            lv_value_help           = lv_value_left_internal.
            lv_value_left_internal  = lv_value_right_internal.
            lv_value_right_internal = lv_value_help.
          ENDIF.
          lv_leng = strlen( lv_value_right_internal ) - 2.
          lv_value_help = lv_value_right_internal+1(lv_leng).
          IF lv_function = co_filter_startswith.
            IF ms_name_mapping-length <> 1.
              CONCATENATE lv_value_help co_db_wild_card INTO lv_value_help.
            ENDIF.
          ELSEIF lv_function = co_filter_endswith.
            IF ms_name_mapping-length <> 1.
              CONCATENATE co_db_wild_card lv_value_help INTO lv_value_help.
            ENDIF.
          ELSEIF lv_function = co_filter_substringof.
            IF lv_value_help IS INITIAL.
              lv_value_help = co_db_wild_card.
            ELSE.
              IF ms_name_mapping-length <> 1.
                CONCATENATE co_db_wild_card lv_value_help co_db_wild_card INTO lv_value_help.
              ENDIF.
            ENDIF.
          ENDIF.
          lv_value_right_internal = cl_abap_dyn_prg=>quote( lv_value_help ).
          lv_operator_internal = 'like'.
          CONCATENATE '('
                      lv_value_left_internal
                      lv_operator_internal
                      lv_value_right_internal
                      ')'
            INTO ev_value_internal SEPARATED BY space.

        WHEN OTHERS.
          RAISE EXCEPTION TYPE /iwcor/cx_ds_proc_unsupported.
      ENDCASE.
    ELSE.
      RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
    ENDIF.

  ENDMETHOD.                    "get_filter_string


  METHOD get_fi_data_by_read.

    DATA: lv_http_method            TYPE string VALUE /iwcor/if_rest_request=>gc_method_get,
          lv_http_method_fi         TYPE string,
          lv_action_name            TYPE string,
          lv_is_target_format       TYPE abap_bool,
          ls_action_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_s,
          lt_action_parameter       TYPE /iwfnd/if_mgw_core_types=>parameter_values_t,
          lv_return_type            TYPE string,
          lo_mgw_runtime            TYPE REF TO /iwfnd/bd_mgw_runtime,
          lo_entity_set             TYPE REF TO /iwcor/if_ds_edm_entity_set,
          lo_edm_return_type        TYPE REF TO /iwcor/if_ds_edm_typed,
          lo_edm_type               TYPE REF TO /iwcor/if_ds_edm_type,
          lv_is_co_deployment       TYPE abap_bool,
          lv_is_independent_read_op TYPE abap_bool,
          lo_function_import        TYPE REF TO /iwcor/if_ds_edm_func_import,
          lt_parameter_value        TYPE /iwcor/if_ds_uri=>parameter_value_t,
          lr_parameter_value        TYPE REF TO /iwcor/if_ds_uri=>parameter_value_s,
          lt_uri_parameters         TYPE /iwfnd/if_mgw_core_types=>parameter_values_t.

    DATA: lv_lib_type_kind          TYPE /iwcor/if_ds_edm_type=>type_kind.
    DATA: lv_sodata_multipli TYPE /iwcor/if_ds_edm=>edm_multiplicity,
          lv_multiplicity    TYPE /iwfnd/med_mdl_cardinality,
          lx_busi_exception  TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception  TYPE REF TO /iwfnd/cx_mgw_tech_exception.


* Initialization
    CLEAR cs_odata_request.
    lo_function_import = is_function_import_info-function_import.
    lt_parameter_value = is_function_import_info-parameter.
    lv_action_name     = lo_function_import->get_name( ).

* Return Type
    lo_edm_return_type = lo_function_import->get_return_type( ).
    lo_edm_type        = lo_edm_return_type->get_type( ).
    lv_return_type     = lo_edm_type->/iwcor/if_ds_edm_named~get_name( ).
    lv_lib_type_kind   = lo_edm_type->kind.
    IF lv_lib_type_kind = /iwcor/if_ds_edm_type=>kind_entity. "The new name mapping currently only exists for entity types
      cs_odata_request-technical_request-action_return_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_edm_type ).
    ELSE.
      cs_odata_request-technical_request-action_return_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name( lo_edm_type ).
    ENDIF.


* Multiplicity
    lv_sodata_multipli = lo_edm_return_type->get_multiplicity( ).
    CASE lv_sodata_multipli.
      WHEN /iwcor/if_ds_edm=>gc_multiplicity_zero_one.
        lv_multiplicity = mcs_cardinality-cardinality_0_1.
      WHEN /iwcor/if_ds_edm=>gc_multiplicity_one.
        lv_multiplicity = mcs_cardinality-cardinality_1.
      WHEN /iwcor/if_ds_edm=>gc_multiplicity_many.
        lv_multiplicity = mcs_cardinality-cardinality_0_n.
    ENDCASE.

    lv_http_method_fi = is_function_import_info-function_import->get_http_method( ).
    IF lv_http_method_fi IS NOT INITIAL.
      lv_http_method = lv_http_method_fi.
    ENDIF.

    me->init_request(
      EXPORTING
        iv_http_method       = lv_http_method
        iv_operation         = mcs_operations-operation
        io_function_import   = lo_function_import
        io_entity_set        = io_entity_set
        it_expand            = it_expand
        it_select            = it_select
        iv_format            = iv_format
        iv_for_ds_operation  = iv_for_ds_operation
        iv_edm_multiplicity  = lv_sodata_multipli
        iv_mdl_cardinality   = lv_multiplicity
      IMPORTING
        et_parameter         = lt_uri_parameters
        eo_target_entity_set = eo_target_entity_set
        es_request           = cs_odata_request
    ).

    cs_odata_request-uri_parameters = lt_uri_parameters.

* Function Import Parameters
    LOOP AT lt_parameter_value REFERENCE INTO lr_parameter_value.
      ls_action_parameter-name  = lr_parameter_value->parameter->get_name( ).
      ls_action_parameter-value = me->convert_value_by_parameter( lr_parameter_value ).
      APPEND ls_action_parameter TO lt_action_parameter.
      ls_action_parameter-name = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lr_parameter_value->parameter ).
      APPEND ls_action_parameter TO cs_odata_request-technical_request-action_parameters.
    ENDLOOP.

* Function Import External Names
    cs_odata_request-function-name = lv_action_name.
    cs_odata_request-function-return_type = lv_return_type.
    lo_entity_set = lo_function_import->get_entity_set( ).
    IF lo_entity_set IS BOUND.
      cs_odata_request-function-entity_set = lo_entity_set->get_name( ).
    ENDIF.
    cs_odata_request-function-http_method = lo_function_import->get_http_method( ).


    lv_is_co_deployment = mo_transaction_handler->is_co_deployment_scenario( ).
    IF mv_batch EQ abap_true.
      lv_is_independent_read_op = mo_batch_helper->is_independent_read( ).
    ENDIF.

    IF lv_is_co_deployment EQ abap_true AND
                ( mv_batch EQ abap_false OR
                 lv_is_independent_read_op EQ abap_true ).

      mo_co_deploym_proxy = mo_transaction_handler->get_co_deploym_proxy( ).

      TRY.

          mo_co_deploym_proxy->init(
            EXPORTING
              iv_service_document_name = mv_service_name
              iv_namespace             = mv_namespace
              iv_version               = mv_service_version
              iv_base_url              = mv_base_url
              iv_entity_name           = mv_target_entity
              iv_function_name         = lv_action_name
              io_context               = mo_mgw_context
              io_batch_helper          = mo_batch_helper ).

          mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~exec_service_operation(
             EXPORTING
               iv_name             = lv_action_name
               iv_return_type      = lv_return_type
               iv_multiplicity     = lv_multiplicity
               is_request_details   = cs_odata_request
               iv_content_format    = mv_content_format
               it_parameter         = lt_action_parameter
             CHANGING
               ct_headers           = mt_entity_headers
               cr_data              = cr_entityset
               cs_response_context  = cs_response_context
               cv_is_target_format  = lv_is_target_format
               ct_inline_info       = ct_inline_info ).

        CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
          raise_business_error( lx_busi_exception ).

        CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
          raise_technical_error( lx_tech_exception ).
      ENDTRY.

    ELSE.

* Get MGW Runtime
      lo_mgw_runtime = get_mgw_runtime( lv_action_name  ).

      TRY.
          CALL BADI lo_mgw_runtime->/iwfnd/if_mgw_core_runtime~exec_service_operation
            EXPORTING
              iv_name             = lv_action_name
              iv_return_type      = lv_return_type
              iv_multiplicity     = lv_multiplicity
              iv_content_format   = mv_content_format
              it_parameter        = lt_action_parameter
              is_request_details  = cs_odata_request
            CHANGING
              cr_data             = cr_entityset
              ct_headers          = mt_entity_headers
              cs_response_context = cs_response_context
              cv_is_target_format = lv_is_target_format
              ct_inline_info      = ct_inline_info.

        CATCH /iwfnd/cx_mgw_busi_exception INTO lx_busi_exception.
          raise_business_error( lx_busi_exception ).

        CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
          raise_technical_error( lx_tech_exception ).
      ENDTRY.
    ENDIF.

  ENDMETHOD.                    "get_fi_data_by_read


  METHOD get_guid.

*/ Use FM 'GUID_CREATE' for compatibility with lower NetWeaver release
    CALL FUNCTION 'GUID_CREATE'         "#EC FB_OLDED
      IMPORTING
        ev_guid_32 = rv_guid.

  ENDMETHOD.


  METHOD get_mgw_context.

    DATA: lv_http_method        TYPE string,
          lv_icf_name           TYPE string,
          lt_all_req_parameters TYPE tihttpnvp,
          lv_host_name          TYPE string,
          lo_mgw_msg_container  TYPE REF TO /iwfnd/if_mgw_msg_container.



    IF mo_mgw_context IS NOT BOUND.

      CREATE OBJECT mo_mgw_context TYPE /iwfnd/cl_mgw_context.


      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_request_user
          iv_value = sy-uname
      ).

      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_service_name
          iv_value = me->mv_service_name
      ).

      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_service_version
          iv_value = me->mv_service_version
      ).

      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_service_namespace
          iv_value = me->mv_namespace
      ).

      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_logger
          iv_value = mo_logger
      ).

      lo_mgw_msg_container = /iwfnd/cl_mgw_msg_container=>get_mgw_msg_container( ).
      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_message_container
          iv_value = lo_mgw_msg_container
      ).

      mo_context->get_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_http_method
        IMPORTING
          ev_value = lv_http_method
      ).
      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_http_method
          iv_value = lv_http_method
      ).

      mo_context->get_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_icf_node_name
        IMPORTING
          ev_value = lv_icf_name
      ).
      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_icf_node_name
          iv_value = lv_icf_name
      ).

      mo_context->get_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_sodata_types=>gcs_cor_request_parameters-host
        IMPORTING
          ev_value = lv_host_name
      ).
      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_mgw_context=>gc_param_host_name
          iv_value = lv_host_name
      ).

* $select must be tunneled through MGW context (not an input parameter)
      IF mt_select_params IS NOT INITIAL.
        mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
          EXPORTING
            iv_name  = /iwfnd/if_mgw_context=>gc_param_dollar_select
            iv_value = mt_select_params
        ).
      ENDIF.

* List of all request header
      mo_context->get_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-all_parameters
        IMPORTING
          ev_value = lt_all_req_parameters
      ).
      mo_mgw_context->/iwfnd/if_mgw_context~set_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-all_parameters
          iv_value = lt_all_req_parameters
      ).


    ENDIF.

    ro_context = mo_mgw_context.

  ENDMETHOD.                    "get_mgw_context


  METHOD get_mgw_runtime.

*    CONSTANTS: lc_default_version TYPE /iwfnd/med_mdl_version VALUE 0001.
*    DATA: lt_segment_params   TYPE tihttpnvp,
*          ls_segment_param    TYPE ihttpnvp.

    DATA: lv_badi_num         TYPE i.
    DATA: lo_mgw_context      TYPE REF TO /iwfnd/if_mgw_context.
    DATA: lv_consumer_type    TYPE /iwfnd/cor_consumer_id.
    DATA: lv_service_name     TYPE string.


    IF mo_mgw_runtime IS BOUND.
      ro_badi = mo_mgw_runtime.
      RETURN.
    ENDIF.

    lv_consumer_type = mo_transaction_handler->get_consumer_type( ).

    lv_service_name = mv_service_name.
    TRANSLATE lv_service_name  TO UPPER CASE.
    TRANSLATE lv_consumer_type TO UPPER CASE.

*   Get the runtime implementation via BAdI
    GET BADI ro_badi
      FILTERS
        service_document_name = lv_service_name
        service_namespace     = mv_namespace
        service_version       = mv_service_version
        consumer              = lv_consumer_type.

*---ro_badi is always bound as it returns the default implementation if no filter matches
    DESCRIBE TABLE ro_badi->imps LINES lv_badi_num.
    IF lv_badi_num GT 0.
      lo_mgw_context = get_mgw_context( ).
      CALL BADI ro_badi->/iwfnd/if_mgw_core_runtime~init
        EXPORTING
          iv_service_document_name = mv_service_name
          iv_namespace             = mv_namespace
          iv_version               = mv_service_version
          iv_base_url              = mv_base_url
          io_context               = lo_mgw_context
          iv_entity_name           = mv_target_entity
          iv_function_name         = iv_function_name.
    ENDIF.

    mo_mgw_runtime = ro_badi.

  ENDMETHOD.                    "get_mgw_runtime


  METHOD get_name_from_property_member.

    DATA: lo_node         TYPE REF TO /iwcor/if_ds_expr_node,
          lo_property     TYPE REF TO /iwcor/if_ds_expr_property,
          lo_member       TYPE REF TO /iwcor/if_ds_expr_member,
          lo_edm_property TYPE REF TO /iwcor/if_ds_edm_property,
          lo_type         TYPE REF TO /iwcor/if_ds_edm_type,
          lt_edm_property TYPE /iwcor/if_ds_uri=>property_t,
          lo_edm_typed    TYPE REF TO /iwcor/if_ds_edm_typed,
          lt_edm_typed    TYPE STANDARD TABLE OF REF TO /iwcor/if_ds_edm_typed.

    DATA: lv_name          TYPE string,
          lv_external_name TYPE string,
          lv_internal_name TYPE string,
          lv_type_name     TYPE string,
          lr_mapping       TYPE REF TO /iwcor/if_ds_edm_mappable=>mapping_s.

    lo_node = io_node.
    DO.
      IF lo_node IS BOUND.
        IF lo_node->kind = /iwcor/if_ds_expr_node=>kind_property.
          lo_property ?= lo_node.
        ELSEIF lo_node->kind = /iwcor/if_ds_expr_node=>kind_member.
          lo_member   ?= lo_node.
          lo_property ?= lo_member->path.
        ELSE.
          RAISE EXCEPTION TYPE /iwcor/cx_ds_proc_unsupported.
        ENDIF.
        lo_type = lo_property->property->get_type( ).
        IF lo_type->kind = /iwcor/if_ds_edm_type=>kind_navigation.
          INSERT lo_property->property INTO lt_edm_typed INDEX 1.
          IF lo_node->kind = /iwcor/if_ds_expr_node=>kind_member.
            lo_node = lo_member->source_object.
            CONTINUE.
          ELSE.
            EXIT.
          ENDIF.
        ELSE.
          lo_edm_property ?= lo_property->property.
          lo_type = lo_edm_property->get_type( ).
          IF lo_type->kind = /iwcor/if_ds_edm_type=>kind_simple OR
             lo_type->kind = /iwcor/if_ds_edm_type=>kind_complex.
            INSERT lo_edm_property INTO lt_edm_property INDEX 1.
          ELSE.
            RAISE EXCEPTION TYPE /iwcor/cx_ds_proc_unsupported.
          ENDIF.
          IF lo_node->kind = /iwcor/if_ds_expr_node=>kind_member.
            lo_node = lo_member->source_object.
          ELSE.
            EXIT.
          ENDIF.
        ENDIF.
      ELSE.
        RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
      ENDIF.
    ENDDO.

* Get Names & Values for Navigation Property
    LOOP AT lt_edm_typed INTO lo_edm_typed.
      IF ev_value_external IS INITIAL.
        ev_value_external = lo_edm_typed->get_name( ).
      ELSE.
        lv_name = lo_edm_typed->get_name( ).
        CONCATENATE ev_value_external
                    '-'
                    lv_name
          INTO ev_value_external.
      ENDIF.
      IF ev_value_internal IS INITIAL.
        ev_value_internal = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_property->property ).

      ELSE.

        lv_name = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_edm_typed ).
        CONCATENATE ev_value_internal
                    '-'
                    lv_name
          INTO ev_value_internal.
      ENDIF.

      " Also append navigation to name mapping
      READ TABLE mt_name_mapping INTO ms_name_mapping
          WITH TABLE KEY internal_name = lv_internal_name.
      IF sy-subrc <> 0.
        ms_name_mapping-internal_name = ev_value_internal.
        ms_name_mapping-external_name = ev_value_external.
        INSERT ms_name_mapping INTO TABLE mt_name_mapping.
      ENDIF.
    ENDLOOP.

    " $filter <navigation> eq/ne 'null' doesn't have any property
    IF lt_edm_property IS INITIAL.
      IF lo_type->kind = /iwcor/if_ds_edm_type=>kind_navigation.
        RETURN.
      ELSE.
        RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
      ENDIF.
    ENDIF.

    LOOP AT lt_edm_property INTO lo_edm_property.

      lv_external_name = lo_edm_property->get_name( ).

* Create Mapping Table for Select Options later (External Names!)
      lr_mapping = lo_edm_property->/iwcor/if_ds_edm_mappable~get_mapping( ).
      IF lr_mapping IS BOUND AND lr_mapping->value IS NOT INITIAL.
        " Internal name of table name mapping is only unique with path
        IF lv_internal_name IS INITIAL.
          " Might be filled due to navigation properties (lt_edm_typed)
          IF ev_value_internal IS INITIAL.
            lv_internal_name = lr_mapping->value.
          ELSE.
            CONCATENATE ev_value_internal
                        '-'
                        lr_mapping->value
                   INTO lv_internal_name.
          ENDIF.
        ELSE.
          CONCATENATE lv_internal_name
                    '-'
                    lr_mapping->value
          INTO lv_internal_name.
        ENDIF.

        READ TABLE mt_name_mapping INTO ms_name_mapping
          WITH TABLE KEY internal_name = lv_internal_name.
        IF sy-subrc <> 0.
          ms_name_mapping-internal_name = lv_internal_name.
          ms_name_mapping-external_name = lv_external_name.
          TRY.
              ms_name_mapping-edm_simple_type ?= lo_edm_property->get_type( ).
              lv_type_name = ms_name_mapping-edm_simple_type->/iwcor/if_ds_edm_named~get_name( ).
              IF lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_time           OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_datetime       OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_datetimeoffset OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_boolean        OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_guid           OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_string.

                /iwfnd/cl_sodata_utils=>get_edm_type_abap_info_via_map(
                  EXPORTING
                    io_edm_instance  = lo_edm_property
                  IMPORTING
                    ev_internal_type = ms_name_mapping-internal_type
                    ev_length_char   = ms_name_mapping-length
                ).

              ELSE.
                CLEAR ms_name_mapping-edm_simple_type.
              ENDIF.
            CATCH cx_root.                               "#EC CATCH_ALL
              CLEAR ms_name_mapping-edm_simple_type.
          ENDTRY.
          INSERT ms_name_mapping INTO TABLE mt_name_mapping.
        ENDIF.
      ENDIF.

      IF ev_value_external IS INITIAL.
        ev_value_external = ms_name_mapping-external_name.
      ELSE.
        CONCATENATE ev_value_external
                    '-'
                    ms_name_mapping-external_name
          INTO ev_value_external.
      ENDIF.

      ev_value_internal = ms_name_mapping-internal_name.

    ENDLOOP.

  ENDMETHOD.                    "expr_node_to_property_path


  METHOD get_name_from_property_path.

    DATA: lv_name          TYPE string,
          lv_internal_name TYPE string,
          lv_type_name     TYPE string,
          lo_property      TYPE REF TO /iwcor/if_ds_edm_property,
          lr_mapping       TYPE REF TO /iwcor/if_ds_edm_mappable=>mapping_s.

    IF it_property_path IS INITIAL.
      RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
    ENDIF.

    LOOP AT it_property_path INTO lo_property.

      lv_name = lo_property->get_name( ).

* Create Mapping Table for Select Options later (External Names!)
      lr_mapping = lo_property->/iwcor/if_ds_edm_mappable~get_mapping( ).
      IF lr_mapping IS BOUND AND lr_mapping->value IS NOT INITIAL.
        " Internal name of table name mapping is only unique with path
        IF lv_internal_name IS INITIAL.
          lv_internal_name = lr_mapping->value.
        ELSE.
          CONCATENATE lv_internal_name
                    '-'
                    lr_mapping->value
          INTO lv_internal_name.
        ENDIF.

        READ TABLE mt_name_mapping INTO ms_name_mapping
          WITH TABLE KEY internal_name = lv_internal_name.
        IF sy-subrc <> 0.
          ms_name_mapping-internal_name = lv_internal_name.
          ms_name_mapping-external_name = lv_name.
          TRY.
              ms_name_mapping-edm_simple_type ?= lo_property->get_type( ).
              lv_type_name = ms_name_mapping-edm_simple_type->/iwcor/if_ds_edm_named~get_name( ).
              IF lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_time           OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_datetime       OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_datetimeoffset OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_boolean        OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_guid           OR
                 lv_type_name = /iwcor/cl_ds_edm_simple_type=>gc_name_string.

                /iwfnd/cl_sodata_utils=>get_edm_type_abap_info_via_map(
                  EXPORTING
                    io_edm_instance = lo_property
                  IMPORTING
                    ev_internal_type = ms_name_mapping-internal_type
                    ev_length_char   = ms_name_mapping-length
                ).

              ELSE.
                CLEAR ms_name_mapping-edm_simple_type.
              ENDIF.
            CATCH cx_root.                               "#EC CATCH_ALL
              CLEAR ms_name_mapping-edm_simple_type.
          ENDTRY.
          INSERT ms_name_mapping INTO TABLE mt_name_mapping.
        ENDIF.
      ENDIF.

      IF iv_is_internal_name = abap_true.
        rv_value = lv_internal_name. " internal name was already concatenate
      ELSE.
        IF rv_value IS INITIAL.
          rv_value = lv_name.
        ELSE.
          CONCATENATE rv_value
                      '-'
                      lv_name
            INTO rv_value.
        ENDIF.
      ENDIF.

    ENDLOOP.
  ENDMETHOD.                    "get_filter_from_property_path


  METHOD get_nav_path_for_message_scope.

    DATA: lo_lib_message_scope_uri     TYPE REF TO /iwcor/if_ds_uri,
          lr_lib_key_predicate         TYPE REF TO /iwcor/if_ds_uri=>key_predicate_s,
          ls_nav_message_scope         TYPE /iwfnd/if_sodata_types=>ty_s_nav_message_scope,
          ls_message_scope_nav_key_tab TYPE /iwfnd/if_mgw_core_types=>nvp_s,
          lv_nav_key_dummy             TYPE string,
          lt_nav_key_dummy             TYPE /iwfnd/if_mgw_core_types=>nvp_t,
          ls_navigation_path           TYPE /iwcor/if_ds_uri=>navigation_path_segment_s.


    TRY.
        lo_lib_message_scope_uri = /iwcor/cl_ds_uri_facade=>parse_uri( io_edm           = mo_context->get_service( )->get_entity_data_model( )
                                                                       iv_resource_path = iv_message_scope_value ).

* Basic navigation with Entity + Key
* Example input: /Employees('0005')
* Example parsed output:
*   ENTITY_SET                Employees
*   TARGET_ENTITY_TYPE        Employee
*   KEY                       ID='0005'
*   KEY_TAB [1x2(68)]Standard Table
*       NAME                  ID
*       VALUE                 0005

        ls_nav_message_scope-entity_set = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name( lo_lib_message_scope_uri->entity_set ).
        ls_nav_message_scope-entity_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_lib_message_scope_uri->entity_set->get_entity_type( ) ).
        LOOP AT lo_lib_message_scope_uri->key_predicates REFERENCE INTO lr_lib_key_predicate.
          ls_message_scope_nav_key_tab-name  = lr_lib_key_predicate->property->get_name( ).
          ls_message_scope_nav_key_tab-value = lr_lib_key_predicate->value.

          set_key_info(
            EXPORTING
              is_key_external        = ls_message_scope_nav_key_tab
              io_edm_instance        = lr_lib_key_predicate->property
            CHANGING
              cv_key_string_external = lv_nav_key_dummy
              cv_key_string_internal = lv_nav_key_dummy
              ct_key_tab_external    = lt_nav_key_dummy
              ct_key_tab_internal    = ls_nav_message_scope-key ).

        ENDLOOP.

        APPEND ls_nav_message_scope TO et_message_scope.


* Navigation
* Example input: Employees(Id='0005')/My_Expanders('0001')
* Example parsed output:
*   ENTITY_SET                Auto_Expands_2
*   TARGET_ENTITY_TYPE        Auto_Expand_2
*   NAV_PROP                  My_Expanders
*   KEY                       TESTID='0001'
*   KEY_TAB [1x2(68)]Standard Table
*       NAME                  TESTID
*       VALUE                 0001
*   MULTIPLICITY               *
*   IS_CONTAINMENT_NAVIGATION ' '

        LOOP AT lo_lib_message_scope_uri->navigation_path INTO ls_navigation_path.
          CLEAR: ls_nav_message_scope,
                 ls_message_scope_nav_key_tab.

          ls_nav_message_scope-entity_set                = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name( ls_navigation_path-target_entity_set ).
          ls_nav_message_scope-entity_type        = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( ls_navigation_path-target_entity_set->get_entity_type( ) ).
          ls_nav_message_scope-nav_prop                  = ls_navigation_path-navigation_property->get_name( ).
          ls_nav_message_scope-multiplicity              = ls_navigation_path-navigation_property->get_multiplicity( ).
          ls_nav_message_scope-is_containment_navigation = /iwfnd/cl_sodata_utils=>is_containment_navi_prop( ls_navigation_path-navigation_property ).

          LOOP AT ls_navigation_path-key_predicates REFERENCE INTO lr_lib_key_predicate.
            ls_message_scope_nav_key_tab-name  = lr_lib_key_predicate->property->get_name( ).
            ls_message_scope_nav_key_tab-value = convert_value_by_predicate( lr_lib_key_predicate ).

            set_key_info(
              EXPORTING
                is_key_external        = ls_message_scope_nav_key_tab
                io_edm_instance        = lr_lib_key_predicate->property
              CHANGING
                cv_key_string_external = lv_nav_key_dummy
                cv_key_string_internal = lv_nav_key_dummy
                ct_key_tab_external    = lt_nav_key_dummy
                ct_key_tab_internal    = ls_nav_message_scope-key ).

          ENDLOOP.

          APPEND ls_nav_message_scope TO et_message_scope.
        ENDLOOP.

      CATCH /iwcor/cx_ds_error INTO DATA(lx_ds_error).
        ev_message_scope_error = lx_ds_error->get_text( ).

    ENDTRY.

  ENDMETHOD.


  METHOD get_orderby_parameters.

    DATA: lv_name_internal     TYPE string,
          lt_property_path     TYPE /iwcor/if_ds_uri=>property_t,
          ls_request_order_ext TYPE /iwfnd/if_mgw_core_types=>order_s,
          ls_request_order_int TYPE /iwfnd/if_mgw_core_types=>ty_s_technical_order,
          lr_order             TYPE REF TO /iwcor/if_ds_expr_orderby=>order_s,
          lv_navi_path_ext     TYPE string,
          lv_navi_path_int     TYPE string.

    IF io_orderby IS BOUND.
      LOOP AT io_orderby->orders REFERENCE INTO lr_order.
        ls_request_order_ext-order = lr_order->sortorder.

        IF lr_order->expression IS BOUND.
          IF lr_order->expression->kind = /iwcor/if_ds_expr_node=>kind_property OR
             lr_order->expression->kind = /iwcor/if_ds_expr_node=>kind_member.
            expr_node_to_property_path(
              EXPORTING
                io_node          = lr_order->expression
              IMPORTING
                et_property      = lt_property_path
                ev_navi_path_ext = lv_navi_path_ext
                ev_navi_path_int = lv_navi_path_int
            ).
            ls_request_order_ext-property = get_name_from_property_path( lt_property_path ).
            IF lv_navi_path_ext IS NOT INITIAL.
              CONCATENATE lv_navi_path_ext
                          ls_request_order_ext-property
                INTO ls_request_order_ext-property SEPARATED BY '-'.
            ENDIF.
            lv_name_internal = get_name_from_property_path(
                                it_property_path    = lt_property_path
                                iv_is_internal_name = abap_true
                              ).
            IF lv_navi_path_int IS NOT INITIAL.
              CONCATENATE lv_navi_path_int
                          lv_name_internal
                INTO lv_name_internal SEPARATED BY '-'.
            ENDIF.
          ELSE.
            RAISE EXCEPTION TYPE /iwcor/cx_ds_proc_unsupported.
          ENDIF.
        ELSE.
          RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
        ENDIF.
        INSERT ls_request_order_ext INTO TABLE et_order_external.

        " New Convention: PROPERTY should be ignored in new DPC
        "   Simple Property:
        "     PROPERTY     : property name (compatibility)
        "     PROPERTY_PATH: property name
        "   Complex Property:
        "     PROPERTY     : empty
        "     PROPERTY_PATH: complex name
        IF lv_name_internal CS '-'.
          ls_request_order_int-property_path = lv_name_internal.
          CLEAR ls_request_order_int-property.
        ELSE.
          ls_request_order_int-property_path = ls_request_order_int-property = lv_name_internal.
        ENDIF.

        ls_request_order_int-order = ls_request_order_ext-order.
        INSERT ls_request_order_int INTO TABLE et_order_internal.
      ENDLOOP.
    ELSE.
      RAISE EXCEPTION TYPE /iwcor/cx_ds_internal_error.
    ENDIF.

  ENDMETHOD.                    "get_orderby_parameters


  METHOD get_processor.

    DATA: lo_processor  TYPE REF TO /iwfnd/cl_sodata_processor.

    CREATE OBJECT lo_processor TYPE /iwfnd/cl_sodata_processor
      EXPORTING
        io_service = io_service.

    ro_processor = lo_processor.

* Get Post Processor for Batch processing
    mo_post_processor = /iwfnd/cl_sodata_post_proc=>get_post_processor( lo_processor ).

* Get Batch Helper for Batch processing
    mo_batch_helper = /iwfnd/cl_mgw_batch_helper=>get_batch_helper( ).

  ENDMETHOD.                    "get_processor


  METHOD get_resource_uri.

    DATA:  lt_uri_query_parameters TYPE        tihttpnvp
          ,lr_uri_query_parameter  TYPE REF TO ihttpnvp
          ,lv_top                  TYPE        string
          ,lv_name_value_pair      TYPE        string
          ,lv_count                TYPE        i
          ,lx_tech_exception       TYPE REF TO /iwfnd/cx_mgw_tech_exception
          ,lo_context              TYPE REF TO /iwcor/if_ds_cntxt
          .

    FIELD-SYMBOLS: <table> TYPE ANY TABLE.

*-deltatoken and skip token
*-In case of service-side paging delta-link appears only in the last page - no next link anymore
*-get delta-set by requesting delta link
*-in case of server-side paging in combination with skiptoken server can pass back a skiptoken in first page of delta - kind of delta processing
*-deltatoken is passed in next link. kind of delta mode
*-response never contains delta link and next link
*-application can never pass back delta and skiptoken
*- Next-Link can contain $deltatoken/!deltatoken in analogy to $filter or custom query options in case of server-side paging
*-deltatoken query option behaves like "normal" system/custom query option

    IF io_context IS BOUND.
      lo_context = io_context.
    ELSE.
      lo_context = mo_context.
    ENDIF.

    rv_resource_uri = lo_context->get_resource_path( ).
    SHIFT rv_resource_uri LEFT DELETING LEADING '/'.

    IF iv_skiptoken IS NOT INITIAL OR iv_deltatoken IS NOT INITIAL.
*-------deltatoken is not supported in combination with skiptoken
      IF iv_skiptoken IS NOT INITIAL AND iv_deltatoken IS NOT INITIAL.
        CREATE OBJECT lx_tech_exception
          EXPORTING
            textid           = /iwfnd/cx_mgw_tech_exception=>query_option_combination
            http_status_code = '400'.

        raise_technical_error( lx_tech_exception ).
      ENDIF.
      ASSIGN ir_data->* TO <table>.
      lv_count = lines( <table> ).

*-Get uri parameters
      lo_context->get_parameter(
        EXPORTING
          iv_name  = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-query_parameters     " Name
        IMPORTING
          ev_value = lt_uri_query_parameters
      ).

      DELETE lt_uri_query_parameters
        WHERE
          name EQ /iwfnd/if_sodata_types=>gcs_uri_parameters-skiptoken OR
          name EQ /iwfnd/if_sodata_types=>gcs_uri_parameters-skip.

      IF iv_skiptoken IS NOT INITIAL.
        DELETE lt_uri_query_parameters
          WHERE
            name EQ /iwfnd/if_sodata_types=>gcs_uri_parameters-skiptoken OR
            name EQ /iwfnd/if_sodata_types=>gcs_uri_parameters-skip.
      ELSE.
        DELETE lt_uri_query_parameters
          WHERE
            name EQ /iwfnd/if_sodata_types=>gcs_uri_parameters-skiptoken   OR
            name EQ /iwfnd/if_sodata_types=>gcs_uri_parameters-skip        OR
            name EQ /iwfnd/if_sodata_types=>gcs_uri_parameters-delta_token OR
            name EQ /iwfnd/if_sodata_types=>gcs_uri_parameters-delta_token_tmp.
      ENDIF.

      IF lines( lt_uri_query_parameters ) > 0.
        CONCATENATE rv_resource_uri '?' INTO rv_resource_uri.
      ENDIF.

      LOOP AT lt_uri_query_parameters REFERENCE INTO lr_uri_query_parameter.
        CLEAR lv_name_value_pair.
        CASE lr_uri_query_parameter->*-name.
          WHEN /iwfnd/if_sodata_types=>gcs_uri_parameters-top.
            lv_top = lr_uri_query_parameter->*-value.

            IF lv_top > lv_count.
              lv_top = lv_top - lv_count.
            ENDIF.
            CONDENSE lv_top NO-GAPS.
            CONCATENATE lr_uri_query_parameter->*-name '=' lv_top INTO lv_name_value_pair.

          WHEN OTHERS.
            CONCATENATE lr_uri_query_parameter->*-name '=' lr_uri_query_parameter->*-value INTO lv_name_value_pair.
        ENDCASE.
        IF sy-tabix GT 1.
          CONCATENATE rv_resource_uri '&' lv_name_value_pair INTO rv_resource_uri.
        ELSE.
          CONCATENATE rv_resource_uri lv_name_value_pair INTO rv_resource_uri.
        ENDIF.
      ENDLOOP.

    ENDIF. "skiptoken and/or deltatoken

  ENDMETHOD.


  METHOD handle_query_result_cnt_id_ref.

    DATA:
      lr_parent_context           TYPE REF TO /iwcor/if_rest_context=>object_parameter_s,
      lt_content_reference        TYPE /iwcor/if_ds_cntxt=>content_reference_t,
      ls_content_reference        TYPE /iwcor/if_ds_cntxt=>content_reference_s,
      ls_location_batch_operation TYPE /iwfnd/cl_mgw_batch_helper=>ty_s_batch_operation,
      lv_length                   TYPE i,
      lv_resource_path            TYPE string,
      lv_service_root_absolute    TYPE string,
      lv_location_header          TYPE string.

    " only handle here query operation with content-id-ref
    CHECK cs_batch_operation-content_id_ref IS NOT INITIAL
      AND cs_batch_operation-changeset IS INITIAL.

    " get 'location' header from previous result with identival content-id setting;
    " previous result must have a corresponding content-id (ensured by lib)
    READ TABLE it_batch_operation INTO ls_location_batch_operation
      WITH KEY content_id = cs_batch_operation-content_id_ref
               processed = abap_true.  " processed previous changeset result with content-id for the content-id-ref
    ASSERT sy-subrc = 0.

    lv_location_header = ls_location_batch_operation-rest_response->get_header_field( if_http_header_fields=>location ).
    CHECK lv_location_header IS NOT INITIAL.             " Q: what to do if location is not filled? Can this happen? Assert this?

    " from read 'location' header with content-id build final resource-path of query operation:
    " - "subtract" service absolute path from location path resulting in resource path of location
    " - get content reference of read operation via context
    " - if present, add navigation path to resource path from content reference info
    " - set read operation's final resource path to its context
    ls_location_batch_operation-ds_context->get_parameter(
      EXPORTING iv_name  = 'service_root_absolute'
      IMPORTING ev_value = lv_service_root_absolute ).
    lv_length = strlen( lv_service_root_absolute ).
    lv_resource_path = lv_location_header+lv_length.

    lr_parent_context ?= cs_batch_operation-ds_context->get_parameter_reference( /iwcor/cl_ds_cntxt=>gc_object_parent ).
    CAST /iwcor/if_rest_context( lr_parent_context->instance )->get_parameter(
      EXPORTING iv_name  = /iwcor/if_ds_cntxt=>gc_param_content_references
      IMPORTING ev_value = lt_content_reference ).
    READ TABLE lt_content_reference INTO ls_content_reference
      WITH KEY id = cs_batch_operation-content_id_ref.
    ASSERT sy-subrc = 0.

    READ TABLE lt_content_reference INDEX 1 INTO ls_content_reference.
    ASSERT sy-subrc = 0.
    IF ls_content_reference-navigation_path IS NOT INITIAL.
      CONCATENATE lv_resource_path ls_content_reference-navigation_path INTO lv_resource_path.
    ENDIF.
    cs_batch_operation-ds_context->set_parameter( iv_name  = 'resource_path' iv_value = lv_resource_path ).

  ENDMETHOD.


  METHOD init_model.

*    CONSTANTS:
*      lc_default_version TYPE /iwfnd/med_mdl_version VALUE 0001.
*
*    DATA:
*      lt_segment_params TYPE tihttpnvp,
*      ls_segment_param  TYPE ihttpnvp.
*
* Get Service Name, Namespace and Base URL
    me->mv_service_name    = mo_context->get_service_name( ).
    me->mv_namespace       = mo_context->get_service_namespace( ).
    me->mv_base_url        = mo_context->get_service_root_absolute( ).

    IF mv_namespace IS NOT INITIAL.
      CONCATENATE '/'
                  mv_namespace
                  '/'
        INTO mv_namespace.
      TRANSLATE mv_namespace TO UPPER CASE.
    ENDIF.
*
** Get Service Version
*    mo_context->get_service_segment_parameter(
*      IMPORTING
*        et_parameter = lt_segment_params ).
*    CLEAR ls_segment_param.
*    READ TABLE lt_segment_params INTO ls_segment_param
*        WITH TABLE KEY name = /iwfnd/if_sodata_types=>gcs_segment_parameters-version.
*    IF ( sy-subrc = 0 ).
*      me->mv_service_version = ls_segment_param-value.
*    ELSE.
*      me->mv_service_version = lc_default_version.
*    ENDIF.

  ENDMETHOD.                    "INIT_MODEL


  METHOD init_request.

    DATA: ls_request_key_tab       TYPE /iwfnd/if_mgw_core_types=>nvp_s,
          ls_dollar_parameter      TYPE /iwfnd/if_mgw_core_types=>parameter_values_s,
          ls_parameter             TYPE /iwfnd/if_mgw_core_types=>parameter_values_s,
          ls_request_navi_path_int TYPE /iwfnd/if_mgw_core_types=>ty_s_technical_navi,
          ls_request_navi_path_ext TYPE /iwfnd/if_mgw_core_types=>nav_s,
          ls_navigation_path       TYPE /iwcor/if_ds_uri=>navigation_path_segment_s,
          ls_select                TYPE /iwcor/if_ds_uri=>select_item_s,
          ls_navigation_prop       TYPE /iwcor/if_ds_uri=>navigation_property_s,
          lr_key_predicate         TYPE REF TO /iwcor/if_ds_uri=>key_predicate_s,
          lo_source_entity_type    TYPE REF TO /iwcor/if_ds_edm_entity_type,
          lo_target_entity_type    TYPE REF TO /iwcor/if_ds_edm_entity_type,
          lo_property              TYPE REF TO /iwcor/if_ds_edm_typed,
          lx_busi_exception        TYPE REF TO /iwfnd/cx_mgw_busi_exception,
          lx_tech_exception        TYPE REF TO /iwfnd/cx_mgw_tech_exception,
          lt_uri_query_parameters  TYPE tihttpnvp,
          lv_slug_header           TYPE string,
          lv_name_external         TYPE string,
          lv_name_internal         TYPE string,
          ls_name_mapping          TYPE ty_name_mapping,
          ls_option                TYPE /iwfnd/if_mgw_core_types=>edm_select_option_s,
          ls_option_ext            TYPE /iwfnd/if_mgw_core_types=>edm_select_option_s,
          ls_ds_option             TYPE /iwcor/if_ds_expr_types=>select_option_s,
          ls_ds_option_ext         TYPE /iwcor/if_ds_expr_types=>select_option_ext_s,
          lt_ds_option             TYPE /iwcor/if_ds_expr_types=>select_option_t,
          lt_ds_option_ext         TYPE /iwcor/if_ds_expr_types=>select_option_ext_t,
          ls_ds_selopt_ext         TYPE /iwcor/if_ds_expr_types=>abap_select_option_ext_s,
          ls_selopt                TYPE /iwfnd/if_mgw_core_types=>abap_select_option_s,
          lt_string                TYPE TABLE OF string,
          lv_string                TYPE string,
          lv_prefix_name           TYPE string,
          lv_navi_external         TYPE string,
          lv_navi_internal         TYPE string,
          lv_navi_internal_h       TYPE string,
          lv_select_string         TYPE string,
          lv_type_name             TYPE string,
          lv_name_part_no          TYPE i,
          lv_filter_error          TYPE xsdboolean,
          lv_totals_error          TYPE abap_bool,
          ls_http_header           TYPE ihttpnvp,
          lt_http_header           TYPE tihttpnvp,
          lv_gwbep_version         TYPE numc3.

    FIELD-SYMBOLS:
      <ls_range>       TYPE /iwfnd/if_mgw_core_types=>abap_select_option_s,
      <lt_http_header> TYPE data.


* Batch Processing Info
    mv_batch = mo_batch_helper->mv_batch.
    mv_changeset = mo_batch_helper->mv_changeset.

* Service Info
    es_request-namespace        = mv_namespace.
    es_request-service_doc_name = mv_service_name.
    es_request-version          = mv_service_version.

* Defaulting for supported runtime features
    es_request-supported_runtime_features-transient_flag_for_messages = abap_true.

* Initialize Model
    me->init_model( ).

* Bep Version
    lv_gwbep_version = /iwfnd/cl_mgw_util_versions=>get_gwbep_version( ).

* X-CSRF Token validation status
    es_request-technical_request-is_csrf_token_protected = mo_transaction_handler->is_csrf_token_protected( ).

* Determine Source and Target Entity without Navigation Path
    IF it_navigation_path IS INITIAL.
      eo_target_entity_set = io_entity_set.
      IF io_entity_set IS BOUND.
        mv_source_entity_set = mv_target_entity_set = io_entity_set->get_name( ).
        es_request-technical_request-source_entity_set = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name( io_entity_set ).
        es_request-technical_request-target_entity_set = es_request-technical_request-source_entity_set.

        lo_source_entity_type = io_entity_set->get_entity_type( ).
        mv_source_entity = mv_target_entity = lo_source_entity_type->get_name( ).

        es_request-technical_request-source_entity_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_source_entity_type ).
        es_request-technical_request-target_entity_type = es_request-technical_request-source_entity_type.

        IF mv_target_entity IS INITIAL.
          CREATE OBJECT lx_busi_exception
            EXPORTING
              textid         = /iwfnd/cx_mgw_busi_exception=>resource_not_found
              entityset_name = mv_source_entity_set.
          raise_business_error( lx_busi_exception ).
        ENDIF.

      ENDIF.

*-----in case of a post to a navigation property - io_navigation_property is bound
      IF io_navigation_property IS BOUND.
        eo_target_entity_set = io_entity_set->get_related_entity_set( io_navigation_property ).
        mv_target_entity_set = eo_target_entity_set->get_name( ).
        es_request-technical_request-target_entity_set = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name( eo_target_entity_set ).

        lo_target_entity_type = eo_target_entity_set->get_entity_type( ).
        mv_target_entity = lo_target_entity_type->get_name( ).
        es_request-technical_request-target_entity_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_target_entity_type ).

        " Create Navigation Path for Provider
        ls_request_navi_path_ext-target_type = mv_target_entity.
        ls_request_navi_path_ext-target_type_namespace = lo_target_entity_type->get_namespace( ).
        ls_request_navi_path_ext-nav_prop = io_navigation_property->get_name( ).
        APPEND ls_request_navi_path_ext TO es_request-navigation_path.

        ls_request_navi_path_int-target_entity_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_target_entity_type ).
        ls_request_navi_path_int-nav_prop = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( io_navigation_property ).

        APPEND ls_request_navi_path_int TO es_request-technical_request-navigation_path.
      ENDIF.

* Determine Source and Target Entity info by Navigation Path
    ELSE.
      mv_source_entity_set = io_entity_set->get_name( ).
      es_request-technical_request-source_entity_set = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name( io_entity_set ).

      lo_source_entity_type = io_entity_set->get_entity_type( ).
      mv_source_entity = lo_source_entity_type->get_name( ).
      es_request-technical_request-source_entity_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_source_entity_type ).

      eo_target_entity_set = /iwcor/cl_ds_uri_facade=>get_end_entity_set(
                               io_entity_set      = io_entity_set
                               it_navigation_path = it_navigation_path
                             ).
      mv_target_entity_set = eo_target_entity_set->get_name( ).
      es_request-technical_request-target_entity_set = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name( eo_target_entity_set ).

      lo_target_entity_type = eo_target_entity_set->get_entity_type( ).
      mv_target_entity = lo_target_entity_type->get_name( ).

      es_request-technical_request-target_entity_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_target_entity_type ).

    ENDIF.

* Fill OData Request for Data Provider
    es_request-operation         = iv_operation.
    es_request-type              = iv_operation_type.
    es_request-entity            = mv_source_entity.
    es_request-source_entity     = mv_source_entity.
    es_request-target_entity     = mv_target_entity.
    es_request-target_entity_set = mv_target_entity_set.

* Function Import
    IF io_function_import IS BOUND.
      es_request-technical_request-action_name = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name( io_function_import ).
    ENDIF.

* Set Content Format
    IF iv_format IS NOT INITIAL.
      es_request-format = iv_format.
    ELSE.
      es_request-format = mcs_formats-xml.
    ENDIF.
    CASE iv_format.
      WHEN  /iwcor/if_ds_uri=>gc_format_json.
        ls_dollar_parameter-name  = /iwfnd/if_sodata_types=>gcs_uri_parameters-format.
        ls_dollar_parameter-value = iv_format.
        INSERT ls_dollar_parameter INTO TABLE et_parameter.
        mv_content_format = 'J'.
      WHEN /iwcor/if_ds_uri=>gc_format_xml OR /iwcor/if_ds_uri=>gc_format_atom.
        mv_content_format = 'O'.
    ENDCASE.

* Key Table
    LOOP AT it_key REFERENCE INTO lr_key_predicate.
      ls_request_key_tab-name  = lr_key_predicate->property->get_name( ).
      ls_request_key_tab-value = me->convert_value_by_predicate( lr_key_predicate ).
      me->set_key_info(
        EXPORTING
          is_key_external        = ls_request_key_tab
          io_edm_instance        = lr_key_predicate->property
        CHANGING
          cv_key_string_external = es_request-key
          cv_key_string_internal = es_request-technical_request-key
          ct_key_tab_external    = es_request-key_tab
          ct_key_tab_internal    = es_request-technical_request-key_tab
      ).
    ENDLOOP.

* Navigation Path
    LOOP AT it_navigation_path INTO ls_navigation_path.
      CLEAR: ls_request_navi_path_ext, ls_request_navi_path_int.
      LOOP AT ls_navigation_path-key_predicates REFERENCE INTO lr_key_predicate.
        ls_request_navi_path_ext-multiplicity = lr_key_predicate->property->get_multiplicity( ).
        ls_request_key_tab-name  = lr_key_predicate->property->get_name( ).
        ls_request_key_tab-value = me->convert_value_by_predicate( lr_key_predicate ).
        me->set_key_info(
          EXPORTING
            is_key_external        = ls_request_key_tab
            io_edm_instance        = lr_key_predicate->property
          CHANGING
            cv_key_string_external = ls_request_navi_path_ext-key
            cv_key_string_internal = ls_request_navi_path_int-key
            ct_key_tab_external    = ls_request_navi_path_ext-key_tab
            ct_key_tab_internal    = ls_request_navi_path_int-key_tab
        ).
      ENDLOOP.

      " With External Names
      lo_target_entity_type = ls_navigation_path-target_entity_set->get_entity_type( ).
      ls_request_navi_path_ext-target_type = lo_target_entity_type->get_name( ).
      ls_request_navi_path_ext-target_type_namespace = lo_target_entity_type->get_namespace( ).
      ls_request_navi_path_ext-nav_prop = ls_navigation_path-navigation_property->get_name( ).

      ls_request_navi_path_ext-multiplicity = ls_request_navi_path_int-multiplicity = ls_navigation_path-navigation_property->get_multiplicity( ).

      APPEND ls_request_navi_path_ext TO es_request-navigation_path.

      " With Internal Names
      ls_request_navi_path_int-target_entity_type = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_target_entity_type ).
      ls_request_navi_path_int-nav_prop = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( ls_navigation_path-navigation_property ).

      " Containment information needs to be stored
      IF  /iwfnd/cl_sodata_utils=>is_containment_navi_prop( ls_navigation_path-navigation_property ) = abap_true.
        ls_request_navi_path_int-is_containment_navigation = abap_true.
      ENDIF.

      APPEND ls_request_navi_path_int TO es_request-technical_request-navigation_path.
    ENDLOOP.

* Select
    CLEAR: mt_select_params, es_request-technical_request-select, es_request-technical_request-select_strings, es_request-technical_request-select_strings_h.
    IF it_select IS NOT INITIAL.
      LOOP AT it_select INTO ls_select.
        CLEAR: lv_navi_external, lv_navi_internal, lv_name_external, lv_name_internal, lv_navi_internal_h.
        IF ls_select-property IS BOUND.
          lv_name_external = ls_select-property->get_name( ).
          lv_name_internal = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( ls_select-property ).

          IF ls_select-navigation_path IS INITIAL.
            APPEND lv_name_external TO mt_select_params.
            APPEND lv_name_internal TO es_request-technical_request-select.
            APPEND lv_name_internal TO es_request-technical_request-select_strings.
            APPEND lv_name_internal TO es_request-technical_request-select_strings_h.
            CONTINUE.
          ENDIF.
        ENDIF.
        LOOP AT ls_select-navigation_path INTO ls_navigation_prop.
          IF ls_navigation_prop-navigation_property IS BOUND.
            lv_string = ls_navigation_prop-navigation_property->get_name( ).
            IF lv_navi_external IS INITIAL.
              lv_navi_external = lv_string.
            ELSE.
              CONCATENATE lv_navi_external
                          lv_string
                INTO lv_navi_external SEPARATED BY '/'.
            ENDIF.
            lv_string = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( ls_navigation_prop-navigation_property ).

            IF lv_navi_internal IS INITIAL.
              lv_navi_internal = lv_string.
            ELSE.
              CONCATENATE lv_navi_internal
                          lv_string
                INTO lv_navi_internal SEPARATED BY '/'.
            ENDIF.
            IF lv_navi_internal_h IS INITIAL.
              lv_navi_internal_h = lv_string.
            ELSE.
              CONCATENATE lv_navi_internal_h
                          lv_string
                INTO lv_navi_internal_h SEPARATED BY '-'.
            ENDIF.
          ENDIF.
        ENDLOOP.
        IF lv_name_external IS INITIAL.
          lv_name_external = lv_navi_external.
        ELSE.
          CONCATENATE lv_navi_external
                      lv_name_external
            INTO lv_name_external SEPARATED BY '/'.
          APPEND lv_name_external TO mt_select_params.
          APPEND lv_name_internal TO es_request-technical_request-select.
        ENDIF.
        IF lv_name_internal IS INITIAL.
          lv_name_internal = lv_navi_internal.
          APPEND lv_name_internal TO es_request-technical_request-select_navis.
        ELSE.
          CLEAR lv_select_string.
          CONCATENATE lv_navi_internal
                      lv_name_internal
            INTO lv_select_string SEPARATED BY '/'.
          APPEND lv_select_string TO es_request-technical_request-select_strings.
          CLEAR lv_select_string.
          CONCATENATE lv_navi_internal_h
                      lv_name_internal
            INTO lv_select_string SEPARATED BY '-'.
          APPEND lv_select_string TO es_request-technical_request-select_strings_h.
        ENDIF.
      ENDLOOP.
    ENDIF.

* Filter
    IF io_filter IS BOUND.

      " Create Filter String
      TRY.
          ls_dollar_parameter-name = gc_param_filter.
          get_filter_string(
            EXPORTING
              io_filter         = io_filter
            IMPORTING
              ev_value_external = ls_dollar_parameter-value
              ev_value_internal = es_request-technical_request-filter_string
          ).
          INSERT ls_dollar_parameter INTO TABLE et_parameter.

        CATCH cx_root.                                   "#EC CATCH_ALL
          "Filter String is empty in all error cases
          CLEAR: es_request-technical_request-filter_string.
      ENDTRY.

*/ Create Tree Representation of $filter Expression
      TRY.
          create_filter_tree(
            EXPORTING
              io_filter = io_filter
          ).
          es_request-technical_request-filter_expressions = mt_expressions.
          es_request-technical_request-filter_functions   = mt_functions.

        CATCH cx_root.                                   "#EC CATCH_ALL
          REFRESH: mt_expressions, mt_functions.
      ENDTRY.

      " Create Select Options (containing Internal Names)
      TRY.
          /iwcor/cl_ds_uri_facade=>create_select_options(
            EXPORTING
              io_expression         = io_filter
            IMPORTING
              et_select_options     = lt_ds_option
              et_select_options_ext = lt_ds_option_ext ).
        CATCH cx_root.                                   "#EC CATCH_ALL
                                                        "#EC NO_HANDLER
          "Select Options is empty in all error cases
      ENDTRY.

      " Prepare Select Options Tables for External/Internal Names
      IF lt_ds_option IS NOT INITIAL AND mt_name_mapping IS NOT INITIAL.
        LOOP AT lt_ds_option INTO ls_ds_option.
          MOVE-CORRESPONDING ls_ds_option TO ls_option.
          lv_name_internal = ls_option-property.

          " Take care of Complexe type
          CLEAR lv_filter_error.
          CLEAR lv_prefix_name.
          SPLIT lv_name_internal AT '-' INTO TABLE lt_string.
          LOOP AT lt_string INTO lv_string.
            lv_name_part_no = sy-tabix.
            " The key 'internal_name' of 'Name-Mapping-Table' is only unique with path
            IF lv_prefix_name IS INITIAL.
              lv_prefix_name = lv_string.
            ELSE.
              CONCATENATE lv_prefix_name '-' lv_string INTO lv_string.
              lv_prefix_name = lv_string.
            ENDIF.

            READ TABLE mt_name_mapping INTO ls_name_mapping
              WITH TABLE KEY internal_name = lv_string.
            IF sy-subrc <> 0.
              lv_filter_error = abap_true.
              EXIT.
            ENDIF.
            IF lv_name_part_no = 1.
              lv_name_external = ls_name_mapping-external_name.
            ELSE.
              CONCATENATE lv_name_external
                          '-'
                          ls_name_mapping-external_name
                INTO lv_name_external.
            ENDIF.
          ENDLOOP.
          IF lv_filter_error = abap_true. CONTINUE. ENDIF.
          ls_option-property = lv_name_external.
          IF ls_name_mapping-edm_simple_type IS BOUND.
            LOOP AT ls_option-select_options ASSIGNING <ls_range>.
              lv_type_name = ls_name_mapping-edm_simple_type->/iwcor/if_ds_edm_named~get_name( ).

              CASE lv_type_name.

                WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_guid.
                  <ls_range>-low = correct_guid_value(
                                     io_simple_type = ls_name_mapping-edm_simple_type
                                     iv_value       = <ls_range>-low
                                   ).
                  IF <ls_range>-high IS NOT INITIAL.
                    <ls_range>-high = correct_guid_value(
                                       io_simple_type = ls_name_mapping-edm_simple_type
                                       iv_value       = <ls_range>-high
                                     ).
                  ENDIF.

                WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_boolean.
                  <ls_range>-low = correct_boolean_value(
                                     io_simple_type = ls_name_mapping-edm_simple_type
                                     iv_value       = <ls_range>-low
                                   ).
                  IF <ls_range>-high IS NOT INITIAL.
                    <ls_range>-high = correct_boolean_value(
                                       io_simple_type = ls_name_mapping-edm_simple_type
                                       iv_value       = <ls_range>-high
                                     ).
                  ENDIF.

                WHEN /iwcor/cl_ds_edm_simple_type=>gc_name_time     OR
                     /iwcor/cl_ds_edm_simple_type=>gc_name_datetime OR
                     /iwcor/cl_ds_edm_simple_type=>gc_name_datetimeoffset.
                  <ls_range>-low = correct_datetime_value(
                                     iv_internal_type = ls_name_mapping-internal_type
                                     iv_length        = ls_name_mapping-length
                                     io_simple_type   = ls_name_mapping-edm_simple_type
                                     iv_value         = <ls_range>-low
                                   ).
                  IF <ls_range>-high IS NOT INITIAL.
                    <ls_range>-high = correct_datetime_value(
                                        iv_internal_type = ls_name_mapping-internal_type
                                        iv_length        = ls_name_mapping-length
                                        io_simple_type   = ls_name_mapping-edm_simple_type
                                        iv_value         = <ls_range>-high
                                      ).
                  ENDIF.
              ENDCASE.
            ENDLOOP.
          ENDIF.

          INSERT ls_option INTO TABLE es_request-filter_select_options.
          ls_option-property = lv_name_internal.
          INSERT ls_option INTO TABLE es_request-technical_request-filter_select_options.

          " handle filter placeholder information for select options
          READ TABLE lt_ds_option_ext INTO ls_ds_option_ext WITH TABLE KEY property = ls_option-property.
          IF sy-subrc = 0.
            CLEAR ls_option_ext.
            ls_option_ext-property = lv_name_internal.
            LOOP AT ls_ds_option_ext-select_options INTO ls_ds_selopt_ext.
              MOVE-CORRESPONDING ls_ds_selopt_ext TO ls_selopt.
              APPEND ls_selopt TO ls_option_ext-select_options.
            ENDLOOP.
            INSERT ls_option_ext INTO TABLE es_request-technical_request-filter_select_placeholders.
          ENDIF.

        ENDLOOP.
      ENDIF.
    ENDIF.

* Order By
    IF io_orderby IS BOUND.
      get_orderby_parameters(
        EXPORTING
          io_orderby        = io_orderby
        IMPORTING
          et_order_external = es_request-order
          et_order_internal = es_request-technical_request-order
      ).
    ENDIF.

* Paging
    IF iv_top IS NOT INITIAL.
      es_request-paging-top  = iv_top.
    ENDIF.

    IF iv_skip IS NOT INITIAL.
      es_request-paging-skip = iv_skip.
    ENDIF.

* Skip Token
    IF iv_skiptoken IS NOT INITIAL.
      ls_dollar_parameter-name  = /iwfnd/if_sodata_types=>gcs_uri_parameters-skiptoken.
      ls_dollar_parameter-value = iv_skiptoken.
      INSERT ls_dollar_parameter INTO TABLE et_parameter.
    ENDIF.

* Inline Count
    IF iv_inlinecount IS NOT INITIAL.
      ls_dollar_parameter-name  = /iwfnd/if_sodata_types=>gcs_uri_parameters-inlinecount.
      ls_dollar_parameter-value = iv_inlinecount.
      INSERT ls_dollar_parameter INTO TABLE et_parameter.
    ENDIF.

* $Count
    IF iv_count = abap_true.
      ls_dollar_parameter-name  = /iwfnd/if_sodata_types=>gcs_uri_parameters-count.
      ls_dollar_parameter-value = iv_count.
      INSERT ls_dollar_parameter INTO TABLE et_parameter.
    ENDIF.

* Expand Clause
    IF it_expand IS NOT INITIAL.
      CLEAR ls_dollar_parameter.
      ls_dollar_parameter-name  = /iwfnd/if_sodata_types=>gcs_uri_parameters-expand.
      me->get_expand_string(
        EXPORTING
          it_expand          = it_expand
        IMPORTING
          ev_expand_external = ls_dollar_parameter-value
          ev_expand_internal = es_request-technical_request-expand
      ).
      INSERT ls_dollar_parameter INTO TABLE et_parameter.
    ENDIF.



* Get uri parameters
    mo_context->get_parameter(
      EXPORTING
        iv_name  = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-query_parameters     " Name
      IMPORTING
        ev_value = lt_uri_query_parameters
    ).

    " Add lt_uri_query_parameters for ST with $skiptoken or at in the request
    LOOP AT lt_uri_query_parameters INTO ls_dollar_parameter.
      ls_dollar_parameter-name = cl_http_utility=>unescape_url(
        EXPORTING
          escaped = ls_dollar_parameter-name ).
      INSERT ls_dollar_parameter INTO TABLE es_request-t_uri_query_parameter.
    ENDLOOP.

    es_request-resource_from_uri = mo_context->get_resource_path( ).
    SHIFT es_request-resource_from_uri LEFT DELETING LEADING '/'.



* Search
    CLEAR ls_dollar_parameter.
    READ TABLE lt_uri_query_parameters INTO ls_dollar_parameter
      WITH KEY name = /iwfnd/if_sodata_types=>gcs_uri_parameters-search.
    IF sy-subrc = 0.
*-----unescape search string
      ls_dollar_parameter-value =
          cl_http_utility=>unescape_url(
              escaped   = ls_dollar_parameter-value
              options   = 1   "Needed in case this a none-unicode system
          ).

      INSERT ls_dollar_parameter INTO TABLE et_parameter.
    ENDIF.


* startIndex for search
    CLEAR ls_dollar_parameter.
    READ TABLE lt_uri_query_parameters INTO ls_dollar_parameter
      WITH KEY name = /iwfnd/if_sodata_types=>gcs_uri_parameters-startindex.
    IF sy-subrc = 0.
      INSERT ls_dollar_parameter INTO TABLE et_parameter.
    ENDIF.

* delta token
    CLEAR ls_dollar_parameter.
    READ TABLE lt_uri_query_parameters INTO ls_dollar_parameter
      WITH KEY name = /iwfnd/if_sodata_types=>gcs_uri_parameters-delta_token_tmp.
    IF sy-subrc = 0.

*-----$top, $skip is not supported in combination with deltatoken
      IF iv_skip IS NOT INITIAL OR iv_top IS NOT INITIAL.
        CREATE OBJECT lx_tech_exception
          EXPORTING
            textid           = /iwfnd/cx_mgw_tech_exception=>query_option_combination
            http_status_code = '400'.

        raise_technical_error( lx_tech_exception ).
      ENDIF.

      ls_dollar_parameter-name = /iwfnd/if_sodata_types=>gcs_uri_parameters-delta_token. " switch temporary solution for deltatoken
      "deleting '' of '<>'
      ls_dollar_parameter-value = cl_http_utility=>unescape_url(
        escaped = ls_dollar_parameter-value
      ).
      SHIFT ls_dollar_parameter-value RIGHT DELETING TRAILING `'`.
      SHIFT ls_dollar_parameter-value LEFT DELETING LEADING ` '`.
      INSERT ls_dollar_parameter INTO TABLE et_parameter.
    ENDIF.

* totals
    CLEAR: es_request-technical_request-totals.
    lv_totals_error = abap_false.
    READ TABLE lt_uri_query_parameters INTO ls_dollar_parameter
        WITH KEY name = /iwfnd/if_sodata_types=>gcs_uri_parameters-totals.
    IF sy-subrc = 0.
      lo_target_entity_type = eo_target_entity_set->get_entity_type( ).
      SPLIT ls_dollar_parameter-value AT ',' INTO TABLE lt_string.
      LOOP AT lt_string INTO lv_string.
        TRY.
            lo_property = lo_target_entity_type->get_property( lv_string ).
            IF lo_property IS INITIAL.
              lv_totals_error = abap_true.
              EXIT.
            ENDIF.
            lv_name_internal = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( lo_property ).

          CATCH /iwcor/cx_ds_error.
            lv_totals_error = abap_true.
            EXIT.
        ENDTRY.
        APPEND lv_name_internal TO es_request-technical_request-totals.
      ENDLOOP.

      IF lv_totals_error = abap_true.
        CREATE OBJECT lx_tech_exception
          EXPORTING
            textid           = /iwfnd/cx_mgw_tech_exception=>totals_invalid_property
            field_name       = lv_string
            http_status_code = '400'.

        raise_technical_error( lx_tech_exception ).
      ENDIF.
    ENDIF.



*---Media Stream
*---See note 1756994 for further details
*---check whether request is already marked as a stream request - happens in methods
*---/IWCOR/IF_DS_PROC_ENTITY_MEDIA~READ,/IWCOR/IF_DS_PROC_ENTITY_MEDIA~UPDATE,/IWCOR/IF_DS_PROC_ENTITY_MEDIA~DELETE
*---creation of a media stream has a different handling. a create request to a media resource needs to be marked
*---as a media required based on the entity type attribute hasStream as the flow for media resources is different
    IF mv_is_stream = abap_true OR
       iv_operation = mcs_operations-create.

      lo_target_entity_type = eo_target_entity_set->get_entity_type( ).
      mv_is_stream = lo_target_entity_type->has_stream( ).

      IF mv_is_stream = abap_true.
*-------get slug header
        mo_context->get_parameter(
          EXPORTING
            iv_name  = /iwfnd/if_sodata_types=>gcs_cor_request_parameters-slug_header
          IMPORTING
            ev_value = lv_slug_header
        ).

        IF lv_slug_header IS NOT INITIAL.
          ls_dollar_parameter-name  = /iwfnd/if_sodata_types=>gcs_cor_request_parameters-slug_header.
          ls_dollar_parameter-value = lv_slug_header.
          INSERT ls_dollar_parameter INTO TABLE et_parameter.
        ENDIF.

*-----set request type to stream
        es_request-type = 'stream'.

      ENDIF.

    ENDIF.
*--set the absolute service URL for the standalone annotation file use case
    es_request-absolute_serv_url = mo_context->get_service_root_absolute( ).

* Get current MGW context
    IF mo_mgw_context IS NOT BOUND.
      me->get_mgw_context( ).
    ENDIF.

* Set Request Header from OData consumer for BEP Provider
    ASSIGN lt_http_header TO <lt_http_header>.
    mo_mgw_context->/iwfnd/if_mgw_context~get_parameter(
      EXPORTING
        iv_name  = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-all_parameters
      IMPORTING
        ev_value = <lt_http_header>
    ).
    LOOP AT lt_http_header INTO ls_http_header
      WHERE name <> 'sap-dtrace'
        AND name <> 'sap-passport'
        AND name <> 'x-csrf-token'
        AND name <> 'x-requested-with'.

      IF ls_http_header-name IS INITIAL OR
         ls_http_header-name(1) <> '~'  OR
         ls_http_header-name = if_http_header_fields_sap=>request_uri OR
         ls_http_header-name = '~remote_addr'.
        APPEND ls_http_header TO es_request-technical_request-request_header.
      ENDIF.

      "Prefer header handling
      IF ls_http_header-name = gc_prefer_header.
        set_requested_preferences(
          EXPORTING
            iv_prefer_header     = ls_http_header-value
          CHANGING
            cs_technical_request = es_request-technical_request ).
      ENDIF.

* The Message-Scope functionality is fully implemented but deactivated as the requirement
* is obsolete for now by the requester component
*
*      IF ls_http_header-name = gc_message_scope
*      AND ls_http_header-value IS NOT INITIAL.
*        get_nav_path_for_message_scope( EXPORTING iv_message_scope_value = ls_http_header-value
*                                        IMPORTING et_message_scope       = es_request-technical_request-message_scope
*                                                  ev_message_scope_error = es_request-technical_request-message_scope_error ).
*      ENDIF.

    ENDLOOP.

    IF mv_batch = abap_true.
      mo_batch_helper->add_batch_http_headers(
        CHANGING
          ct_http_header = es_request-technical_request-request_header
      ).
    ENDIF.

* Idempotent behavior http request header attributes - RequestID  client-generated GUID - identifier for the request
    CLEAR ls_parameter.
    ls_parameter-value     = mo_transaction_handler->get_request_id( ).
    IF ls_parameter-value IS NOT INITIAL.
      ls_parameter-name = /iwfnd/if_sodata_types=>gcs_cor_request_parameters-request_id.
      INSERT ls_parameter INTO TABLE et_parameter.
    ENDIF.

* Idempotent behavior http request header attributes - RepeatabilityCreation  the date and time at which the request was first created
    CLEAR ls_parameter.
    mo_context->get_parameter(
           EXPORTING
             iv_name  = /iwfnd/if_sodata_types=>gcs_cor_request_parameters-repeatability
           IMPORTING
             ev_value = ls_parameter-value
         ).
    IF ls_parameter-value IS NOT INITIAL.
      ls_parameter-name = /iwfnd/if_sodata_types=>gcs_cor_request_parameters-repeatability.
      INSERT ls_parameter INTO TABLE et_parameter.
    ENDIF.

    IF lv_gwbep_version >= '009'.
*  eTag handling get conditions
      get_conditions( EXPORTING iv_http_method       = iv_http_method
                                io_target_entity_set = eo_target_entity_set
                                io_function_import   = io_function_import
                      IMPORTING es_conditions        = es_request-technical_request-conditions ).
    ENDIF.

* cached request processing
    mo_context->get_parameter(
      EXPORTING
        iv_name  = /iwfnd/if_mgw_context=>gc_param_uri_pattern
      IMPORTING
        ev_value = es_request-uri_pattern ).
    IF es_request-uri_pattern IS NOT INITIAL.
      " URI pattern is filled for co-deployment if json format is requested
      " and URI was recognized successfully -> crp allowed from fnd layer point of view
      es_request-crp_on_hub_allowed = abap_true.
    ENDIF.
    mo_context->get_parameter(
      EXPORTING
        iv_name  = /iwfnd/if_mgw_context=>gc_param_placeholder_valuation
      IMPORTING
        ev_value = es_request-placeholder_valuation ).

* Batch Processing --> Set Context Info
    IF mv_batch = abap_true AND
       mo_batch_helper->mv_etag_processing = abap_false.

      mo_batch_helper->batch_set_context_info(
        EXPORTING
          iv_format            = iv_format
          iv_for_ds_operation  = iv_for_ds_operation
          iv_target_entity     = mv_target_entity
          iv_target_entity_set = mv_target_entity_set
          iv_service_name      = mv_service_name
          iv_service_version   = mv_service_version
          iv_namespace         = mv_namespace
          iv_base_url          = mv_base_url
          iv_source_entity     = mv_source_entity
          iv_source_entity_set = mv_source_entity_set
          iv_edm_multiplicity  = iv_edm_multiplicity
          iv_mdl_cardinality   = iv_mdl_cardinality
          iv_content_format    = mv_content_format
          iv_stream            = mv_is_stream
          io_entity_set        = eo_target_entity_set
          io_function_import   = io_function_import
          io_ds_context        = mo_context
          io_mgw_context       = mo_mgw_context
          io_mdl_service       = mo_service
          io_post_processor    = mo_post_processor
          is_request_details   = es_request
          it_navigation_path   = it_navigation_path
          it_select            = it_select
          it_expand            = it_expand ).
    ENDIF.

    es_request-reset_dp =  mo_transaction_handler->get_is_dp_reset_required( ).

* Soft-State
    mo_transaction_handler->get_soft_state_session_info(
      IMPORTING
        ev_enabled           = es_request-softstate_enabled ).

  ENDMETHOD.                    "init_request


  METHOD raise_business_error.

    DATA: lv_http_status_code TYPE /iwfnd/mgw_http_status_code.


*   Log Exception
    mo_logger->log_step_completion_exception(
      io_exception        = ix_business_exception
      iv_msg_id           = mc_message_class
      iv_msg_number       = mcs_messages-operation_failed
      iv_agent            = mc_agent
      iv_msg_handle       = mv_msg_handle
    ).

*   Add context object to exception
    ix_business_exception->mgw_context = me->get_mgw_context( ).

    lv_http_status_code = ix_business_exception->get_http_status_code( ).

*   Raise an IWCOR Exception matching the http status code (availabe as of BEP SP05) or simply a bad request
    /iwfnd/cx_sodata=>raise_cor_exception(
      EXPORTING
        ix_previous         = ix_business_exception
        iv_http_status_code = lv_http_status_code
        iv_exception_type   = /iwfnd/cx_sodata=>gcs_cor_exceptions-client_bad_request
    ).

  ENDMETHOD.                    "raise_business_error


  METHOD raise_technical_error.

    DATA: lv_http_status_code TYPE /iwfnd/mgw_http_status_code.


*   Log Exception
    mo_logger->log_step_completion_exception(
      io_exception        = ix_technical_exception
      iv_msg_id           = mc_message_class
      iv_msg_number       = mcs_messages-operation_failed
      iv_agent            = mc_agent
      iv_msg_handle       = mv_msg_handle
    ).

*   Add context object to exception
    ix_technical_exception->mgw_context = me->get_mgw_context( ).

    lv_http_status_code = ix_technical_exception->get_http_status_code( ).

*   Raise an IWCOR Exception
    /iwfnd/cx_sodata=>raise_cor_exception(
      EXPORTING
        ix_previous         = ix_technical_exception
        iv_http_status_code = lv_http_status_code
        iv_exception_type   = /iwfnd/cx_sodata=>gcs_cor_exceptions-server_internal_error
    ).

  ENDMETHOD.                    "raise_technical_error


  METHOD read_entityset_post_pro_excel.

    DATA: lo_entity_type      TYPE REF TO /iwfnd/if_med_mdl_node.
    DATA: lo_post_proc_xlsx   TYPE REF TO /iwfnd/if_sodata_post_pro_xlsx.
    DATA: lv_does_class_exist TYPE abap_bool.
    DATA: lx_tech_exception   TYPE REF TO /iwfnd/cx_mgw_tech_exception.
    DATA: lv_time_start       TYPE i.
    DATA: lv_time_stop        TYPE i.
    DATA: lv_duration         TYPE i.


*   Check if active version of class /IWFND/CL_SODATA_POST_PRO_XLSX exists
    lv_does_class_exist = /iwfnd/cl_class_util=>does_class_exist( iv_class_name = /iwfnd/if_sodata_post_pro_xlsx=>co_shortcut_proxy_impl ).

    IF ( lv_does_class_exist = abap_false )
    OR ( is_request_info-technical_request-expand IS NOT INITIAL ).
      CREATE OBJECT lx_tech_exception
        EXPORTING
          textid           = /iwfnd/cx_mgw_tech_exception=>xslx_not_supported
          sap_note_id      = '2050963'     " Format 'xslx' is not supported for this request
          http_status_code = /iwfnd/cx_mgw_tech_exception=>gc_status_not_implemented.
      raise_technical_error( lx_tech_exception ).
    ENDIF.


    CREATE OBJECT lo_post_proc_xlsx
      TYPE (/iwfnd/if_sodata_post_pro_xlsx=>co_shortcut_proxy_impl).

    mo_service->get_entity(
      EXPORTING
        iv_namespace  = ''
        iv_name       = is_request_info-target_entity
      RECEIVING
        ro_entity     = lo_entity_type
    ).


    lv_time_start = mo_sutil_runtime->performance_start(
                      iv_action_name      = TEXT-001       "Excel generation via ALV
                      iv_agent = mc_agent
                    ).
    IF lv_time_start IS INITIAL.
      GET RUN TIME FIELD lv_time_start.
    ENDIF.

    TRY.
        lo_post_proc_xlsx->entity_set_read(
          EXPORTING
            is_request_info              = is_request_info  " Request Info
            ir_data                      = ir_data          " Data of the entity set
            io_entity_type               = lo_entity_type   " Entity type of the entity set
            io_ds_context                = mo_context       " OData context
          RECEIVING
            ro_provider                  = ro_provider      " Binary Provider containing an Excel
        ).

      CATCH /iwfnd/cx_mgw_tech_exception INTO lx_tech_exception.
        raise_technical_error( lx_tech_exception ).

    ENDTRY.

    lv_time_stop = mo_sutil_runtime->performance_stop( lv_time_start ).
    IF lv_time_stop IS INITIAL.
      GET RUN TIME FIELD lv_time_stop.
    ENDIF.
    lv_duration = ( lv_time_stop - lv_time_start ) / 1000.
    mo_sutil_runtime->add_non_gw_time( lv_duration ).

  ENDMETHOD.        " EXCEL_READ_ENTITYSET_POST_PROC


  METHOD read_opensearch_description.

    DATA:  lo_opensrch_desrc_prov TYPE REF TO /iwcor/cl_opensrch_desrc_prov
          ,ls_opensrch_desrc      TYPE        /iwcor/if_opensrch_types=>opensearch_description_s
          ,ls_url                 TYPE        /iwcor/if_opensrch_types=>url_s
          ,ls_meter               TYPE        /iwfnd/s_metering_col
          ,lv_operation           TYPE        char10
          .


    DATA: lv_perf_handle        TYPE i.

    lv_perf_handle = mo_sutil_runtime->performance_start( iv_agent = mc_agent ).

    lv_operation = mcs_operations-search.

    " initialize logger header
    mo_logger->set_header_operation( lv_operation  ).

    mo_logger->set_header_direction( /iwfnd/cl_logger=>gw2iwf ).

* Log - Start
    mv_msg_handle = mo_logger->log_step_init(
                      iv_msg_number    = mcs_messages-operation_start
                      iv_msg_id        = mc_message_class
                      iv_msg_v1        = lv_operation
                      iv_agent         = mc_agent
                    ).


*-Get Service Name, Namespace and Base URL
    mv_service_name    = mo_context->get_service_name( ).
    mv_namespace       = mo_context->get_service_namespace( ).
    mv_base_url        = mo_context->get_service_root_absolute( ).


    CREATE OBJECT lo_opensrch_desrc_prov.

    CONCATENATE 'Search '
                mv_service_name
                ' '
                iv_entity_set
      INTO ls_opensrch_desrc-short_name RESPECTING BLANKS.  "#EC NOTEXT

    CONCATENATE 'Search '
                mv_service_name
                ' '
                iv_entity_set
      INTO ls_opensrch_desrc-description  RESPECTING BLANKS. "#EC NOTEXT

*  search via custom query option (and use top instead of count)
    CONCATENATE mv_base_url
                iv_entity_set
                '?search={searchTerms}&startIndex={startIndex?}&$top={count?}'
      INTO ls_url-template.

    ls_url-type = /iwcor/if_rest_media_type=>gc_appl_atom_xml.
    APPEND ls_url TO ls_opensrch_desrc-urls.
    ls_url-type = /iwcor/if_rest_media_type=>gc_text_html.
    APPEND ls_url TO ls_opensrch_desrc-urls.

    lo_opensrch_desrc_prov->set_opensearch_description( ls_opensrch_desrc ).
    ro_provider = lo_opensrch_desrc_prov.

*-metering
    ls_meter-access_type = mc_consumer_type.
    ls_meter-operation   = mcs_operations-search.
    ls_meter-odc_used    = abap_true.

    mo_context->set_parameter(
      iv_name  = /iwfnd/cl_metering_api=>gc_metering
      iv_value = ls_meter
    ).

*-log - Complete
    mo_logger->log_step_completion(
      iv_msg_type     = /iwfnd/cl_logger=>success
      iv_msg_number   = mcs_messages-operation_finished
      iv_msg_id       = mc_message_class
      iv_agent        = mc_agent
      iv_msg_handle   = mv_msg_handle
    ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.                    "read_opensearch_description


  METHOD serialize_batch_opdata.

    DATA lv_perf_handle  TYPE i.
    DATA lo_rest_entity  TYPE REF TO /iwcor/if_rest_entity.


    lo_rest_entity = io_rest_response->create_entity( ).

    lv_perf_handle = mo_sutil_runtime->performance_start(
                       iv_action_name = 'Lib Serialization - write_to'
                       iv_agent       = mc_agent
                     ).                                     "#EC NOTEXT

    io_provider->write_to( io_entity = lo_rest_entity ).

    mo_sutil_runtime->performance_stop( lv_perf_handle ).

  ENDMETHOD.


  METHOD session_is_terminated.

    TRY.

        " Co-Deployment
        IF mo_co_deploym_proxy IS BOUND.
          mo_co_deploym_proxy->/iwfnd/if_mgw_core_runtime~session_is_terminated( iv_reason = iv_reason ).

          " Separate Backend: Call Provider Delegator
        ELSEIF mo_mgw_runtime IS BOUND.
          CALL BADI mo_mgw_runtime->/iwfnd/if_mgw_core_runtime~session_is_terminated
            EXPORTING
              iv_reason = iv_reason.
        ENDIF.

      CATCH /iwfnd/cx_mgw_tech_exception /iwfnd/cx_mgw_busi_exception. "#EC NO_HANDLER
        "Ignore any exception
    ENDTRY.

  ENDMETHOD.


  METHOD set_cache_ctrl_for_ctxt_token.

    DATA:
      lv_has_context_token          TYPE abap_bool,
      lv_has_context_token_language TYPE abap_bool.


    rv_have_been_set = abap_false.


*   Has there been a valid context token?
    mo_context->get_parameter(
      EXPORTING
        iv_name = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-has_context_token
      IMPORTING
        ev_value = lv_has_context_token ).

    IF lv_has_context_token = abap_false.
      RETURN.
    ENDIF.

*   Has there been a valid language?
    IF iv_is_resource_lang_dependent = abap_true.
      mo_context->get_parameter(
        EXPORTING
          iv_name = /iwfnd/if_sodata_types=>gcs_iwf_context_parameters-has_context_token_language
        IMPORTING
          ev_value = lv_has_context_token_language ).

      IF lv_has_context_token_language = abap_false.
        /iwfnd/cx_sodata=>raise_cor_exception(
         EXPORTING
           ix_previous         = NEW /iwfnd/cx_mgw_tech_exception( textid = /iwfnd/cx_mgw_tech_exception=>context_token_needs_sap_langu )
           iv_exception_type   = /iwfnd/cx_sodata=>gcs_cor_exceptions-client_bad_request ).
      ENDIF.
    ENDIF.


*   standard cache header
    io_lib_provider->set_header(
        iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-cache_control
        iv_value = gcs_cache_header_value-max_age_one_year ).

*   With these ICM specific cache headers a 10 days caching of the response header and payload in the ICF is triggered
    io_lib_provider->set_header(
        iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-icm_cache_control
        iv_value = gcs_cache_header_value-icm_cache_ctrl ).
    io_lib_provider->set_header(
        iv_name  = /iwfnd/if_sodata_types=>gcs_cor_response_parameters-icm_cache_agent
        iv_value = gcs_cache_header_value-icm_cache_agent ).

    rv_have_been_set = abap_true.

  ENDMETHOD.


  METHOD set_cache_hit_info.

    mo_transaction_handler->set_is_bep_cache_hit( is_response_context-is_cache_hit ).

    mo_transaction_handler->set_is_bep_cache_hit_shm( is_response_context-is_cache_hit_shm ).

  ENDMETHOD.


  METHOD set_common_response_headers.

    DATA lv_last_modified TYPE tzntstmps.


    mo_service->get_last_modified( IMPORTING ev_timestamp = lv_last_modified ).

    IF io_provider IS SUPPLIED.
      /iwfnd/cl_sodata_utils=>add_common_response_headers(
        EXPORTING
          io_provider              = io_provider
          is_response_context      = is_response_context
          iv_do_add_cache_header   = iv_do_add_cache_header
          iv_service_last_modified = lv_last_modified ).

    ELSEIF ct_headers IS SUPPLIED.
      /iwfnd/cl_sodata_utils=>add_common_response_headers(
        EXPORTING
          is_response_context      = is_response_context
          iv_do_add_cache_header   = iv_do_add_cache_header
          iv_service_last_modified = lv_last_modified
        CHANGING
          ct_header                = ct_headers ).

    ENDIF.

  ENDMETHOD.


  METHOD set_key_info.

    DATA: lv_key_string_external TYPE string,
          lv_key_string_internal TYPE string,
          ls_key_tab_internal    TYPE /iwfnd/if_mgw_core_types=>ty_s_technical_pair.

    APPEND is_key_external TO ct_key_tab_external.

    ls_key_tab_internal-value = is_key_external-value.
    ls_key_tab_internal-name = /iwfnd/cl_sodata_utils=>get_edm_type_abap_name_via_map( io_edm_instance ).

    APPEND ls_key_tab_internal TO ct_key_tab_internal.

* External names
    CONCATENATE is_key_external-name
                '='
                ''''
                is_key_external-value
                ''''
      INTO lv_key_string_external.

* Internal names
    CONCATENATE ls_key_tab_internal-name
                '='
                ''''
                ls_key_tab_internal-value
                ''''
      INTO lv_key_string_internal.

* Key string with external names
    IF cv_key_string_external IS INITIAL.
      cv_key_string_external = lv_key_string_external.
    ELSE.
      CONCATENATE cv_key_string_external
                  ','
                  lv_key_string_external
        INTO cv_key_string_external.
    ENDIF.

* Key string with internal names
    IF cv_key_string_internal IS INITIAL.
      cv_key_string_internal = lv_key_string_internal.
    ELSE.
      CONCATENATE cv_key_string_internal
                  ','
                  lv_key_string_internal
        INTO cv_key_string_internal.
    ENDIF.

  ENDMETHOD.


  METHOD set_requested_preferences.

    DATA: lv_prefer_header TYPE ihttpval,
          lt_prefer_header TYPE STANDARD TABLE OF ihttpval.

    lv_prefer_header = iv_prefer_header.

    "According to [RFC 7240], it is possible to define the values with quotes, e.g. return="minimal"
    "Therefore, before comparing the values, we get rid of the quotes
    REPLACE ALL OCCURRENCES OF '"' IN lv_prefer_header WITH ''.

    "It is possible that several prefer headers were sent, separated by "," (see [RFC 7240])
    "E.g. return=minimal, handling=strict, return=representation
    "Therefore, we try to split the header value at ","
    SPLIT lv_prefer_header AT ',' INTO TABLE lt_prefer_header.
    CLEAR lv_prefer_header.

    "Loop over all found prefer header and change the technical request correspondingly
    LOOP AT lt_prefer_header INTO lv_prefer_header.

      CASE lv_prefer_header.
        WHEN gc_strict_handling.
          cs_technical_request-is_strict_handling = abap_true.
        WHEN OTHERS. "Nothing else supported at the moment, but can be added easily
          "Do nothing (see [RFC 7240])
      ENDCASE.

    ENDLOOP.

  ENDMETHOD.


  METHOD set_st_metering_info.

    DATA: ls_metering  TYPE /iwfnd/s_metering_col.

    ls_metering-access_type = 'ODATA'.
    ls_metering-operation   = 'read feed'.
    ls_metering-odc_used    = abap_true.
    ls_metering-format_type = is_request_info-format.
    ls_metering-entity_type = is_request_info-technical_request-target_entity_set.
    IF ls_metering-entity_type IS INITIAL.
      ls_metering-entity_type = is_request_info-technical_request-source_entity_set.
    ENDIF.
    ls_metering-entity_depth = is_request_info-technical_request-expand.
    ls_metering-num_entries  = iv_num_entries.
    ls_metering-xml_size     = iv_response_size.

    " Batch Operation
    IF mo_batch_helper->mv_batch = abap_true.
      mo_batch_helper->add_num_entries( iv_num_entries ).
      CONCATENATE 'batch'
                  ls_metering-operation
        INTO ls_metering-operation SEPARATED BY '-'.
    ENDIF.

    io_ds_context->set_parameter(
      iv_name  = /iwfnd/cl_metering_api=>gc_metering
      iv_value = ls_metering
    ).

  ENDMETHOD.
ENDCLASS.