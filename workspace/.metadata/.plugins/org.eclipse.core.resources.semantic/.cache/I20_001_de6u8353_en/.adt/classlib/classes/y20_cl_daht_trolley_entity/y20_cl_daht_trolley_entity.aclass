CLASS y20_cl_daht_trolley_entity DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES y20_if_odata_entity.

    TYPES:
      "!<p class="shorttext synchronized">Type structure of trolley and position</p>
      BEGIN OF tys_trolley_position,
        trolley  TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleycontentent-trolley,
        position TYPE /scwm/huhdr-logpos,
      END OF tys_trolley_position .
    TYPES:
      "!<p class="shorttext synchronized">Type structure of warehouse number and trolley</p>
      BEGIN OF tys_lgnum_trolley,
        lgnum   TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent-lgnum,
        trolley TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent-trolley,
      END OF tys_lgnum_trolley .
    "!<p class="shorttext synchronized">Internal Process Step</p>
    CONSTANTS mc_intern_procs_spr TYPE /scwm/de_iproc  VALUE 'SPR'.
    "!<p class="shorttext synchronized">Constructor</p>
    METHODS constructor
      IMPORTING
        !io_entity TYPE REF TO y20_cl_daht_entity .
    "!<p class="shorttext synchronized">Execute POST request deep entity</p>
    METHODS create_trolley_deep_entity
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_entity)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Handle/Execute user command on UI</p>
    METHODS execute_usercomm_on_trolley
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_entity)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Execute GET request expanded entity</p>
    METHODS get_trolley_expanded_entity
      IMPORTING
        !it_key_tab      TYPE /iwbep/t_mgw_name_value_pair
      RETURNING
        VALUE(ro_entity) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
  PROTECTED SECTION.
  PRIVATE SECTION.
    "!<p class="shorttext synchronized">Instance of current class</p>
    DATA mo_entity TYPE REF TO y20_cl_daht_entity .
    "!<p class="shorttext synchronized">Trolley entity attribute</p>
    DATA ms_trolleyheader TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent .
    "!<p class="shorttext synchronized">Trolley content attribute</p>
    DATA mt_trolleycontent TYPE y20_cl_dass_hu_trolley_mpc=>tt_trolleycontentent .
    "!<p class="shorttext synchronized">Global material attribute</p>
    DATA mt_material_global TYPE /scwm/tt_material_global .
    DATA:
      "!<p class="shorttext synchronized">Scan trolley expanded entity attribute</p>
      BEGIN OF ms_trolley_expanded_entity.
        INCLUDE TYPE y20_cl_dass_hu_trolley_mpc=>ts_scantrolleyent.
    DATA: scantrolley_trolleynav  TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent,
        scantrol_trolcontentnav TYPE STANDARD TABLE OF y20_cl_dass_hu_trolley_mpc=>ts_trolleycontentent WITH EMPTY KEY,
      END OF ms_trolley_expanded_entity .

    DATA:
      "!<p class="shorttext synchronized">UserCommand - Trolley entity attribute</p>
      BEGIN OF ms_user_command_trolley.
        INCLUDE TYPE y20_cl_dass_hu_trolley_mpc=>ts_usercommandent.
    DATA: usercomm_trolley        TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent,
        usercomm_trolleycontent TYPE STANDARD TABLE OF y20_cl_dass_hu_trolley_mpc=>ts_trolleycontentent WITH EMPTY KEY,
        usercomm_trolleylist    TYPE y20_cl_dass_hu_trolley_mpc=>tt_trolleylist,
      END OF ms_user_command_trolley .

    DATA:
      "!<p class="shorttext synchronized">UserCommand - Trolley List(pop-up screen)</p>
      BEGIN OF ms_user_command_trolley_list.
        INCLUDE TYPE y20_cl_dass_hu_trolley_mpc=>ts_usercommandent.
    DATA: usercomm_trolleylist TYPE y20_cl_dass_hu_trolley_mpc=>tt_trolleylist,
      END OF ms_user_command_trolley_list .

    "!<p class="shorttext synchronized">Single open task attribute</p>
    DATA ms_open_task TYPE /scwm/s_to_det_mon .
    "!<p class="shorttext synchronized">Execution method indicator</p>
    DATA mv_execute_method TYPE string .
    DATA mo_trolley TYPE REF TO y20_cl_trolley.
    DATA mo_hu TYPE REF TO y20_cl_odata_handling_unit.

    "!<p class="shorttext synchronized">Build table with MAIDs</p>
    METHODS build_matid_table_from_huitems
      IMPORTING
        !it_stock       TYPE /scwm/tt_stock_select
      RETURNING
        VALUE(rt_matid) TYPE /scwm/tt_matid .

    "!<p class="shorttext synchronized">Change queue in warehouse order</p>
    METHODS change_who_queue
      IMPORTING
        !iv_lgnum TYPE /scwm/lgnum
        !it_who   TYPE /scwm/tt_whoid
        !iv_queue TYPE /scwm/de_queue
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Compare activity areas of last HU and trolley</p>
    METHODS compare_aarea_lasthu_trolley
      IMPORTING
        !iv_lasthu TYPE y20_s_handling_unit_odata-huident
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Convert position number to string</p>
    METHODS convert_level_pos_to_string
      IMPORTING
        !iv_position       TYPE /scwm/huhdr-logpos
      RETURNING
        VALUE(rv_position) TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleycontentent-position .
    "!<p class="shorttext synchronized">Create new task item to resource</p>
    METHODS create_split_task_to_trolley
      IMPORTING
        !iv_huident    TYPE y20_s_handling_unit_odata-huident
        iv_hu_position TYPE /scwm/de_logpos
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">OBSOLETE: Create HU task to resource</p>
    METHODS create_taskhu_to_resource
      IMPORTING
        !is_trolley TYPE tys_trolley_position
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Create HU task to bin</p>
    METHODS create_task_undo_hu_to_bin
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Close multiple trolleys</p>
    METHODS execute_close_trolley_multi
      RAISING
        /iwbep/cx_mgw_tech_exception.
    "!<p class="shorttext synchronized">Close trolley single</p>
    METHODS execute_close_trolley_single
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Handle UNDO command on UI</p>
    METHODS execute_undo_command
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Determine destination bin for task</p>
    METHODS get_bin_for_unto_task
      IMPORTING
        !iv_huident      TYPE y20_s_handling_unit_odata-huident
        !iv_lgnum        TYPE y20_cl_dass_hu_trolley_mpc=>ts_handlingunitent-lgnum OPTIONAL
      RETURNING
        VALUE(rv_result) TYPE /scwm/ordim_c-vlpla
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Determine activity area of last scan HU</p>
    METHODS get_lasthu_activity_area
      IMPORTING
        !iv_huident     TYPE y20_s_handling_unit_odata-huident
      RETURNING
        VALUE(rv_aarea) TYPE /scwm/s_to_det_mon-aarea .
    "!<p class="shorttext synchronized">Get material from MT_MATERIAL_GLOBAL</p>
    METHODS get_material_global
      IMPORTING
        !iv_matid          TYPE /scwm/de_matid
      RETURNING
        VALUE(rs_material) TYPE /scwm/s_material_global .
    "!<p class="shorttext synchronized">Read Delivery Item Text</p>
    METHODS read_delivery_item_text
      IMPORTING
        iv_id          TYPE tdid
        is_docid       TYPE /scwm/s_docid_itemid
      RETURNING
        VALUE(rv_text) TYPE tline-tdline.
    "!<p class="shorttext synchronized">Read defined queue name in parameter framework</p>
    METHODS read_queue_from_parameter
      RETURNING
        VALUE(rv_queue) TYPE /scwm/de_queue .
    "!<p class="shorttext synchronized">Get trolley content from MT_TROLLEYCONTENT</p>
    METHODS get_trolley_content
      RETURNING
        VALUE(rt_entity) TYPE y20_cl_dass_hu_trolley_mpc_ext=>tyt_trolleycontentent .
    "!<p class="shorttext synchronized">Get supplied key values form UI</p>
    METHODS get_trolley_entity_key
      IMPORTING
        !it_key_tab          TYPE /iwbep/t_mgw_name_value_pair
      RETURNING
        VALUE(rs_key_values) TYPE y20_cl_dass_hu_trolley_mpc=>ts_scantrolleyent .
    "!<p class="shorttext synchronized">Get trolley data from MS_TROLLEYHEADER</p>
    METHODS get_trolley_header
      RETURNING
        VALUE(rs_entity) TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent .
    "!<p class="shorttext synchronized">Read/set trolley content data</p>
    METHODS read_set_trolley_content
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set open task data in attribute MS_OPEN_TASK</p>
    METHODS set_open_task_for_hu
      IMPORTING
        !it_open_hutask TYPE /scwm/tt_to_det_mon
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set activity area for trolley</p>
    METHODS set_trolley_activity_area
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set Trolley content in attribute MT_TROLLEYCONTENT</p>
    METHODS set_trolley_content
      IMPORTING
        !it_huitems   TYPE /scwm/tt_stock_select
        !it_huheaders TYPE /scwm/tt_huhdr .
    "!<p class="shorttext synchronized">Split scanned number to trolley and position</p>
    METHODS split_scan_num_to_trol_and_pos
      IMPORTING
        !iv_scan_trolley_number TYPE y20_cl_dass_hu_trolley_mpc=>ts_scantrolleyent-scantrolley
      RETURNING
        VALUE(rs_result)        TYPE tys_trolley_position .
    "!<p class="shorttext synchronized">Validate last scan handling unit</p>
    METHODS validate_last_scanhu
      IMPORTING
        !iv_huident TYPE y20_s_handling_unit_odata-huident
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Validate and set trolley to attribute MS_TROLLEYHEADER</p>
    METHODS validate_set_trolley
      IMPORTING
        !is_entity_keys TYPE tys_lgnum_trolley
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Validate trolley input number</p>
    METHODS validate_trolley
      IMPORTING
        !is_trolley TYPE tys_lgnum_trolley
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Addition validations on trolley</p>
    METHODS validate_trolley_extend_checks
      IMPORTING
        !iv_lasthu      TYPE y20_s_handling_unit_odata-huident
        !iv_hu_position TYPE /scwm/huhdr-logpos
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Check that trolley is open to be used</p>
    METHODS validate_trolley_is_open
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Check scanned position on trolley is free</p>
    METHODS validate_trolley_pos_is_empty
      IMPORTING
        !is_trolley TYPE tys_trolley_position
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Check HUs for return tasks are on trolley</p>
    METHODS validate_undo_items_on_trolley
      IMPORTING
        it_trolley_undo_items TYPE y20_cl_dass_hu_trolley_mpc=>tt_trolleycontentent
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS get_trolley_list_expanded
      IMPORTING
        it_key_tab       TYPE /iwbep/t_mgw_name_value_pair
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception.
    METHODS read_resources
      IMPORTING
        iv_lgnum       TYPE /scwm/lgnum
      RETURNING
        VALUE(rt_rsrc) TYPE /scwm/tt_rsrce
      RAISING
        /iwbep/cx_mgw_tech_exception.
ENDCLASS.



CLASS y20_cl_daht_trolley_entity IMPLEMENTATION.


  METHOD build_matid_table_from_huitems.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Build table with material IDs from stock
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( it_stock ) = 0.
      RETURN.
    ENDIF.

    rt_matid = VALUE #( FOR <ls_stock> IN it_stock
      ( <ls_stock>-matid ) ).
  ENDMETHOD.


  METHOD change_who_queue.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Change warehouse order queue name attribute
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA ls_attributes  TYPE /scwm/s_who_att.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(ls_queue) = mo_entity->read_queue( iv_lgnum = iv_lgnum
                                            iv_queue = iv_queue ).
    DATA(lt_who) = mo_entity->read_warehouse_order( iv_lgnum    = iv_lgnum
                                                    it_who      = it_who
                                                    iv_lock_who = abap_true ).
    LOOP AT lt_who ASSIGNING FIELD-SYMBOL(<ls_who>).
      CHECK <ls_who>-queue <> ls_queue-queue.
      MOVE-CORRESPONDING <ls_who> TO ls_attributes.
      ls_attributes-queue = ls_queue-queue.

      TRY.
          mo_entity->update_warehouse_order( iv_lgnum      = iv_lgnum
                                       iv_who        = <ls_who>-who
                                       iv_update_queue = abap_true
                                       is_attributes = ls_attributes ).
          ##NO_HANDLER
        CATCH /iwbep/cx_mgw_tech_exception.
      ENDTRY.
    ENDLOOP.

    CALL FUNCTION 'DEQUEUE_ALL'.
  ENDMETHOD.


  METHOD compare_aarea_lasthu_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Compare activity areas of last HU and trolley
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " if the trolley is empty no no activity area will be filled
    " nothing to compare in this case
    IF ms_trolleyheader-aarea IS INITIAL.
      RETURN.
    ENDIF.

    IF get_lasthu_activity_area( iv_lasthu ) <> ms_trolleyheader-aarea.
      ##NEEDED
      MESSAGE e006(y20_ui5) WITH iv_lasthu ms_trolleyheader-trolley INTO DATA(lv_msg).
      mo_entity->raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD constructor.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Build instance of current class
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_entity = io_entity.

    mo_trolley = y20_cl_trolley=>get_instance( ).

    mo_hu = NEW y20_cl_odata_handling_unit( ).
  ENDMETHOD.


  METHOD convert_level_pos_to_string.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Convert position level of HU to string for UI
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_position IS INITIAL.
      RETURN.
    ENDIF.

    rv_position = |{ iv_position(1) }-{ iv_position+1(1) }|.
  ENDMETHOD.


  METHOD create_split_task_to_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Create new task item to resource. Split main task to new
*                 item. Main task stays open
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA ls_attributes  TYPE /scwm/s_who_att.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_task) = mo_entity->read_open_hutask_by_huident(
        iv_lgnum       = ms_trolleyheader-lgnum
        iv_huident     = iv_huident
        iv_read_buffer = abap_true ).
    TRY.
        DATA(ls_task) = REF #( lt_task[ 1 ] ).
      CATCH cx_sy_itab_line_not_found.
        ##NEEDED
        MESSAGE e001(y20_ui5) WITH iv_huident INTO DATA(lv_msg).
        mo_entity->raise_exception( ).
    ENDTRY.

    mo_entity->create_spit_task_to_resource( iv_lgnum = ms_trolleyheader-lgnum
                                             iv_resource = ms_trolleyheader-trolley
                                             is_open_task = ls_task ).

    DATA(lt_who) = mo_entity->read_warehouse_order(
        iv_lgnum    = ms_trolleyheader-lgnum
        it_who      = VALUE #( ( who = ls_task->who ) )
        iv_lock_who = abap_true ).

    ASSIGN lt_who[ 1 ] TO FIELD-SYMBOL(<ls_who>).
    IF sy-subrc = 0.
      MOVE-CORRESPONDING <ls_who> TO ls_attributes.
      ls_attributes-rsrc = ms_trolleyheader-trolley.
      ls_attributes-processor = sy-uname.
      mo_entity->update_warehouse_order( iv_lgnum      = ms_trolleyheader-lgnum
                                         iv_who        = ls_task->who
                                         is_attributes = ls_attributes ).
    ENDIF.

    " update HU position
    mo_entity->update_hu_logpos(
      EXPORTING
        iv_huident = iv_huident
        iv_logpos  = iv_hu_position ).
  ENDMETHOD.


  METHOD create_taskhu_to_resource.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Create HU task to resource. Process type for the task is
*                 determined form parameter framework
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_proctype) = zswl_cl_param=>read_const(
                         iv_lgnum     = ms_trolleyheader-lgnum
                         iv_context   = zswl_if_param_c=>mc_ui5_deco
                         iv_parameter = zswl_if_param_c=>mc_ui5_deco_procty ).

    mo_entity->create_taskhu_to_resource(
      EXPORTING
        iv_lgnum    = ms_trolleyheader-lgnum
        iv_guid_hu  = ms_trolleyheader-lasthu-guid_hu
        iv_resource = ms_trolleyheader-trolley
        iv_proctype = CONV #( lv_proctype )
        iv_logpos   = is_trolley-position
      IMPORTING
        et_bapiret  = DATA(lt_bapiret)
        ev_severity = DATA(lv_severity) ).

    IF lv_severity CA wmegc_severity_ea.
      LOOP AT lt_bapiret ASSIGNING FIELD-SYMBOL(<ls_bapiret>) WHERE type CA wmegc_severity_ea.
        ##NEEDED
        MESSAGE ID <ls_bapiret>-id TYPE <ls_bapiret>-type NUMBER <ls_bapiret>-number
              WITH <ls_bapiret>-message_v1 <ls_bapiret>-message_v2
                   <ls_bapiret>-message_v3 <ls_bapiret>-message_v4 INTO DATA(lv_msg).
      ENDLOOP.
      mo_entity->raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD create_task_undo_hu_to_bin.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Create HU task from resource to destination bin. Process type
*                 is read from parameter framework. Destination bin is
*                 determined from the open task
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " no check needed. MT_TROLLEYCONTENT already validated
    DATA(ls_trolley) = REF #( mt_trolleycontent[ 1 ] ).

    DATA(lv_destination_bin) = get_bin_for_unto_task( iv_lgnum = ls_trolley->lgnum
                                                      iv_huident = ls_trolley->hu-huident ).

    DATA(lv_proctype) = zswl_cl_param=>read_const(
                         iv_lgnum     = ms_trolleyheader-lgnum
                         iv_context   = zswl_if_param_c=>mc_ui5_deco
                         iv_parameter = zswl_if_param_c=>mc_ui5_deco_procty ).

    LOOP AT mt_trolleycontent ASSIGNING FIELD-SYMBOL(<ls_hu>).
      TRY.
          mo_entity->create_taskhu_to_bin(
              iv_lgnum    = ls_trolley->lgnum
              iv_guid_hu  = ls_trolley->hu-guid_hu
              iv_bin      = lv_destination_bin
              iv_proctype = CONV #( lv_proctype ) ).
          ##NEEDED
          MESSAGE i014(y20_ui5) WITH ls_trolley->hu-huident lv_destination_bin INTO DATA(lv_msg).
          mo_entity->mo_log->add_message( ).
          ##NO_HANDLER
        CATCH /iwbep/cx_mgw_tech_exception.
      ENDTRY.
    ENDLOOP.

  ENDMETHOD.


  METHOD create_trolley_deep_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Handle POST request of scanned trolley.
*                1. Scanned data is validated and attributes are set
*                2. Split task is created to resource
*                3. Set and return data to UI
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data( IMPORTING es_data = ms_trolley_expanded_entity ).
    validate_last_scanhu( ms_trolley_expanded_entity-scantrolley_trolleynav-lasthu-huident ).

    DATA(ls_trolley_position) = mo_trolley->split_scannum_to_trol_and_pos( CONV #( ms_trolley_expanded_entity-scantrolley ) ).

    validate_set_trolley( is_entity_keys = VALUE #( lgnum = ms_trolley_expanded_entity-lgnum
                                                    trolley = ls_trolley_position-trolley ) ).
    ms_trolleyheader-lasthu = ms_trolley_expanded_entity-scantrolley_trolleynav-lasthu.

    read_set_trolley_content( ).

    set_trolley_activity_area( ).

    IF ms_trolleyheader-lasthu IS NOT INITIAL.
      validate_trolley_extend_checks( iv_lasthu = ms_trolleyheader-lasthu-huident
                                      iv_hu_position = ls_trolley_position-position ).

      create_split_task_to_trolley( iv_huident = ms_trolleyheader-lasthu-huident
                                    iv_hu_position = ls_trolley_position-position ).

      read_set_trolley_content( ).
    ENDIF.

    ms_trolley_expanded_entity-lgnum = ms_trolleyheader-lgnum.
    ms_trolley_expanded_entity-scantrolley_trolleynav = ms_trolleyheader.
    ms_trolley_expanded_entity-scantrol_trolcontentnav = mt_trolleycontent.

    ro_entity = mo_entity->copy_data_to_ref( is_data = ms_trolley_expanded_entity ).
  ENDMETHOD.


  METHOD execute_close_trolley_multi.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Close all trolleys
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    LOOP AT ms_user_command_trolley-usercomm_trolleylist ASSIGNING FIELD-SYMBOL(<ls_trolley>).
      ms_user_command_trolley-usercomm_trolley-lgnum = <ls_trolley>-lgnum.
      ms_user_command_trolley-usercomm_trolley-trolley = <ls_trolley>-trolley.

      execute_close_trolley_single( ).
    ENDLOOP.
  ENDMETHOD.


  METHOD execute_close_trolley_single.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Close single trolley. Queues of warehouse orders linked
*                 to the HUs on trolley are changed to new queue.
*                 If trolley is in this queue that means that trolley
*                 is closed.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_rsrc_who TYPE /scwm/tt_who_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    validate_set_trolley( is_entity_keys = VALUE #( lgnum = ms_user_command_trolley-usercomm_trolley-lgnum
                                                    trolley = ms_user_command_trolley-usercomm_trolley-trolley ) ).

    " get all who assigned to this trolley
    CALL FUNCTION '/SCWM/RSRC_WHO_RESOURCE_GET'
      EXPORTING
        iv_lgnum    = ms_trolleyheader-lgnum
        iv_rsrc     = ms_trolleyheader-trolley
      IMPORTING
        et_rsrc_who = lt_rsrc_who.
    IF lines( lt_rsrc_who ) = 0.
      MESSAGE e036(y20_ui5) WITH ms_trolleyheader-trolley INTO DATA(lv_msg).
      mo_entity->raise_exception( ).
    ENDIF.

    DATA(lt_who) = VALUE /scwm/tt_whoid( FOR <ls_who> IN lt_rsrc_who
        WHERE ( who IS NOT INITIAL )
        ( who = <ls_who>-who ) ).

    change_who_queue( iv_lgnum = ms_trolleyheader-lgnum
                      it_who   = lt_who
                      iv_queue = read_queue_from_parameter( ) ).
  ENDMETHOD.


  METHOD execute_undo_command.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Handle UNDO command on UI. Create tasks for selected HUs
*                 from resource back to the source HU
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( ms_user_command_trolley-usercomm_trolleycontent ) = 0.
      ##NEEDED
      MESSAGE e003(y20_ui5) INTO DATA(lv_msg).
      mo_entity->raise_exception( ).
    ENDIF.

    mt_trolleycontent = ms_user_command_trolley-usercomm_trolleycontent.

    DATA(ls_trolley) = REF #( mt_trolleycontent[ 1 ] ).
    validate_set_trolley( is_entity_keys = VALUE #( lgnum = ls_trolley->lgnum
                                                    trolley = ls_trolley->trolley ) ).
    validate_undo_items_on_trolley( mt_trolleycontent ).

    create_task_undo_hu_to_bin( ).

    read_set_trolley_content( ).

    ms_user_command_trolley-usercomm_trolleycontent = mt_trolleycontent.
  ENDMETHOD.


  METHOD execute_usercomm_on_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Handle user command on UI. Dispatch command to the
*                correct execution method
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data(
      IMPORTING es_data = ms_user_command_trolley ).

    CASE ms_user_command_trolley-usercommand.
      WHEN y20_cl_dass_hu_trolley_mpc_ext=>ms_user_command-close_trolley.
        execute_close_trolley_single( ).
      WHEN y20_cl_dass_hu_trolley_mpc_ext=>ms_user_command-close_all_trolley.
        execute_close_trolley_multi( ).
      WHEN y20_cl_dass_hu_trolley_mpc_ext=>ms_user_command-undo.
        execute_undo_command( ).
    ENDCASE.

    ro_entity = mo_entity->copy_data_to_ref( is_data = ms_user_command_trolley ).
  ENDMETHOD.


  METHOD get_bin_for_unto_task.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Find destination bin for UNDO task
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_ordim_c TYPE /scwm/tt_ordim_c.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_task) = mo_entity->read_open_hutask_by_huident( iv_lgnum       = iv_lgnum
                                                            iv_huident     = iv_huident
                                                            iv_read_buffer = abap_true ).
    " no checks for LT_TASK needed. Exception is raised from the read method
    CALL FUNCTION '/SCWM/TO_READ_SINGLE'
      EXPORTING
        iv_lgnum     = iv_lgnum
        iv_tanum     = lt_task[ 1 ]-tanum
      IMPORTING
        et_ordim_c   = lt_ordim_c
      EXCEPTIONS
        wrong_input  = 1
        not_found    = 2
        foreign_lock = 3
        error        = 4
        OTHERS       = 5.
    IF sy-subrc <> 0.
      ##NEEDED
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_msg).
      mo_entity->raise_exception( ).
    ENDIF.

    rv_result = lt_ordim_c[ 1 ]-vlpla.
  ENDMETHOD.


  METHOD get_lasthu_activity_area.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Get activity area of task linked to the supplied HU
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(lt_open_tasks) = mo_entity->read_open_hutask_by_huident( iv_lgnum     = ms_trolleyheader-lgnum
                                                                      iv_huident   = iv_huident ).
        rv_aarea = lt_open_tasks[ 1 ]-aarea.
        ##NO_HANDLER
      CATCH /iwbep/cx_mgw_tech_exception.
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
  ENDMETHOD.


  METHOD get_material_global.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Get material from attribute table MT_MATERIAL_GLOBAL
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        rs_material = mt_material_global[ matid = iv_matid ].
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
  ENDMETHOD.


  METHOD get_trolley_content.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Return trolley content
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    rt_entity = mt_trolleycontent.
  ENDMETHOD.


  METHOD get_trolley_entity_key.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Determine key values supplied from UI
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    LOOP AT it_key_tab ASSIGNING FIELD-SYMBOL(<ls_key>).
      CASE <ls_key>-name.
        WHEN y20_cl_dass_hu_trolley_mpc_ext=>ms_field_name-lgnum.
          rs_key_values-lgnum = <ls_key>-value.
        WHEN y20_cl_dass_hu_trolley_mpc_ext=>ms_field_name-scantrolley.
          rs_key_values-scantrolley = <ls_key>-value.
      ENDCASE.
    ENDLOOP.
  ENDMETHOD.


  METHOD get_trolley_expanded_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Read and return HU data.
*               1. Validate scanned input
*               2. Build and return entity data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(ls_entity_keys) = get_trolley_entity_key( it_key_tab ).

    DATA(ls_trolley_position) = split_scan_num_to_trol_and_pos( ls_entity_keys-scantrolley ).

    validate_set_trolley( is_entity_keys = VALUE #( lgnum = ls_entity_keys-lgnum
                                                    trolley = ls_trolley_position-trolley ) ).
    TRY.
        read_set_trolley_content( ).
        set_trolley_activity_area( ).
        ##NO_HANDLER
      CATCH /iwbep/cx_mgw_tech_exception.
    ENDTRY.

    ms_trolley_expanded_entity-scantrolley_trolleynav = get_trolley_header( ).
    ms_trolley_expanded_entity-scantrol_trolcontentnav = get_trolley_content( ).

    ro_entity = mo_entity->copy_data_to_ref( is_data = ms_trolley_expanded_entity ).
  ENDMETHOD.


  METHOD get_trolley_header.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Return HU entity data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    rs_entity = ms_trolleyheader.
  ENDMETHOD.


  METHOD read_queue_from_parameter.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Read queue name from parameter framework
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_result) =  zswl_cl_param=>read_const( iv_lgnum     = ms_trolleyheader-lgnum
                                                  iv_context   = zswl_if_param_c=>mc_deco
                                                  iv_parameter = zswl_if_param_c=>mc_deco_putq ).
    rv_queue = CONV #( lv_result ).
  ENDMETHOD.


  METHOD read_set_trolley_content.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Read trolley content data and set attribute MT_TROLLEYCONTENT
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: mt_trolleycontent.

    mo_entity->read_trolley_content(
      EXPORTING
        iv_lgnum   = ms_trolleyheader-lgnum
        iv_trolley = ms_trolleyheader-trolley
      IMPORTING
        et_huitems = DATA(lt_huitems)
        et_huheaders = DATA(lt_huheaders) ).
    IF lines( lt_huitems ) = 0 OR lines( lt_huheaders ) = 0.
      ##NEEDED
      MESSAGE i010(y20_ui5) WITH ms_trolleyheader-trolley INTO DATA(lv_msg).
      mo_entity->mo_log->add_message( ).
      RETURN.
    ENDIF.

    set_trolley_content( it_huitems = lt_huitems
                         it_huheaders = lt_huheaders ).
  ENDMETHOD.


  METHOD set_open_task_for_hu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Set open task in attribute MS_OPEN_TASK
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        ms_open_task = it_open_hutask[ 1 ].
      CATCH cx_sy_itab_line_not_found.
        IF lines( mt_trolleycontent ) > 0.
          mo_entity->raise_exception( ).
        ENDIF.
    ENDTRY.
  ENDMETHOD.


  METHOD set_trolley_activity_area.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Determine activity area for trolley.
*               Get any HU in the trolley and read its task to get the
*                activity area.
*                Tasks for all HUs in the trolley will have the same
*                activity area
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(lt_open_hutask) = mo_entity->read_open_hutask_by_huident( iv_lgnum     = ms_trolleyheader-lgnum
                                                                       iv_huident   = VALUE #( mt_trolleycontent[ 1 ]-hu-huident ) ).
        set_open_task_for_hu( it_open_hutask = lt_open_hutask ).
        ms_trolleyheader-aarea = ms_open_task-aarea.
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
  ENDMETHOD.


  METHOD set_trolley_content.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Set trolley content to attribute MT_TROLLEYCONTENT
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: mt_trolleycontent.

    IF lines( mt_material_global ) = 0 AND
       lines( it_huitems ) > 0.
      " read materials for each HU
      mo_entity->read_material_multi_by_matid(
        EXPORTING
          it_matid           = build_matid_table_from_huitems( it_huitems )
          iv_lgnum           = ms_trolleyheader-lgnum
        IMPORTING
          et_material_global = mt_material_global ).
    ENDIF.

    LOOP AT it_huitems ASSIGNING FIELD-SYMBOL(<ls_huitm>).
      TRY.
          DATA(ls_huhdr) = REF #( it_huheaders[ guid_hu = <ls_huitm>-guid_parent ] ).
        CATCH cx_sy_itab_line_not_found.
          CONTINUE.
      ENDTRY.

      " get material for current HU
      DATA(ls_material_global) = get_material_global( <ls_huitm>-matid ).

      INSERT VALUE #( lgnum = ms_trolleyheader-lgnum
                      trolley = ms_trolleyheader-trolley
                      position = convert_level_pos_to_string( ls_huhdr->logpos )
                      hu-guid_hu = ls_huhdr->guid_hu
                      hu-huident = ls_huhdr->huident
                      hu-hutype = ls_huhdr->letyp
                      hu-packmat-matid = ls_huhdr->pmat_guid
                      product-matid = ls_material_global-matid
                      product-matnr = ls_material_global-matnr
                      quantity-quan = <ls_huitm>-quan
                      quantity-unit = <ls_huitm>-meins ) INTO TABLE mt_trolleycontent.
    ENDLOOP.
  ENDMETHOD.


  METHOD split_scan_num_to_trol_and_pos.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Split scanned number to trolley and position number
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CONSTANTS lc_position_length TYPE i VALUE 2.
    IF iv_scan_trolley_number IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lv_number_length) = strlen( iv_scan_trolley_number ).
    lv_number_length = lv_number_length - lc_position_length.

    rs_result-trolley = iv_scan_trolley_number(lv_number_length).
    rs_result-position = iv_scan_trolley_number+lv_number_length(lc_position_length).
  ENDMETHOD.


  METHOD validate_last_scanhu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Validate HU identification number. If not found exception
*                is raised from the read method
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_entity->read_hu_by_huident( iv_huident = iv_huident ).
  ENDMETHOD.


  METHOD validate_set_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Validate input data(warehouse & trolley).
*                Set verified values to attribute MS_TROLLEYHEADER
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: ms_trolleyheader.

    mo_entity->validate_warehouse_number( is_entity_keys-lgnum ).

    validate_trolley( is_entity_keys ).

    ms_trolleyheader-lgnum = is_entity_keys-lgnum.
    ms_trolleyheader-trolley = is_entity_keys-trolley.
  ENDMETHOD.


  METHOD validate_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Validate trolley input number. If not found exception is
*                 raised from the read method
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_entity->read_trolley_single( iv_lgnum   = is_trolley-lgnum
                                    iv_trolley = is_trolley-trolley ).
  ENDMETHOD.


  METHOD validate_trolley_extend_checks.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Do additional/business checks for trolley
*                1. check trolley is open - validate WO queue
*                2. Compare activity area of last scan HU to the trolley
*                   If different HU cannot be moved to the trolley
*                3. Check position on trolley is empty
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    validate_trolley_is_open( ).

    compare_aarea_lasthu_trolley( iv_lasthu = iv_lasthu ).

    validate_trolley_pos_is_empty( VALUE #( trolley = ms_trolleyheader-trolley
                                            position = iv_hu_position ) ).
  ENDMETHOD.


  METHOD validate_trolley_is_open.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Check trolley is open. Check is based on the WO queue.
*               There is a certain queue which contained only closed trolleys
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_queue_for_closed_docs) = read_queue_from_parameter( ).

    IF lv_queue_for_closed_docs = ms_open_task-queue.
      ##NEEDED
      MESSAGE e007(y20_ui5) WITH ms_trolleyheader-trolley INTO DATA(lv_msg).
      mo_entity->raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD validate_trolley_pos_is_empty.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Check scanned position on trolley is empty
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(ls_trolley_pos) = REF #( mt_trolleycontent[ trolley = is_trolley-trolley
                                                         position = convert_level_pos_to_string( iv_position = is_trolley-position  ) ] ).
        IF ls_trolley_pos->hu IS NOT INITIAL.
          ##NEEDED
          MESSAGE e002(y20_ui5) WITH is_trolley-position ls_trolley_pos->hu-huident INTO DATA(lv_msg).
          mo_entity->raise_exception( ).
        ENDIF.
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
  ENDMETHOD.


  METHOD validate_undo_items_on_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Check selected HUs/items for UNDO tasks exist on trolley
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    ##NEEDED
    DATA lv_msg TYPE string.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_entity->read_trolley_content(
      EXPORTING
        iv_lgnum     = ms_trolleyheader-lgnum
        iv_trolley   = ms_trolleyheader-trolley
      IMPORTING
        et_huitems   = DATA(lt_trolley_content) ).
    IF lines( lt_trolley_content ) = 0.
      MESSAGE e004(y20_ui5) WITH ms_trolleyheader-trolley INTO lv_msg.
      mo_entity->raise_exception( ).
    ENDIF.

    LOOP AT it_trolley_undo_items ASSIGNING FIELD-SYMBOL(<ls_undo_item>).
      TRY.
          ##NEEDED
          DATA(ls_item_on_trolley) = REF #( lt_trolley_content[ guid_parent = <ls_undo_item>-hu-guid_hu ] ).
        CATCH cx_sy_itab_line_not_found.
          MESSAGE e005(y20_ui5) WITH <ls_undo_item>-hu-huident ms_trolleyheader-trolley INTO lv_msg.
          DATA(lv_error) = abap_true.
          EXIT.
      ENDTRY.
    ENDLOOP.

    IF lv_error = abap_true.
      mo_entity->raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD y20_if_odata_entity~create_deep_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Execute POST request for trolley. Dispatch to the correct
*               function execution based on MV_EXECUTE_METHOD. This
*               attribute is set in the factory class
*               Y20_CL_ODATA_DAHT_ENT_FACTORY
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CASE mv_execute_method.
      WHEN  y20_cl_dass_hu_trolley_mpc_ext=>mc_techclause_scantr_trcontnav.
        ro_deep_entity = create_trolley_deep_entity( io_data_provider = io_data_provider ).

      WHEN y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_trolleycontent OR
           y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_trolleylist.
        ro_deep_entity = execute_usercomm_on_trolley( io_data_provider = io_data_provider ).

*      WHEN y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_trolleylist.
*        ro_deep_entity = execute_usercomm_c( io_data_provider = io_data_provider ).
    ENDCASE.

  ENDMETHOD.


  METHOD y20_if_odata_entity~get_expanded_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Execute GET request for trolley. Dispatch to the correct
*               function execution based on MV_EXECUTE_METHOD. This
*               attribute is set in the factory class
*               Y20_CL_ODATA_DAHT_ENT_FACTORY
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CASE mv_execute_method.
      WHEN  y20_cl_dass_hu_trolley_mpc_ext=>mc_techclause_scantr_trcontnav.
        er_entity = get_trolley_expanded_entity( it_key_tab = it_key_tab ).
        APPEND y20_cl_dass_hu_trolley_mpc_ext=>mc_techclause_scantr_trollenav TO et_expanded_tech_clauses.
        APPEND y20_cl_dass_hu_trolley_mpc_ext=>mc_techclause_scantr_trcontnav TO et_expanded_tech_clauses.

      WHEN y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_trolleylist.
        er_entity = get_trolley_list_expanded( it_key_tab = it_key_tab ).
        APPEND y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_trolleylist TO et_expanded_tech_clauses.

    ENDCASE.

  ENDMETHOD.


  METHOD y20_if_odata_entity~get_log.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Return message log
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    ro_log = mo_entity->mo_log.
  ENDMETHOD.


  METHOD y20_if_odata_entity~set_execute_method.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Set attribute MV_EXECUTE_METHOD. Gives indication for
*                 the correct function execution
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mv_execute_method = iv_key.
  ENDMETHOD.

  METHOD get_trolley_list_expanded.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(ls_entity_keys) = get_trolley_entity_key( it_key_tab ).

    mo_entity->validate_warehouse_number( iv_lgnum = ls_entity_keys-lgnum ).

    DATA(lt_rsrc) = read_resources( iv_lgnum = ls_entity_keys-lgnum ).

    LOOP AT lt_rsrc ASSIGNING FIELD-SYMBOL(<ls_rsrc>).
      mo_entity->read_trolley_content(
        EXPORTING
          iv_lgnum     = ls_entity_keys-lgnum
          iv_trolley   = <ls_rsrc>
        IMPORTING
          et_huheaders = DATA(lt_huhdr) ).
      DATA(lv_lines) = lines( lt_huhdr ).
      IF lv_lines = 0.
        CONTINUE.
      ENDIF.

      APPEND VALUE #( lgnum = ls_entity_keys-lgnum
                      trolley = <ls_rsrc>
                      hucount = lv_lines ) TO ms_user_command_trolley_list-usercomm_trolleylist.
    ENDLOOP.

    ms_user_command_trolley_list-lgnum = ls_entity_keys-lgnum.

    ro_result = mo_entity->copy_data_to_ref( is_data = ms_user_command_trolley_list ).
  ENDMETHOD.


  METHOD read_resources.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Find resources(trolleys) for DECO
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_msg TYPE string.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_rsrc_type) = zswl_cl_param=>read_const_list(
        iv_lgnum     = iv_lgnum
        iv_context   = zswl_if_param_c=>mc_deco
        iv_parameter = zswl_if_param_c=>mc_deco_inbr_rsrctype ).
    IF lines( lt_rsrc_type ) = 0.
      MESSAGE e025(y20_ui5) INTO lv_msg.
      mo_entity->raise_exception( ).
    ENDIF.

    DATA(lt_selopt_rsrc_type) = VALUE rseloption( FOR <ls_rtype> IN lt_rsrc_type
                                   ( sign   = wmegc_sign_inclusive
                                     option = wmegc_option_eq
                                     low    = <ls_rtype> ) ).
    SELECT a~rsrc INTO TABLE @DATA(lt_rsrc)
      FROM /scwm/rsrc AS a
     INNER JOIN /scwm/tworkst AS b ON b~lgtyp = a~lgtyp
     INNER JOIN /scwm/tprocs  AS c ON c~procs = b~procs
     WHERE c~iproc = @mc_intern_procs_spr
       AND b~lgnum = @iv_lgnum
       AND a~lgnum = @iv_lgnum
       AND a~rsrc_type IN @lt_selopt_rsrc_type.
    IF sy-subrc <> 0 OR lines( lt_rsrc ) = 0.
      MESSAGE e026(y20_ui5) INTO lv_msg.
      mo_entity->raise_exception( ).
    ENDIF.

    rt_rsrc = VALUE #( FOR <ls_rsrc> IN lt_rsrc ( <ls_rsrc>-rsrc ) ).
  ENDMETHOD.

  METHOD read_delivery_item_text.

  ENDMETHOD.

ENDCLASS.