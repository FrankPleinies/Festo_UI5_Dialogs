CLASS y20_cl_odata_daht_ent_factory DEFINITION
  PUBLIC
  INHERITING FROM y20_cl_odata_entity_factory
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    METHODS create_entity REDEFINITION.
  PROTECTED SECTION.
    METHODS create_odata_db_function REDEFINITION.
  PRIVATE SECTION.
ENDCLASS.



CLASS y20_cl_odata_daht_ent_factory IMPLEMENTATION.

  METHOD create_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Return entity execution class
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA lv_method TYPE i.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_entity) = io_expand->get_tech_entity_type( ).

    CASE lv_entity.
      WHEN y20_cl_dass_hu_trolley_mpc=>gc_scanhandlingunitent.
        ro_entity = NEW y20_cl_daht_hu_entity( io_entity = create_odata_db_function( ) ).
      WHEN y20_cl_dass_hu_trolley_mpc=>gc_scantrolleyent.
        ro_entity = NEW y20_cl_daht_trolley_entity( io_entity = create_odata_db_function( ) ).
    ENDCASE.

    DATA(lt_entities) = io_expand->get_children( ).
    LOOP AT lt_entities ASSIGNING FIELD-SYMBOL(<ls_entity>).
      CASE <ls_entity>-tech_nav_prop_name.
        WHEN y20_cl_dass_hu_trolley_mpc_ext=>mc_techclause_scanhu_hunav.
          lv_method = build_execute_method_key( iv_new_key = 1
                                                iv_existing_key = lv_method ).
        WHEN y20_cl_dass_hu_trolley_mpc_ext=>mc_techclause_scantr_trollenav.
          lv_method = build_execute_method_key( iv_new_key = 2
                                                iv_existing_key = lv_method ).
        WHEN y20_cl_dass_hu_trolley_mpc_ext=>mc_techclause_scantr_trcontnav.
          lv_method = build_execute_method_key( iv_new_key = 3
                                                iv_existing_key = lv_method ).
        WHEN y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_hu.
          lv_method = build_execute_method_key( iv_new_key = 6
                                                iv_existing_key = lv_method ).
        WHEN y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_trolley.
          lv_method = build_execute_method_key( iv_new_key = 7
                                                iv_existing_key = lv_method ).
        WHEN y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_trolleycontent.
          lv_method = build_execute_method_key( iv_new_key = 8
                                                iv_existing_key = lv_method ).
      ENDCASE.
    ENDLOOP.

    CASE lv_method.
      WHEN 1.
        " get HU expanded entity
        ro_entity->set_execute_method( iv_key = y20_cl_dass_hu_trolley_mpc_ext=>mc_techclause_scanhu_hunav ).
      WHEN 6.
        ro_entity = NEW y20_cl_daht_hu_entity( io_entity = create_odata_db_function( ) ).
        ro_entity->set_execute_method( iv_key = y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_hu ).
      WHEN 7 OR 8.
        ro_entity = NEW y20_cl_daht_trolley_entity( io_entity = create_odata_db_function( ) ).
        ro_entity->set_execute_method( iv_key = y20_cl_dass_hu_trolley_mpc_ext=>mc_usercomm_trolleycontent ).
      WHEN 23.
        " get trolley expanded entity
        ro_entity->set_execute_method( iv_key = y20_cl_dass_hu_trolley_mpc_ext=>mc_techclause_scantr_trcontnav ).
    ENDCASE.

    IF ro_entity IS NOT BOUND.
      ##NEEDED
      MESSAGE e016(y20_ui5) INTO DATA(lv_msg).
      RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
    ENDIF.
  ENDMETHOD.

  METHOD create_odata_db_function.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Create instance of general function class Y20_CL_DAHT_ENTITY
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    ro_db_funtion = NEW y20_cl_daht_entity( create_log( y20_cl_dass_hu_trolley_mpc_ext=>mc_log_extnumber ) ).
  ENDMETHOD.

ENDCLASS.