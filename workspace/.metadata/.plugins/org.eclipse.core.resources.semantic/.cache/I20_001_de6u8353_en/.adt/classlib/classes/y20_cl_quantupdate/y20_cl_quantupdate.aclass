CLASS y20_cl_quantupdate DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    CLASS-METHODS set_dimension
      IMPORTING
        !iv_guid_parent TYPE /lime/guid_parent
        !iv_guid_stock  TYPE /lime/guid_stock
        !is_heigth      TYPE /scwm/s_quan
        !is_length      TYPE /scwm/s_quan
        !is_width       TYPE /scwm/s_quan
      EXPORTING
        !ev_success     TYPE abap_bool
      RAISING
        /scwm/cx_lock .

    CLASS-METHODS set_single_weight
      IMPORTING
        !iv_guid_parent    TYPE /lime/guid_parent
        !iv_guid_stock     TYPE /lime/guid_stock
        !iv_zzsingleweight TYPE y20_de_zzsingleweight
        !iv_uom            TYPE unit DEFAULT 'G'
      EXPORTING
        !ev_success        TYPE abap_bool
      RAISING
        /scwm/cx_lock .
    CLASS-METHODS set_weight
      IMPORTING
        !iv_guid_parent        TYPE /lime/guid_parent
        !iv_guid_stock         TYPE /lime/guid_stock
        !iv_zzsingleweight     TYPE y20_de_zzsingleweight
        !iv_zzsingleweight_uom TYPE unit DEFAULT 'G'
      EXPORTING
        !ev_success            TYPE abap_bool
      RAISING
        /scwm/cx_lock .
    CLASS-METHODS set_initialqty
      IMPORTING
        !iv_guid_parent     TYPE /lime/guid_parent
        !iv_guid_stock      TYPE /lime/guid_stock
        !iv_zzinitialqnty   TYPE y20_de_zzinitialqnty
        !iv_zzinitialqnty_u TYPE unit
      EXPORTING
        !ev_success         TYPE abap_bool
      RAISING
        /scwm/cx_lock .
    CLASS-METHODS increment_expiryinx
      IMPORTING
        !iv_guid_parent TYPE /lime/guid_parent
        !iv_guid_stock  TYPE /lime/guid_stock
        !iv_reset       TYPE abap_bool OPTIONAL
      EXPORTING
        !ev_success     TYPE abap_bool
      RAISING
        /scwm/cx_lock .
    CLASS-METHODS set_expiryinx
      IMPORTING
        !iv_guid_parent    TYPE /lime/guid_parent
        !iv_guid_stock     TYPE /lime/guid_stock
        !iv_zzexpdateindex TYPE y20_de_zzexpdateindex
      EXPORTING
        !ev_success        TYPE abap_bool
      RAISING
        /scwm/cx_lock .
    CLASS-METHODS set_purchaseorder
      IMPORTING
        !iv_guid_parent         TYPE /lime/guid_parent
        !iv_guid_stock          TYPE /lime/guid_stock
        !iv_purchase_order      TYPE ebeln
        !iv_purchase_order_item TYPE ebelp
      EXPORTING
        !ev_success             TYPE abap_bool
      RAISING
        /scwm/cx_lock .
    CLASS-METHODS set_returns
      IMPORTING
        !iv_guid_parent TYPE /lime/guid_parent
        !iv_guid_stock  TYPE /lime/guid_stock
        !iv_zzreturn    TYPE abap_bool
      EXPORTING
        !ev_success     TYPE abap_bool
      RAISING
        /scwm/cx_lock .
    CLASS-METHODS set_expiration_date
      IMPORTING
        !iv_guid_parent TYPE /lime/guid_parent
        !iv_guid_stock  TYPE /lime/guid_stock
        !iv_vfdat       TYPE /scwm/sled
      EXPORTING
        !ev_success     TYPE abap_bool
      RAISING
        /scwm/cx_lock .
  PROTECTED SECTION.

    CLASS-METHODS update_quan
      IMPORTING
        !iv_guid_parent   TYPE /lime/guid_parent
        !iv_guid_stock    TYPE /lime/guid_stock
        !is_custom_fields TYPE y20_s_eew_quan
      EXPORTING
        !ev_success       TYPE abap_bool
      RAISING
        /scwm/cx_lock .
  PRIVATE SECTION.
ENDCLASS.



CLASS y20_cl_quantupdate IMPLEMENTATION.


  METHOD increment_expiryinx.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : update the expiry extend index
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦F. G. Slavov ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_quan_last_op_rc LIKE sy-subrc.
    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "

    IF iv_reset = abap_true.

      UPDATE /scwm/quan SET zzexpdateindex = 0 WHERE guid_parent = iv_guid_parent  AND guid_stock = iv_guid_stock.
      lv_quan_last_op_rc = sy-subrc.

    ELSE.

      UPDATE /scwm/quan SET zzexpdateindex = zzexpdateindex + 1 WHERE guid_parent = iv_guid_parent  AND guid_stock = iv_guid_stock.
      lv_quan_last_op_rc = sy-subrc.

    ENDIF.

    ev_success = boolc( lv_quan_last_op_rc = 0 ).

  ENDMETHOD.


  METHOD set_expiration_date.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : Central place to update the append structures of the
* quan and aqua tables
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦G. Slavov ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_quan_last_op_rc LIKE sy-subrc.
    DATA: ls_quan TYPE /scwm/quan.

    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "
    " Current assumptions: all the Z fields are not relevant for the stock index( pure attributes )
    CLEAR ev_success.

    SELECT SINGLE * FROM /scwm/quan WHERE guid_parent = @iv_guid_parent AND guid_stock = @iv_guid_stock INTO @ls_quan.
    lv_quan_last_op_rc = sy-subrc.

*    TODO: handle the locking, also search for a more suitable standard API
    CALL FUNCTION 'ENQUEUE_/SCWM/E_QUAN'
      EXPORTING
        guid_parent    = ls_quan-guid_parent
        guid_stock     = ls_quan-guid_stock
      EXCEPTIONS
        foreign_lock   = 1
        system_failure = 2
        OTHERS         = 3.

    IF sy-subrc  <> 0.
      RAISE EXCEPTION TYPE /scwm/cx_lock.
    ENDIF.

    IF lv_quan_last_op_rc = 0.
      UPDATE /scwm/quan SET vfdat = iv_vfdat WHERE guid_parent = iv_guid_parent AND guid_stock = iv_guid_stock.
      lv_quan_last_op_rc = sy-subrc.
    ENDIF.

    ev_success = boolc( lv_quan_last_op_rc = 0 ).

  ENDMETHOD.


  METHOD set_expiryinx.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : update the expiry extend index
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦F. G. Slavov ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_custom_fields TYPE y20_s_eew_quan.
    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "
    ls_custom_fields-zzexpdateindex = iv_zzexpdateindex.

    update_quan(
      EXPORTING
        iv_guid_parent   = iv_guid_parent
        iv_guid_stock    = iv_guid_stock
        is_custom_fields = ls_custom_fields
      IMPORTING
        ev_success       = ev_success ).

  ENDMETHOD.


  METHOD set_initialqty.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : Update the initial quantity on stock level *
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦G. Slavov ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_custom_fields TYPE y20_s_eew_quan.
    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "

    ls_custom_fields-zzinitialqnty = iv_zzinitialqnty.
    ls_custom_fields-zzinitialqnty_u = iv_zzinitialqnty_u.

    update_quan(
      EXPORTING
        iv_guid_parent   = iv_guid_parent
        iv_guid_stock    = iv_guid_stock
        is_custom_fields = ls_custom_fields
      IMPORTING
        ev_success       = ev_success ).

  ENDMETHOD.


  METHOD set_purchaseorder.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : Update the Purchase Order related fields on stock level *
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦G. Slavov ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_custom_fields TYPE y20_s_eew_quan.
    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "

    ls_custom_fields-zzpono = iv_purchase_order.
    ls_custom_fields-zzpoitem = iv_purchase_order_item.
    update_quan(
      EXPORTING
        iv_guid_parent   = iv_guid_parent
        iv_guid_stock    = iv_guid_stock
        is_custom_fields = ls_custom_fields
      IMPORTING
        ev_success       = ev_success ).

  ENDMETHOD.


  METHOD set_returns.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : Update the customer returns field
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦G. Slavov ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_custom_fields TYPE y20_s_eew_quan.
    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "

    ls_custom_fields-zzreturn = iv_zzreturn.
    update_quan(
      EXPORTING
        iv_guid_parent   = iv_guid_parent
        iv_guid_stock    = iv_guid_stock
        is_custom_fields = ls_custom_fields
      IMPORTING
        ev_success       = ev_success ).

  ENDMETHOD.


  METHOD set_single_weight.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : Update single piece and quant weight
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦G. Slavov ¦Initial
*
************************************************************************
*Single piece weight (for MTO/SOS/KONF products) - filled at DECO receiving process
*Quant weight will be updated to quant z-field. Additionally also quant/HU netto weight will be updated according calculated weight.

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_custom_fields TYPE y20_s_eew_quan.
    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "

    ls_custom_fields-zzsingleweight  = iv_zzsingleweight.
    ls_custom_fields-zzsingleweight_u = iv_uom.
    update_quan(
      EXPORTING
        iv_guid_parent   = iv_guid_parent
        iv_guid_stock    = iv_guid_stock
        is_custom_fields = ls_custom_fields
      IMPORTING
        ev_success       = ev_success ).

  ENDMETHOD.


  METHOD set_weight.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : Update single piece and quant weight
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦G. Slavov ¦Initial
*
************************************************************************
*Single piece weight (for MTO/SOS/KONF products) - filled at DECO receiving process
*Quant weight will be updated to quant z-field. Additionally also quant/HU netto weight will be updated according calculated weight.

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_custom_fields TYPE y20_s_eew_quan.
    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "
    ls_custom_fields-zzsingleweight  = iv_zzsingleweight.
    ls_custom_fields-zzsingleweight_u = iv_zzsingleweight_uom.

    update_quan(
      EXPORTING
        iv_guid_parent   = iv_guid_parent
        iv_guid_stock    = iv_guid_stock
        is_custom_fields = ls_custom_fields
      IMPORTING
        ev_success       = ev_success ).

  ENDMETHOD.


  METHOD update_quan.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : Central place to update the append structures of the
* quan and aqua tables
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦G. Slavov ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lr_struct_descr TYPE REF TO cl_abap_structdescr.
    DATA: lv_quan_last_op_rc LIKE sy-subrc.
    DATA: ls_quan TYPE /scwm/quan.

    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "
    " Current assumptions: all the Z fields are not relevant for the stock index( pure attributes )
    CLEAR ev_success.

    SELECT SINGLE * FROM /scwm/quan WHERE guid_parent = @iv_guid_parent AND guid_stock = @iv_guid_stock INTO @ls_quan.
    lv_quan_last_op_rc = sy-subrc.

    lr_struct_descr ?= cl_abap_structdescr=>describe_by_data( is_custom_fields ).
    IF lr_struct_descr IS NOT BOUND.
      ev_success = abap_false.
      RETURN.
    ENDIF.

    LOOP AT lr_struct_descr->get_components( ) ASSIGNING FIELD-SYMBOL(<ls_struc_comp>).

      ASSIGN COMPONENT <ls_struc_comp>-name OF STRUCTURE is_custom_fields TO FIELD-SYMBOL(<lv_comp_value>).

      CHECK <lv_comp_value> IS ASSIGNED AND <lv_comp_value> IS NOT INITIAL.

      ASSIGN COMPONENT <ls_struc_comp>-name OF STRUCTURE ls_quan TO FIELD-SYMBOL(<lv_quan_field_update>).
      IF <lv_quan_field_update> IS ASSIGNED.
        <lv_quan_field_update> = <lv_comp_value>.
      ENDIF.

    ENDLOOP.

*    TODO: handle the locking, also search for a more suitable standard API
    CALL FUNCTION 'ENQUEUE_/SCWM/E_QUAN'
      EXPORTING
        guid_parent    = ls_quan-guid_parent
        guid_stock     = ls_quan-guid_stock
      EXCEPTIONS
        foreign_lock   = 1
        system_failure = 2
        OTHERS         = 3.

    IF sy-subrc  <> 0.
      RAISE EXCEPTION TYPE /scwm/cx_lock.
    ENDIF.

    IF lv_quan_last_op_rc = 0.
      UPDATE /scwm/quan FROM ls_quan.
      lv_quan_last_op_rc = sy-subrc.
    ENDIF.

*    CALL FUNCTION 'DEQUEUE_/SCWM/E_QUAN'
*      EXPORTING
*        guid_parent = ss_huitm-guid_parent
*        guid_stock  = ss_huitm-guid_stock.

    ev_success = boolc( lv_quan_last_op_rc = 0 ).

  ENDMETHOD.


  METHOD set_dimension.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_CORE
* Description : Set stock dimensions. Only applicable for KONF products
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_custom_fields TYPE y20_s_eew_quan.
    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "
*    IF is_heigth-quan IS NOT INITIAL.
    ls_custom_fields-zzheight  = is_heigth-quan.
    ls_custom_fields-zzunit_h  = is_heigth-unit.
*    ENDIF.

*    IF is_length-quan IS NOT INITIAL.
    ls_custom_fields-zzlength  = is_length-quan.
    ls_custom_fields-zzunit_l  = is_length-unit.
*    ENDIF.

*    IF is_width-quan IS NOT INITIAL.
    ls_custom_fields-zzwidth   = is_width-quan.
    ls_custom_fields-zzunit_w  = is_width-unit.
*    ENDIF.

    update_quan(
      EXPORTING
        iv_guid_parent   = iv_guid_parent
        iv_guid_stock    = iv_guid_stock
        is_custom_fields = ls_custom_fields
      IMPORTING
        ev_success       = ev_success ).
  ENDMETHOD.

ENDCLASS.