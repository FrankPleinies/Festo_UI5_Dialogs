CLASS y20_cl_odata_pphutrl_factory DEFINITION
  PUBLIC
  FINAL
  INHERITING FROM y20_cl_odata_entity_factory
  CREATE PUBLIC .

  PUBLIC SECTION.
    METHODS create_entity REDEFINITION.

  PROTECTED SECTION.
private section.

    "!<p class="shorttext synchronized">Message log</p>
  data MO_LOG type ref to Y20_CL_LOG .

  methods GET_EXPAND_KEY_NUMBER
    importing
      !IO_EXPAND type ref to /IWBEP/IF_MGW_ODATA_EXPAND
    returning
      value(RV_RESULT) type I .
    "!<p class="shorttext synchronized">Get key values from reques</p>
  methods GET_KEYTAB_VALUES
    importing
      !IT_KEY_TAB type /IWBEP/T_MGW_NAME_VALUE_PAIR
      !IT_FILTER_SELECT_OPTIONS type /IWBEP/T_MGW_SELECT_OPTION
    returning
      value(RS_KEYS) type Y20_CL_PICK_PREP_HUTRL_MPC_EXT=>TYS_ENTITY_KEYS .
    "!<p class="shorttext synchronized">Get execution method</p>
  methods GET_METHOD_EXECUTE
    importing
      !IV_ENTITY_NAME type STRING
      !IT_KEY_TAB type /IWBEP/T_MGW_NAME_VALUE_PAIR optional ##NEEDED
      !IO_EXPAND type ref to /IWBEP/IF_MGW_ODATA_EXPAND optional
    returning
      value(RV_RESULT) type STRING .
ENDCLASS.



CLASS Y20_CL_ODATA_PPHUTRL_FACTORY IMPLEMENTATION.


  METHOD create_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Create execution entity instance
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_log = create_log( iv_extid = y20_cl_pick_prep_hutrl_mpc_ext=>mc_log_extnumber ).

    DATA(ls_keys) = get_keytab_values( it_key_tab = it_key_tab
                                       it_filter_select_options = it_filter_select_options ).
    IF ls_keys IS INITIAL.
      ro_entity = NEW y20_cl_odata_pphutrl_assign( io_log = mo_log ).
    ELSE.
      ro_entity = NEW y20_cl_odata_pphutrl_assign( is_entity_key = ls_keys
                                                   io_log = mo_log ).
    ENDIF.

    DATA(lv_method_execute) = get_method_execute( iv_entity_name = iv_entity_name
                                                  io_expand      = io_expand ).

    ro_entity->set_execute_method( lv_method_execute ).
  ENDMETHOD.


  METHOD get_expand_key_number.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF io_expand IS NOT BOUND.
      RETURN.
    ENDIF.

    DATA(lt_children) = io_expand->get_children( ).
    LOOP AT lt_children ASSIGNING FIELD-SYMBOL(<ls_child>).
      CASE <ls_child>-tech_nav_prop_name.
        WHEN 'SCANTRL_TROLLEY'.
          rv_result = build_execute_method_key( iv_new_key      = 1
                                                iv_existing_key = rv_result ).
        WHEN 'SCANTRL_TRLCONTENT'.
          rv_result = build_execute_method_key( iv_new_key      = 2
                                                iv_existing_key = rv_result ).
        WHEN 'SCANTRL_HU'.
          rv_result = build_execute_method_key( iv_new_key      = 3
                                                iv_existing_key = rv_result ).

      ENDCASE.
    ENDLOOP.
  ENDMETHOD.


  METHOD get_keytab_values.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Get key values
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( it_key_tab ) > 0.
      LOOP AT it_key_tab ASSIGNING FIELD-SYMBOL(<ls_key>).
        CASE <ls_key>-name.
          WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_field_name-lgnum.
            rs_keys-lgnum = <ls_key>-value.

          WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_field_name-scanhu.
            rs_keys-scanhu = |{ <ls_key>-value ALPHA = IN }|.

          WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_field_name-scantrolley.
            rs_keys-scantrolley = <ls_key>-value.

          WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_field_name-huident.
            rs_keys-huident = <ls_key>-value.

        ENDCASE.
      ENDLOOP.
    ENDIF.

    IF lines( it_filter_select_options ) > 0.
      LOOP AT it_filter_select_options ASSIGNING FIELD-SYMBOL(<ls_filter>).
        TRY.
            CASE <ls_filter>-property.
              WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_field_name-lgnum.
                rs_keys-lgnum = <ls_filter>-select_options[ 1 ]-low.

            ENDCASE.
          CATCH cx_sy_itab_line_not_found.
            CONTINUE.
        ENDTRY.
      ENDLOOP.
    ENDIF.

  ENDMETHOD.


  METHOD get_method_execute.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Determine execution method
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_method_numb) = get_expand_key_number( io_expand = io_expand ).

    CASE iv_entity_name.
      WHEN y20_cl_pick_prep_hutrl_mpc=>gc_scanhandlingunitent.
        rv_result = y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-scanhu_huent.

      WHEN y20_cl_pick_prep_hutrl_mpc=>gc_scantrolleyent.
        IF lv_method_numb = 123 or lv_method_numb = 12.
          rv_result = y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-scantrolley.
        ENDIF.
      WHEN y20_cl_pick_prep_hutrl_mpc=>gc_trolleyent.
        rv_result = y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-trolley_entity.

      WHEN y20_cl_pick_prep_hutrl_mpc=>gc_trolleycontentent.
        rv_result = y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-trolley_content.

      WHEN y20_cl_pick_prep_hutrl_mpc=>gc_itemtextent.
        rv_result = y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-text_dlvitem.

      WHEN y20_cl_pick_prep_hutrl_mpc=>gc_trolleycloselistent.
        rv_result = y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-trolley_close_list.

    ENDCASE.

  ENDMETHOD.
ENDCLASS.