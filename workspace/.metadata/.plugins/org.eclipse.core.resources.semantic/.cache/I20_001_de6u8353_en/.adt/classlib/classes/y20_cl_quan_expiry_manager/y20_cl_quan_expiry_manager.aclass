class Y20_CL_QUAN_EXPIRY_MANAGER definition
  public
  final
  create public .

public section.

  methods CALC_EXP_DATE_RETURNS_MAT
    importing
      !IV_MATID type /SCWM/DE_MATID
      !IV_WDATU type /SCWM/LVS_WDATU
    returning
      value(RV_VFDAT) type /SCWM/SLED .
  methods CONSTRUCTOR
    importing
      !IV_LGNUM type /SCWM/LGNUM
      !IO_LOG type ref to Y20_CL_LOG .
  methods CONSTRUCT_STOCK_POSTING
    importing
      !IV_UNBLOCK type ABAP_BOOL optional
      !IV_VFDAT type /SCWM/SLED
      !IS_DATA type /SCWM/QUAN
    exporting
      value(ES_ITEM) type /SCWM/S_SPITEM
    returning
      value(RV_SUCCESS) type ABAP_BOOL .
  methods CONSTRUCT_STOCK_POST_HEADER
    importing
      !IV_POST type ABAP_BOOL
    returning
      value(RS_POST_HEADER) type /SCWM/S_GMHEADER .
  methods MAX_EXTND_CNT_REACHED
    importing
      !IS_QUAN type /SCWM/QUAN
    returning
      value(RV_RESULT) type ABAP_BOOL .
  methods CALC_NEW_EXPIRATION_DATE
    importing
      !IV_MATID type /SCWM/DE_MATID
    changing
      !CV_VFDAT type /SCWM/SLED
    returning
      value(RV_SUCCESS) type ABAP_BOOL .
protected section.
private section.

  data MO_LOG type ref to Y20_CL_LOG .
  data MV_LGNUM type /SCWM/LGNUM .

  methods TOGGLE_BLOCKED_STOCK
    importing
      !IV_UNBLOCK type ABAP_BOOL optional
      !IV_VFDAT type DATS
      !IV_CAT type /SCWM/DE_CAT
    changing
      !CS_ITEM type /SCWM/S_SPITEM
    returning
      value(RV_SUCCESS) type ABAP_BOOL .
ENDCLASS.



CLASS Y20_CL_QUAN_EXPIRY_MANAGER IMPLEMENTATION.


METHOD calc_exp_date_returns_mat.
  DATA: ls_mat_global      TYPE /scwm/s_material_global,
        lv_dummy_timestamp TYPE timestamp,
        lt_date_time       TYPE /scwm/tt_tstmp_date_time,
        lv_seconds         TYPE i.

  CLEAR rv_vfdat.

  TRY.
      CALL FUNCTION '/SCWM/MATERIAL_READ_SINGLE'
        EXPORTING
          iv_matid      = iv_matid
          iv_lgnum      = mv_lgnum
        IMPORTING
          es_mat_global = ls_mat_global.

    CATCH /scwm/cx_md INTO DATA(lx_md).
      mo_log->add_cx_message( lx_md ).
      RETURN.
  ENDTRY.

  ls_mat_global-shelf_life_dur  = ls_mat_global-shelf_life_dur / 2.

  CALL FUNCTION '/SAPAPO/DURATION_GET_SECONDS'
    EXPORTING
      iv_duration = ls_mat_global-shelf_life_dur
    IMPORTING
      ev_seconds  = lv_seconds.

  CALL FUNCTION '/SAPAPO/TIMESTAMP_ADD'
    EXPORTING
      iv_timestamp        = iv_wdatu
      iv_duration_integer = lv_seconds
    IMPORTING
      ev_timestamp        = lv_dummy_timestamp
    EXCEPTIONS
      out_of_range        = 1
      invalid_parameter   = 2
      OTHERS              = 3.

  IF sy-subrc = 0.

    CALL FUNCTION '/SCWM/CONVERT_TIMESTAMP'
      EXPORTING
        iv_lgnum       = mv_lgnum
        it_timestamp   = VALUE /scwm/tt_timestamp( ( lv_dummy_timestamp ) )
      IMPORTING
        et_date_time   = lt_date_time
      EXCEPTIONS
        input_error    = 1
        data_not_found = 2
        OTHERS         = 3.

    IF sy-subrc = 0.

      READ TABLE lt_date_time INDEX 1 ASSIGNING FIELD-SYMBOL(<ls_date_time>).
      IF sy-subrc = 0 AND <ls_date_time> IS ASSIGNED.
        rv_vfdat = <ls_date_time>-date.
      ENDIF.

    ENDIF.

  ENDIF.

ENDMETHOD.


METHOD calc_new_expiration_date.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : Central place to update the append structures of the
* quan and aqua tables
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦G. Slavov ¦Initial
*
************************************************************************

  " ------------------------------------------ "
  " Local Declarations..
  " ------------------------------------------ "
  DATA: lv_seconds         TYPE i.
  DATA: ls_mat_global TYPE /scwm/s_material_global.
  " ------------------------------------------ "
  " Processing Logic.
  " ------------------------------------------ "

  CLEAR rv_success.

  TRY.
      CALL FUNCTION '/SCWM/MATERIAL_READ_SINGLE'
        EXPORTING
          iv_matid      = iv_matid
          iv_lgnum      = mv_lgnum
        IMPORTING
          es_mat_global = ls_mat_global.

    CATCH /scwm/cx_md INTO DATA(lx_md).
      mo_log->add_cx_message( lx_md ).
      RETURN.
  ENDTRY.

  ls_mat_global-shelf_life_dur  = ls_mat_global-shelf_life_dur / 2.

  CALL FUNCTION '/SAPAPO/DURATION_GET_SECONDS'
    EXPORTING
      iv_duration = ls_mat_global-shelf_life_dur
    IMPORTING
      ev_seconds  = lv_seconds.

  cv_vfdat = cv_vfdat + ( lv_seconds / 86400 ).
  rv_success = abap_true.

ENDMETHOD.


METHOD constructor.
  mv_lgnum = iv_lgnum.
  mo_log = io_log.
ENDMETHOD.


METHOD construct_stock_posting.

  DATA:
    ls_huitm TYPE /scwm/s_huitm_int,
    ls_quan  TYPE /scwm/s_quan.

* Prepare item data
  CLEAR: ls_huitm, es_item, ls_quan.

  MOVE-CORRESPONDING is_data-cust TO es_item-cust.

*   Org data
  es_item-id_group = '000001'.
  es_item-direction = wmegc_lime_post_transfer.
  es_item-squant_set = 'X'.

*   Parent
  es_item-guid_hu = is_data-guid_parent.
*   Read and lock HU and read HU item in order to have all relevant
*   stock attributes
  CALL FUNCTION '/SCWM/HUHEADER_READ'
    EXPORTING
      iv_appl      = wmegc_huappl_wme
      iv_guid_hu   = is_data-guid_parent
      iv_db_select = 'A'
      iv_lock      = 'E'
    EXCEPTIONS
      not_found    = 1
      input        = 2
      OTHERS       = 3.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
            INTO DATA(lv_msg).
    mo_log->add_message( ).
    RETURN.
  ENDIF.

  CALL FUNCTION '/SCWM/HUITM_READ'
    EXPORTING
      iv_guid_hu    = is_data-guid_parent
      iv_guid_stock = is_data-guid_stock
    IMPORTING
      es_huitm      = ls_huitm
    EXCEPTIONS
      no_item       = 1
      OTHERS        = 2.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
            INTO lv_msg.
    mo_log->add_message( ).
    RETURN.
  ENDIF.

  IF ls_huitm-altme <> ls_huitm-meins.
    TRY.
        CALL FUNCTION '/SCMB/MDL_QUAN_CONVERSION'
          EXPORTING
            iv_product_id = ls_huitm-matid
            iv_quan       = ls_huitm-quan
            iv_unit_from  = ls_huitm-meins
            iv_unit_to    = ls_huitm-altme
          IMPORTING
            ev_quan       = ls_huitm-quana.
      CATCH cx_sy_arithmetic_overflow.
        MESSAGE e003(/scmb/mdl_basic) INTO lv_msg.
        mo_log->add_message( ).
        RETURN.
      CATCH /scmb/cx_mdl_interface.
        MESSAGE e002(/scmb/mdl_basic) INTO lv_msg.
        mo_log->add_message( ).
        RETURN.
      CATCH /scmb/cx_mdl_result.
        MESSAGE e003(/scmb/mdl_basic) INTO lv_msg.
        mo_log->add_message( ).
        RETURN.
      CATCH /scmb/cx_mdl.
        MESSAGE e002(/scmb/mdl_basic) INTO lv_msg.
        mo_log->add_message( ).
        RETURN.
    ENDTRY.
  ENDIF.

*   Source quant (just take GUID_STOCK)
  es_item-source_s-idx_stock   = ls_huitm-idx_stock.
  es_item-source_s-guid_stock  = ls_huitm-guid_stock.
*   Set destination quant (take all stock attributes together with
*   the new stock category)
  es_item-dest_s-matid         = ls_huitm-matid.
  es_item-dest_s-batchid       = ls_huitm-batchid.

  IF NOT  toggle_blocked_stock(
    EXPORTING
      iv_unblock = iv_unblock
      iv_vfdat = iv_vfdat
      iv_cat   = ls_huitm-cat
    CHANGING
      cs_item  = es_item ).

    rv_success = abap_false.
    RETURN.

  ENDIF.

  es_item-dest_s-stock_doccat  = ls_huitm-stock_doccat.
  es_item-dest_s-stock_docno   = ls_huitm-stock_docno.
  es_item-dest_s-stock_itmno   = ls_huitm-stock_itmno.
  es_item-dest_s-doccat        = ls_huitm-doccat.
  es_item-dest_s-stock_usage   = ls_huitm-stock_usage.
  es_item-dest_s-owner         = ls_huitm-owner.
  es_item-dest_s-owner_role    = ls_huitm-owner_role.
  es_item-dest_s-entitled      = ls_huitm-entitled.
  es_item-dest_s-entitled_role = ls_huitm-entitled_role.
  es_item-dest_s-insptyp       = ls_huitm-insptyp.
  es_item-dest_s-inspid        = ls_huitm-inspid.
  es_item-dest_s-idplate       = ls_huitm-idplate.
  es_item-dest_s-qdoccat       = ls_huitm-qdoccat.
  es_item-dest_s-qdocid        = ls_huitm-qdocid.
  es_item-dest_s-qitmid        = ls_huitm-qitmid.

*   Set quantities (if quantity in alternative uom is available then
*   use it instead of quantity in base uom)
  IF ls_huitm-quana IS NOT INITIAL.
    ls_quan-quan = ls_huitm-quana.
    ls_quan-unit = ls_huitm-altme.
  ELSE.
    ls_quan-quan = ls_huitm-quan.
    ls_quan-unit = ls_huitm-meins.
  ENDIF.

  APPEND ls_quan TO es_item-t_quan.

  IF ls_huitm-cwquan IS NOT INITIAL.
    CLEAR ls_quan.
    ls_quan-quan = ls_huitm-cwquan.
    ls_quan-unit = ls_huitm-cwunit.
    APPEND ls_quan TO es_item-t_quan.
  ENDIF.

  rv_success = abap_true.

ENDMETHOD.


METHOD CONSTRUCT_STOCK_POST_HEADER.

  rs_post_header-lgnum = mv_lgnum.
  rs_post_header-created_by = sy-uname.
  GET TIME STAMP FIELD rs_post_header-created_at.
  rs_post_header-compl = 'X'.
  rs_post_header-code = '/SCWM/MON'.
  rs_post_header-post = iv_post.

ENDMETHOD.


  METHOD max_extnd_cnt_reached.
************************************************************************
* Company : Swisslog AG *
* Package : Y20_CORE
* Description : update the expiry extend index
************************************************************************
* REVISIONS:
* --------- *
* Ver. ¦Date ¦Author ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0 ¦2020-04-07¦F. G. Slavov ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    " Processing Logic.
    " ------------------------------------------ "

    CLEAR rv_result.

    rv_result = boolc( is_quan-zzexpdateindex =  zswl_cl_param=>read_const(
                                                                iv_lgnum     = mv_lgnum
                                                                iv_context   = zswl_if_param_c=>mc_core
                                                                iv_parameter = zswl_if_param_c=>mc_max_extend_exp_date ) ).

  ENDMETHOD.


METHOD toggle_blocked_stock.

  DATA(lo_stock_id) = NEW /scwm/cl_stock_id( ).

  rv_success = abap_false.

  TRY.

      IF iv_unblock = abap_true AND iv_vfdat > sy-datum AND iv_cat = 'B6' OR iv_cat = 'B5'.
        " Unblock
        DATA(lv_cat) = lo_stock_id->get_next_cat(
                                                  iv_lgnum = mv_lgnum
                                                  iv_cat = iv_cat
                                                  iv_new_catlocn = wmegc_catlocn_unrestr ).

        IF lv_cat IS NOT INITIAL AND lv_cat <> iv_cat.
          cs_item-dest_s-cat  = lv_cat.
          cs_item-reason_wm = zswl_cl_param=>read_const(
                                          iv_lgnum     = mv_lgnum
                                          iv_context   = zswl_cl_param=>zswl_if_param_c~mc_core
                                          iv_parameter =  zswl_cl_param=>zswl_if_param_c~mc_reason_unblock ).
        ENDIF.

      ELSEIF iv_unblock = abap_false AND iv_vfdat < sy-datum AND iv_cat = 'F1' OR iv_cat = 'F2'.
        " Block
        lv_cat = lo_stock_id->get_next_cat(
                                            iv_lgnum = mv_lgnum
                                            iv_cat = iv_cat
                                            iv_new_catlocn = wmegc_catlocn_blocked ).

        IF lv_cat IS NOT INITIAL AND lv_cat <> iv_cat.
          cs_item-dest_s-cat  = lv_cat.
          cs_item-reason_wm = zswl_cl_param=>read_const(
                        iv_lgnum     = mv_lgnum
                        iv_context   = zswl_cl_param=>zswl_if_param_c~mc_core
                        iv_parameter =  zswl_cl_param=>zswl_if_param_c~mc_reason_jbbl ).
        ENDIF.

      ELSE.
        rv_success = abap_false.

      ENDIF.

    CATCH /scwm/cx_core INTO DATA(lx_core).
      mo_log->add_cx_message( lx_core ).
      rv_success = abap_false.
  ENDTRY.

  rv_success = abap_true.

ENDMETHOD.
ENDCLASS.