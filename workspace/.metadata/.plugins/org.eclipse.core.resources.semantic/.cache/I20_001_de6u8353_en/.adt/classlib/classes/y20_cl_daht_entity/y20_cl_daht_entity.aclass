CLASS y20_cl_daht_entity DEFINITION
  PUBLIC
  CREATE PUBLIC .

  PUBLIC SECTION.
    CONSTANTS:
      BEGIN OF ms_message_string,
        t300 TYPE string VALUE '/SCWM/T300',
      END OF ms_message_string.

    "!<p class="shorttext synchronized">Log instance</p>
    DATA mo_log TYPE REF TO y20_cl_log READ-ONLY .

    "!<p class="shorttext synchronized">Constructor method</p>
    METHODS constructor
      IMPORTING
        !io_log TYPE REF TO y20_cl_log .
    "!<p class="shorttext synchronized">Convert value to output format</p>
    METHODS convert_to_format
      IMPORTING
        !iv_input  TYPE any
      EXPORTING
        !ev_output TYPE any .
    "!<p class="shorttext synchronized">Convert structure to reference data</p>
    METHODS copy_data_to_ref
      IMPORTING
        !is_data       TYPE any
      RETURNING
        VALUE(ro_data) TYPE REF TO data .
    "!<p class="shorttext synchronized">Complete HU process step</p>
    METHODS complete_hu_process_step
      IMPORTING
        iv_lgnum TYPE /scwm/lgnum
        is_huhdr TYPE /scwm/s_huhdr_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Create new task to resource</p>
    METHODS create_spit_task_to_resource
      IMPORTING
        !iv_lgnum     TYPE /scwm/lgnum
        !is_open_task TYPE REF TO /scwm/s_to_det_mon
        !iv_resource  TYPE /scwm/de_rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Create HU task to bin</p>
    METHODS create_taskhu_to_bin
      IMPORTING
        !iv_lgnum    TYPE y20_cl_dass_hu_trolley_mpc=>ts_handlingunitent-lgnum
        !iv_guid_hu  TYPE y20_cl_dass_hu_trolley_mpc=>ts_handlingunitent-guid_hu
        !iv_bin      TYPE /scwm/de_lgpla
        !iv_proctype TYPE /scwm/de_procty
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Create HU task to resource</p>
    METHODS create_taskhu_to_resource
      IMPORTING
        !iv_lgnum    TYPE y20_cl_dass_hu_trolley_mpc=>ts_handlingunitent-lgnum
        !iv_guid_hu  TYPE y20_cl_dass_hu_trolley_mpc=>ts_handlingunitent-guid_hu
        !iv_resource TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent-trolley
        !iv_proctype TYPE /scwm/de_procty
        !iv_logpos   TYPE /scwm/de_logpos OPTIONAL
      EXPORTING
        !et_bapiret  TYPE bapirettab
        !ev_severity TYPE bapi_mtype .
    "!<p class="shorttext synchronized">Raise exception: /IWBEP/CX_MGW_TECH_EXCEPTION</p>
    METHODS raise_exception
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Retrieve HU by HU identification number</p>
    METHODS read_hu_by_huident
      IMPORTING
        !iv_huident TYPE /scwm/de_huident
      EXPORTING
        !es_huhdr   TYPE /scwm/s_huhdr_int
        !et_huitem  TYPE /scwm/tt_huitm_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Retrieve material data by MATID</p>
    METHODS read_material_by_matid
      IMPORTING
        !iv_matid          TYPE y20_cl_dass_hu_trolley_mpc=>ts_handlingunitent-product-matid
      RETURNING
        VALUE(rs_material) TYPE /scwm/s_material_global .
    "!<p class="shorttext synchronized">Retrieve multiple materials by MATID</p>
    METHODS read_material_multi_by_matid
      IMPORTING
        !it_matid           TYPE /scwm/tt_matid
        !iv_lgnum           TYPE /scwm/lgnum
      EXPORTING
        !et_material_global TYPE /scwm/tt_material_global .
    "!<p class="shorttext synchronized">Retrieve open tasks by HU identification number</p>
    METHODS read_open_hutask_by_huident
      IMPORTING
        !iv_lgnum           TYPE y20_cl_dass_hu_trolley_mpc=>ts_handlingunitent-lgnum
        !iv_huident         TYPE y20_cl_dass_hu_trolley_mpc=>ts_handlingunitent-huident
        !iv_read_buffer     TYPE boole_d DEFAULT abap_false
      RETURNING
        VALUE(rt_task_open) TYPE /scwm/tt_to_det_mon
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read Queue</p>
    METHODS read_queue
      IMPORTING
        !iv_lgnum       TYPE /scwm/lgnum
        !iv_queue       TYPE /scwm/de_queue
      RETURNING
        VALUE(rs_queue) TYPE /scwm/t346
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Retrieve content of trolley(resource)</p>
    METHODS read_trolley_content
      IMPORTING
        !iv_lgnum     TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent-lgnum
        !iv_trolley   TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent-trolley
      EXPORTING
        !et_huitems   TYPE /scwm/tt_stock_select
        !et_huheaders TYPE /scwm/tt_huhdr
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Retrieve trolley(resource) header data</p>
    METHODS read_trolley_single
      IMPORTING
        !iv_lgnum         TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent-lgnum
        !iv_trolley       TYPE y20_cl_dass_hu_trolley_mpc=>ts_trolleyent-trolley
      RETURNING
        VALUE(rs_trolley) TYPE /scwm/rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read warehouse order</p>
    METHODS read_warehouse_order
      IMPORTING
        !iv_lgnum      TYPE /scwm/lgnum
        !it_who        TYPE /scwm/tt_whoid
        !iv_lock_who   TYPE boole_d
      RETURNING
        VALUE(rt_whos) TYPE /scwm/tt_who_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Update HU log position number</p>
    METHODS update_hu_logpos
      IMPORTING
        iv_huident TYPE /scwm/de_huident
        iv_logpos  TYPE /scwm/de_logpos
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Change warehouse order attributes</p>
    METHODS update_warehouse_order
      IMPORTING
        !iv_lgnum        TYPE /scwm/lgnum
        !iv_who          TYPE /scwm/de_who
        !iv_update_queue TYPE boole_d DEFAULT abap_false
        !is_attributes   TYPE /scwm/s_who_att
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Check warehouse number is valid</p>
    METHODS validate_warehouse_number
      IMPORTING
        !iv_lgnum TYPE y20_cl_dass_hu_trolley_mpc=>ts_handlingunitent-lgnum
      RAISING
        /iwbep/cx_mgw_tech_exception .
  PROTECTED SECTION.
  PRIVATE SECTION.
    "!<p class="shorttext synchronized">Table with open warehouse tasks</p>
    DATA mt_task_open TYPE /scwm/tt_to_det_mon .
    "!<p class="shorttext synchronized">Lock task</p>
    METHODS lock_task
      IMPORTING
        !iv_lgnum TYPE /scwm/lgnum
        !iv_task  TYPE /scwm/s_to_det_mon-tanum
      RAISING
        /iwbep/cx_mgw_tech_exception .
ENDCLASS.



CLASS y20_cl_daht_entity IMPLEMENTATION.


  METHOD constructor.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Build instance of current class. Assign attributes
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_log = io_log.
  ENDMETHOD.


  METHOD convert_to_format.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Convert value to output format
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    ev_output = |{ iv_input ALPHA = OUT }|.
    ev_output = |{ ev_output ALPHA = IN }|.
  ENDMETHOD.


  METHOD copy_data_to_ref.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Convert structure to reference data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    FIELD-SYMBOLS: <ls_data> TYPE any.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CREATE DATA ro_data LIKE is_data.
    ASSIGN ro_data->* TO <ls_data>.
    <ls_data> = is_data.
  ENDMETHOD.


  METHOD create_spit_task_to_resource.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Split main task. Create new item task to the resource.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_task_conf TYPE /scwm/to_conf_tt,
          ##NEEDED
          lv_msg       TYPE string,
          lt_bapiret   TYPE bapirettab,
          lv_severity  TYPE bapi_mtype.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    lock_task( iv_lgnum = iv_lgnum
               iv_task = is_open_task->tanum ).

    INSERT VALUE #( tanum = is_open_task->tanum
                    nista = is_open_task->vsolm
                    altme = is_open_task->altme
                    vlenr = is_open_task->vlenr
                    huent = abap_true
                    nlenr = is_open_task->vlenr
                    drsrc = iv_resource ) INTO TABLE lt_task_conf.

    CALL FUNCTION '/SCWM/TO_CONFIRM'
      EXPORTING
        iv_lgnum         = iv_lgnum
        iv_wtcode        = wmegc_wtcode_rsrc
        iv_update_task   = abap_false
        iv_commit_work   = abap_true
        iv_processor_det = abap_true
        it_conf          = lt_task_conf
      IMPORTING
        et_bapiret       = lt_bapiret
        ev_severity      = lv_severity.
    IF lv_severity CA wmegc_severity_ea.
      LOOP AT lt_bapiret ASSIGNING FIELD-SYMBOL(<ls_bapiret>) WHERE type CA wmegc_severity_ea.
        MESSAGE ID <ls_bapiret>-id TYPE <ls_bapiret>-type NUMBER <ls_bapiret>-number
           WITH <ls_bapiret>-message_v1 <ls_bapiret>-message_v2
                <ls_bapiret>-message_v3 <ls_bapiret>-message_v4 INTO lv_msg.
        EXIT.
      ENDLOOP.
      raise_exception( ).
    ENDIF.

    MESSAGE i011(y20_ui5) WITH is_open_task->vlenr iv_resource INTO lv_msg.
    mo_log->add_message( ).
  ENDMETHOD.


  METHOD create_taskhu_to_bin.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Create HU task to bin. Task is created with class
*                 /SCWM/CL_WM_PACKING so confirmation is done automatically
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    ##NEEDED
    DATA lv_msg TYPE string.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    /scwm/cl_tm=>cleanup( iv_lgnum = iv_lgnum ).
    /scwm/cl_tm=>register_cleanup_wt( ).

    /scwm/cl_wm_packing=>get_instance(
      IMPORTING eo_instance = DATA(lo_packing) ).

    lo_packing->init(
      EXPORTING
        iv_lgnum  = iv_lgnum
        iv_procty = iv_proctype
      EXCEPTIONS
        error     = 1
        OTHERS    = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ENDIF.

    lo_packing->move_hu(
      EXPORTING
        iv_hu  = iv_guid_hu
        iv_bin = iv_bin
      EXCEPTIONS
        error  = 1
        OTHERS = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ENDIF.

    lo_packing->save(
      EXCEPTIONS
        error     = 1
        OTHERS    = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD create_taskhu_to_resource.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Create HU task to resource. Task is created with class
*                 /SCWM/CL_WM_PACKING so confirmation is done automatically
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    /scwm/cl_tm=>cleanup( iv_lgnum = iv_lgnum ).
    /scwm/cl_tm=>register_cleanup_wt( ).

    /scwm/cl_wm_packing=>get_instance(
      IMPORTING eo_instance = DATA(lo_packing) ).

    lo_packing->init(
      EXPORTING
        iv_lgnum  = iv_lgnum
        iv_procty = iv_proctype
      EXCEPTIONS
        error     = 1
        OTHERS    = 2 ).
    IF sy-subrc <> 0.
      et_bapiret = VALUE #( ( id   = sy-msgid
                              type = sy-msgty
                              number = sy-msgno
                              message_v1 = sy-msgv1
                              message_v2 = sy-msgv2
                              message_v3 = sy-msgv3
                              message_v4 = sy-msgv4 ) ).
      ev_severity = wmegc_severity_err.
      RETURN.
    ENDIF.

    lo_packing->move_hu_to_resource(
      EXPORTING
        iv_hu         = iv_guid_hu
        iv_resource   = iv_resource
        iv_logpos     = iv_logpos
*        iv_who        =
      EXCEPTIONS
        error         = 1
        OTHERS        = 2 ).
    ##NEEDED
    IF sy-subrc <> 0.
      " no handling here
    ENDIF.
    et_bapiret = lo_packing->go_log->get_prot( ).
    ev_severity = lo_packing->go_log->get_severity( ).
  ENDMETHOD.


  METHOD lock_task.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Lock warehouse task
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/TO_READ_SINGLE'
      EXPORTING
        iv_lgnum     = iv_lgnum
        iv_tanum     = iv_task
        iv_flglock   = abap_true
      EXCEPTIONS
        wrong_input  = 1
        not_found    = 2
        foreign_lock = 3
        error        = 4
        OTHERS       = 5.
    IF sy-subrc <> 0.
      ##NEEDED
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_msg).
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD raise_exception.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Raise exception
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_log->add_message( ).
    RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception(
        textid = VALUE #( msgno = sy-msgno
                          msgid = sy-msgid
                          attr1 = sy-msgv1
                          attr2 = sy-msgv2
                          attr3 = sy-msgv3
                          attr4 = sy-msgv4 ) ).
  ENDMETHOD.


  METHOD read_hu_by_huident.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Retrieve HU header, items by HU identification number
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA lt_huhdr TYPE /scwm/tt_huhdr_int.
    DATA lt_huitm TYPE /scwm/tt_huitm_int.
    DATA lt_bapiret TYPE bapiret2_t.
    ##NEEDED
    DATA lv_msg TYPE string.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: es_huhdr, et_huitem.
    IF iv_huident IS INITIAL.
      MESSAGE e157(/scwm/hugeneral) INTO lv_msg.
      raise_exception( ).
    ENDIF.

    CALL FUNCTION '/SCWM/HU_SELECT'
      EXPORTING
        it_huident = VALUE /scwm/tt_huident( ( huident = iv_huident ) )
      IMPORTING
        et_huhdr   = lt_huhdr
        et_huitm   = lt_huitm
        et_bapiret = lt_bapiret
      EXCEPTIONS
        not_found  = 1
        error      = 2
        OTHERS     = 3.
    IF sy-subrc <> 0.
      LOOP AT  lt_bapiret ASSIGNING FIELD-SYMBOL(<ls_bapiret>)
                                    WHERE type CA wmegc_severity_ea.
        MESSAGE ID <ls_bapiret>-id TYPE <ls_bapiret>-type NUMBER <ls_bapiret>-number
              WITH <ls_bapiret>-message_v1 <ls_bapiret>-message_v2
                   <ls_bapiret>-message_v3 <ls_bapiret>-message_v4 INTO lv_msg.
        EXIT.
      ENDLOOP.
      raise_exception( ).
    ENDIF.

    TRY.
        es_huhdr = lt_huhdr[ 1 ].
        et_huitem = lt_huitm.
      CATCH cx_sy_itab_line_not_found.
        CLEAR: es_huhdr, et_huitem.
    ENDTRY.
  ENDMETHOD.


  METHOD read_material_by_matid.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Retrieve material data by MATID
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_matid IS INITIAL.
      RETURN.
    ENDIF.
    TRY.
        CALL FUNCTION '/SCWM/MATERIAL_READ_SINGLE'
          EXPORTING
            iv_matid      = iv_matid
          IMPORTING
            es_mat_global = rs_material.
      CATCH /scwm/cx_md.
        CLEAR: rs_material.
    ENDTRY.
  ENDMETHOD.


  METHOD read_material_multi_by_matid.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Retrieve multiple materials by MATID
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: et_material_global.
    TRY.
        CALL FUNCTION '/SCWM/MATERIAL_READ_MULTIPLE'
          EXPORTING
            it_matid      = it_matid
            iv_langu      = sy-langu
            iv_lgnum      = iv_lgnum
          IMPORTING
            et_mat_global = et_material_global.
      CATCH /scwm/cx_md.
        CLEAR: et_material_global.
    ENDTRY.
  ENDMETHOD.


  METHOD read_open_hutask_by_huident.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Retrieve open tasks by HU identification number
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    ##NEEDED
    DATA lv_msg TYPE string.
    DATA lt_task_open TYPE /scwm/tt_to_det_mon.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_huident IS INITIAL.
      MESSAGE e015(y20_ui5) INTO lv_msg.
      raise_exception( ).
    ENDIF.

    " check buffer. if nothing found will continue with DB read
    IF iv_read_buffer = abap_true.
      ASSIGN mt_task_open[ vlenr = iv_huident ] TO FIELD-SYMBOL(<ls_buffer_task>).
      IF sy-subrc = 0.
        APPEND <ls_buffer_task> TO rt_task_open.
        RETURN.
      ENDIF.
    ENDIF.

    DATA(ls_selcrit) = VALUE /scwm/s_to_selcrit_mon(
      r_vlenr = VALUE #( ( sign   = wmegc_sign_inclusive
                           option = wmegc_option_eq
                           low    = iv_huident ) ) ).
    CALL FUNCTION '/SCWM/TO_GET_WIP'
      EXPORTING
        iv_lgnum   = iv_lgnum
        iv_open    = abap_true
        is_selcrit = ls_selcrit
      IMPORTING
        et_to      = lt_task_open.
    IF lines( lt_task_open ) = 0.
      MESSAGE e001(y20_ui5) WITH iv_huident INTO lv_msg.
      raise_exception( ).
    ENDIF.

    APPEND LINES OF lt_task_open TO mt_task_open.

    rt_task_open = lt_task_open.
  ENDMETHOD.


  METHOD read_queue.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Retrieve Queue
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " Check queue
    CALL FUNCTION '/SCWM/T346_READ_SINGLE'
      EXPORTING
        iv_lgnum  = iv_lgnum
        iv_queue  = iv_queue
      IMPORTING
        es_t346   = rs_queue
      EXCEPTIONS
        not_found = 1
        OTHERS    = 2.
    IF sy-subrc <> 0.
      ##NEEDED
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_msg).
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD read_trolley_content.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Retrieve content of trolley. In system trolley is the
*                resource. Physical stock on resource is selected.
*                Function module /SCWM/SELECT_STOCK is used.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: et_huheaders, et_huitems.

    DATA(ls_resource) = read_trolley_single( iv_lgnum   = iv_lgnum
                                             iv_trolley = iv_trolley ).

    DATA(lt_guid_loc) = VALUE /scwm/tt_guid_loc( ( guid_loc = ls_resource-guid_loc ) ).
    CALL FUNCTION '/SCWM/SELECT_STOCK'
      EXPORTING
        iv_lgnum = iv_lgnum
        it_rsrc  = lt_guid_loc
        iv_vsi   = wmegc_physical_stock
      IMPORTING
        et_huitm = et_huitems
        et_huhdr = et_huheaders
      EXCEPTIONS
        error    = 1
        OTHERS   = 2.
    IF sy-subrc <> 0.
      ##NEEDED
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_msg).
      mo_log->add_message( ).
    ENDIF.
  ENDMETHOD.


  METHOD read_trolley_single.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Retrieve trolley(resource) header data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_trolley IS INITIAL OR iv_lgnum IS INITIAL.
      ##NEEDED
      MESSAGE e009(y20_ui5) INTO DATA(lv_msg).
      raise_exception( ).
    ENDIF.

    CALL FUNCTION '/SCWM/RSRC_READ_SINGLE'
      EXPORTING
        iv_lgnum    = iv_lgnum
        iv_rsrc     = iv_trolley
      IMPORTING
        es_rsrc     = rs_trolley
      EXCEPTIONS
        wrong_input = 1
        not_found   = 2
        OTHERS      = 3.
    IF sy-subrc <> 0 OR rs_trolley IS INITIAL.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD read_warehouse_order.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Read warehouse order
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_lgnum    = iv_lgnum
            it_who      = it_who
            iv_lock_who = iv_lock_who
          IMPORTING
            et_who      = rt_whos.
      CATCH /scwm/cx_core.
        ##NEEDED
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
           WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_msg).
        raise_exception( ).
    ENDTRY.
  ENDMETHOD.


  METHOD update_warehouse_order.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Change warehouse order attributes
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    ##NEEDED
    DATA lv_msg TYPE string.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/WHO_UPDATE'
      EXPORTING
        iv_lgnum      = iv_lgnum
        iv_db_update  = abap_true
        iv_who        = iv_who
        iv_queue      = abap_true
        is_attributes = is_attributes
      EXCEPTIONS
        read_error    = 1
        attributes    = 2
        OTHERS        = 3.
    IF sy-subrc <> 0.
      ROLLBACK WORK.                                  "#EC CI_ROLLBACK.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ELSE.
      COMMIT WORK AND WAIT.
      MESSAGE i012(y20_ui5) WITH iv_who INTO lv_msg.
      mo_log->add_message( ).
    ENDIF.
  ENDMETHOD.


  METHOD validate_warehouse_number.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Check warehouse number is valid in the system
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_t300 TYPE /scwm/s_t300.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/T300_READ_SINGLE'
      EXPORTING
        iv_lgnum  = iv_lgnum
      IMPORTING
        es_t300   = ls_t300
      EXCEPTIONS
        not_found = 1
        OTHERS    = 2.
    IF sy-subrc <> 0 OR
       ls_t300-lgnum IS INITIAL.
      ##NEEDED
      MESSAGE e009(33) WITH iv_lgnum ms_message_string-t300 INTO DATA(lv_msg).
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD update_hu_logpos.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_msg       TYPE string,
          ls_huhdr_upd TYPE /scwm/s_huhdr_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    /scwm/cl_tm=>cleanup( ).

    read_hu_by_huident(
      EXPORTING
        iv_huident = iv_huident
      IMPORTING
        es_huhdr   = DATA(ls_huhdr) ).
    IF ls_huhdr IS INITIAL.
      RETURN.
    ENDIF.
    " initialize pack instance
    /scwm/cl_wm_packing=>get_instance(
      IMPORTING
        eo_instance = DATA(lo_packing) ).

    lo_packing->init_pack(
      EXPORTING
        iv_badi_appl  = wmegc_huappl_wme
        it_guid_hu    = VALUE #( ( guid_hu = ls_huhdr-guid_hu ) )
      EXCEPTIONS
        error         = 1
        OTHERS        = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ENDIF.

    ls_huhdr-logpos = iv_logpos.
    lo_packing->change_huhdr(
      EXPORTING
        is_huhdr   = ls_huhdr
      IMPORTING
        es_huhdr   = ls_huhdr_upd
      EXCEPTIONS
        error      = 1
        not_locked = 2
        OTHERS     = 3 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ENDIF.

    " save changes
    lo_packing->save(
      EXCEPTIONS
        error     = 1
        OTHERS    = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.

  METHOD complete_hu_process_step.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Complete HU process step ZIB2
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA lv_msg TYPE string.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    /scwm/cl_wm_packing=>get_instance( IMPORTING eo_instance = DATA(lo_pack) ).

    " read workstation for DECO from parameter framework
    DATA(lv_wrkst) = zswl_cl_param=>read_const( iv_lgnum     = iv_lgnum
                                                iv_context   = zswl_if_param_c=>mc_deco
                                                iv_parameter = zswl_if_param_c=>mc_wrkst_deco ).

    " initialize packing. Set global parameters, log instance
    lo_pack->init_by_workstation(
      EXPORTING
        is_workstation   = VALUE #( lgnum = iv_lgnum
                                    workstation = lv_wrkst
                                    procs = is_huhdr-procs )
        ir_huident       = VALUE #( ( sign   = wmegc_sign_inclusive
                                      option = wmegc_option_eq
                                      low    = is_huhdr-huident ) )
      EXCEPTIONS
        error            = 1
        OTHERS           = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ENDIF.

    lo_pack->hu_process_completed(
      EXPORTING
        iv_hu  = is_huhdr-guid_hu
      EXCEPTIONS
        error  = 1
        OTHERS = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ENDIF.

    lo_pack->save(
      EXCEPTIONS
        error     = 1
        OTHERS    = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.

ENDCLASS.