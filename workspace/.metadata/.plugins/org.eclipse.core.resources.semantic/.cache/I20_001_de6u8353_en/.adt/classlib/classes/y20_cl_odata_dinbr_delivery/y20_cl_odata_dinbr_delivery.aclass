CLASS y20_cl_odata_dinbr_delivery DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.

    INTERFACES y20_if_odata_entity .

    TYPES:
      "!<p class="shorttext synchronized">TU - Delivery type</p>
      BEGIN OF tys_tu_delivery,
        docid TYPE /scwm/de_docid.
        INCLUDE TYPE y20_cl_dinb_receiving_mpc=>transportunit.
    TYPES: END OF tys_tu_delivery .
    TYPES:
      "!<p class="shorttext synchronized">Mat ID: X16/X22</p>
      BEGIN OF tys_matid,
        matid_x16 TYPE /scwm/de_matid,
        matid_x22 TYPE /sapapo/matid,
      END OF tys_matid .
    TYPES:
      "!<p class="shorttext synchronized">HU process steps in DECO</p>
      BEGIN OF tys_deco_procs,
        zib1 TYPE /scwm/de_procs,
        zib2 TYPE /scwm/de_procs,
        zib3 TYPE /scwm/de_procs,
      END OF tys_deco_procs .
    TYPES:
      "!<p class="shorttext synchronized">Task create input parameters</p>
      BEGIN OF tys_task_create,
        update_task TYPE /scwm/rl03averbu,
        commit_work TYPE /scwm/rl03acomit,
        wtcode      TYPE /scwm/de_wtcode,
        rfc_queue   TYPE /scwm/s_rfc_queue,
        create_hu   TYPE /scwm/tt_to_crea_hu,
      END OF   tys_task_create .
    TYPES:
      "!<p class="shorttext synchronized">Fest units: Pack/Bulk quantities</p>
      BEGIN OF tys_festo_units,
        pack TYPE /scwm/s_quan,
        bulk TYPE /scwm/s_quan,
        delv TYPE /scwm/s_quan,
      END OF tys_festo_units .
    TYPES tyt_item_text TYPE STANDARD TABLE OF y20_cl_dinb_receiving_mpc=>ts_itemtext WITH EMPTY KEY.
    "!<p class="shorttext synchronized">Internal Process Step</p>
    CONSTANTS mc_intern_procs_spr TYPE /scwm/de_iproc VALUE 'SPR' ##NO_TEXT.

    "!<p class="shorttext synchronized">Constructor</p>
    METHODS constructor
      IMPORTING
        !io_log   TYPE REF TO y20_cl_log
        !iv_lgnum TYPE /scwm/lgnum OPTIONAL
        !it_docid TYPE /scwm/tt_docid_itmid OPTIONAL
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set handling unit, attribute MT_HUHDR</p>
    METHODS set_handling_unit
      IMPORTING
        !it_huhdr TYPE /scwm/tt_huhdr_int .
    METHODS test .
  PROTECTED SECTION.
  PRIVATE SECTION.
    TYPES: BEGIN OF tys_rsrc_who,
             who     TYPE /scwm/who-who,
             rsrc    TYPE /scwm/who-rsrc,
             areawho TYPE /scwm/who-areawho,
             queue   TYPE /scwm/who-queue,
           END OF tys_rsrc_who.
    TYPES tyt_rsrc_who TYPE TABLE OF tys_rsrc_who WITH EMPTY KEY.

    "!<p class="shorttext synchronized">Message string</p>
    DATA mv_msg TYPE string .
    "!<p class="shorttext synchronized">Warehouse number</p>
    DATA mv_lgnum TYPE /scwm/lgnum .
    "!<p class="shorttext synchronized">Navigation key, indicates execution method</p>
    DATA mv_key_navigation TYPE string .
    DATA:
        "!<p class="shorttext synchronized">TU - Delivery table</p>
      mt_tu_delivery TYPE STANDARD TABLE OF tys_tu_delivery WITH EMPTY KEY .
    DATA:
        "!<p class="shorttext synchronized">Entity: Popup delivery select</p>
      mt_popup_delivery TYPE STANDARD TABLE OF y20_cl_dinb_receiving_mpc=>ts_popupselectdocent WITH EMPTY KEY .
    "!<p class="shorttext synchronized">Table: Material for Warehouse-Number-Specific Data</p>
    DATA mt_mat_lgnum TYPE /scwm/tt_material_lgnum .
    "!<p class="shorttext synchronized">Table: HU header</p>
    DATA mt_huhdr TYPE /scwm/tt_huhdr_int .
    "!<p class="shorttext synchronized">Delivery header/item IDs</p>
    DATA mt_docid TYPE /scwm/tt_docid_itmid .
    DATA:
        "!<p class="shorttext synchronized">Entity: Delivery Items</p>
      mt_delivery_item TYPE STANDARD TABLE OF y20_cl_dinb_receiving_mpc=>ts_documentitement WITH EMPTY KEY .
    DATA:
        "!<p class="shorttext synchronized">Entity: Created HUs for delivery item</p>
      mt_created_hu TYPE STANDARD TABLE OF y20_cl_dinb_receiving_mpc=>ts_createdhuent WITH EMPTY KEY .
    "!<p class="shorttext synchronized">Entity: Product</p>
    DATA ms_product TYPE y20_cl_dinb_receiving_mpc=>ts_productent .
    "!<p class="shorttext synchronized">Entity: New packing</p>
    DATA ms_new_pack TYPE y20_cl_dinb_receiving_mpc=>ts_newpackingent .
    "!<p class="shorttext synchronized">Table: HU process steps in DECO</p>
    DATA ms_hu_procs TYPE tys_deco_procs .
    "!<p class="shorttext synchronized">Entity: Delivery header</p>
    DATA ms_delivery_header TYPE y20_cl_dinb_receiving_mpc=>ts_documentheaderent .
    "!<p class="shorttext synchronized">Message log</p>
    DATA mo_log TYPE REF TO y20_cl_log .
    "!<p class="shorttext synchronized">Delivery BO manager</p>
    DATA mo_delivery_bom TYPE REF TO /scdl/cl_bo_management .
    "!<p class="shorttext synchronized">Handling Unit: general functions</p>
    DATA mo_hu TYPE REF TO y20_cl_odata_handling_unit.

    "!<p class="shorttext synchronized">Add message to log from Service Provider</p>
    METHODS add_message_sp
      IMPORTING
        !io_message TYPE REF TO /scdl/cl_sp_message_box .
    "!<p class="shorttext synchronized">Cancel Warehouse Tasks</p>
    METHODS cancel_warehouse_tasks
      IMPORTING
        !it_cancel TYPE /scwm/tt_cancl
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS change_stock_type_to_initial
      IMPORTING
        !it_tasks_open TYPE /scwm/tt_ordim_o
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Complete HU process step</p>
    METHODS complete_hu_process_step
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS convert_uom_output
      IMPORTING
        !iv_uom          TYPE /scdl/dl_qty_str-uom
      RETURNING
        VALUE(rv_result) TYPE char3 .
    "!<p class="shorttext synchronized">Convert structure to reference data</p>
    METHODS copy_data_to_ref
      IMPORTING
        !is_data       TYPE any
      RETURNING
        VALUE(ro_data) TYPE REF TO data .
    "!<p class="shorttext synchronized">Create new packing: Create HU/Pack/Create WT</p>
    METHODS create_deep_entity_new_packing
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_result)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Execute packing for existing HU</p>
    METHODS create_deep_entity_old_packing
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_result)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Execute Undo: Cancel WT/New WT to GR zone/Delete HU</p>
    METHODS create_deep_entity_ucomm_undo
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_result)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Create new HU by HU type</p>
    METHODS create_hu_by_hutype
      IMPORTING
        !io_dlv_pack    TYPE REF TO /scwm/cl_dlv_pack_ibdl
      RETURNING
        VALUE(rs_huhdr) TYPE /scwm/s_huhdr_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Create HU task: /SCWM/TO_CREATE_MOVE_HU</p>
    METHODS create_hu_task
      IMPORTING
        !is_task_create TYPE tys_task_create
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Create HU task: /SCWM/TO_CREATE_MOVE_HU</p>
    METHODS create_hu_task_putaway
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Create HU task to DECO</p>
    METHODS create_hu_task_to_deco
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Create HU task to receiving zone</p>
    METHODS create_hu_task_to_receiving
      IMPORTING
        !it_tasks_open TYPE /scwm/tt_ordim_o
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Delete Handling Units</p>
    METHODS delete_hus
      IMPORTING
        !it_guid_hu TYPE /scwm/tt_guid_hu
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS determine_destination_bin
      IMPORTING
        !it_tasks_open      TYPE /scwm/tt_ordim_o
      RETURNING
        VALUE(rt_task_open) TYPE /scwm/tt_ordim_o
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Process HU with process step(PROCS = '')</p>
    METHODS execute_hu_init_process_step
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Get BO delivery item</p>
    METHODS get_bo_document_item
      IMPORTING
        !is_docid        TYPE /scwm/s_docid_itmid
      RETURNING
        VALUE(ro_result) TYPE REF TO /scdl/cl_dl_item_prd
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Get BO delivery items</p>
    METHODS get_bo_document_items
      IMPORTING
        !iv_docid        TYPE /scwm/s_docid_itmid-docid
      RETURNING
        VALUE(rt_result) TYPE /scdl/dl_item_tab
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Get BO delivery header</p>
    METHODS get_bo_header
      IMPORTING
        !iv_docid    TYPE /scwm/s_docid_itmid-docid
      RETURNING
        VALUE(ro_bo) TYPE REF TO /scdl/cl_dl_header_prd
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Get delivery header status text</p>
    METHODS get_delivery_status_txt
      IMPORTING
        !io_header       TYPE REF TO /scdl/cl_dl_header_prd
      RETURNING
        VALUE(rv_result) TYPE y20_cl_dinb_receiving_mpc=>status .
    "!<p class="shorttext synchronized">Build instance of delivery packing. Class /SCWM/CL_DLV_PACK_</p>
    METHODS get_dlv_pack_instance
      RETURNING
        VALUE(ro_result) TYPE REF TO /scwm/cl_dlv_pack_ibdl
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Build single entity for new packing</p>
    METHODS get_entity_new_pack_quantity
      IMPORTING
        !it_key_tab      TYPE /iwbep/t_mgw_name_value_pair
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Build document item entity with expanded entities</p>
    METHODS get_expanded_document_item
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Build entity for new packing</p>
    METHODS get_expanded_new_packhu_scan
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Build entity for popup for delivery selection</p>
    METHODS get_expanded_popup_document
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Determine data for popup Info entity</p>
    METHODS get_expanded_pop_product_info
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Build expanded entity for doc scan</p>
    METHODS get_expanded_scan_document
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Build entity for available trolleys</p>
    METHODS get_expand_available_trolleys
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Get material warehouse data</p>
    METHODS get_material_lgnum
      IMPORTING
        !iv_matid        TYPE /scdl/dl_product_str-productid
      RETURNING
        VALUE(rs_result) TYPE /scwm/s_material_lgnum .
    "!<p class="shorttext synchronized">Determine and propose pack quantity</p>
    METHODS get_pack_quantity_proposed
      IMPORTING
        !iv_user_quan TYPE /scwm/de_quantity OPTIONAL
        !io_item      TYPE REF TO /scdl/cl_dl_item_prd
      RETURNING
        VALUE(rs_qty) TYPE /scdl/dl_qty_str
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Get PO number from delivery item</p>
    METHODS get_refdoc_po_from_delivery
      IMPORTING
        !is_docid        TYPE /scwm/s_docid_itmid
      RETURNING
        VALUE(rv_result) TYPE /scdl/dl_refdoc_str-refdocno .
    "!<p class="shorttext synchronized">Get TU from attribute MT_TU_DELIVERY by delivery ID</p>
    METHODS get_tu_by_docid
      IMPORTING
        !iv_docid        TYPE /scdl/dl_docid
      RETURNING
        VALUE(rs_result) TYPE tys_tu_delivery .
    "!<p class="shorttext synchronized">get UoM from delivery item</p>
    METHODS get_unit_from_dlv_item
      IMPORTING
        !is_docid_itemid TYPE /scwm/s_docid_itmid
      RETURNING
        VALUE(rv_result) TYPE /scwm/s_quan-unit
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Check QI doc sample size is initial</p>
    METHODS is_qidoc_sample_size_initial
      RETURNING
        VALUE(rv_result) TYPE abap_bool .
    "!<p class="shorttext synchronized">Check packing quantity is multiple to Festo pack qty</p>
    METHODS is_qty_multiple_to_festo_pack
      IMPORTING
        !is_festo_units  TYPE y20_cl_odata_dinbr_delivery=>tys_festo_units
        !iv_user_qty     TYPE /scwm/de_quantity
      RETURNING
        VALUE(rv_result) TYPE abap_bool .
    "!<p class="shorttext synchronized">Pack delivery product to new HU</p>
    "! <ol><li>Create new HU</li>
    "! <li>Pack stock in the new HU</li>
    "! <li>Add newly created HU to buffer</li></ol>
    METHODS pack_product_to_new_hu
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Post goods receipt for HU</p>
    METHODS post_goods_receipt_hu
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Raise message from BAPIRET</p>
    METHODS raise_bapiret_message
      IMPORTING
        !it_bapiret TYPE bapirettab
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Raise exception</p>
    METHODS raise_exception
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS read_cat_from_gr_post_task
      IMPORTING
        !iv_huident   TYPE /scwm/s_huhdr_int-huident
      RETURNING
        VALUE(rv_cat) TYPE /scwm/s_to_det_mon-cat .
    "!<p class="shorttext synchronized">Read Delivery Item Text</p>
    METHODS read_delivery_item_text
      IMPORTING
        !is_docid      TYPE /scwm/s_docid_itemid
      RETURNING
        VALUE(rt_text) TYPE tyt_item_text.
    "!<p class="shorttext synchronized">Read destination parameters for new task to DECO</p>
    METHODS read_dest_param_for_deco_task
      RETURNING
        VALUE(rs_to_crea) TYPE /scwm/s_to_crea_hu
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read HUs in DECO bins</p>
    METHODS read_hus_in_deco_bins
      RETURNING
        VALUE(rt_huhdr) TYPE /scwm/tt_huhdr_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read HUs by HU guid</p>
    METHODS read_hu_by_huguid
      IMPORTING
        !it_guid_hu TYPE /scwm/tt_guid_hu
      EXPORTING
        !et_huhdr   TYPE /scwm/tt_huhdr_int
        !et_huitem  TYPE /scwm/tt_huitm_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read HUs for document item</p>
    METHODS read_hu_for_docitem
      IMPORTING
        !is_docid  TYPE /scwm/s_docid_itmid
      EXPORTING
        !et_huhdr  TYPE /scwm/tt_huhdr_int
        !et_huitem TYPE /scwm/tt_huitm_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read material FESTO units</p>
    METHODS read_material_festo_units
      RETURNING
        VALUE(rs_result) TYPE tys_festo_units .
    "!<p class="shorttext synchronized">Read materials by delivery items</p>
    METHODS read_material_multi_by_dlvitem
      IMPORTING
        !it_document_items TYPE /scdl/dl_item_tab .
    "!<p class="shorttext synchronized">Read SAPAPO material data</p>
    METHODS read_material_sapapo
      IMPORTING
        !io_item         TYPE REF TO /scdl/cl_dl_item_prd
      RETURNING
        VALUE(rs_result) TYPE /sapapo/matkey_out .
    "!<p class="shorttext synchronized">Read open tasks for HUs</p>
    METHODS read_open_tasks_for_hu
      IMPORTING
        !it_huident       TYPE /scwm/tt_huident
      RETURNING
        VALUE(rt_ordim_o) TYPE /scwm/tt_ordim_o
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read pack material by HU type</p>
    METHODS read_packmat_by_hutype
      IMPORTING
        !iv_hutype       TYPE /scwm/de_hutyp
      RETURNING
        VALUE(rs_matkey) TYPE tys_matid .
    "!<p class="shorttext synchronized">Read QI document for delivery item</p>
    METHODS read_qi_docno_for_dlvitem
      IMPORTING
        !is_docid        TYPE /scwm/s_docid_itmid
      RETURNING
        VALUE(rs_result) TYPE /scdl/af_df_link_attr_str .
    "!<p class="shorttext synchronized">Find resources(trolleys) for DECO</p>
    METHODS read_resources
      IMPORTING
        !iv_lgnum      TYPE /scwm/lgnum
      RETURNING
        VALUE(rt_rsrc) TYPE /scwm/tt_rsrce
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read HU header</p>
    METHODS read_set_hu_header
      IMPORTING
        !iv_guid_hu TYPE y20_s_handling_unit_odata-guid_hu
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read storage type</p>
    METHODS read_storage_type
      IMPORTING
        !iv_lgtyp        TYPE /scwm/s_t301-lgtyp
      RETURNING
        VALUE(rs_result) TYPE /scwm/s_t301
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read TU by delivery ID. Set found data in MT_TU_DELIVERY</p>
    METHODS read_trasport_unit_by_docid
      IMPORTING
        !it_docid TYPE /scdl/dl_guid_tab .
    "!<p class="shorttext synchronized">Read handling units in TU</p>
    METHODS read_tu_handling_units
      IMPORTING
        !is_trasport    TYPE /scwm/s_tu_sr_act_num
      RETURNING
        VALUE(rt_tu_tu) TYPE /scwm/tt_bo_tu_dlv_no_sort .
    "!<p class="shorttext synchronized">Read HUs in TU for specific delivery</p>
    METHODS read_tu_hus_for_docid
      IMPORTING
        !iv_docid       TYPE /scdl/dl_docid
        !is_trasport    TYPE /scwm/s_tu_sr_act_num
      RETURNING
        VALUE(rt_huhdr) TYPE /scwm/tt_huhdr_int .
    "!<p class="shorttext synchronized">Reverse goods receipt</p>
    METHODS reverse_goods_receipt
      IMPORTING
        !is_docid   TYPE /scwm/s_docid_itemid
        !it_guid_hu TYPE /scwm/tt_guid_hu
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Round down quantity to FESTO quantity</p>
    METHODS rounddown_quan_to_pack
      IMPORTING
        !is_festo_units  TYPE y20_cl_odata_dinbr_delivery=>tys_festo_units
        !iv_remain_qty   TYPE /scdl/dl_addmeas_str-qty
      RETURNING
        VALUE(rs_result) TYPE /scdl/dl_qty_str
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set data for entity "CreatedHUEnt"</p>
    METHODS set_entity_created_hu_by_docid
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set data for entity "DocumentHeaderEnt"</p>
    METHODS set_entity_header_document
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set data for entity "DocumentItemEnt"</p>
    METHODS set_entity_item_document .
    "!<p class="shorttext synchronized">Set data for entity "NewPackingEnt"</p>
    METHODS set_entity_new_pack_by_docid
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set data for entity "PopupSelectDocEnt"</p>
    METHODS set_entity_pop_header_document
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set data for entity "ProductEnt"</p>
    METHODS set_entity_product_by_docid
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set delivery BO manager</p>
    METHODS set_inbound_doc_manager .
    "!<p class="shorttext synchronized">Read HU process steps from parameter framework</p>
    METHODS set_list_hu_procs
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set display data for new pack entity from last HU created</p>
    METHODS set_newpack_from_last_hucrea .
    "!<p class="shorttext synchronized">Resort HUITEM entries by HUident</p>
    METHODS sort_huitems_by_huhdr
      IMPORTING
        !it_huhdr               TYPE /scwm/tt_huhdr_int
        !it_huitem              TYPE /scwm/tt_huitm_int
      RETURNING
        VALUE(rt_huitem_sorted) TYPE /scwm/tt_huitm_int .
    "!<p class="shorttext synchronized">Check source storage type in switch</p>
    METHODS switch_check_vltyp
      IMPORTING
        !iv_vltyp        TYPE /scwm/ordim_o-vltyp
      RETURNING
        VALUE(rv_result) TYPE abap_bool .
    "!<p class="shorttext synchronized">Update delivery item single weight</p>
    METHODS update_dlv_item_eew
      IMPORTING
*        !is_docid TYPE /scwm/s_docid_itmid OPTIONAL
*        !is_quan  TYPE /scwm/s_quan OPTIONAL
        is_dimensions TYPE y20_cl_dinb_receiving_mpc=>ts_popupweight OPTIONAL
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Update single weight entered from user</p>
    METHODS update_entity_weight_popup
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_result)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Update HU attributes</p>
    METHODS update_hu_attributes
      IMPORTING
        !is_huhdr TYPE /scwm/s_huhdr_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Update HU item attributes (inspid, insptyp)</p>
    METHODS update_hu_inspection_from_gmhu
      IMPORTING
        !it_tasks TYPE /scwm/tt_ordim_o .
    "!<p class="shorttext synchronized">Change HU Type in HUHDR</p>
    METHODS update_hu_type
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Validate delivery keys</p>
    METHODS validate_delivery_keys
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Validate HU type</p>
    METHODS validate_hutype
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Check warehouse number is valid</p>
    METHODS validate_set_warehouse_number
      IMPORTING
        iv_lgnum TYPE /scwm/lgnum
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read resources linked to WHOs</p>
    METHODS read_rsrc_who_for_queue
      IMPORTING
        iv_queue           TYPE /scwm/de_queue
*        iv_aarea           TYPE /scwm/de_aarea
      RETURNING
        VALUE(rt_rsrc_who) TYPE tyt_rsrc_who.
    METHODS read_who_for_queue
      IMPORTING
        iv_queue      TYPE /scwm/de_queue
*        iv_aarea      TYPE /scwm/de_aarea
      RETURNING
        VALUE(rt_who) TYPE /scwm/tt_whoid.
ENDCLASS.



CLASS y20_cl_odata_dinbr_delivery IMPLEMENTATION.


  METHOD add_message_sp.
    IF io_message IS NOT BOUND.
      RETURN.
    ENDIF.

    DATA(lt_messages) = io_message->get_messages( ).

    LOOP AT lt_messages ASSIGNING FIELD-SYMBOL(<ls_msg>) WHERE msgty CA wmegc_severity_ea.
      MESSAGE ID <ls_msg>-msgid TYPE <ls_msg>-msgty NUMBER <ls_msg>-msgno
         WITH <ls_msg>-msgv1 <ls_msg>-msgv2 <ls_msg>-msgv3 <ls_msg>-msgv4 INTO mv_msg.
      mo_log->add_message( ).
      EXIT.
    ENDLOOP.

  ENDMETHOD.


  METHOD cancel_warehouse_tasks.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_bapiret  TYPE bapirettab,
          lv_severity TYPE bapi_mtype.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/TO_CANCEL'
      EXPORTING
        iv_lgnum       = mv_lgnum
        iv_update_task = abap_false
        iv_commit_work = abap_true
        it_cancl       = it_cancel
      IMPORTING
        et_bapiret     = lt_bapiret
        ev_severity    = lv_severity.
    IF lv_severity CA wmegc_severity_ea.
      raise_bapiret_message( lt_bapiret ).
    ENDIF.
  ENDMETHOD.


  METHOD change_stock_type_to_initial.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Change stock type back to first one
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_header   TYPE /scwm/s_gmheader,
          lt_item     TYPE /scwm/tt_spitem,
          lt_bapiret  TYPE bapiret2_t,
          lv_severity TYPE bapi_mtype.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    read_hu_by_huguid(
      EXPORTING
        it_guid_hu = VALUE #( FOR <ls_task> IN it_tasks_open
                        ( guid_hu = <ls_task>-sguid_hu ) )
      IMPORTING
        et_huhdr   = DATA(lt_huhdr)
        et_huitem  = DATA(lt_huitm) ).

    " read stock category from good receipt task
    DATA(lv_cat) = read_cat_from_gr_post_task( lt_huhdr[ 1 ]-huident ).

    " check if stock category has been changed
    LOOP AT lt_huitm ASSIGNING FIELD-SYMBOL(<ls_huitm>).
      CHECK <ls_huitm>-cat = lv_cat.
      DELETE lt_huitm.
    ENDLOOP.

    IF lines( lt_huitm ) = 0.
      RETURN.
    ENDIF.
    ls_header-lgnum = mv_lgnum.
    ls_header-code = 'DINBR_DELIVERY'.
    ls_header-compl = abap_true.

    LOOP AT lt_huitm ASSIGNING FIELD-SYMBOL(<ls_item>).
      DATA(ls_item) = VALUE /scwm/s_spitem( ).
      ADD 1 TO /scwm/cl_qfu_super=>gv_id.
      ls_item-id = /scwm/cl_qfu_super=>gv_id.
      ls_item-id_group = 2.
      ls_item-direction = wmegc_lime_post_transfer.
      ls_item-guid_hu = <ls_item>-guid_parent.

      " move stock attributes
      MOVE-CORRESPONDING <ls_item> TO ls_item-source_s.     "#EC ENHOK
      MOVE-CORRESPONDING <ls_item> TO ls_item-dest_s.
      ls_item-dest_s-cat = lv_cat.
      CLEAR:
      ls_item-source_s-guid_stock0,
      ls_item-dest_s-guid_stock,
      ls_item-dest_s-guid_stock0,
      ls_item-dest_s-stock_cnt.

      ls_item-loc-lgnum = mv_lgnum.
      ASSIGN lt_huhdr[ guid_hu = <ls_item>-guid_parent ] TO FIELD-SYMBOL(<ls_huhdr>).
      IF sy-subrc = 0.
        ls_item-loc-lgtyp = <ls_huhdr>-lgtyp.
        ls_item-loc-lgpla = <ls_huhdr>-lgpla.
      ENDIF.
      ls_item-t_quan = VALUE #( ( quan = <ls_item>-quan
                                  unit = <ls_item>-meins ) ).

      APPEND ls_item TO lt_item.
    ENDLOOP.

    CALL FUNCTION '/SCWM/STOCK_CHANGE'
      EXPORTING
        is_header   = ls_header
        it_item     = lt_item
      IMPORTING
        et_bapiret  = lt_bapiret
        ev_severity = lv_severity.
    IF lv_severity CA wmegc_severity_ea.
      raise_bapiret_message( it_bapiret = lt_bapiret ).
    ENDIF.

    TRY.
        CALL FUNCTION '/SCWM/GM_POST'
          IMPORTING
            et_bapiret  = lt_bapiret
            ev_severity = lv_severity.
      CATCH /scwm/cx_core.
    ENDTRY.
    IF lv_severity CA wmegc_severity_ea.
      CALL FUNCTION 'DB_ROLLBACK'.
      raise_bapiret_message( it_bapiret = lt_bapiret ).
    ENDIF.

    MESSAGE s164(/scwm/lc1) INTO mv_msg.
    mo_log->add_message( ).

    COMMIT WORK AND WAIT.
  ENDMETHOD.


  METHOD complete_hu_process_step.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    /scwm/cl_wm_packing=>get_instance( IMPORTING eo_instance = DATA(lo_pack) ).

    " read workstation for DECO from parameter framework
    DATA(lv_wrkst) = zswl_cl_param=>read_const( iv_lgnum     = mv_lgnum
                                                iv_context   = zswl_if_param_c=>mc_deco
                                                iv_parameter = zswl_if_param_c=>mc_wrkst_deco ).

    " initialize packing. Set global parameters, log instance
    lo_pack->init_by_workstation(
      EXPORTING
        is_workstation   = VALUE #( lgnum = mv_lgnum
                                    workstation = lv_wrkst
                                    procs = mt_huhdr[ 1 ]-procs )
        ir_huident       = VALUE #( ( sign   = wmegc_sign_inclusive
                                      option = wmegc_option_eq
                                      low    = mt_huhdr[ 1 ]-huident ) )
      EXCEPTIONS
        error            = 1
        OTHERS           = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    lo_pack->hu_process_completed(
      EXPORTING
        iv_hu  = mt_huhdr[ 1 ]-guid_hu
      EXCEPTIONS
        error  = 1
        OTHERS = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    lo_pack->save(
      EXCEPTIONS
        error     = 1
        OTHERS    = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD constructor.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Build Instance for Inbound document execution
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF io_log IS NOT BOUND.
      RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
    ENDIF.

    mo_log   = io_log.

    mo_hu = y20_cl_odata_handling_unit=>get_instance( ).

    " not validation of DOCIDs here. Validation and read is done in execution
    mt_docid = it_docid.

    " validate and set warehouse number is supplied
    IF iv_lgnum IS SUPPLIED.
      validate_set_warehouse_number( iv_lgnum = iv_lgnum ).
    ENDIF.
  ENDMETHOD.


  METHOD convert_uom_output.
    CALL FUNCTION 'CONVERSION_EXIT_CUNIT_OUTPUT'
      EXPORTING
        input    = iv_uom
        language = 'E'
      IMPORTING
        output   = rv_result.
  ENDMETHOD.


  METHOD copy_data_to_ref.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Convert structure to reference data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    FIELD-SYMBOLS: <ls_data> TYPE any.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CREATE DATA ro_data LIKE is_data.
    ASSIGN ro_data->* TO <ls_data>.
    <ls_data> = is_data.
  ENDMETHOD.


  METHOD create_deep_entity_new_packing.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      BEGIN OF ls_entity.
        INCLUDE TYPE y20_cl_dinb_receiving_mpc=>ts_newpackingent.
    DATA: newpack_product   TYPE y20_cl_dinb_receiving_mpc=>ts_productent,
        newpack_createdhu TYPE y20_cl_dinb_receiving_mpc=>tt_createdhuent,
      END OF ls_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data( IMPORTING es_data = ls_entity ).

    MOVE-CORRESPONDING ls_entity TO ms_new_pack.

    IF ms_new_pack-quan-quan IS INITIAL.
      MESSAGE e003(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.
    validate_set_warehouse_number( iv_lgnum = ms_new_pack-lgnum ).

    APPEND VALUE #( docid = ms_new_pack-docid
                    itmid = ms_new_pack-itemid
                    doccat = /scdl/if_dl_c=>sc_doccat_inb_prd ) TO mt_docid.

    validate_hutype( ).

    validate_delivery_keys( ).
    " read/set delivery documents
    set_inbound_doc_manager( ).

    IF ms_new_pack-quan-unit IS INITIAL.
      ms_new_pack-quan-unit = get_unit_from_dlv_item( VALUE #( mt_docid[ 1 ] ) ).
    ENDIF.

    pack_product_to_new_hu( ).
    DATA(lt_addmeas) = get_bo_document_item( is_docid = mt_docid[ 1 ] )->get_addmeas(
        iv_nodynamic    = abap_false
        iv_qty_role     =  /scdl/if_dl_addmeas_c=>sc_qtyrole_pack
        iv_qty_category = /scdl/if_dl_addmeas_c=>sc_qtycat_complete ).

    TRY.
        IF lt_addmeas[ 1 ]-qty IS INITIAL.
          post_goods_receipt_hu( ).
          WAIT UP TO 2 SECONDS.
        ENDIF.
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.

    " waiting for QI sample size from ERP
*    IF is_qidoc_sample_size_initial( ) = abap_true.
*      WAIT UP TO 2 SECONDS.
*    ENDIF.

    create_hu_task_putaway( ).

*    complete_hu_process_step( ).

*    set_created_hu_entity_by_docid( ).

*    DATA(ls_new_pack) = ms_new_pack.
**    set_new_pack_entity_by_docid( ).
*
*    ms_new_pack-last_hutype = ls_new_pack-hutype.
*    ms_new_pack-hutype = ls_new_pack-hutype.
*    ms_new_pack-quan = ls_new_pack-quan.

    MOVE-CORRESPONDING ms_new_pack TO ls_entity.
    ls_entity-newpack_createdhu = mt_created_hu.
    ro_result = copy_data_to_ref( is_data = ls_entity ).
  ENDMETHOD.


  METHOD create_deep_entity_old_packing.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    DATA:
      BEGIN OF ls_entity.
        INCLUDE TYPE y20_cl_dinb_receiving_mpc=>ts_newpackingent.
    DATA: newpack_product   TYPE y20_cl_dinb_receiving_mpc=>ts_productent,
        newpack_createdhu TYPE y20_cl_dinb_receiving_mpc=>tt_createdhuent,
      END OF ls_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data( IMPORTING es_data = ls_entity ).

    validate_set_warehouse_number( iv_lgnum = ls_entity-lgnum ).

    MOVE-CORRESPONDING ls_entity TO ms_new_pack.
    " read and set in attribute MT_HUHDR HU-header data
    read_set_hu_header( iv_guid_hu = ms_new_pack-hu-guid_hu ).

    " if new HU type is enter update HU-header field "LETYP"
    update_hu_type( ).

    set_list_hu_procs( ).
    " make a decision based on HU process step
    CASE mt_huhdr[ 1 ]-procs.
      WHEN ''.
        " initial process step
        execute_hu_init_process_step( ).
      WHEN ms_hu_procs-zib1.
        " process step ZIB1
        MESSAGE e020(y20_ui5) WITH ms_new_pack-hu-huident INTO mv_msg.
        raise_exception( ).
      WHEN ms_hu_procs-zib2.
        " process step ZIB2
        complete_hu_process_step( ).
      WHEN ms_hu_procs-zib3.
        " process step ZIB3
        MESSAGE i021(y20_ui5) INTO mv_msg.
        mo_log->add_message( ).
    ENDCASE.

    ro_result = copy_data_to_ref( is_data = ls_entity ).
  ENDMETHOD.


  METHOD create_deep_entity_ucomm_undo.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Execute undo command for packed HU
*               1. Cancel existing warehouse task
*               2. Create HU task to receiving area
*               3. Update HUitem attributes, otherwise reverse goods receipt
*                  is not possible
*               4. Do reverse goods receipt
*               5. Delete HU
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      BEGIN OF ls_entity.
        INCLUDE TYPE y20_cl_dinb_receiving_mpc=>ts_usercommand.
    DATA: usercomm_createdhu TYPE STANDARD TABLE OF y20_cl_dinb_receiving_mpc=>ts_createdhuent WITH EMPTY KEY,
      END OF ls_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data( IMPORTING es_data = ls_entity ).

    validate_set_warehouse_number( iv_lgnum = ls_entity-lgnum ).

    mt_docid = VALUE #( FOR <ls_hus> IN ls_entity-usercomm_createdhu
        ( docid = <ls_hus>-docid
          itmid = <ls_hus>-itemid
          doccat = /scdl/if_dl_c=>sc_doccat_inb_prd ) ).

    validate_delivery_keys( ).

    DATA(lt_tasks_open) = read_open_tasks_for_hu( VALUE #( FOR <ls_hu> IN ls_entity-usercomm_createdhu
        ( huident = <ls_hu>-hu-huident ) )  ).

    cancel_warehouse_tasks( VALUE #( FOR <ls_task> IN lt_tasks_open
        ( tanum = <ls_task>-tanum ) ) ).

    create_hu_task_to_receiving( it_tasks_open = lt_tasks_open ).

    change_stock_type_to_initial( it_tasks_open = lt_tasks_open ).

    update_hu_inspection_from_gmhu( it_tasks = lt_tasks_open ).

    reverse_goods_receipt( is_docid = VALUE #( docid = ls_entity-docid )
                           it_guid_hu = VALUE #( FOR <ls_hu> IN ls_entity-usercomm_createdhu
                                            ( guid_hu = <ls_hu>-hu-guid_hu ) ) ).

    delete_hus( VALUE #( FOR <ls_hu> IN ls_entity-usercomm_createdhu ( guid_hu = <ls_hu>-hu-guid_hu ) ) ).

    MESSAGE i061(y20_ui5) INTO mv_msg.
    mo_log->add_message( ).
*    set_inbound_doc_manager( ).
*    " read and set created HUs
*    set_entity_created_hu_by_docid( ).
*    ls_document_entity-usercomm_createdhu = mt_created_hu.
    CLEAR: ls_entity-usercomm_createdhu.
    ro_result = copy_data_to_ref( is_data = ls_entity ).
  ENDMETHOD.


  METHOD create_hu_by_hutype.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Create Handling Unit. Packaging material is determined
*               from the HU type provided
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(ls_packmat_key) = read_packmat_by_hutype( iv_hutype = ms_new_pack-hutype ).
    io_dlv_pack->create_hu(
      EXPORTING
        iv_pmat      = ls_packmat_key-matid_x16
      RECEIVING
        es_huhdr     = DATA(ls_huhdr)
      EXCEPTIONS
        error        = 1
        OTHERS       = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    rs_huhdr = ls_huhdr.
  ENDMETHOD.


  METHOD create_hu_task.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_bapiret  TYPE bapirettab,
          lv_severity TYPE bapi_mtype.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/TO_CREATE_MOVE_HU'
      EXPORTING
        iv_lgnum       = mv_lgnum
        iv_bname       = sy-uname
        iv_update_task = is_task_create-update_task
        iv_commit_work = is_task_create-commit_work
        iv_wtcode      = is_task_create-wtcode
        it_create_hu   = is_task_create-create_hu
      IMPORTING
        et_bapiret     = lt_bapiret
        ev_severity    = lv_severity.
    IF lv_severity CA wmegc_severity_ea.
      raise_bapiret_message( lt_bapiret ).
    ENDIF.
  ENDMETHOD.


  METHOD create_hu_task_putaway.
    IF lines( mt_huhdr ) <> 1.
      raise_exception( ).
    ENDIF.

    DATA(ls_huhdr) = REF #( mt_huhdr[ 1 ] ).

    DATA(lo_item) = get_bo_document_item( VALUE #( mt_docid[ 1 ] ) ).

    lo_item->get_sapext( IMPORTING es_sapext_i = DATA(ls_sapext) ).

    DATA(lt_create) = VALUE /scwm/tt_to_crea_hu(
        ( guid_hu = ls_huhdr->guid_hu
          procty =  ls_sapext-/scwm/procty
          seqno  = 1
          rdocrelevant = abap_true ) ).

    create_hu_task( VALUE #( commit_work = abap_true
                             update_task = abap_false
                             create_hu = lt_create ) ).
  ENDMETHOD.


  METHOD create_hu_task_to_deco.
    DATA(ls_to_create) = read_dest_param_for_deco_task( ).
    ls_to_create-guid_hu = mt_huhdr[ 1 ]-guid_hu.
    ls_to_create-huident = mt_huhdr[ 1 ]-huident.

    create_hu_task( VALUE #( commit_work = abap_true
                             update_task = abap_false
                             wtcode = wmegc_wtcode_adhoc_hu
                             create_hu = VALUE #( ( ls_to_create ) ) ) ).
  ENDMETHOD.


  METHOD create_hu_task_to_receiving.
    DATA: lt_create   TYPE /scwm/tt_to_crea_hu.

    " read destination storage type from parameter framework
    DATA(lv_nltyp) = zswl_cl_param=>read_const(
                                      iv_lgnum     = mv_lgnum
                                      iv_context   = zswl_if_param_c=>mc_deco
                                      iv_parameter = zswl_if_param_c=>mc_nltyp_02 ).

    " read process type form parameter framework
    DATA(lv_procty) = zswl_cl_param=>read_const(
                                      iv_lgnum     = mv_lgnum
                                      iv_context   = zswl_if_param_c=>mc_ui5_deco
                                      iv_parameter = zswl_if_param_c=>mc_ui5_deco_procty ).

    DATA(ls_storage_type) = read_storage_type( CONV #( lv_nltyp ) ).
    DATA(lt_task_nlpla) = determine_destination_bin( it_tasks_open ).
    " build create task data
    lt_create = VALUE #( FOR <ls_task> IN lt_task_nlpla
          ( guid_hu = <ls_task>-sguid_hu
            procty = CONV #( lv_procty )
            squit  = abap_true
            norou  = wmegc_norou_yes
            nltyp = ls_storage_type-lgtyp
            nlpla = <ls_task>-nlpla
            rdocrelevant = abap_true ) ).

    create_hu_task( VALUE #( commit_work = abap_true
                             update_task = abap_false
                             create_hu = lt_create ) ).
  ENDMETHOD.


  METHOD delete_hus.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Delete handling units
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " initialize pack instance
    /scwm/cl_dlv_pack_ibdl=>get_instance(
      IMPORTING
        eo_instance = DATA(lo_dlv_pack) ).

    lo_dlv_pack->init_pack(
      EXPORTING
        iv_badi_appl  = wmegc_huappl_wme
        it_guid_hu    = it_guid_hu
      EXCEPTIONS
        error         = 1
        OTHERS        = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    LOOP AT it_guid_hu ASSIGNING FIELD-SYMBOL(<ls_guid_hu>).
      lo_dlv_pack->delete_hu(
        EXPORTING
          iv_hu  = <ls_guid_hu>-guid_hu
        EXCEPTIONS
          error  = 1
          OTHERS = 2 ).
      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
          WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
        raise_exception( ).
      ENDIF.
    ENDLOOP.

    lo_dlv_pack->save(
      EXCEPTIONS
        error     = 1
        OTHERS    = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD determine_destination_bin.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Determine destination bin from confirmed tasks for HU
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_ordim_c TYPE /scwm/tt_ordim_c.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( it_tasks_open ) = 0.
      RETURN.
    ENDIF.

    CALL FUNCTION '/SCWM/TO_READ_SRC'
      EXPORTING
        iv_lgnum     = mv_lgnum
        iv_nobuf     = abap_true
        it_huident   = VALUE /scwm/tt_huident( FOR <ls_task> IN it_tasks_open
                           ( huident = <ls_task>-vlenr ) )
      IMPORTING
        et_ordim_c   = lt_ordim_c
      EXCEPTIONS
        wrong_input  = 1
        not_found    = 2
        foreign_lock = 3
        OTHERS       = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    " remove all with status different from confirmed
    DELETE lt_ordim_c WHERE tostat <> wmegc_to_confirmed.
    " remove all with task different from putaway
    DELETE lt_ordim_c WHERE trart <> wmegc_trart_put.
    IF lines( lt_ordim_c ) = 0.
      RETURN.
    ENDIF.

    LOOP AT it_tasks_open INTO DATA(ls_open).
      ASSIGN lt_ordim_c[ vlenr = ls_open-vlenr ] TO FIELD-SYMBOL(<ls_conf>).
      CHECK sy-subrc = 0.

      ls_open-nlpla = <ls_conf>-vlpla.
      APPEND ls_open TO rt_task_open.
    ENDLOOP.
  ENDMETHOD.


  METHOD execute_hu_init_process_step.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : HU is scanned with process step = ''(initial)
*               1. Read open tasks for HU
*               2. check source storage type in switch
*                 2.1 storage type is in the switch - send info msg to user
*                 2.2 storage type is not in the switch
*                   - cancel warehouse task
*                   - create new task to DECO
*                   - update HU process step to ZIB2
*                   - complete process step
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(lt_ordim_o) = read_open_tasks_for_hu( VALUE #( ( huident = mt_huhdr[ 1 ]-huident ) )  ).

        DATA(ls_task_open) = REF #( lt_ordim_o[ 1 ] ).
        IF switch_check_vltyp( iv_vltyp = ls_task_open->vltyp ) = abap_false.
          RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
        ENDIF.
      CATCH cx_sy_itab_line_not_found.
        raise_exception( ).
      CATCH /iwbep/cx_mgw_tech_exception.
        IF lines( lt_ordim_o ) > 0.
          " cancel warehouse task.
          cancel_warehouse_tasks( it_cancel = VALUE #( FOR <ls_to> IN lt_ordim_o
                                                  ( tanum = <ls_to>-tanum ) ) ).
        ENDIF.
        " create HU task to DECO
        create_hu_task_to_deco( ).
        " update HU Type to ZIB2
        ms_new_pack-hutype = ms_hu_procs-zib2.
        update_hu_type( ).
        " complete step
        complete_hu_process_step( ).
    ENDTRY.

    MESSAGE i022(y20_ui5) WITH mt_huhdr[ 1 ]-huident INTO mv_msg.
    mo_log->add_message( ).
  ENDMETHOD.


  METHOD get_bo_document_item.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Return instance of delivery item
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(lt_document_items) = get_bo_document_items( iv_docid = is_docid-docid ).
        ro_result = CAST /scdl/cl_dl_item_prd( lt_document_items[ itemid = is_docid-itmid ]-item ).
        IF ro_result IS NOT BOUND.
          RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception( ).
        ENDIF.
      CATCH cx_sy_itab_line_not_found.
        raise_exception( ).
      CATCH /iwbep/cx_mgw_tech_exception.
        raise_exception( ).
    ENDTRY.
  ENDMETHOD.


  METHOD get_bo_document_items.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Return instance of all delivery items
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lo_bo_delivery) = mo_delivery_bom->get_bo_by_id( iv_docid = iv_docid ).
    IF lo_bo_delivery IS NOT BOUND.
      raise_exception( ).
    ENDIF.

    lo_bo_delivery->get_item_tab(
      IMPORTING
        et_item = rt_result ).
  ENDMETHOD.


  METHOD get_bo_header.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Return instance of delivery header
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lo_bo_delivery) = mo_delivery_bom->get_bo_by_id( iv_docid = iv_docid ).
    IF lo_bo_delivery IS NOT BOUND.
      raise_exception( ).
    ENDIF.

    ro_bo = CAST /scdl/cl_dl_header_prd( lo_bo_delivery->get_header( ) ).
  ENDMETHOD.


  METHOD get_delivery_status_txt.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Return text of delivery header status
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF io_header IS NOT BOUND.
      RETURN.
    ENDIF.

    DATA(lt_status) = io_header->get_status( iv_status_type = /scdl/if_dl_status_c=>sc_t_warehouse_activity ).
    IF lines( lt_status ) = 0.
      RETURN.
    ENDIF.

    DATA(lo_status_management) = /scdl/cl_stm=>get_instance( ).
    TRY.
        lo_status_management->get_short_text(
          EXPORTING
            is_status         = VALUE #( status_type = lt_status[ 1 ]-status_type
                                         status_value = lt_status[ 1 ]-status_value )
            iv_language       = sy-langu
          IMPORTING
            es_status_text    = DATA(ls_status_txt) ).
      CATCH /scdl/cx_stm.
        RETURN.
    ENDTRY.

    rv_result-statusValue =  ls_status_txt-status_value.
    rv_result-statusValueText =  ls_status_txt-status_value_text.
  ENDMETHOD.


  METHOD get_dlv_pack_instance.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Build instance of delivery packing. Class /SCWM/CL_DLV_PACK_IBDL
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    /scwm/cl_tm=>cleanup( iv_lgnum = mv_lgnum ).

    /scwm/cl_dlv_pack_ibdl=>get_instance_dlv(
      IMPORTING
        eo_instance = ro_result ).

    ro_result->init(
        iv_lgnum         = mv_lgnum
        it_docid         = VALUE #( FOR <ls_docid> IN mt_docid
                              ( docid = <ls_docid>-docid ) )
        iv_doccat        = /scdl/if_dl_c=>sc_doccat_inb_prd ).

    ro_result->init_pack(
      EXPORTING
        iv_badi_appl  = wmegc_huappl_wme
        iv_loc_type   = wmegc_whs
        iv_loc_index  = wmegc_idx_refdoc
        iv_lgnum      = mv_lgnum
      EXCEPTIONS
        error         = 1
        OTHERS        = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD get_entity_new_pack_quantity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : build single entity for new packing
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(lv_quan_str) = it_key_tab[ name = y20_cl_dinb_receiving_mpc_ext=>ms_field_name-scanquan ]-value.
        DATA(lv_pack_quan) = CONV /scwm/de_quantity( lv_quan_str ).
        IF lv_pack_quan IS INITIAL.
          RETURN.
        ENDIF.
      CATCH cx_sy_itab_line_not_found.
        RETURN.
    ENDTRY.

    DATA(ls_new_qty) = VALUE y20_cl_dinb_receiving_mpc=>ts_scanquantityent( ).

    validate_delivery_keys( ).
    " read/set delivery documents
    set_inbound_doc_manager( ).

*    DATA(ls_festo_units) = read_material_festo_units( ).

    get_pack_quantity_proposed( iv_user_quan = lv_pack_quan
                                io_item = get_bo_document_item( VALUE #( mt_docid[ 1 ] ) ) ).
*    get_proposed_new_pack_quantity( iv_pack_quan = lv_pack_quan ).

    DATA(ls_docid) = REF #( mt_docid[ 1 ] ).
    ls_new_qty-docid = ls_docid->docid.
    ls_new_qty-itemid = ls_docid->itmid.
    ls_new_qty-lgnum = mv_lgnum.
    ls_new_qty-scanquan = lv_pack_quan.
    ls_new_qty-unit = get_unit_from_dlv_item( is_docid_itemid = VALUE #( mt_docid[ 1 ] ) ).

    ro_result = copy_data_to_ref( is_data = ls_new_qty ).
  ENDMETHOD.


  METHOD get_expanded_document_item.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Get document item data with expanded entities
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      BEGIN OF ls_document_entity.
        INCLUDE TYPE y20_cl_dinb_receiving_mpc=>ts_documentitement.
    DATA: docitem_product   TYPE y20_cl_dinb_receiving_mpc=>ts_productent,
        docitem_newpack   TYPE y20_cl_dinb_receiving_mpc=>ts_newpackingent,
        docitem_createdhu TYPE STANDARD TABLE OF y20_cl_dinb_receiving_mpc=>ts_createdhuent WITH EMPTY KEY,
      END OF ls_document_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    validate_delivery_keys( ).
    " read/set delivery documents
    set_inbound_doc_manager( ).

    set_entity_product_by_docid( ).
    set_entity_new_pack_by_docid( ).
    set_entity_created_hu_by_docid( ).
    set_newpack_from_last_hucrea( ).

    ls_document_entity-docitem_product = ms_product.
    ls_document_entity-docitem_newpack = ms_new_pack.
    ls_document_entity-docitem_createdhu = mt_created_hu.

    ro_result = copy_data_to_ref( is_data = ls_document_entity ).
  ENDMETHOD.


  METHOD get_expanded_new_packhu_scan.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Build entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      BEGIN OF ls_document_entity.
        INCLUDE TYPE y20_cl_dinb_receiving_mpc=>ts_scandocumentent.
    DATA: scandoc_newpack TYPE y20_cl_dinb_receiving_mpc=>ts_newpackingent,
        scandoc_product TYPE y20_cl_dinb_receiving_mpc=>ts_productent,
      END OF ls_document_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( mt_huhdr ) = 0.
      MESSAGE e017(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    DATA(ls_huhdr) = REF #( mt_huhdr[ 1 ] ).

    read_hu_by_huguid(
      EXPORTING
        it_guid_hu = VALUE #( ( guid_hu = ls_huhdr->guid_hu ) )
      IMPORTING
        et_huitem  = DATA(lt_huitem) ).
    IF lines( lt_huitem ) = 0.
      MESSAGE e018(y20_ui5) WITH ls_huhdr->huident INTO mv_msg.
      raise_exception( ).
    ENDIF.

    " set HU display details
    DATA(ls_huitem) = REF #( lt_huitem[ 1 ] ).
    ms_new_pack-hu-guid_hu = ls_huhdr->guid_hu.
    ms_new_pack-hu-huident = ls_huhdr->huident.
    ms_new_pack-last_hutype = ls_huhdr->letyp.
    ms_new_pack-lgnum  = mv_lgnum.
*    ms_new_pack-scanquan = ls_huitem->quan.
    ms_new_pack-quan-quan = ls_huitem->quan.
    ms_new_pack-quan-unit = convert_uom_output( ls_huitem->meins ).

    " determine and set product details
    DATA(lo_dlv_access) = NEW /scwm/cl_dlv_md_access( ).
    lo_dlv_access->/scdl/if_af_md_product~get_prodno_by_prodid(
      EXPORTING
        it_productid = VALUE #( ( productid = ls_huitem->matid ) )
      IMPORTING
        et_product   = DATA(lt_product) ).
    IF lines( lt_product ) = 0.
      MESSAGE e019(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.
    ms_product-lgnum = mv_lgnum.
    ms_product-matid = lt_product[ 1 ]-prodid.
    ms_product-matnr = lt_product[ 1 ]-prodno.
    ms_product-mat_txt = lt_product[ 1 ]-prodtext.

    ls_document_entity-lgnum = mv_lgnum.
    ls_document_entity-scandoc_newpack = ms_new_pack.
    ls_document_entity-scandoc_product = ms_product.

    ro_result = copy_data_to_ref( is_data = ls_document_entity ).
  ENDMETHOD.


  METHOD get_expanded_popup_document.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Build entity with deliveries for popup screen
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      BEGIN OF ls_document_entity.
        INCLUDE TYPE y20_cl_dinb_receiving_mpc=>ts_popupselectdocent.
    DATA: popdochead_dochead TYPE y20_cl_dinb_receiving_mpc=>ts_documentheaderent,
        popdochead_docitem TYPE STANDARD TABLE OF y20_cl_dinb_receiving_mpc=>ts_documentitement WITH EMPTY KEY,
      END OF ls_document_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    validate_delivery_keys( ).
    " read/set delivery documents
    set_inbound_doc_manager( ).

    read_trasport_unit_by_docid( it_docid = mt_docid ).

    set_entity_header_document( ).

    IF ms_delivery_header IS NOT INITIAL.
      set_entity_item_document( ).
    ENDIF.

    ls_document_entity-popdochead_dochead = ms_delivery_header.
    ls_document_entity-popdochead_docitem = mt_delivery_item.

    ro_result = copy_data_to_ref( is_data = ls_document_entity ).
  ENDMETHOD.


  METHOD get_expanded_pop_product_info.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Determine data for popup Info entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: BEGIN OF ls_entity.
            INCLUDE TYPE y20_cl_dinb_receiving_mpc=>ts_usercommand.
    DATA:   usercomm_popupinfo TYPE y20_cl_dinb_receiving_mpc=>ts_popupinfoent,
            usercomm_itemtext  TYPE y20_cl_dinb_receiving_mpc=>tt_itemtext,
          END OF ls_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    validate_delivery_keys( ).
    " read/set delivery documents
    set_inbound_doc_manager( ).

    DATA(lo_item) = get_bo_document_item( VALUE #( mt_docid[ 1 ] ) ).
    DATA(lt_dlv_items) = get_bo_document_items( iv_docid = mt_docid[ 1 ]-docid ).
    DELETE lt_dlv_items WHERE itemid <> mt_docid[ 1 ]-itmid.

    DATA(ls_festo_units) = read_material_festo_units( ).
    read_material_multi_by_dlvitem( it_document_items = lt_dlv_items ).
    DATA(ls_mat_lgnum) = get_material_lgnum( iv_matid = lo_item->get_product( )-productid ).

    ls_entity-docid = lo_item->mv_docid.
    ls_entity-itemid = lo_item->mv_itemid.
    ls_entity-lgnum = mv_lgnum.
    ls_entity-usercomm_popupinfo-docid = lo_item->mv_docid.
    ls_entity-usercomm_popupinfo-itemid = lo_item->mv_itemid.
    ls_entity-usercomm_popupinfo-lgnum = mv_lgnum.
    ls_entity-usercomm_popupinfo-matid = lo_item->get_product( )-productid.
    ls_entity-usercomm_popupinfo-matnr = lo_item->get_product( )-productno.
    ls_entity-usercomm_popupinfo-mat_txt = lo_item->get_product( )-product_text.
    ls_entity-usercomm_popupinfo-qelivqty = ls_festo_units-delv.
    ls_entity-usercomm_popupinfo-packqty = ls_festo_units-pack.
    ls_entity-usercomm_popupinfo-bulkqty = ls_festo_units-bulk.
    ls_entity-usercomm_popupinfo-ptwyind = ls_mat_lgnum-put_stra.
    ls_entity-usercomm_popupinfo-stockremoveind = ls_mat_lgnum-rem_stra.
    ls_entity-usercomm_itemtext = read_delivery_item_text( VALUE #( docid = lo_item->mv_docid
                                                                    itemid = lo_item->mv_itemid ) ).

    ro_result = copy_data_to_ref( ls_entity ).
  ENDMETHOD.


  METHOD get_expanded_scan_document.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : return delivery document data, header and item entities
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      BEGIN OF ls_document_entity.
        INCLUDE TYPE y20_cl_dinb_receiving_mpc=>ts_scandocumentent.
    DATA: scandoc_dochead    TYPE y20_cl_dinb_receiving_mpc=>ts_documentheaderent,
        scandoc_docitem    TYPE STANDARD TABLE OF y20_cl_dinb_receiving_mpc=>ts_documentitement WITH EMPTY KEY,
        scandoc_popdochead TYPE STANDARD TABLE OF y20_cl_dinb_receiving_mpc=>ts_popupselectdocent WITH EMPTY KEY,
      END OF ls_document_entity.

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    validate_delivery_keys( ).
    " read/set delivery documents
    set_inbound_doc_manager( ).

    read_trasport_unit_by_docid( it_docid = mt_docid ).

    set_entity_header_document( ).

    set_entity_pop_header_document( ).
    IF ms_delivery_header IS NOT INITIAL.
      set_entity_item_document( ).
    ENDIF.

    ls_document_entity-scandoc_dochead = ms_delivery_header.
    ls_document_entity-scandoc_docitem = mt_delivery_item.
    ls_document_entity-scandoc_popdochead = mt_popup_delivery.

    ro_result = copy_data_to_ref( is_data = ls_document_entity ).
  ENDMETHOD.


  METHOD get_expand_available_trolleys.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Get available trolleys
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      BEGIN OF ls_entity.
        INCLUDE TYPE y20_cl_dinb_receiving_mpc=>ts_usercommand.
    DATA: usercomm_trolley TYPE y20_cl_dinb_receiving_mpc=>tt_trolleyent,
      END OF ls_entity.

    DATA: lt_entity TYPE y20_cl_dinb_receiving_mpc=>tt_trolleyent,
          lv_seqno  TYPE i.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_result) =  zswl_cl_param=>read_const( iv_lgnum     = mv_lgnum
                                                  iv_context   = zswl_if_param_c=>mc_deco
                                                  iv_parameter = zswl_if_param_c=>mc_deco_prepq ).
    DATA(lv_queue) = CONV /scwm/de_queue( lv_result ).

    DATA(lt_rsrc_who) = read_rsrc_who_for_queue( iv_queue = lv_queue ).

    LOOP AT lt_rsrc_who ASSIGNING FIELD-SYMBOL(<ls_area>)
                        GROUP BY ( areawho = <ls_area>-areawho
                                   rsrc = <ls_area>-rsrc
                                   size = GROUP SIZE )
                        ASSIGNING FIELD-SYMBOL(<ls_rsrc_who>).
      CHECK <ls_rsrc_who>-rsrc IS NOT INITIAL.

      DATA(lv_not_ass) = 0.
      " read RSRC WHO table with initial resources. This will be the number of not assigned WHO
      LOOP AT lt_rsrc_who ASSIGNING FIELD-SYMBOL(<ls_not_ass>) WHERE areawho = <ls_rsrc_who>-areawho
                                                                 AND rsrc IS INITIAL.
        lv_not_ass = lv_not_ass + 1.
      ENDLOOP.

      lv_seqno = lv_seqno + 1.
      APPEND VALUE #( aarea = <ls_rsrc_who>-areawho
                      lgnum = mv_lgnum
                      trolley = <ls_rsrc_who>-rsrc
                      seqno = lv_seqno
                      ass = <ls_rsrc_who>-size
                      nas = lv_not_ass ) TO lt_entity.
    ENDLOOP.

    ls_entity-usercomm_trolley = lt_entity.
    ro_result = copy_data_to_ref( is_data = ls_entity ).
  ENDMETHOD.


  METHOD get_material_lgnum.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Get material warehouse data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        rs_result = mt_mat_lgnum[ matid = iv_matid ].
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
  ENDMETHOD.


  METHOD get_pack_quantity_proposed.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Determine and propose quantity for packing
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF io_item IS NOT BOUND.
      RETURN.
    ENDIF.

    DATA(lt_goods_receipt) = io_item->get_addmeas( iv_qty_role     = /scdl/if_dl_addmeas_c=>sc_qtyrole_gr_pack
                                                   iv_qty_category = /scdl/if_dl_addmeas_c=>sc_qtycat_open ).
    IF lines( lt_goods_receipt ) = 0.
      RETURN.
    ENDIF.

    DATA(ls_goods_receipt) = REF #( lt_goods_receipt[ 1 ] ).
    IF ls_goods_receipt->qty = io_item->get_qty( )-qty AND iv_user_quan IS INITIAL.
      rs_qty = io_item->get_qty( ).
      RETURN.
    ENDIF.

    DATA(ls_festo_units) = read_material_festo_units( ).
    IF ls_festo_units-pack-quan IS INITIAL OR iv_user_quan IS INITIAL.
      RETURN.
    ENDIF.
    " if remaining less than the input quantity -> round down to Festo packing
    IF ls_goods_receipt->qty < iv_user_quan.
      rs_qty = rounddown_quan_to_pack( is_festo_units = ls_festo_units
                                       iv_remain_qty = ls_goods_receipt->qty ).
    ENDIF.

    IF is_qty_multiple_to_festo_pack( is_festo_units = ls_festo_units
                                   iv_user_qty = iv_user_quan ) = abap_false.
      MESSAGE e031(y20_ui5) WITH iv_user_quan ls_festo_units-pack-quan INTO mv_msg.
      raise_exception( ).
    ENDIF.


*    DATA(lt_putaway_planned) = io_item->get_addmeas( iv_qty_role     = /scdl/if_dl_addmeas_c=>sc_qtyrole_w4
*                                                     iv_qty_category = /scdl/if_dl_addmeas_c=>sc_qtycat_open ).
*    IF lines( lt_putaway_planned ) = 0.
*      RETURN.
*    ENDIF.
*    DATA(ls_putaway_planned) = REF #( lt_putaway_planned[ 1 ] ).
*
*    " If available item quantity is more than the last packed quantity use last packed qty
*    IF  ls_putaway_planned->qty >= ms_new_pack-remain_quan-quan.
*      rs_qty-qty = ms_new_pack-remain_quan-quan.
*      RETURN.
*    ENDIF.
*
*    rs_qty-qty  = ls_putaway_planned->qty. " this should be round-down to Festo z-field “bulk quantity”
  ENDMETHOD.


  METHOD get_refdoc_po_from_delivery.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Get refdoc PO from delivery item
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(lo_item) = get_bo_document_item( is_docid = is_docid ).
      CATCH /iwbep/cx_mgw_tech_exception.
        RETURN.
    ENDTRY.

    DATA(lt_refdocs) = lo_item->get_refdoc( iv_refdoccat = /scdl/if_dl_doc_c=>sc_doccat_inb_po ).
    IF lines( lt_refdocs ) = 0.
      RETURN.
    ENDIF.

    rv_result = lt_refdocs[ 1 ]-refdocno.
  ENDMETHOD.


  METHOD get_tu_by_docid.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : get TU by docid. Read MT_TU_DELIVERY attribute
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        rs_result = mt_tu_delivery[ docid = iv_docid ].
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
  ENDMETHOD.


  METHOD get_unit_from_dlv_item.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Get Unit of measure from delivery item
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lo_item) = get_bo_document_item( is_docid_itemid ).
    rv_result = lo_item->get_qty( )-uom.
  ENDMETHOD.


  METHOD is_qidoc_sample_size_initial.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read QI sample size. Return true if still = 0
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(lo_item) = get_bo_document_item( is_docid = VALUE #( mt_docid[ 1 ] ) ).
      CATCH /iwbep/cx_mgw_tech_exception.
        RETURN.
    ENDTRY.

    " get QI number from delivery item document flow
    DATA(ls_qual_insp_ref) = read_qi_docno_for_dlvitem( VALUE #( doccat = lo_item->mv_doccat
                                                                 docid =  lo_item->mv_docid
                                                                 itmid =  lo_item->mv_itemid ) ).
    DATA(lv_insp_doc_num) = CONV qie_tv_insp_document_number( |{ ls_qual_insp_ref-docnoto ALPHA = IN }| ).
    IF lv_insp_doc_num IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lo_qi) = cl_qie_insp_doc_persistency=>get_instance( ).
    TRY.
        " get inspection document by number from delivery item
        DATA(lo_qi_doc) = lo_qi->load_by_number( iv_insp_doc_number = lv_insp_doc_num
                                                 iv_read_only       = abap_true ).
      CATCH cx_qie_object_not_found.
        RETURN.
    ENDTRY.

    TRY.
        lo_qi_doc->get_attributes(
          IMPORTING
            es_ext_qmsyst_data   = DATA(ls_ext_data) ).
      CATCH cx_qie_no_authority.
        RETURN.
    ENDTRY.

    IF ls_ext_data-samp_size_ex = 0.
      rv_result = abap_true.
    ENDIF.
  ENDMETHOD.


  METHOD is_qty_multiple_to_festo_pack.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Check packing quantity is multiple to FESTO pack qty
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_delim  TYPE i,
          lv_module TYPE i.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF is_festo_units-pack-quan IS INITIAL.
      RETURN.
    ENDIF.

    lv_delim = floor( iv_user_qty / is_festo_units-pack-quan ).
    lv_module = iv_user_qty MOD is_festo_units-pack-quan.
    IF lv_delim = 0 OR lv_module <> 0.
      RETURN.
    ENDIF.
    rv_result = abap_true.
  ENDMETHOD.


  METHOD pack_product_to_new_hu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Pack delivery product to new HU
*               1. Create new HU
*               2. Pack stock in the new HU
*               3. Add newly created HU to buffer
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lo_dlv_pack) = get_dlv_pack_instance( ).

    DATA(ls_huhdr) = create_hu_by_hutype( io_dlv_pack = lo_dlv_pack ).

    lo_dlv_pack->pack_stock(
      EXPORTING
        iv_dest_hu   = ls_huhdr-guid_hu
        is_material  = VALUE #( qdoccat = /scdl/if_dl_c=>sc_doccat_inb_prd
                                qdocid = ms_new_pack-docid
                                qitmid = ms_new_pack-itemid )
        is_quantity  = VALUE #( quan = ms_new_pack-quan-quan
                                unit = ms_new_pack-quan-unit )
      EXCEPTIONS
        error        = 1
        OTHERS       = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    lo_dlv_pack->save(
      EXPORTING
        iv_commit = abap_true
        iv_wait   = abap_true
      EXCEPTIONS
        error     = 1
        OTHERS    = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    set_handling_unit( it_huhdr = VALUE #( ( ls_huhdr ) ) ).
  ENDMETHOD.


  METHOD post_goods_receipt_hu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Post goods receipt for HU using service providers
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
          ##NEEDED
          lt_sp_head_out TYPE /scdl/t_sp_a_head.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( mt_huhdr ) <> 1.
      raise_exception( ).
    ENDIF.

    DATA(ls_huhdr) = REF #( mt_huhdr[ 1 ] ).

    /scwm/cl_tm=>cleanup( iv_lgnum = mv_lgnum ).
    " build service provider
    TRY.
        DATA(lo_message) = NEW /scdl/cl_sp_message_box( ).
        DATA(lo_sp) = NEW /scdl/cl_sp_prd_inb( io_message_box = lo_message
                                               iv_mode        = /scdl/cl_sp=>sc_mode_classic
                                               iv_doccat      = /scdl/if_dl_doc_c=>sc_doccat_inb_prd ).
      CATCH /scdl/cx_sp_message_box.
        raise_exception( ).
    ENDTRY.

    DATA(lt_sp_a_head) = VALUE /scdl/t_sp_k_head( ( docid = ms_new_pack-docid ) ).
    " lock delivery to do the GR
    lo_sp->lock(
      EXPORTING
        inkeys    = lt_sp_a_head
        aspect    = /scdl/if_sp_c=>sc_asp_head
        lockmode  = /scdl/if_sp1_locking=>sc_exclusive_lock
      IMPORTING
        rejected     = DATA(lv_rejected) ).
    IF lv_rejected = abap_true.
      raise_exception( ).
    ENDIF.
    " do select to load BOM in buffer
    lo_sp->select(
      EXPORTING
        inkeys       = lt_sp_a_head
        aspect       = /scdl/if_sp_c=>sc_asp_head
      IMPORTING
        outrecords   = lt_sp_head_out
        rejected     = lv_rejected ).
    IF lv_rejected = abap_true.
      raise_exception( ).
    ENDIF.
    " execute GR for HU
    lo_sp->execute(
      EXPORTING
        aspect             = /scwm/if_sp_c=>sc_asp_hu
        inkeys             = VALUE /scwm/t_sp_k_hu( ( docid = ms_new_pack-docid
                                                      huid  = ls_huhdr->guid_hu ) )
        action             = '/SCWM/ACT_HU_POST_GM'
        relation_inkey     = VALUE /scdl/s_sp_k_head( docid = ms_new_pack-docid )
        relation           = '/SCWM/HEAD_TO_HU'
      IMPORTING
        rejected           = lv_rejected ).
    IF lv_rejected = abap_true.
      raise_exception( ).
    ENDIF.
    " save changes
    lo_sp->save( EXPORTING synchronously = abap_true
                 IMPORTING rejected = lv_rejected ).
    IF lv_rejected = abap_true.
      CALL FUNCTION 'DB_ROLLBACK'.
*      ROLLBACK WORK.
      /scwm/cl_tm=>cleanup( iv_reason = /scmb/if_sp_transaction=>sc_cleanup_end ).
*      lo_sp->cleanup( reason = /scmb/if_sp_transaction=>sc_cleanup_end ).
      raise_exception( ).
    ENDIF.

    " do commit and clean up. Reason COMMIT not to clear BO buffers
    COMMIT WORK AND WAIT.
    /scwm/cl_tm=>cleanup( iv_reason = /scmb/if_sp_transaction=>sc_cleanup_commit ).
*    lo_sp->cleanup( reason = /scmb/if_sp_transaction=>sc_cleanup_commit ).
  ENDMETHOD.


  METHOD raise_bapiret_message.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Raise message from BAPIRET
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    LOOP AT it_bapiret ASSIGNING FIELD-SYMBOL(<ls_bapiret>) WHERE type CA wmegc_severity_ea.
      MESSAGE ID <ls_bapiret>-id TYPE <ls_bapiret>-type NUMBER <ls_bapiret>-number
         WITH <ls_bapiret>-message_v1 <ls_bapiret>-message_v2 <ls_bapiret>-message_v3 <ls_bapiret>-message_v4
         INTO mv_msg.
      raise_exception( ).
    ENDLOOP.
  ENDMETHOD.


  METHOD raise_exception.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Raise exception
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_log->add_message( ).
    RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception(
        textid = VALUE #( msgno = sy-msgno
                          msgid = sy-msgid
                          attr1 = sy-msgv1
                          attr2 = sy-msgv2
                          attr3 = sy-msgv3
                          attr4 = sy-msgv4 ) ).
  ENDMETHOD.


  METHOD read_cat_from_gr_post_task.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read Stock category from GR posting task
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_tasks TYPE /scwm/tt_to_det_mon.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_selcrit) = VALUE /scwm/s_to_selcrit_mon(
      r_nlenr = VALUE #( ( sign   = wmegc_sign_inclusive
                           option = wmegc_option_eq
                           low    = iv_huident ) )
      r_trart = VALUE #( ( sign   = wmegc_sign_inclusive
                           option = wmegc_option_eq
                           low    = wmegc_trart_gr ) ) ).
    CALL FUNCTION '/SCWM/TO_GET_WIP'
      EXPORTING
        iv_lgnum   = mv_lgnum
        is_selcrit = lt_selcrit
        iv_conf    = abap_true
      IMPORTING
        et_to      = lt_tasks.
    IF lines( lt_tasks ) = 0.
      RETURN.
    ENDIF.
    rv_cat = lt_tasks[ 1 ]-cat.
  ENDMETHOD.


  METHOD read_delivery_item_text.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Read delivery item text
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    CONSTANTS lc_object TYPE tdobject VALUE '/SCWM/DLVI'.
    DATA: ""ls_head TYPE thead,
      lt_line TYPE STANDARD TABLE OF tline,
      lv_name TYPE  tdobname.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_param) = zswl_cl_param=>read_const(
        iv_lgnum     = mv_lgnum
        iv_context   = zswl_if_param_c=>mc_ui5
        iv_parameter = zswl_if_param_c=>mc_text_dlvitem_plie ).

    DATA(lv_id) = CONV tdid( lv_param ).
    IF lv_id IS INITIAL.
      lv_id = '0002'.
    ENDIF.

    lv_name = |{ is_docid-docid }{ is_docid-itemid }|.
    CALL FUNCTION 'READ_TEXT'
      EXPORTING
        client                  = sy-mandt
        id                      = lv_id
        language                = sy-langu
        name                    = lv_name
        object                  = lc_object
      TABLES
        lines                   = lt_line
      EXCEPTIONS
        id                      = 1
        language                = 2
        name                    = 3
        not_found               = 0
        object                  = 5
        reference_check         = 6
        wrong_access_to_archive = 7
        OTHERS                  = 8.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    DELETE lt_line WHERE tdline IS INITIAL.
    IF lines( lt_line ) = 0.
      RETURN.
    ENDIF.

    rt_text = VALUE #( FOR <ls_line> IN lt_line ( text = <ls_line>-tdline ) ).
  ENDMETHOD.


  METHOD read_dest_param_for_deco_task.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Read destination parameters for new task to DECO
*               Read storage type and storage section from parameter
*               framework. Validate maintained data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_t302 TYPE /scwm/t302.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_destination_params) = zswl_cl_param=>read_const_list(
                                      iv_lgnum     = mv_lgnum
                                      iv_context   = zswl_if_param_c=>mc_deco
                                      iv_parameter = zswl_if_param_c=>mc_nltyp_01 ).
    TRY.
        DATA(ls_storage_type) = read_storage_type( CONV #( lt_destination_params[ 1 ] ) ).

        " read storage section
        CALL FUNCTION '/SCWM/T302_READ_SINGLE'
          EXPORTING
            iv_lgnum  = mv_lgnum
            iv_lgtyp  = ls_storage_type-lgtyp
            iv_lgber  = CONV /scwm/lgber( lt_destination_params[ 2 ] )
          IMPORTING
            es_t302   = ls_t302
          EXCEPTIONS
            not_found = 1
            OTHERS    = 2.
        IF sy-subrc <> 0.
          MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_lgnum.
          raise_exception( ).
        ENDIF.

        rs_to_crea-nltyp = ls_storage_type-lgtyp.
        rs_to_crea-nlber = ls_t302-lgber.
      CATCH cx_sy_itab_line_not_found.
        MESSAGE e023(y20_ui5) INTO mv_msg.
        raise_exception( ).
    ENDTRY.
  ENDMETHOD.


  METHOD read_hus_in_deco_bins.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read HUs in DECO bins
*               Start selection from /SCWM/TPROCS with IPROC = 'SPR'
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_rsrc_type) = zswl_cl_param=>read_const_list(
        iv_lgnum     = mv_lgnum
        iv_context   = zswl_if_param_c=>mc_deco
        iv_parameter = zswl_if_param_c=>mc_deco_inbr_rsrctype ).
    IF lines( lt_rsrc_type ) = 0.
      MESSAGE e025(y20_ui5) INTO mv_lgnum.
      raise_exception( ).
    ENDIF.

    " Query storage bins
    SELECT a~lgpla INTO TABLE @DATA(lt_lagp)
      FROM /scwm/lagp AS a
                                                       "#EC CI_BUFFJOIN
     LEFT JOIN /scwm/tworkst AS b ON b~lgtyp = a~lgtyp
                                                       "#EC CI_BUFFJOIN
     LEFT JOIN /scwm/tprocs  AS c ON c~procs = b~procs
     WHERE a~lgnum = @mv_lgnum
       AND b~lgnum = @mv_lgnum
       AND c~iproc = @mc_intern_procs_spr.
    IF sy-subrc <> 0.
      MESSAGE e037(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    " remove lines with empty storage bin value
    DELETE lt_lagp WHERE lgpla IS INITIAL.
    IF lines( lt_lagp ) = 0.
      MESSAGE e037(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    CALL FUNCTION '/SCWM/HU_SELECT_GEN'
      EXPORTING
        iv_lgnum     = mv_lgnum
        ir_lgpla     = VALUE rseloption( FOR <ls_lagp> IN lt_lagp
                          ( sign   = wmegc_sign_inclusive
                            option = wmegc_option_eq
                            low    = <ls_lagp>-lgpla ) )
      IMPORTING
        et_huhdr     = rt_huhdr
      EXCEPTIONS
        wrong_input  = 1
        not_possible = 2
        error        = 3
        OTHERS       = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

  ENDMETHOD.


  METHOD read_hu_by_huguid.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read HUs by HU guid
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: et_huhdr, et_huitem.

    CALL FUNCTION '/SCWM/HU_SELECT'
      EXPORTING
        it_guid_hu = it_guid_hu
      IMPORTING
        et_huhdr   = et_huhdr
        et_huitm   = et_huitem
      EXCEPTIONS
        not_found  = 1
        error      = 2
        OTHERS     = 3.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD read_hu_for_docitem.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Read HUs for document item
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " read HUs items
    CALL FUNCTION '/SCWM/HU_SELECT_GEN'
      EXPORTING
        iv_lgnum     = mv_lgnum
        ir_qdoccat   = VALUE rseloption( ( sign = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low    = is_docid-doccat ) )
        it_qdocid    = VALUE /scwm/tt_docid( ( docid = is_docid-docid ) )
        it_qitmid    = VALUE /scwm/tt_itemid( ( is_docid-itmid ) )
        ir_vhi       = VALUE rseloption( ( sign = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low = wmegc_vhi_real ) )
      IMPORTING
        et_huhdr     = et_huhdr
        et_huitm     = et_huitem
      EXCEPTIONS
        wrong_input  = 1
        not_possible = 2
        error        = 3
        OTHERS       = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD read_material_festo_units.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read material FESTO units
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(lo_item) = get_bo_document_item( VALUE #( mt_docid[ 1 ] ) ).
      CATCH  /iwbep/cx_mgw_tech_exception .
        RETURN.
    ENDTRY.

    TRY.
        y20_cl_product=>get_festo_units(
          EXPORTING
            iv_matid      = lo_item->get_product( )-productid
            iv_quantity   = CONV #( '' )
          IMPORTING
            ev_zzpackunit = DATA(lv_pack)
            ev_zzbulkunit = DATA(lv_bulk)
            ev_zzdelvunit = DATA(lv_delv) ).
      CATCH zcx_ewm_general_exception.
        RETURN.
    ENDTRY.

    rs_result-pack-quan = lv_pack.
    rs_result-pack-unit = lo_item->get_qty( )-uom.

    rs_result-bulk-quan = lv_bulk.
    rs_result-bulk-unit = lo_item->get_qty( )-uom.

    rs_result-delv-quan = lv_delv.
    rs_result-delv-unit = lo_item->get_qty( )-uom.
  ENDMETHOD.


  METHOD read_material_multi_by_dlvitem.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read materials by delivery items
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_matid TYPE /scwm/tt_matid.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    LOOP AT it_document_items ASSIGNING FIELD-SYMBOL(<ls_doc_i>).
      CHECK <ls_doc_i>-item IS BOUND.
      APPEND <ls_doc_i>-item->get_product( )-productid TO lt_matid.
    ENDLOOP.
    IF lines( lt_matid ) = 0.
      RETURN.
    ENDIF.

    " get extended data to find the entitled
    DATA(lo_item) = CAST /scdl/cl_dl_item_prd( <ls_doc_i>-item ).
    lo_item->get_sapext( IMPORTING es_sapext_i = DATA(ls_sapext) ).

    " read material data
    TRY.
        CALL FUNCTION '/SCWM/MATERIAL_READ_MULTIPLE'
          EXPORTING
            it_matid     = lt_matid
            iv_entitled  = ls_sapext-entitled
            iv_lgnum     = mv_lgnum
          IMPORTING
            et_mat_lgnum = mt_mat_lgnum.
        ##NO_HANDLER
      CATCH /scwm/cx_md.
    ENDTRY.
  ENDMETHOD.


  METHOD read_material_sapapo.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read SAPAPO material data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_item->get_sapext( IMPORTING es_sapext_i = DATA(ls_sapext) ).

    DATA(lo_prod) = NEW /scwm/cl_mon_prod( ).

    lo_prod->get_prod_node_data(
      EXPORTING
        iv_lgnum            = mv_lgnum
        it_matnr_range      = VALUE #( ( sign = wmegc_sign_inclusive
                                         option = wmegc_option_eq
                                         low = io_item->get_product( )-productno ) )
        it_entit_range      = VALUE #( ( sign = wmegc_sign_inclusive
                                         option = wmegc_option_eq
                                         low = ls_sapext-entitled ) )
      IMPORTING
        et_material         = DATA(lt_material) ).
    IF lines( lt_material ) = 0.
      RETURN.
    ENDIF.

    rs_result = lt_material[ 1 ].
  ENDMETHOD.


  METHOD read_open_tasks_for_hu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read open tasks for HUs
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( it_huident ) = 0.
      RETURN.
    ENDIF.

    CALL FUNCTION '/SCWM/TO_READ_SRC'
      EXPORTING
        iv_lgnum     = mv_lgnum
        iv_nobuf     = abap_true
        it_huident   = it_huident
      IMPORTING
        et_ordim_o   = rt_ordim_o
      EXCEPTIONS
        wrong_input  = 1
        not_found    = 2
        foreign_lock = 3
        OTHERS       = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
    ENDIF.
  ENDMETHOD.


  METHOD read_packmat_by_hutype.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read pack material by HU type
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_hutype IS INITIAL.
      RETURN.
    ENDIF.

    SELECT SINGLE matid                                 "#EC CI_NOORDER
      FROM /sapapo/matpack
      INTO @rs_matkey-matid_x22
      ##WARN_OK
      WHERE hutyp = @iv_hutype.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    TRY.
        cl_system_uuid=>convert_uuid_c22_static(
          EXPORTING
            uuid     = rs_matkey-matid_x22
          IMPORTING
            uuid_x16 = rs_matkey-matid_x16 ).
      CATCH cx_uuid_error.
        RETURN.
    ENDTRY.
  ENDMETHOD.


  METHOD read_qi_docno_for_dlvitem.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read QI document for delivery item
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lo_docflow TYPE REF TO /scdl/if_af_docflow.

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        " get service
        DATA(lo_instctr) = /scdl/cl_af_management=>get_instance( ).
        lo_docflow ?= lo_instctr->get_service(
                                  /scdl/if_af_management_c=>sc_docflow ).
        " read docflow
        lo_docflow->read( EXPORTING it_docnode   = VALUE #( ( docid = is_docid-docid
                                                              itemid = is_docid-itmid
                                                              doccat = is_docid-doccat ) )
                                    iv_direction = /scdl/if_af_docflow=>sc_direction_both
                                    iv_steps     = 1
                          IMPORTING et_result    = DATA(lt_docflow) ).
        IF lines( lt_docflow ) = 0.
          RETURN.
        ENDIF.
      CATCH /scdl/cx_delivery.
        RETURN.
    ENDTRY.

    TRY.
        rs_result = lt_docflow[ doccatto = /scdl/if_dl_doc_c=>sc_doccat_q01 ].
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
  ENDMETHOD.


  METHOD read_resources.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Find resources(trolleys) for DECO
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_rsrc_type) = zswl_cl_param=>read_const_list(
        iv_lgnum     = iv_lgnum
        iv_context   = zswl_if_param_c=>mc_deco
        iv_parameter = zswl_if_param_c=>mc_deco_inbr_rsrctype ).
    IF lines( lt_rsrc_type ) = 0.
      MESSAGE e025(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    DATA(lt_selopt_rsrc_type) = VALUE rseloption( FOR <ls_rtype> IN lt_rsrc_type
                                   ( sign   = wmegc_sign_inclusive
                                     option = wmegc_option_eq
                                     low    = <ls_rtype> ) ).
    SELECT a~rsrc INTO TABLE @DATA(lt_rsrc)
      FROM /scwm/rsrc AS a
                                                       "#EC CI_BUFFJOIN
     INNER JOIN /scwm/tworkst AS b ON b~lgtyp = a~lgtyp
                                                       "#EC CI_BUFFJOIN
     INNER JOIN /scwm/tprocs  AS c ON c~procs = b~procs
     WHERE c~iproc = @mc_intern_procs_spr
       AND b~lgnum = @iv_lgnum
       AND a~lgnum = @iv_lgnum
       AND a~rsrc_type IN @lt_selopt_rsrc_type.
    IF sy-subrc <> 0 OR lines( lt_rsrc ) = 0.
      MESSAGE e026(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    rt_rsrc = VALUE #( FOR <ls_rsrc> IN lt_rsrc ( <ls_rsrc>-rsrc ) ).
  ENDMETHOD.


  METHOD read_set_hu_header.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Read HU header data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_huhdr TYPE /scwm/s_huhdr_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/HUHEADER_READ'
      EXPORTING
        iv_appl     = wmegc_huappl_wme
        iv_guid_hu  = iv_guid_hu
      IMPORTING
        es_huheader = ls_huhdr
      EXCEPTIONS
        not_found   = 1
        input       = 2
        error       = 3
        deleted     = 4
        OTHERS      = 5.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    set_handling_unit( it_huhdr = VALUE #( ( ls_huhdr ) ) ).
  ENDMETHOD.


  METHOD read_storage_type.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read storage type
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/T301_READ_SINGLE'
      EXPORTING
        iv_lgnum  = mv_lgnum
        iv_lgtyp  = iv_lgtyp
      IMPORTING
        es_t301   = rs_result
      EXCEPTIONS
        not_found = 1
        OTHERS    = 2.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_lgnum.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD read_trasport_unit_by_docid.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read TU by delivery ID. Set found data in MT_TU_DELIVERY
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( it_docid ) = 0.
      RETURN.
    ENDIF.

    " get TU assigned to delivery
    /scwm/cl_sr_db_tu=>read_dlv(
      EXPORTING
        it_docid  = it_docid
      IMPORTING
        et_tu_dlv = DATA(lt_tu_dlv) ).
    IF lines( lt_tu_dlv ) = 0.
      RETURN.
    ENDIF.

    " add unique entries in LT_TU_SR_ACT, because /SCWM/TT_TU_SR_ACTIVITY is type sorted table with unique key
    DATA(lt_tu_sr_act) = VALUE /scwm/tt_tu_sr_activity( ( ) ).
    LOOP AT lt_tu_dlv ASSIGNING FIELD-SYMBOL(<ls_tu>).
      ASSIGN lt_tu_sr_act[ tu_num = <ls_tu>-tu_num
                           tu_sr_act_num = <ls_tu>-tu_sr_act_num ] TO FIELD-SYMBOL(<ls_tu_sr>).
      CHECK sy-subrc <> 0.
      lt_tu_sr_act = VALUE #( BASE lt_tu_sr_act ( tu_num = <ls_tu>-tu_num
                                                  tu_sr_act_num = <ls_tu>-tu_sr_act_num ) ).
    ENDLOOP.

    TRY.
        /scwm/cl_sr_db_tu=>read_hdr(
          EXPORTING
            it_tu_sr_act = lt_tu_sr_act
          IMPORTING
            et_tu_hdr    = DATA(lt_tu_hdr) ).
      CATCH /scwm/cx_sr_error.
        RETURN.
    ENDTRY.

    LOOP AT lt_tu_dlv ASSIGNING FIELD-SYMBOL(<ls_tu_dlv>).
      TRY.
          DATA(ls_tu_hdr) = REF #( lt_tu_hdr[ tu_num = <ls_tu_dlv>-tu_num ] ).
        CATCH cx_sy_itab_line_not_found.
          CONTINUE.
      ENDTRY.

      INSERT VALUE #( docid = <ls_tu_dlv>-docid
                      tunum = <ls_tu_dlv>-tu_num
                      tuactnum = <ls_tu_dlv>-tu_sr_act_num
                      tunumext = ls_tu_hdr->tu_num_ext ) INTO TABLE mt_tu_delivery.
    ENDLOOP.
  ENDMETHOD.


  METHOD read_tu_handling_units.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read assigned HU to TU
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " Get all assigned HUs (one record for each packed item)
    DATA(lo_tudlv_manager) = /scwm/cl_sr_tudlv=>get_instance( ).
    lo_tudlv_manager->get_bo_tu_dlv(
      EXPORTING
        is_tu_act_key        = is_trasport
        iv_one_record_per_hu = abap_false
      IMPORTING
        et_bo_tu_hu          = rt_tu_tu ).
    IF lines( rt_tu_tu ) = 0.
      RETURN.
    ENDIF.
    DELETE rt_tu_tu WHERE chg_ind = wmesr_objstate_del.
  ENDMETHOD.


  METHOD read_tu_hus_for_docid.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read HUs in TU for specific delivery
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA lt_guid_hu  TYPE /scwm/tt_guid_hu.
    DATA ls_guid_hu  TYPE /scwm/s_guid_hu.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_tu_handling_unit) = read_tu_handling_units( is_trasport ).
    IF lines( lt_tu_handling_unit ) = 0.
      RETURN.
    ENDIF.
    " remove HUs for other deliveries
    DELETE lt_tu_handling_unit WHERE docid <> iv_docid.

    LOOP AT lt_tu_handling_unit ASSIGNING FIELD-SYMBOL(<ls_bo_tu_hu>).
      IF <ls_bo_tu_hu>-top_hu = ls_guid_hu-guid_hu.
        CONTINUE.
      ENDIF.
      ls_guid_hu-guid_hu = <ls_bo_tu_hu>-top_hu.
      APPEND ls_guid_hu TO lt_guid_hu.
    ENDLOOP.

    " read HU's with UI fields filled (MATNR, STATUS ect.)
    CALL FUNCTION '/SCWM/HU_READ_MULT'
      EXPORTING
        it_guid_hu   = lt_guid_hu
      IMPORTING
        et_huhdr     = rt_huhdr
      EXCEPTIONS
        wrong_input  = 1
        not_possible = 2
        OTHERS       = 3.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.
  ENDMETHOD.


  METHOD reverse_goods_receipt.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Reverse goods receipt using SP
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      ##NEEDED
      lt_sp_head_out TYPE /scdl/t_sp_a_head,
      ##NEEDED
      lt_sp_k_hu_out TYPE /scwm/t_sp_a_hu,
      ##NEEDED
      ls_sp_a_head   TYPE /scdl/s_sp_a_head.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    /scwm/cl_tm=>cleanup( iv_lgnum = mv_lgnum ).
    " build service provider
    TRY.
        DATA(lo_message) = NEW /scdl/cl_sp_message_box( ).
        DATA(lo_sp) = NEW /scdl/cl_sp_prd_inb( io_message_box = lo_message
                                               iv_mode        = /scdl/cl_sp=>sc_mode_classic
                                               iv_doccat      = /scdl/if_dl_doc_c=>sc_doccat_inb_prd ).
      CATCH /scdl/cx_sp_message_box.
        add_message_sp( lo_message ).
        raise_exception( ).
    ENDTRY.

    DATA(lt_sp_a_head) = VALUE /scdl/t_sp_k_head( ( docid = is_docid-docid ) ).
    " lock delivery to do the GR
    lo_sp->lock(
      EXPORTING
        inkeys    = lt_sp_a_head
        aspect    = /scdl/if_sp_c=>sc_asp_head
        lockmode  = /scdl/if_sp1_locking=>sc_exclusive_lock
      IMPORTING
        rejected     = DATA(lv_rejected) ).
    IF lv_rejected = abap_true.
      add_message_sp( lo_message ).
      raise_exception( ).
    ENDIF.
    " do select to load BOM in buffer
    lo_sp->select(
      EXPORTING
        inkeys       = lt_sp_a_head
        aspect       = /scdl/if_sp_c=>sc_asp_head
      IMPORTING
        outrecords   = lt_sp_head_out
        rejected     = lv_rejected ).
    IF lv_rejected = abap_true.
      add_message_sp( lo_message ).
      raise_exception( ).
    ENDIF.

    LOOP AT it_guid_hu ASSIGNING FIELD-SYMBOL(<ls_guid_hu>).
      lo_sp->execute(
        EXPORTING
          aspect             = /scwm/if_sp_c=>sc_asp_hu
          inkeys             = VALUE /scwm/t_sp_k_hu( ( docid = is_docid-docid
                                                        huid = <ls_guid_hu>-guid_hu ) )
          action             = '/SCWM/ACT_HU_CANCEL_GM'
          relation_inkey     = VALUE /scdl/s_sp_k_head( docid = is_docid-docid )
          relation           = '/SCWM/HEAD_TO_HU'
        IMPORTING
          outrecords         = lt_sp_k_hu_out
          rejected           = lv_rejected
          relation_outrecord = ls_sp_a_head ).
      IF lv_rejected = abap_true.
        add_message_sp( lo_message ).
        raise_exception( ).
      ENDIF.
    ENDLOOP.
    " save changes
    lo_sp->save(
      EXPORTING synchronously = abap_true
      IMPORTING rejected = lv_rejected ).
    IF lv_rejected = abap_true.
      CALL FUNCTION 'DB_ROLLBACK'.
*        ROLLBACK WORK.
      /scwm/cl_tm=>cleanup( iv_reason = /scdl/if_sp1_transaction=>sc_cleanup_rollback ).
      add_message_sp( lo_message ).
      raise_exception( ).
    ENDIF.

    " do commit and clean up. Reason = COMMIT not to clear BO buffers
    COMMIT WORK AND WAIT.
    /scwm/cl_tm=>cleanup( iv_reason = /scdl/if_sp1_transaction=>sc_cleanup_commit ).
  ENDMETHOD.


  METHOD rounddown_quan_to_pack.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Round down quantity to FESTO quantity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA lv_delim TYPE i.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF is_festo_units-pack-quan IS INITIAL.
      RETURN.
    ENDIF.

    lv_delim = floor( iv_remain_qty / is_festo_units-pack-quan ).
    IF lv_delim = 0.
      MESSAGE e030(y20_ui5) WITH iv_remain_qty INTO mv_msg.
      raise_exception( ).
    ENDIF.
    DATA(lv_new_qty) = lv_delim * is_festo_units-pack-quan.
    rs_result-qty = lv_new_qty.
    rs_result-uom = is_festo_units-pack-unit.
  ENDMETHOD.


  METHOD set_entity_created_hu_by_docid.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Set data in entity CreatedHUEnt
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA lv_iterator TYPE i.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lo_item) = get_bo_document_item( VALUE #( mt_docid[ 1 ] ) ).

    read_hu_for_docitem(
      EXPORTING
        is_docid  = VALUE #( mt_docid[ 1 ] )
      IMPORTING
        et_huhdr  = DATA(lt_huhdr)
        et_huitem = DATA(lt_huitem) ).
    TRY.
        DATA(lt_tasks_open) = read_open_tasks_for_hu( it_huident = VALUE #( FOR <ls_huhdr> IN lt_huhdr
            ( huident = <ls_huhdr>-huident ) ) ).
        ##NO_HANDLER
      CATCH /iwbep/cx_mgw_tech_exception.
    ENDTRY.

    SORT lt_huhdr BY huident.
    lt_huitem = sort_huitems_by_huhdr( it_huhdr  = lt_huhdr
                                       it_huitem = lt_huitem ).
    LOOP AT lt_huitem ASSIGNING FIELD-SYMBOL(<ls_huitem>).
      TRY.
          DATA(ls_hdr) = REF #( lt_huhdr[ guid_hu = <ls_huitem>-guid_parent ] ).
        CATCH cx_sy_itab_line_not_found.
          CONTINUE.
      ENDTRY.
      lv_iterator = lv_iterator + 1.

      TRY.
          DATA(ls_task) = REF #( lt_tasks_open[ sguid_hu = <ls_huitem>-guid_parent ] ).
          DATA(lv_aarea) = ls_task->aarea.
          ##NO_HANDLER
        CATCH cx_sy_itab_line_not_found.
      ENDTRY.

      APPEND VALUE #( docid = lo_item->mv_docid
                      itemid = lo_item->mv_itemid
                      itemno = lo_item->mv_itemno
                      lgnum = mv_lgnum
                      product-matid = lo_item->get_product( )-productid
                      product-matnr = lo_item->get_product( )-productno
                      quantity-quan = <ls_huitem>-quan
                      quantity-unit = <ls_huitem>-meins
                      seqno = lv_iterator
                      hu-guid_hu = ls_hdr->guid_hu
                      hu-huident = ls_hdr->huident
                      hu-hutype = ls_hdr->letyp
                      activityarea = lv_aarea ) TO mt_created_hu.
    ENDLOOP.
  ENDMETHOD.


  METHOD set_entity_header_document.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Set data in entity DocumentHeaderEnt
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lo_header) = get_bo_header( iv_docid = VALUE #( mt_docid[ 1 ]-docid ) ).
    DATA(ls_trasport) = get_tu_by_docid( lo_header->get_docid( ) ).

    DATA(lt_huhdr) = read_tu_hus_for_docid( iv_docid = lo_header->get_docid( )
                                            is_trasport = VALUE #( tu_num = ls_trasport-tunum
                                                                   tu_sr_act_num = ls_trasport-tuactnum ) ).

    ms_delivery_header-doccat = lo_header->get_doccat( ).
    ms_delivery_header-docid  = lo_header->get_docid( ).
    ms_delivery_header-docno  = lo_header->get_docno( ).
    TRY.
        ms_delivery_header-hu-huident = lt_huhdr[ 1 ]-huident.
        ms_delivery_header-hu-guid_hu = lt_huhdr[ 1 ]-guid_hu.
        ms_delivery_header-hu-hutype = lt_huhdr[ 1 ]-letyp.
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
    ms_delivery_header-lgnum  = mv_lgnum.
    ms_delivery_header-status = get_delivery_status_txt( lo_header ).
    ms_delivery_header-tu-tunum = ls_trasport-tunum.
    ms_delivery_header-tu-tuactnum = ls_trasport-tuactnum.
    ms_delivery_header-tu-tunumext = ls_trasport-tunumext.
  ENDMETHOD.


  METHOD set_entity_item_document.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Set data for entity "DocumentItemEnt"
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(ls_docid) = REF #( mt_docid[ 1 ] ).
      CATCH cx_sy_itab_line_not_found.
        RETURN.
    ENDTRY.

    TRY.
        DATA(lt_document_items) = get_bo_document_items( iv_docid = ls_docid->docid ).
      CATCH /iwbep/cx_mgw_tech_exception.
        RETURN.
    ENDTRY.

    read_material_multi_by_dlvitem( lt_document_items ).

    LOOP AT lt_document_items ASSIGNING FIELD-SYMBOL(<ls_item>).
      DATA(ls_item) = VALUE y20_cl_dinb_receiving_mpc=>ts_documentitement( ).
      DATA(lo_item) = CAST /scdl/cl_dl_item_prd( <ls_item>-item ).
      DATA(ls_product) = lo_item->get_product( ).
      DATA(ls_quantity) = lo_item->get_qty( ).

      ls_item-docid = lo_item->mv_docid.
      ls_item-itemid = lo_item->mv_itemid.
      ls_item-itemno = lo_item->mv_itemno.
      ls_item-lgnum = mv_lgnum.
      ls_item-product-matid = ls_product-productid.
      ls_item-product-matnr = ls_product-productno.
      ls_item-prod_txt = ls_product-product_text.
      ls_item-quantity-quan = ls_quantity-qty.
      ls_item-quantity-unit = ls_quantity-uom.
      ls_item-putcontrol = get_material_lgnum( ls_product-productid )-put_stra.
      APPEND ls_item TO mt_delivery_item.
    ENDLOOP.

  ENDMETHOD.


  METHOD set_entity_new_pack_by_docid.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Set data for entity NewPackingEnt
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lo_item) = get_bo_document_item( VALUE #( mt_docid[ 1 ] ) ).

    DATA(ls_qty_proposed) = get_pack_quantity_proposed( lo_item ).

    ms_new_pack-docid = lo_item->mv_docid.
    ms_new_pack-itemid = lo_item->mv_itemid.
*    ms_new_pack-last_hutype
*    ms_new_pack-last_quan =
    ms_new_pack-lgnum = mv_lgnum.
    ms_new_pack-product-matid = lo_item->get_product( )-productid.
    ms_new_pack-product-matnr = lo_item->get_product( )-productno.
    ms_new_pack-quan-quan = ls_qty_proposed-qty.
    ms_new_pack-quan-unit = ls_qty_proposed-uom.

    DATA(lt_gr_open_qty) = lo_item->get_addmeas(
                                iv_qty_role     = /scdl/if_dl_addmeas_c=>sc_qtyrole_gr_pack
                                iv_qty_category = /scdl/if_dl_addmeas_c=>sc_qtycat_open ).
    IF lines( lt_gr_open_qty ) = 1.
      ms_new_pack-remain_quan-quan = lt_gr_open_qty[ 1 ]-qty.
      ms_new_pack-remain_quan-unit = lt_gr_open_qty[ 1 ]-uom.
    ENDIF.

  ENDMETHOD.


  METHOD set_entity_pop_header_document.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Fill in deliveries in pop-up for user manual selection.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA lv_index TYPE i.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
*    IF lines( mt_docid ) < 2.
*      RETURN.
*    ENDIF.
    LOOP AT mt_docid INTO DATA(ls_gr) GROUP BY ( docid = ls_gr-docid ) ASSIGNING FIELD-SYMBOL(<ls_doc>).
      lv_index = lv_index + 1.
    ENDLOOP.
    IF lv_index < 2.
      RETURN.
    ENDIF.


    LOOP AT mt_docid ASSIGNING FIELD-SYMBOL(<ls_docid>).
      DATA(ls_pophead) = VALUE y20_cl_dinb_receiving_mpc=>ts_popupselectdocent( ).
      DATA(lo_header) = get_bo_header( iv_docid = <ls_docid>-docid ).
      DATA(lo_item) = get_bo_document_item( VALUE #( docid = <ls_docid>-docid
                                                     itmid = <ls_docid>-itmid ) ).

      ls_pophead-doccat = lo_header->get_doccat( ).
      ls_pophead-docid = lo_header->get_docid( ).
      ls_pophead-docno = lo_header->get_docno( ).
      ls_pophead-lgnum = mv_lgnum.
      ls_pophead-matnr = lo_item->get_product( )-productno.
      ls_pophead-mattxt = lo_item->get_product( )-product_text.
      ls_pophead-quan-quan = lo_item->get_qty( )-qty.
      ls_pophead-quan-unit = lo_item->get_qty( )-uom.

      APPEND ls_pophead TO mt_popup_delivery.
    ENDLOOP.

  ENDMETHOD.


  METHOD set_entity_product_by_docid.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Set product in attribute MS_PRODUCT for entity ProductEnt
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lo_item) = get_bo_document_item( VALUE #( mt_docid[ 1 ] ) ).

    ms_product-docid = lo_item->mv_docid.
    ms_product-itemid = lo_item->mv_itemid.
    ms_product-lgnum = mv_lgnum.
    ms_product-matid = lo_item->get_product( )-productid.
    ms_product-matnr = lo_item->get_product( )-productno.
    ms_product-mat_txt = lo_item->get_product( )-product_text.
    ms_product-ind_conf = read_material_sapapo( lo_item )-kzkfg.
    DATA(ls_festo_units) = read_material_festo_units( ).
    ms_product-packqty = ls_festo_units-pack.
    ms_product-bulkqty = ls_festo_units-bulk.
  ENDMETHOD.


  METHOD set_handling_unit.
    mt_huhdr = it_huhdr.
  ENDMETHOD.


  METHOD set_inbound_doc_manager.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Set delivery BO manager. Add DOCIDs in the buffer
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_delivery_bom = /scdl/cl_bo_management=>get_instance( ).
    IF mo_delivery_bom IS NOT BOUND.
      RETURN.
    ENDIF.

    DATA(lo_dl_query) = NEW /scdl/cl_dl_query( iv_doccat = /scdl/if_dl_doc_c=>sc_doccat_inb_prd ).
    LOOP AT mt_docid ASSIGNING FIELD-SYMBOL(<ls_docid>).
      lo_dl_query->add_docid( iv_docid = <ls_docid>-docid ).
    ENDLOOP.
    " execute query to load the BOs
    mo_delivery_bom->query(
      EXPORTING
        io_query            = lo_dl_query
        iv_lock_mode        = /scdl/if_dl_object_c=>sc_lock_none
        iv_data_source      = /scdl/if_dl_query_c=>sc_source_db ).
  ENDMETHOD.


  METHOD set_list_hu_procs.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read HU process steps for inbound from parameter framework
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_proces) = zswl_cl_param=>read_const_list(
                        iv_lgnum     = mv_lgnum
                        iv_context   = zswl_if_param_c=>mc_hu_hdr
                        iv_parameter = zswl_if_param_c=>mc_hu_procs_receive ).

    TRY.
        ms_hu_procs-zib1 = lt_proces[ 1 ].
        ms_hu_procs-zib2 = lt_proces[ 2 ].
        ms_hu_procs-zib3 = lt_proces[ 3 ].
      CATCH cx_sy_itab_line_not_found.
        MESSAGE e024(y20_ui5) INTO mv_msg.
        raise_exception( ).
    ENDTRY.
  ENDMETHOD.


  METHOD set_newpack_from_last_hucrea.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Set display data for new pack entity from last HU created
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_lines) = lines( mt_created_hu ).
    IF lv_lines > 0.
      DATA(ls_created_hu) = REF #( mt_created_hu[ lv_lines ] ).
      IF ms_new_pack-last_hutype IS INITIAL.
        ms_new_pack-last_hutype = ls_created_hu->hu-hutype.
        ms_new_pack-hutype = ls_created_hu->hu-hutype.
      ENDIF.

      IF ms_new_pack-quan-quan IS INITIAL AND ms_new_pack-remain_quan-quan IS NOT INITIAL.
        ms_new_pack-quan = ls_created_hu->quantity.
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD sort_huitems_by_huhdr.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Resort HUITEM entries by HUident
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    LOOP AT it_huhdr ASSIGNING FIELD-SYMBOL(<ls_huhdr>).
      LOOP AT it_huitem ASSIGNING FIELD-SYMBOL(<ls_item>) WHERE guid_parent = <ls_huhdr>-guid_hu.
        APPEND <ls_item> TO rt_huitem_sorted.
      ENDLOOP.
    ENDLOOP.
  ENDMETHOD.


  METHOD switch_check_vltyp.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Check source storage type in switch
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    rv_result = zswl_cl_switch=>get_switch_state(
                    iv_lgnum  = mv_lgnum
                    iv_ipnum  = zswl_if_switch_c=>mc_ewm1166
                    it_fields = VALUE #( ( combination = zswl_if_switch_c=>mc_group_2
                                           field = zswl_if_switch_c=>mc_vltyp
                                           field_value = iv_vltyp ) ) ).
  ENDMETHOD.


  METHOD test.
*    change_stock_type_to_initial( it_tasks_open = VALUE #( ( sguid_hu = '005056A172051EEAA899051095DC4712' ) ) ).
*    CATCH /iwbep/cx_mgw_tech_exception.
  ENDMETHOD.


  METHOD update_dlv_item_eew.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Update delivery item custom field: single weight
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_sp_item_eew_prd     TYPE /scdl/t_sp_a_item_eew_prd,
          ##NEEDED
          lt_sp_item_eew_prd_out TYPE /scdl/t_sp_a_item_eew_prd.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
*    IF is_quan IS INITIAL OR is_docid IS INITIAL.
*      RETURN.
*    ENDIF.
    IF is_dimensions-docid  IS INITIAL OR
       is_dimensions-itemid IS INITIAL.
      RETURN.
    ENDIF.
    " set warehouse that is used
    /scwm/cl_tm=>set_lgnum( mv_lgnum ).
    " build service provider
    TRY.
        DATA(lo_message) = NEW /scdl/cl_sp_message_box( ).
        DATA(lo_sp) = NEW /scdl/cl_sp_prd_inb( io_message_box = lo_message
                                               iv_mode        = /scdl/cl_sp=>sc_mode_classic
                                               iv_doccat      = /scdl/if_dl_doc_c=>sc_doccat_inb_prd ).
      CATCH /scdl/cx_sp_message_box.
        raise_exception( ).
    ENDTRY.
    DATA(lt_sp_a_item) = VALUE /scdl/t_sp_k_item( ( docid = is_dimensions-docid
                                                    itemid = is_dimensions-itemid ) ).
    " lock delivery
    lo_sp->lock(
      EXPORTING
        inkeys    = lt_sp_a_item
        aspect    = /scdl/if_sp_c=>sc_asp_item
        lockmode  = /scdl/if_sp1_locking=>sc_exclusive_lock
      IMPORTING
        rejected     = DATA(lv_rejected) ).
    IF lv_rejected = abap_true.
      raise_exception( ).
    ENDIF.
    " do select to load BOM in buffer
    lo_sp->select(
      EXPORTING
        inkeys       = lt_sp_a_item
        aspect       = /scdl/if_sp_c=>sc_asp_item_eew_prd
      IMPORTING
        outrecords   = lt_sp_item_eew_prd
        rejected     = lv_rejected ).
    IF lv_rejected = abap_true.
      raise_exception( ).
    ENDIF.

    IF lines( lt_sp_item_eew_prd ) = 0.
      RETURN.
    ENDIF.

    " update quantity
    DATA(ls_item_ewm) = REF #( lt_sp_item_eew_prd[ 1 ] ).
    IF is_dimensions-weight-quan IS NOT INITIAL.
      ls_item_ewm->zzsingleweight = is_dimensions-weight-quan.
      ls_item_ewm->zzsingleweight_u = is_dimensions-weight-unit.
    ENDIF.

    IF is_dimensions-height-quan IS NOT INITIAL.
      ls_item_ewm->zzquan_height = is_dimensions-height-quan.
      ls_item_ewm->zzquan_unit_h = is_dimensions-height-unit.
    ENDIF.

    IF is_dimensions-length-quan IS NOT INITIAL.
      ls_item_ewm->zzquan_length = is_dimensions-length-quan.
      ls_item_ewm->zzquan_unit_l = is_dimensions-length-unit.
    ENDIF.

    IF is_dimensions-width-quan IS NOT INITIAL.
      ls_item_ewm->zzquan_width = is_dimensions-width-quan.
      ls_item_ewm->zzquan_unit_w = is_dimensions-width-unit.
    ENDIF.

    " update delivery item EEW structure
    lo_sp->update(
      EXPORTING
        aspect       = /scdl/if_sp_c=>sc_asp_item_eew_prd
        inrecords    = lt_sp_item_eew_prd
      IMPORTING
        outrecords   = lt_sp_item_eew_prd_out
        rejected     = lv_rejected ).
    IF lv_rejected = abap_true.
      raise_exception( ).
    ENDIF.

    lo_sp->save( IMPORTING rejected = lv_rejected ).
    IF lv_rejected = abap_true.
      CALL FUNCTION 'DB_ROLLBACK'.
*      ROLLBACK WORK.
      /scwm/cl_tm=>cleanup( iv_reason = /scdl/if_sp1_transaction=>sc_cleanup_rollback ).
      RETURN.
    ENDIF.

    COMMIT WORK AND WAIT.
    /scwm/cl_tm=>cleanup( iv_reason = /scdl/if_sp1_transaction=>sc_cleanup_commit ).
  ENDMETHOD.


  METHOD update_entity_weight_popup.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Get single weight entered from user and update delivery
*                 item custom field: single weight
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    DATA: ls_dim  TYPE y20_cl_dinb_receiving_mpc_ext=>ts_popupweight,
          ls_unit TYPE t006-msehi,
          lv_text TYPE t006t-txdim,
          lv_dimension TYPE t006-dimid.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(ls_new_pack) = VALUE  y20_cl_dinb_receiving_mpc=>ts_popupweight( ).
    io_data_provider->read_entry_data( IMPORTING es_data = ls_new_pack ).

    validate_set_warehouse_number( mv_lgnum ).

    IF ls_new_pack-weight-quan IS INITIAL.
      MESSAGE e027(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    mt_docid = VALUE #( ( doccat = /scdl/if_dl_c=>sc_doccat_inb_prd
                          docid =  ls_new_pack-docid
                          itmid = ls_new_pack-itemid ) ).
    validate_delivery_keys( ).

    ls_new_pack-weight-unit = 'G'.

    CALL FUNCTION 'DIMENSION_GET_FOR_UNIT'
      EXPORTING
        unit                = ls_new_pack-height-unit
        use_buffer_for_text = 'X'
      IMPORTING
        dimension           = lv_dimension
      EXCEPTIONS
        unit_not_found      = 01
        OTHERS              = 02.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    IF lv_dimension <> 'LENGTH'.
      MESSAGE e071(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    ls_new_pack-length-unit = ls_new_pack-height-unit.
    ls_new_pack-width-unit  = ls_new_pack-height-unit.

    update_dlv_item_eew( is_dimensions = ls_new_pack ).

    MESSAGE i029(y20_ui5) INTO mv_msg.
    mo_log->add_message( ).

    CLEAR: ls_new_pack-weight.
    ro_result = copy_data_to_ref( is_data = ls_new_pack ).
  ENDMETHOD.


  METHOD update_hu_attributes.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Update HU attributes
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
*    DATA: ls_huhdr_upd TYPE /scwm/s_huhdr_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    " initialize pack instance
    /scwm/cl_wm_packing=>get_instance(
      IMPORTING
        eo_instance = DATA(lo_packing) ).

    lo_packing->init_pack(
      EXPORTING
        iv_badi_appl  = wmegc_huappl_wme
        it_guid_hu    = VALUE #( ( guid_hu = is_huhdr-guid_hu ) )
      EXCEPTIONS
        error         = 1
        OTHERS        = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    lo_packing->change_huhdr(
      EXPORTING
        is_huhdr   = is_huhdr
      EXCEPTIONS
        error      = 1
        not_locked = 2
        OTHERS     = 3 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.

    " save changes
    lo_packing->save(
      EXCEPTIONS
        error     = 1
        OTHERS    = 2 ).
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD update_hu_inspection_from_gmhu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Update HU item attributes (inspid, insptyp)
*               assign values from /SCWM/GMHUITM to HUITEM. This will
*               allow to execute reverse goods receipt to the HU
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_huitem   TYPE /scwm/tt_huitm_int,
          lo_log      TYPE REF TO /scwm/cl_log,
          lv_update   TYPE boole_d,
          lv_severity TYPE bapi_mtype.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( it_tasks ) = 0.
      RETURN.
    ENDIF.

    " query HU from /SCWM/GMHUITM. get INSPID/INSPTYP
    SELECT guid_parent, inspid, insptyp FROM /scwm/gmhuitm
      INTO TABLE @DATA(lt_gmhuitm)
     FOR ALL ENTRIES IN @it_tasks
     WHERE guid_parent = @it_tasks-sguid_hu.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    " read relevant stock for HU
    CALL FUNCTION '/SCWM/HU_READ_MULT'
      EXPORTING
        it_guid_hu   = VALUE /scwm/tt_guid_hu( FOR <ls_hu> IN lt_gmhuitm ( guid_hu = <ls_hu>-guid_parent ) )
      IMPORTING
        et_huitm     = lt_huitem
      EXCEPTIONS
        wrong_input  = 1
        not_possible = 2
        OTHERS       = 3.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      mo_log->add_message( ).
    ENDIF.

    SELECT * FROM /scwm/quan
      INTO TABLE @DATA(lt_quan)
      FOR ALL ENTRIES IN @lt_huitem
     WHERE guid_parent = @lt_huitem-guid_parent
       AND guid_stock = @lt_huitem-guid_stock.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    LOOP AT lt_quan ASSIGNING FIELD-SYMBOL(<ls_huitm>).
      TRY.
          " update stock attributes from /SCWM/GMHUITM
          DATA(ls_gmitem) = REF #( lt_gmhuitm[ guid_parent = <ls_huitm>-guid_parent ] ).
          <ls_huitm>-inspid = ls_gmitem->inspid.
          <ls_huitm>-insptyp = ls_gmitem->insptyp.
          lv_update = abap_true.
          ##ENH_OK
*          DATA(ls_stock) = CORRESPONDING /scwm/s_quan_att( <ls_huitm> ).
*          ls_stock-inspid = ls_gmitem->inspid.
*          ls_stock-insptyp = ls_gmitem->insptyp.

*          CALL FUNCTION '/SCWM/HUITM_ATTR_CHANGE'
*            EXPORTING
*              is_key   = VALUE /scwm/s_quan_key( guid_parent = <ls_huitm>-guid_parent
*                                                 guid_stock = <ls_huitm>-guid_stock )
*              is_stock = ls_stock.
*        CATCH /scwm/cx_basics.
*          " just return in case of error. Reverse goods receipt will fail and appropriate message will be send to user
*          RETURN.
        CATCH cx_sy_itab_line_not_found.
          DELETE lt_quan.
      ENDTRY.
*      " Now store the changes on the database
*      CREATE OBJECT lo_log.
*      lo_log->init( ).
*      CALL FUNCTION '/SCWM/TO_POST'
*        EXPORTING
*          iv_update_task = 'X'
*          iv_commit_work = ' '
*          iv_mat_check   = 'X'
*        IMPORTING
*          ev_severity    = lv_severity.
*          IF lv_severity CA wmegc_severity_ea.
*            DATA(ls_message) = lo_log->get_last_message( ).
*            MESSAGE s121(/scwm/lc1) WITH ls_message INTO mv_msg.
*            mo_log->add_message( ).
*            CALL FUNCTION 'DB_ROLLBACK'.
**        ROLLBACK WORK.                                 "#EC CI_ROLLBACK
*            CONTINUE.
*          ELSE.
*            COMMIT WORK AND WAIT.
*            MESSAGE s030(/scwm/pi_appl) INTO mv_msg.
*            mo_log->add_message( ).
*          ENDIF.

    ENDLOOP.
    IF lv_update = abap_true.
      UPDATE /scwm/quan FROM TABLE lt_quan.
      IF sy-subrc <> 0.
        CALL FUNCTION 'DB_ROLLBACK'.
      ELSE.
        COMMIT WORK AND WAIT.
        MESSAGE s030(/scwm/pi_appl) INTO mv_msg.
        mo_log->add_message( ).
      ENDIF.
    ENDIF.
  ENDMETHOD.


  METHOD update_hu_type.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Change HU Type in HUHDR
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_huhdr_upd TYPE /scwm/s_huhdr_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF ms_new_pack-hutype IS INITIAL OR
       ms_new_pack-hutype = mt_huhdr[ 1 ]-letyp.
      RETURN.
    ENDIF.

    " update HU header with new HU type
    DATA(ls_huhdr) = VALUE #( mt_huhdr[ guid_hu = ms_new_pack-hu-guid_hu ] ).

    ls_huhdr-letyp = ms_new_pack-hutype.

    update_hu_attributes( is_huhdr = ls_huhdr ).

    ASSIGN mt_huhdr[ guid_hu = ls_huhdr_upd-guid_hu ] TO FIELD-SYMBOL(<ls_huhdr>).
    IF sy-subrc = 0.
      <ls_huhdr> = ls_huhdr_upd.
    ENDIF.
  ENDMETHOD.


  METHOD validate_delivery_keys.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Validate delivery keys
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( mt_docid ) = 0.
      RETURN.
    ENDIF.

    DATA(lo_dlv) = /scwm/cl_dlv_management_prd=>get_instance( ).
    TRY.
        CALL METHOD lo_dlv->query
          EXPORTING
            it_docid        = VALUE #( FOR <ls_docid_temp> IN mt_docid
                                  ( doccat = <ls_docid_temp>-doccat
                                    docid  = <ls_docid_temp>-docid
                                    itemid = <ls_docid_temp>-itmid ) )
            is_read_options = VALUE /scwm/dlv_query_contr_str( keys_only = abap_true )
            is_include_data = VALUE /scwm/dlv_query_incl_str_prd( )
          IMPORTING
            et_items        = DATA(lt_items).
      CATCH /scdl/cx_delivery.
        raise_exception( ).
    ENDTRY.

    REFRESH: mt_docid.
    mt_docid = VALUE #( FOR <ls_id> IN lt_items
      ( doccat = <ls_id>-doccat
        docid  = <ls_id>-docid
        itmid  = <ls_id>-itemid ) ).
  ENDMETHOD.


  METHOD validate_hutype.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Check entered HU Type exist in the system
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_hutyp TYPE /scwm/s_hutyp.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF ms_new_pack-hutype IS INITIAL.
      MESSAGE e003(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    CALL FUNCTION '/SCWM/THUTYP_READ_SINGLE'
      EXPORTING
        iv_hutyp = ms_new_pack-hutype
      IMPORTING
        es_hutyp = ls_hutyp.
    IF ls_hutyp IS INITIAL.
      " MESSAGE
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD validate_set_warehouse_number.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Check warehouse number is valid in the system
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-16¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    CONSTANTS lc_tab_name TYPE string VALUE '/SCWM/T300'.
    DATA: ls_t300 TYPE /scwm/s_t300.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/T300_READ_SINGLE'
      EXPORTING
        iv_lgnum  = iv_lgnum
      IMPORTING
        es_t300   = ls_t300
      EXCEPTIONS
        not_found = 1
        OTHERS    = 2.
    IF sy-subrc <> 0 OR ls_t300-lgnum IS INITIAL.
      MESSAGE e009(33) WITH iv_lgnum lc_tab_name INTO mv_msg.
      raise_exception( ).
    ENDIF.

    mv_lgnum = ls_t300-lgnum.
  ENDMETHOD.


  METHOD y20_if_odata_entity~create_deep_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Processing Logic..
    " ------------------------------------------ "
    CASE mv_key_navigation.
      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_method_key-newpack_docitem.
        ro_deep_entity = create_deep_entity_new_packing( io_data_provider = io_data_provider ).

      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_method_key-newpack_oldhu.
        ro_deep_entity = create_deep_entity_old_packing( io_data_provider = io_data_provider ).

      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_method_key-ucomm_undo.
        ro_deep_entity = create_deep_entity_ucomm_undo( io_data_provider = io_data_provider ).

    ENDCASE.
  ENDMETHOD.


  METHOD y20_if_odata_entity~get_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CASE mv_key_navigation.
      WHEN y20_cl_dinb_receiving_mpc=>gc_scanquantityent.
        ro_entity = get_entity_new_pack_quantity( it_key_tab =  it_key_tab ).

    ENDCASE.
  ENDMETHOD.


  METHOD y20_if_odata_entity~get_expanded_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Execute GET request for expanded entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: et_expanded_tech_clauses, er_entity.

    CASE mv_key_navigation.
      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_docitem.
        er_entity = get_expanded_scan_document( ).
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_dochead TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_docitem TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_popdochead TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_newpack TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_product TO et_expanded_tech_clauses.

      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_techclause-popdochead_docitem.
        er_entity = get_expanded_popup_document( ).
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-popdochead_dochead TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-popdochead_docitem TO et_expanded_tech_clauses.

      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_techclause-docitem_createdhu.
        er_entity = get_expanded_document_item( ).
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-docitem_createdhu TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-docitem_newpack TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-docitem_product TO et_expanded_tech_clauses.

      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_method_key-ucomm_trolley.
        er_entity = get_expand_available_trolleys( ).
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-usercomm_trolley TO et_expanded_tech_clauses.

      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_newpack.
        er_entity = get_expanded_new_packhu_scan( ).
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_newpack TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_product TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_dochead TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_docitem TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-scandoc_popdochead TO et_expanded_tech_clauses.

      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_method_key-popupinfo.
        er_entity = get_expanded_pop_product_info( ).
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-usercomm_popupinfo TO et_expanded_tech_clauses.
        APPEND y20_cl_dinb_receiving_mpc_ext=>ms_techclause-usercomm_itemtext TO et_expanded_tech_clauses.

    ENDCASE.

  ENDMETHOD.


  METHOD y20_if_odata_entity~get_log.
    ro_log = mo_log.
  ENDMETHOD.


  METHOD y20_if_odata_entity~set_execute_method.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Set string with execution method
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mv_key_navigation = iv_key.
  ENDMETHOD.


  METHOD y20_if_odata_entity~update_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CASE mv_key_navigation.
*      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_techclause-newpacking.
*        ro_entity = update_entity_new_packing( io_data_provider = io_data_provider ).
      WHEN y20_cl_dinb_receiving_mpc_ext=>ms_method_key-popup_weight.
        ro_entity = update_entity_weight_popup( io_data_provider = io_data_provider ).

    ENDCASE.
  ENDMETHOD.

  METHOD read_rsrc_who_for_queue.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read all WHO - RSRC link for queue
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lt_selopt_status) = VALUE rseloption(
      ( sign   = wmegc_sign_inclusive
        option = wmegc_option_eq
        low    = wmegc_wo_open )
      ( sign   = wmegc_sign_inclusive
        option = wmegc_option_eq
        low    = wmegc_wo_in_process )
      ( sign   = wmegc_sign_inclusive
        option = wmegc_option_eq
        low    = wmegc_wo_on_hold ) ).

    SELECT a~who, a~rsrc, b~areawho, b~queue
      FROM /scwm/wo_rsrc_ty AS a
      JOIN /scwm/who AS b
        ON a~who = b~who
      INTO TABLE @DATA(lt_who)
     WHERE b~lgnum   = @mv_lgnum
       AND b~status IN @lt_selopt_status
       AND b~queue   = @iv_queue
       AND a~lgnum   = @mv_lgnum.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    DELETE ADJACENT DUPLICATES FROM lt_who COMPARING who rsrc.
    rt_rsrc_who = lt_who.
  ENDMETHOD.


  METHOD read_who_for_queue.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DIBR
* Description : Read all WHO for queue and activity area
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_queue IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lt_selopt_status) = VALUE rseloption(
      ( sign   = wmegc_sign_inclusive
        option = wmegc_option_eq
        low    = wmegc_wo_open )
      ( sign   = wmegc_sign_inclusive
        option = wmegc_option_eq
        low    = wmegc_wo_in_process )
      ( sign   = wmegc_sign_inclusive
        option = wmegc_option_eq
        low    = wmegc_wo_on_hold ) ).

    SELECT who FROM /scwm/who
      INTO TABLE @rt_who
     WHERE lgnum = @mv_lgnum
       AND status IN @lt_selopt_status
       AND queue = @iv_queue.
    IF sy-subrc <> 0.
      LOG-POINT ID y20_odata_deco_inbound_receive FIELDS  iv_queue.
    ENDIF.
  ENDMETHOD.

ENDCLASS.