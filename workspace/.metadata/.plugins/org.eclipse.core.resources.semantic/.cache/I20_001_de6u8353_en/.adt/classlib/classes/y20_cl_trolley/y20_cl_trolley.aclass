CLASS y20_cl_trolley DEFINITION
  PUBLIC
  FINAL
  CREATE PRIVATE .

  PUBLIC SECTION.

    TYPES:
      "!<p class="shorttext synchronized">Type structure of trolley and position</p>
      BEGIN OF tys_trolley_position,
        trolley  TYPE /scwm/de_rsrc,
        position TYPE /scwm/huhdr-logpos,
      END OF tys_trolley_position .
    TYPES:
      "!<p class="shorttext synchronized">Type structure of trolley</p>
      BEGIN OF tys_trolley,
        tolley TYPE /scwm/de_rsrc,
      END OF tys_trolley .
    TYPES:
      tyt_trolley TYPE STANDARD TABLE OF tys_trolley WITH EMPTY KEY .
    "!<p class="shorttext synchronized">Get Instance</p>
    CLASS-METHODS get_instance
      RETURNING
        VALUE(ro_instance) TYPE REF TO y20_cl_trolley .
    "!<p class="shorttext synchronized">Convert position number to string</p>
    METHODS convert_logpos_to_string
      IMPORTING
        !iv_position       TYPE /scwm/huhdr-logpos
      RETURNING
        VALUE(rv_position) TYPE y20_de_trolley_position .
    "!<p class="shorttext synchronized">Convert position string to system type</p>
    METHODS convert_pos_string_to_logpos
      IMPORTING
        !iv_posstring    TYPE y20_de_trolley_position
      RETURNING
        VALUE(rv_logpos) TYPE /scwm/huhdr-logpos .
    "!<p class="shorttext synchronized">Retrieve content of trolley(resource)</p>
    METHODS read_trolley_content
      IMPORTING
        !iv_lgnum     TYPE /scwm/lgnum
        !iv_trolley   TYPE /scwm/de_rsrc
      EXPORTING
        !et_huitems   TYPE /scwm/tt_stock_select
        !et_huheaders TYPE /scwm/tt_huhdr .
    "!<p class="shorttext synchronized">Retrieve HUs on trolley(resource)</p>
    METHODS read_trolley_hus
      IMPORTING
        !iv_lgnum     TYPE /scwm/lgnum
        !iv_trolley   TYPE /scwm/de_rsrc
      EXPORTING
        !et_huheaders TYPE  /scwm/tt_huhdr_int.
    "!<p class="shorttext synchronized">Retrieve content of trolley(resource)</p>
    METHODS read_trolley_content_multi
      IMPORTING
        !iv_lgnum     TYPE /scwm/lgnum
        it_guid_loc   TYPE /scwm/tt_guid_loc
      EXPORTING
        !et_huitems   TYPE /scwm/tt_stock_select
        !et_huheaders TYPE /scwm/tt_huhdr .
    "!<p class="shorttext synchronized">Retrieve trolley(resource) header data</p>
    METHODS read_trolley_multi
      IMPORTING
        !iv_lgnum         TYPE /scwm/lgnum
        !it_trolley       TYPE tyt_trolley
      RETURNING
        VALUE(rt_trolley) TYPE /scwm/tt_rsrc .
    "!<p class="shorttext synchronized">Retrieve trolley(resource) header data</p>
    METHODS read_trolley_single
      IMPORTING
        !iv_lgnum         TYPE /scwm/lgnum
        !iv_trolley       TYPE /scwm/de_rsrc
      RETURNING
        VALUE(rs_trolley) TYPE /scwm/rsrc .
    "!<p class="shorttext synchronized">Retrieve content of trolley(resource)</p>
    METHODS read_trolley_who
      IMPORTING
        !iv_lgnum          TYPE /scwm/lgnum
        !it_trolley        TYPE tyt_trolley OPTIONAL
        it_who             TYPE /scwm/tt_who OPTIONAL
      RETURNING
        VALUE(rt_who_rsrc) TYPE /scwm/tt_wo_rsrc_ty.
    "!<p class="shorttext synchronized">Split scanned number to trolley and position</p>
    METHODS split_scannum_to_trol_and_pos
      IMPORTING
        !iv_scan_trolley_number TYPE y20_de_trolley_expno
      RETURNING
        VALUE(rs_result)        TYPE tys_trolley_position .
  PROTECTED SECTION.
  PRIVATE SECTION.
    CLASS-DATA: mo_trolley TYPE REF TO y20_cl_trolley.
    DATA: mt_trolley TYPE /scwm/tt_rsrc.

ENDCLASS.



CLASS y20_cl_trolley IMPLEMENTATION.


  METHOD convert_logpos_to_string.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Convert position level of HU to string for UI
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_position IS INITIAL.
      RETURN.
    ENDIF.
    rv_position = |{ iv_position(1) }-{ iv_position+1(1) }|.
  ENDMETHOD.


  METHOD convert_pos_string_to_logpos.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Convert position string of HU to logpos field in HU HDR
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_posstring IS INITIAL.
      RETURN.
    ENDIF.

    SPLIT iv_posstring AT '-' INTO TABLE DATA(lt_splitted).
    TRY.
        rv_logpos = |{ lt_splitted[ 1 ] }{ lt_splitted[ 2 ] }|.
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
  ENDMETHOD.


  METHOD get_instance.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_CA
* Description : Create Trolley class instance
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF mo_trolley IS NOT BOUND.
      mo_trolley = NEW y20_cl_trolley( ).
    ENDIF.

    IF mo_trolley IS BOUND.
      ro_instance = mo_trolley.
    ENDIF.
  ENDMETHOD.


  METHOD read_trolley_content.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_CA
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: et_huheaders, et_huitems.

    DATA(ls_resource) = read_trolley_single( iv_lgnum   = iv_lgnum
                                             iv_trolley = iv_trolley ).

    DATA(lt_guid_loc) = VALUE /scwm/tt_guid_loc( ( guid_loc = ls_resource-guid_loc ) ).
    CALL FUNCTION '/SCWM/SELECT_STOCK'
      EXPORTING
        iv_lgnum = iv_lgnum
        it_rsrc  = lt_guid_loc
        iv_vsi   = wmegc_physical_stock
      IMPORTING
        et_huitm = et_huitems
        et_huhdr = et_huheaders
      EXCEPTIONS
        error    = 1
        OTHERS   = 2.
    IF sy-subrc <> 0.
      ##NEEDED
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_msg).
    ENDIF.
  ENDMETHOD.


  METHOD read_trolley_multi.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_CA
* Description : Read list of trolleys/resources
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_trolley TYPE rseloption.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_lgnum IS INITIAL OR lines( it_trolley ) = 0.
      RETURN.
    ENDIF.

    " first check buffer for already selected trolleys(resources)
    LOOP AT it_trolley ASSIGNING FIELD-SYMBOL(<ls_input>).
      ASSIGN mt_trolley[ lgnum = iv_lgnum rsrc = <ls_input>-tolley ] TO FIELD-SYMBOL(<ls_trolley>).
      IF sy-subrc = 0.
        APPEND <ls_trolley> TO rt_trolley.
      ELSE.
        " new trolley, not in the buffer. Build selection option table for new select
        APPEND VALUE #( sign   = wmegc_sign_inclusive
                        option = wmegc_option_eq
                        low    = <ls_input>-tolley ) TO lt_trolley.
      ENDIF.
    ENDLOOP.

    IF lines( lt_trolley ) = 0.
      RETURN.
    ENDIF.

    SELECT * FROM /scwm/rsrc
      INTO TABLE @DATA(lt_rsrc)
     WHERE lgnum = @iv_lgnum
       AND rsrc  IN @lt_trolley.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.
    " add new selected entries to the buffer
    APPEND LINES OF lt_rsrc TO mt_trolley.
    " add new selected entries to returning parameter
    APPEND LINES OF lt_rsrc TO rt_trolley.
  ENDMETHOD.


  METHOD read_trolley_single.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_CA
* Description : Read single trolley/resource
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_trolley IS INITIAL OR iv_lgnum IS INITIAL.
      ##NEEDED
      MESSAGE e009(y20_ui5) INTO DATA(lv_msg).
      RETURN.
    ENDIF.

    CALL FUNCTION '/SCWM/RSRC_READ_SINGLE'
      EXPORTING
        iv_lgnum    = iv_lgnum
        iv_rsrc     = iv_trolley
      IMPORTING
        es_rsrc     = rs_trolley
      EXCEPTIONS
        wrong_input = 1
        not_found   = 2
        OTHERS      = 3.
    IF sy-subrc <> 0 OR rs_trolley IS INITIAL.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO lv_msg.
      RETURN.
    ENDIF.
  ENDMETHOD.


  METHOD split_scannum_to_trol_and_pos.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_DAHT
* Description : Split scanned number to trolley and position number
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    CONSTANTS lc_position_length TYPE i VALUE 2.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_scan_trolley_number IS INITIAL.
      RETURN.
    ENDIF.

    DATA(lv_number_length) = strlen( iv_scan_trolley_number ).
    lv_number_length = lv_number_length - lc_position_length.

    rs_result-trolley = iv_scan_trolley_number(lv_number_length).
    rs_result-position = iv_scan_trolley_number+lv_number_length(lc_position_length).
  ENDMETHOD.

  METHOD read_trolley_content_multi.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_CA
* Description : Read content for multiple trolleys
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/SELECT_STOCK'
      EXPORTING
        iv_lgnum = iv_lgnum
        it_rsrc  = it_guid_loc
        iv_vsi   = wmegc_physical_stock
      IMPORTING
        et_huitm = et_huitems
        et_huhdr = et_huheaders
      EXCEPTIONS
        error    = 1
        OTHERS   = 2.
    IF sy-subrc <> 0.
      ##NEEDED
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_msg).
    ENDIF.
  ENDMETHOD.

  METHOD read_trolley_hus.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_CA
* Description : Read HUs assign to a trolley
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: et_huheaders.

    DATA(ls_resource) = read_trolley_single( iv_lgnum   = iv_lgnum
                                             iv_trolley = iv_trolley ).

    CALL FUNCTION '/SCWM/HU_SELECT_GEN'
      EXPORTING
        iv_lgnum     = iv_lgnum
        ir_rsrc      = VALUE rseloption( ( sign   = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low    = ls_resource-rsrc ) )
        ir_vhi       = VALUE rseloption( ( sign   = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low    = wmegc_vhi_plan )
                                         ( sign   = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low    = wmegc_vhi_real ) )
      IMPORTING
        et_huhdr     = et_huheaders
      EXCEPTIONS
        wrong_input  = 1
        not_possible = 2
        OTHERS       = 3.
    IF sy-subrc <> 0.
      ##NEEDED
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_msg).
    ENDIF.
  ENDMETHOD.

  METHOD read_trolley_who.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_CA
* Description : Read Trolley - WHO. Query from table /SCWM/WO_
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    CONSTANTS: lc_tabname TYPE string VALUE '/SCWM/WO_RSRC_TY'.
    DATA lv_msg TYPE string.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF iv_lgnum IS INITIAL.
      MESSAGE e001(y20_ca) INTO lv_msg.
      RETURN.
    ENDIF.

    IF lines( it_who ) > 0.
      DATA(lt_selopt_who) = VALUE rseloption( FOR <ls_who> IN it_who
        ( sign   = wmegc_sign_inclusive
          option = wmegc_option_eq
          low    = <ls_who>-who ) ).
    ENDIF.

    IF lines( it_trolley ) > 0.
      DATA(lt_selopt_trolley) = VALUE rseloption( FOR <ls_trol> IN it_trolley
        ( sign   = wmegc_sign_inclusive
          option = wmegc_option_eq
          low    = <ls_trol>-tolley ) ).
    ENDIF.

    IF lines( lt_selopt_who ) = 0 AND lines( lt_selopt_trolley ) = 0.
      MESSAGE e001(y20_ca) INTO lv_msg.
      RETURN.
    ENDIF.

    SELECT * FROM /scwm/wo_rsrc_ty
      INTO TABLE rt_who_rsrc
     WHERE lgnum = iv_lgnum
       AND who IN lt_selopt_who
       AND rsrc IN lt_selopt_trolley.
    IF sy-subrc <> 0.
      MESSAGE e002(y20_ca) WITH lc_tabname INTO lv_msg.
      RETURN.
    ENDIF.

    DELETE ADJACENT DUPLICATES FROM rt_who_rsrc COMPARING who rsrc.
  ENDMETHOD.

ENDCLASS.