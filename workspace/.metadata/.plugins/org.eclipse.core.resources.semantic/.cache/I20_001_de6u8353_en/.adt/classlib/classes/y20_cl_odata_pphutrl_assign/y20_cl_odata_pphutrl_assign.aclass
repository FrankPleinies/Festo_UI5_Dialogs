CLASS y20_cl_odata_pphutrl_assign DEFINITION
  PUBLIC
  FINAL
  CREATE PUBLIC .

  PUBLIC SECTION.
    INTERFACES y20_if_odata_entity .

    TYPES:
      "!<p class="shorttext synchronized">Type Str.: HU-Bin</p>
      BEGIN OF tys_hu_bin,
        huident TYPE /scwm/de_huident,
        bin     TYPE /scwm/de_lgpla,
      END OF tys_hu_bin.
    "!<p class="shorttext synchronized">Type Table.: HU-Bin</p>
    TYPES tyt_hu_bin TYPE STANDARD TABLE OF tys_hu_bin WITH EMPTY KEY.
    "!<p class="shorttext synchronized">Constructor: Create instance of current class</p>
    METHODS constructor
      IMPORTING
        is_entity_key TYPE y20_cl_pick_prep_hutrl_mpc_ext=>tys_entity_keys OPTIONAL
        io_log        TYPE REF TO y20_cl_log
      RAISING
        /iwbep/cx_mgw_tech_exception.

  PROTECTED SECTION.
  PRIVATE SECTION.
    ##NEEDED
    "!<p class="shorttext synchronized">Message string</p>
    DATA mv_msg TYPE string .
    "!<p class="shorttext synchronized">Warehouse Number</p>
    DATA mv_lgnum TYPE /scwm/s_t300-lgnum .
    "!<p class="shorttext synchronized">Navigation key, indicates execution method</p>
    DATA mv_key_navigation TYPE string .
    "!<p class="shorttext synchronized">Trolley content attribute</p>
    DATA mt_trolleycontent TYPE y20_cl_pick_prep_hutrl_mpc=>tt_trolleycontentent .
    "!<p class="shorttext synchronized">Global material attribute</p>
    DATA mt_material_global TYPE /scwm/tt_material_global .
*  data MT_HUITEM type /SCWM/TT_HUITM_INT .
    "!<p class="shorttext synchronized">Trolley number with position</p>
    DATA ms_trolley_position TYPE y20_cl_trolley=>tys_trolley_position .
    "!<p class="shorttext synchronized">Trolley entity</p>
    DATA ms_trolley TYPE y20_cl_pick_prep_hutrl_mpc=>ts_trolleyent .
    "!<p class="shorttext synchronized">Resource data</p>
    DATA ms_resource TYPE /scwm/rsrc .
    "!<p class="shorttext synchronized">HU Header</p>
    DATA ms_huhdr TYPE /scwm/s_huhdr_int .
    "!<p class="shorttext synchronized">Entity keys</p>
    DATA ms_entity_key TYPE y20_cl_pick_prep_hutrl_mpc_ext=>tys_entity_keys .
    "!<p class="shorttext synchronized">Instance of Trolley</p>
    DATA mo_trolley TYPE REF TO y20_cl_trolley .
    "!<p class="shorttext synchronized">Message log</p>
    DATA mo_log TYPE REF TO y20_cl_log .
    "!<p class="shorttext synchronized">Instance of Handling Unit</p>
    DATA mo_hu TYPE REF TO y20_cl_odata_handling_unit .

    "!<p class="shorttext synchronized">Change WHO attributes</p>
    METHODS change_who_queue
      IMPORTING
        !iv_lgnum TYPE /scwm/lgnum
        !iv_queue TYPE /scwm/de_queue
        !it_who   TYPE /scwm/tt_who_int .
    "!<p class="shorttext synchronized">Check that trolley is open to be used</p>
    METHODS check_trolley_is_open
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Check scanned position on trolley is free</p>
    METHODS check_trolley_posit_is_empty
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Close trolley single</p>
    METHODS close_trolley_single
      IMPORTING
        !iv_trolley TYPE /scwm/de_rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Convert structure to reference data</p>
    METHODS copy_data_to_ref
      IMPORTING
        !is_data       TYPE any
      RETURNING
        VALUE(ro_data) TYPE REF TO data .
    "!<p class="shorttext synchronized">POST request: Close several trolleys</p>
    METHODS create_deep_close_trolley_mult
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_result)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">POST request: Take off HU from Trolley</p>
    METHODS create_deep_content_undo
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_result)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">POST request: Assign HU to Trolley</p>
    METHODS create_deep_scan_trolley
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_result)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">POST request: Close single trolley</p>
    METHODS create_ent_close_trolley_singl
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(ro_result)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Determine destination bin</p>
    METHODS get_bin_for_unto_task
      RETURNING
        VALUE(rt_hu_bin) TYPE tyt_hu_bin
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">GET request: Determine possible trolleys for closing</p>
    METHODS get_entityset_close_alltrolley
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Get Item Text</p>
    METHODS get_entity_dlvitem_text
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">GET Request: Handling unit/</p>
    METHODS get_expanded_hu
      RETURNING
        VALUE(ro_result) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">''</p>
    METHODS get_expanded_trolley
      RETURNING
        VALUE(ro_result) TYPE REF TO data.
    "!<p class="shorttext synchronized">Create task: move HU to trolley</p>
    METHODS move_hu_to_trolley
      IMPORTING
        !is_scanned_hu TYPE y20_cl_pick_prep_hutrl_mpc=>ts_handlingunitent
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Raise exception</p>
    METHODS raise_exception
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Raise exception from BAPIRET</p>
    METHODS raise_exception_from_bapiret
      IMPORTING
        !it_bapiret TYPE bapirettab
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read Queue name from parameter framework</p>
    METHODS read_queue_from_parameter
      IMPORTING
        !iv_context     TYPE zswl_de_param_id1
        !iv_parameter   TYPE zswl_de_param_id2
      RETURNING
        VALUE(rs_queue) TYPE /scwm/t346
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Read/set trolley content data MT_TROLLEYCONTENT</p>
    METHODS read_set_trolley_content
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set activity area for trolley</p>
    METHODS set_activity_area_and_queue
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Set trolley content to global variable</p>
    METHODS set_trolley_content
      IMPORTING
*        !it_huitems   TYPE /scwm/tt_stock_select
        !it_huheaders TYPE /scwm/tt_huhdr_int .
    "!<p class="shorttext synchronized">Update WHO attributes</p>
    METHODS update_warehouse_order
      IMPORTING
        !iv_lgnum        TYPE /scwm/lgnum
        !iv_who          TYPE /scwm/s_who_int-who
        !iv_update_queue TYPE boole_d DEFAULT abap_false
        !is_attributes   TYPE /scwm/s_who_att
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Compare HU vs Trolley activity area</p>
    METHODS validate_hu_vs_trolley_aarea
      IMPORTING
        !is_hu TYPE y20_cl_pick_prep_hutrl_mpc=>ts_handlingunitent
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Validate and set scanned trolley</p>
    METHODS validate_set_scanned_trolley
      IMPORTING
        !iv_scantrolley TYPE y20_de_trolley_expno
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Check warehouse number is valid</p>
    METHODS validate_set_warehouse_number
      IMPORTING
        !iv_lgnum TYPE /scwm/lgnum
      RAISING
        /iwbep/cx_mgw_tech_exception .
    "!<p class="shorttext synchronized">Additional checks on trolley</p>
    METHODS validate_trolley_extend_checks
      IMPORTING
        is_scanned_hu TYPE y20_cl_pick_prep_hutrl_mpc=>ts_handlingunitent
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS read_whohu_by_huident
      IMPORTING
        it_huident      TYPE /scwm/tt_huident
      RETURNING
        VALUE(rt_whohu) TYPE /scwm/tt_whohu.

ENDCLASS.



CLASS y20_cl_odata_pphutrl_assign IMPLEMENTATION.


  METHOD change_who_queue.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Change WHO attributes
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA ls_attributes  TYPE /scwm/s_who_att.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    LOOP AT it_who ASSIGNING FIELD-SYMBOL(<ls_who>).
      CHECK <ls_who>-queue <> iv_queue.
      ##ENH_OK
      MOVE-CORRESPONDING <ls_who> TO ls_attributes.
      ls_attributes-queue = iv_queue.

      TRY.
          update_warehouse_order( iv_lgnum      = iv_lgnum
                                  iv_who        = <ls_who>-who
                                  iv_update_queue = abap_true
                                  is_attributes = ls_attributes ).
          ##NO_HANDLER
        CATCH /iwbep/cx_mgw_tech_exception.
      ENDTRY.
    ENDLOOP.

    CALL FUNCTION 'DEQUEUE_ALL'.
  ENDMETHOD.


  METHOD check_trolley_is_open.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Check trolley is open. Check is based on the WO queue.
*               There is a certain queue which contained only closed trolleys
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF ms_trolley-queue IS INITIAL.
      RETURN.
    ENDIF.

    DATA(ls_queue) = read_queue_from_parameter( iv_context = zswl_if_param_c=>mc_pick_prepare
                                                iv_parameter = zswl_if_param_c=>mc_pp_hucre_queue_def ).

    IF ls_queue-queue <> ms_trolley-queue.
      ##NEEDED
      MESSAGE e007(y20_ui5) WITH ms_trolley-trolley INTO mv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD check_trolley_posit_is_empty.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Check scanned position on trolley is empty
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    TRY.
        DATA(ls_trolley_pos) = REF #( mt_trolleycontent[ trolley = ms_trolley_position-trolley
                                                         position = ms_trolley_position-position ] ).
        IF ls_trolley_pos->hu IS NOT INITIAL.
          ##NEEDED
          MESSAGE e002(y20_ui5) WITH ms_trolley_position-position ls_trolley_pos->hu-huident INTO mv_msg.
          raise_exception( ).
        ENDIF.
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
    ENDTRY.
  ENDMETHOD.


  METHOD close_trolley_single.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Close single trolley. Queues of warehouse orders linked
*                 to the HUs on trolley are changed to new queue.
*                 If trolley is in this queue that means that trolley
*                 is closed.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_rsrc_who TYPE /scwm/tt_who_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " get all who assigned to this trolley
    CALL FUNCTION '/SCWM/RSRC_WHO_RESOURCE_GET'
      EXPORTING
        iv_lgnum    = mv_lgnum
        iv_rsrc     = iv_trolley
      IMPORTING
        et_rsrc_who = lt_rsrc_who.
    IF lines( lt_rsrc_who ) = 0.
      MESSAGE e036(y20_ui5) WITH iv_trolley INTO mv_msg.
      raise_exception( ).
    ENDIF.

    DATA(ls_queue) = read_queue_from_parameter( iv_context   = zswl_if_param_c=>mc_pick_prepare
                                                iv_parameter = zswl_if_param_c=>mc_pp_close_queue ).

    change_who_queue( iv_lgnum = mv_lgnum
                      iv_queue = ls_queue-queue
                      it_who   = lt_rsrc_who ).

  ENDMETHOD.


  METHOD constructor.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Create instance
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_huident TYPE /scwm/de_huident.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_log = io_log.

    mo_hu = y20_cl_odata_handling_unit=>get_instance( ).

    mo_trolley = y20_cl_trolley=>get_instance( ).

    IF is_entity_key IS SUPPLIED.
      validate_set_warehouse_number( is_entity_key-lgnum ).

      IF is_entity_key-scanhu IS NOT INITIAL OR
         is_entity_key-huident IS NOT INITIAL.

        IF is_entity_key-scanhu IS NOT INITIAL.
          lv_huident = is_entity_key-scanhu.
        ELSEIF is_entity_key-huident IS NOT INITIAL.
          lv_huident = is_entity_key-huident.
        ENDIF.

        mo_hu->read_hu_single_by_huident(
          EXPORTING
            iv_huident = lv_huident
          IMPORTING
            es_huhdr   = ms_huhdr ).
      ENDIF.

      IF is_entity_key-trolley IS NOT INITIAL.
        ms_resource = mo_trolley->read_trolley_single( iv_lgnum   = mv_lgnum
                                                       iv_trolley = is_entity_key-trolley ).
        IF ms_resource IS INITIAL.
          raise_exception( ).
        ENDIF.
      ENDIF.
      ms_entity_key = is_entity_key.
    ENDIF.
  ENDMETHOD.


  METHOD copy_data_to_ref.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    FIELD-SYMBOLS: <ls_data> TYPE any.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CREATE DATA ro_data LIKE is_data.
    ASSIGN ro_data->* TO <ls_data>.
    <ls_data> = is_data.
  ENDMETHOD.


  METHOD create_deep_close_trolley_mult.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : POST request. Close multiple trolleys
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: BEGIN OF ls_entity.
            INCLUDE TYPE y20_cl_pick_prep_hutrl_mpc=>ts_trolleycloselistent.
    DATA:   trolleycloselist TYPE y20_cl_pick_prep_hutrl_mpc=>tt_trolleycloselistent,
          END OF ls_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data( IMPORTING es_data = ls_entity ).

    validate_set_warehouse_number( ls_entity-lgnum ).

    LOOP AT ls_entity-trolleycloselist ASSIGNING FIELD-SYMBOL(<ls_trl>).
      close_trolley_single( <ls_trl>-trolley ).
    ENDLOOP.

    ro_result = copy_data_to_ref( ls_entity ).
  ENDMETHOD.


  METHOD create_deep_content_undo.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Return HU back to the source bin
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    DATA: BEGIN OF ls_entity.
            INCLUDE TYPE y20_cl_pick_prep_hutrl_mpc=>ts_trolleycontentent.
    DATA:   trolleycontent TYPE y20_cl_pick_prep_hutrl_mpc=>tt_trolleycontentent,
          END OF ls_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data( IMPORTING es_data = ls_entity ).
    IF lines( ls_entity-trolleycontent ) = 0.
      MESSAGE e062(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    mt_trolleycontent = ls_entity-trolleycontent.
    ms_trolley-trolley = mt_trolleycontent[ 1 ]-trolley.
    ms_trolley_position-trolley = mt_trolleycontent[ 1 ]-trolley.
    " validate and set attribute MV_LGNUM
    ##OPERATOR[MT_TROLLEYCONTENT]
    validate_set_warehouse_number( iv_lgnum = mt_trolleycontent[ 1 ]-lgnum ).

    " set queue name of trolley
    set_activity_area_and_queue( ).

    " validate that trolley is open
    check_trolley_is_open( ).

    " get destination bin from confirmed task for HU
    DATA(lt_hu_bin) = get_bin_for_unto_task( ).

    DATA(lv_procty) = zswl_cl_param=>read_const(
        iv_lgnum     = mv_lgnum
        iv_context   = zswl_if_param_c=>mc_pick_prepare
        iv_parameter = zswl_if_param_c=>mc_pp_procty_z999 ).

    LOOP AT mt_trolleycontent ASSIGNING FIELD-SYMBOL(<ls_entry>).
      ASSIGN lt_hu_bin[ huident = <ls_entry>-hu-huident ] TO FIELD-SYMBOL(<ls_bin>).
      CHECK sy-subrc = 0.

      mo_hu->create_taskhu_to_bin(
        EXPORTING
          iv_lgnum    = mv_lgnum
          iv_guid_hu  = <ls_entry>-hu-guid_hu
          iv_bin      = <ls_bin>-bin
          iv_proctype = CONV #( lv_procty )
        IMPORTING
          et_bapiret  = DATA(lt_bapiret)
          ev_severity = DATA(lv_severity) ).
      IF lv_severity CA wmegc_severity_ea.
        raise_exception_from_bapiret( lt_bapiret ).
      ENDIF.
    ENDLOOP."

    " read HUs on trolley and return content to UI
    read_set_trolley_content( ).

    ls_entity-trolleycontent = mt_trolleycontent.
    ro_result = copy_data_to_ref( ls_entity ).
  ENDMETHOD.


  METHOD create_deep_scan_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : POST Request: Scan Trolley
*               1. Check trolley is compatible with last scan HU
*               2. Move HU to trolley
*               3. Refresh Trolley content list
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: BEGIN OF ls_entity.
            INCLUDE TYPE y20_cl_pick_prep_hutrl_mpc=>ts_scantrolleyent.
    DATA:   scantrl_trolley    TYPE y20_cl_pick_prep_hutrl_mpc=>ts_trolleyent,
            scantrl_trlcontent TYPE y20_cl_pick_prep_hutrl_mpc=>tt_trolleycontentent,
            scantrl_hu         TYPE y20_cl_pick_prep_hutrl_mpc=>ts_handlingunitent,
          END OF ls_entity.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data( IMPORTING es_data = ls_entity ).
*    TRY.
    validate_set_warehouse_number( iv_lgnum = ls_entity-lgnum ).

    validate_set_scanned_trolley( ls_entity-scantrolley ).

    read_set_trolley_content( ).

    set_activity_area_and_queue( ).

    ms_trolley-trolley = ms_trolley_position-trolley.
    IF ls_entity-scantrl_hu IS NOT INITIAL.

      mo_hu->read_hu_single_by_huident(
        EXPORTING iv_huident = ls_entity-scantrl_hu-huident
        IMPORTING es_huhdr   = ms_huhdr ).

      validate_trolley_extend_checks( is_scanned_hu = ls_entity-scantrl_hu ).

      move_hu_to_trolley( ls_entity-scantrl_hu ).

      read_set_trolley_content( ).
    ENDIF.

    CLEAR: ls_entity-scantrolley, ls_entity-scantrl_hu, ls_entity-scantrolley.

    ls_entity-lgnum = mv_lgnum.
    ms_trolley-lgnum = mv_lgnum.

    ls_entity-scantrl_trolley = ms_trolley.
    ls_entity-scantrl_trlcontent = mt_trolleycontent.

    ro_result = copy_data_to_ref( ls_entity ).
  ENDMETHOD.


  METHOD create_ent_close_trolley_singl.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : POST request on single entity. Trolley entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    io_data_provider->read_entry_data( IMPORTING es_data = ms_trolley ).

    validate_set_warehouse_number( ms_trolley-lgnum ).

    IF ms_trolley-trolley IS NOT INITIAL.
      close_trolley_single( ms_trolley-trolley ).
    ENDIF.

    ro_result = copy_data_to_ref( ms_trolley ).
  ENDMETHOD.


  METHOD get_bin_for_unto_task.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Determine destination bin for tasks from resource to bin
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_tasks TYPE /scwm/tt_to_det_mon.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
*    TRY.
*        DATA(ls_queue) = read_queue_from_parameter( iv_context   = zswl_if_param_c=>mc_pick_prepare
*                                                    iv_parameter = zswl_if_param_c=>mc_pp_hucre_queue_def ).
*        ##NO_HANDLER
*      CATCH  /iwbep/cx_mgw_tech_exception.
*    ENDTRY.

    DATA(ls_selcrit) = VALUE /scwm/s_to_selcrit_mon(
      r_nlenr = VALUE #( FOR <ls_cont> IN mt_trolleycontent ( sign   = wmegc_sign_inclusive
                                                              option = wmegc_option_eq
                                                              low    = <ls_cont>-hu-huident ) )
      r_trart = VALUE #( ( sign   = wmegc_sign_inclusive
                           option = wmegc_option_eq
                           low    = wmegc_trart_int ) ) ).
*      r_queue = VALUE #( ( sign   = wmegc_sign_inclusive
*                           option = wmegc_option_eq
*                           low    = ls_queue-queue ) ) ).

    " read confirmed tasks to find the source bin
    CALL FUNCTION '/SCWM/TO_GET_WIP'
      EXPORTING
        iv_lgnum   = mv_lgnum
        iv_conf    = abap_true
        is_selcrit = ls_selcrit
      IMPORTING
        et_to      = lt_tasks.

    rt_hu_bin = VALUE #( FOR <ls_task> IN lt_tasks ( huident = <ls_task>-nlenr
                                                     bin = <ls_task>-vlpla ) ).
  ENDMETHOD.


  METHOD get_entityset_close_alltrolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Get list of trolleys for closing
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    DATA lt_trolley_list TYPE y20_cl_pick_prep_hutrl_mpc=>tt_trolleycloselistent.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(ls_prep_queue) = read_queue_from_parameter(
        iv_context   = zswl_if_param_c=>mc_pick_prepare
        iv_parameter = zswl_if_param_c=>mc_pp_hucre_queue_def ).
    DATA(lt_queue) = VALUE rseloption( ( sign   = wmegc_sign_inclusive
                                         option = wmegc_option_eq
                                         low    = ls_prep_queue-queue ) ).
    " Select the data according to the queue maintained in the customizing
    SELECT * FROM /scwm/rsrc
      INTO TABLE @DATA(lt_rsrc)
      WHERE lgnum     =  @mv_lgnum
        AND act_queue IN @lt_queue.
    IF lines( lt_rsrc ) = 0.
      RETURN.
    ENDIF.

    mo_trolley->read_trolley_content_multi(
      EXPORTING
        iv_lgnum     = mv_lgnum
        it_guid_loc  = VALUE #( FOR <ls_rsrc> IN lt_rsrc ( guid_loc = <ls_rsrc>-guid_loc ) )
      IMPORTING
        et_huitems = DATA(lt_huitm) ).
    LOOP AT lt_huitm INTO DATA(ls_hu) GROUP BY ( guid_loc = ls_hu-guid_loc
                                                 size = GROUP SIZE )
                     WITHOUT MEMBERS
                     ASSIGNING FIELD-SYMBOL(<ls_huitm>).
      ASSIGN lt_rsrc[ guid_loc = <ls_huitm>-guid_loc ] TO FIELD-SYMBOL(<ls_trolley>).
      CHECK sy-subrc = 0.

      lt_trolley_list = VALUE #( BASE lt_trolley_list ( lgnum = mv_lgnum
                                                        trolley = <ls_trolley>-rsrc
                                                        hucount = <ls_huitm>-size ) ).
    ENDLOOP.

    ro_result = copy_data_to_ref( lt_trolley_list ).
  ENDMETHOD.


  METHOD get_entity_dlvitem_text.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : GET request: determine delivery item text
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA ls_entity TYPE y20_cl_pick_prep_hutrl_mpc=>ts_itemtextent.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_param) = zswl_cl_param=>read_const(
        iv_lgnum     = mv_lgnum
        iv_context   = zswl_if_param_c=>mc_ui5
        iv_parameter = zswl_if_param_c=>mc_text_dlvitem_plie ).

    IF lv_param IS INITIAL.
      lv_param = '0002'.
    ENDIF.

    ls_entity-lgnum = mv_lgnum.
    ls_entity-huident = ms_huhdr-huident.

    ls_entity-text = mo_hu->read_dlv_item_text_by_huident( iv_lgnum   = mv_lgnum
                                                           iv_huident = ms_huhdr-huident
                                                           iv_id      = CONV #( lv_param ) ).
    ro_result = copy_data_to_ref( ls_entity ).
  ENDMETHOD.


  METHOD get_expanded_hu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Get HU entity after HU is scanned
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      BEGIN OF ls_entity.
        INCLUDE TYPE y20_cl_pick_prep_hutrl_mpc=>ts_scanhandlingunitent.
    DATA: scanhu_hu TYPE y20_cl_pick_prep_hutrl_mpc=>ts_handlingunitent,
      END OF ls_entity,
      ls_who TYPE /scwm/s_who_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
*    DATA(lt_ordim_o) = mo_hu->read_open_taskhu_by_huident(
*        iv_lgnum       = mv_lgnum
*        it_huident     = VALUE #( ( huident = ms_entity_key-scanhu ) )
*        iv_read_buffer = abap_true ).
*    TRY.
*        DATA(ls_ordim_o) = REF #( lt_ordim_o[ 1 ] ).
*        ##NO_HANDLER
*      CATCH cx_sy_itab_line_not_found.
*    ENDTRY.

    DATA(lt_whohu) = read_whohu_by_huident( VALUE #( ( huident = ms_entity_key-scanhu ) ) ).
    IF lines( lt_whohu ) = 0.
      MESSAGE e065(y20_ui5) WITH ms_entity_key-scanhu INTO mv_msg.
      raise_exception( ).
    ENDIF.
    TRY.
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_lgnum = mv_lgnum
            iv_who   = lt_whohu[ 1 ]-who
          IMPORTING
            es_who   = ls_who.
        ##NO_HANDLER
      CATCH /scwm/cx_core.
    ENDTRY.

    IF ls_who IS INITIAL.
      MESSAGE e060(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    IF ls_who-queue <> read_queue_from_parameter(
                          iv_context   = zswl_if_param_c=>mc_pick_prepare
                          iv_parameter = zswl_if_param_c=>mc_pp_hucre_queue_def )-queue.
      MESSAGE e077(y20_ui5) WITH ms_huhdr-huident INTO mv_msg.
      raise_exception( ).
    ENDIF.

    ls_entity-lgnum = mv_lgnum.
    ls_entity-scanhu_hu-aarea = ls_who-areawho.
    ls_entity-scanhu_hu-queue = ls_who-queue.
    ls_entity-scanhu_hu-guid_hu = ms_huhdr-guid_hu.
    ls_entity-scanhu_hu-huident = ms_huhdr-huident.
    ls_entity-scanhu_hu-hutype = ms_huhdr-letyp.
    ls_entity-scanhu_hu-lgnum = mv_lgnum.
    ls_entity-scanhu_hu-warehouseorder = ls_who-who.
*    ls_entity-scanhu_hu-warehousetask = ls_ordim_o->tanum.

    ro_result = copy_data_to_ref( ls_entity ).
  ENDMETHOD.


  METHOD get_expanded_trolley.
*    DATA:
*      BEGIN OF ls_entity.
*        INCLUDE TYPE y20_cl_pick_prep_hutrl_mpc=>ts_trolleycontentent.
*    DATA: trolleycontent TYPE y20_cl_pick_prep_hutrl_mpc=>tt_trolleycontentent,
*      END OF ls_entity.
*
*    mv_lgnum = 'TMPL'.
*    ms_trolley_position-trolley = 'TM001'.
*    read_set_trolley_content( ).
*    ls_entity-trolleycontent = mt_trolleycontent.
*    ro_result = copy_data_to_ref( ls_entity ).
  ENDMETHOD.


  METHOD move_hu_to_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Create new item task to the resource.
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    DATA: ls_who TYPE /scwm/s_who_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_procty) = zswl_cl_param=>read_const(
        iv_lgnum     = mv_lgnum
        iv_context   = zswl_if_param_c=>mc_pick_prepare
        iv_parameter = zswl_if_param_c=>mc_pp_procty_z999 ).

    " Create warehouse task to trolley
    mo_hu->create_taskhu_to_trolley(
      EXPORTING
        iv_lgnum    = mv_lgnum
        iv_guid_hu  = ms_huhdr-guid_hu
        iv_resource = ms_trolley_position-trolley
        iv_proctype = CONV #( lv_procty )
        iv_logpos   = ms_trolley_position-position
        iv_who      = is_scanned_hu-warehouseorder
      IMPORTING
        et_bapiret  = DATA(lt_bapiret)
        ev_severity = DATA(lv_severity) ).
    IF lv_severity CA wmegc_severity_ea.
      raise_exception_from_bapiret( lt_bapiret ).
    ENDIF.

    " read the WHO and create a link between it and the resource
    " it is expected this link to be created during task creation but it does not
    " so we do that explicitly
    TRY.
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_lgnum = mv_lgnum
            iv_who   = is_scanned_hu-warehouseorder
          IMPORTING
            es_who   = ls_who.
      CATCH /scwm/cx_core .
        RETURN.
    ENDTRY.
    IF ls_who-rsrc = ms_trolley_position-trolley.
      RETURN.
    ENDIF.

    DATA(ls_attrib) = CORRESPONDING /scwm/s_who_att( ls_who ).
    ls_attrib-rsrc = ms_trolley_position-trolley.
    update_warehouse_order(
      EXPORTING
        iv_lgnum        = mv_lgnum
        iv_who          = is_scanned_hu-warehouseorder
        is_attributes   = ls_attrib ).

  ENDMETHOD.


  METHOD raise_exception.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Raise exception
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mo_log->add_message( ).
    RAISE EXCEPTION NEW /iwbep/cx_mgw_tech_exception(
        textid = VALUE #( msgno = sy-msgno
                          msgid = sy-msgid
                          attr1 = sy-msgv1
                          attr2 = sy-msgv2
                          attr3 = sy-msgv3
                          attr4 = sy-msgv4 ) ).
  ENDMETHOD.


  METHOD raise_exception_from_bapiret.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Raise exception from BAPIRET messages
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    LOOP AT it_bapiret ASSIGNING FIELD-SYMBOL(<ls_bapiret>) WHERE type CA wmegc_severity_ea.
      MESSAGE ID <ls_bapiret>-id TYPE <ls_bapiret>-type NUMBER <ls_bapiret>-number
         WITH <ls_bapiret>-message_v1 <ls_bapiret>-message_v2 <ls_bapiret>-message_v3 <ls_bapiret>-message_v4
         INTO mv_msg.
      raise_exception( ).
    ENDLOOP.
  ENDMETHOD.


  METHOD read_queue_from_parameter.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Read queue name from parameter framework
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    DATA(lv_def_queue) = zswl_cl_param=>read_const(
        iv_lgnum     = mv_lgnum
        iv_context   = iv_context
        iv_parameter = iv_parameter ).
    IF lv_def_queue IS INITIAL.
      MESSAGE e055(y20_ui5) INTO mv_msg.
      raise_exception( ).
    ENDIF.

    CALL FUNCTION '/SCWM/T346_READ_SINGLE'
      EXPORTING
        iv_lgnum  = mv_lgnum
        iv_queue  = CONV /scwm/de_queue( lv_def_queue )
      IMPORTING
        es_t346   = rs_queue
      EXCEPTIONS
        not_found = 1
        OTHERS    = 2.
    IF sy-subrc <> 0.
      ##NEEDED
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO DATA(lv_msg).
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD read_set_trolley_content.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: mt_trolleycontent.

    mo_trolley->read_trolley_hus(
      EXPORTING
        iv_lgnum     = mv_lgnum
        iv_trolley   = ms_trolley_position-trolley
      IMPORTING
        et_huheaders = DATA(lt_huheaders) ).
    IF lines( lt_huheaders ) = 0.
      ##NEEDED
      MESSAGE i010(y20_ui5) WITH ms_trolley_position-trolley INTO mv_msg.
      mo_log->add_message( ).
      RETURN.
    ENDIF.

    set_trolley_content( it_huheaders = lt_huheaders ).
  ENDMETHOD.


  METHOD set_activity_area_and_queue.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Determine and set trolley data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    DATA: ls_who TYPE /scwm/s_who_int.
    DATA: lt_ordim_o TYPE /scwm/tt_ordim_o .
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/TO_READ_MULT'
      EXPORTING
        iv_lgnum    = mv_lgnum
        ir_vlenr    = VALUE rseloption( FOR <ls_cont> IN mt_trolleycontent
                         ( sign   = wmegc_sign_inclusive
                           option = wmegc_option_eq
                           low    = <ls_cont>-hu-huident  ) )
      IMPORTING
        et_ordim_o  = lt_ordim_o
      EXCEPTIONS
        wrong_input = 1
        not_found   = 2
        OTHERS      = 3.
    IF sy-subrc <> 0.
      " no handling
*      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
*        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
    ENDIF.

    IF lines( lt_ordim_o ) > 0.
      DATA(ls_queue) = read_queue_from_parameter( iv_context   = zswl_if_param_c=>mc_pick_prepare
                                                  iv_parameter = zswl_if_param_c=>mc_pp_hucre_queue_def ).
      TRY.
          DATA(ls_ordim) = REF #( lt_ordim_o[ queue = ls_queue-queue ] ).
        CATCH cx_sy_itab_line_not_found.
          MESSAGE e078(y20_ui5) WITH ms_trolley_position-trolley INTO mv_msg.
          raise_exception( ).
      ENDTRY.
    ENDIF.

    TRY.
        DATA(lt_whohu) = read_whohu_by_huident( VALUE #( ( huident = mt_trolleycontent[ 1 ]-hu-huident ) ) ).

        TRY.
            CALL FUNCTION '/SCWM/WHO_SELECT'
              EXPORTING
                iv_lgnum = mv_lgnum
                iv_who   = lt_whohu[ 1 ]-who
              IMPORTING
                es_who   = ls_who.
            ##NO_HANDLER
          CATCH /scwm/cx_core.
        ENDTRY.

        IF ls_who IS INITIAL.
          MESSAGE e060(y20_ui5) INTO mv_msg.
          mo_log->add_message( ).
          RETURN.
        ENDIF.
        ms_trolley-aarea = ls_who-areawho.
        ms_trolley-queue = ls_who-queue.
        ##NO_HANDLER
      CATCH cx_sy_itab_line_not_found.
        MESSAGE e060(y20_ui5) INTO mv_msg.
        mo_log->add_message( ).
      CATCH /iwbep/cx_mgw_tech_exception.
        MESSAGE e060(y20_ui5) INTO mv_msg.
        mo_log->add_message( ).
    ENDTRY.
  ENDMETHOD.


  METHOD set_trolley_content.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Set trolley content data
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: mt_trolleycontent.
    LOOP AT it_huheaders ASSIGNING FIELD-SYMBOL(<ls_huhdr>).
      " get material for current HU
*      NEW /scwm/cl_ui_stock_fields( )->get_matkey_by_id(
*          EXPORTING iv_matid = <ls_huitm>-matid
*          IMPORTING ev_matnr = DATA(lv_matnr)
*                    ev_maktx = DATA(lv_mattxt) ).
      INSERT VALUE #( lgnum = mv_lgnum
                      trolley = ms_trolley_position-trolley
                      position = mo_trolley->convert_logpos_to_string( <ls_huhdr>-logpos )
                      hu-guid_hu = <ls_huhdr>-guid_hu
                      hu-huident = <ls_huhdr>-huident
                      hu-hutype = <ls_huhdr>-letyp
*                      product-matid = <ls_huitm>-matid
*                      product-matnr = lv_matnr
*                      product-MatTXT = lv_mattxt
*                      quantity-quan = <ls_huitm>-quan
*                      quantity-unit = <ls_huitm>-meins
                      ) INTO TABLE mt_trolleycontent.
    ENDLOOP.
*    IF lines( mt_material_global ) = 0 AND
*       lines( it_huitems ) > 0.
*      " read materials for each HU
*      DATA(lt_matid) = VALUE /scmb/mdl_matid_tab(
*                          FOR <ls_stock> IN it_huitems
*                          WHERE ( matid IS NOT INITIAL )
*                          ( matid = <ls_stock>-matid ) ).
*      " Load MATIDs in the buffer
*      NEW /scwm/cl_ui_stock_fields( )->prefetch_matkey_by_id( it_matid = lt_matid ).
**      read_material_multi_by_matid( EXPORTING it_matid = lt_matid
**                                    IMPORTING et_mat_global = mt_material_global ).
*    ENDIF.
*
*    LOOP AT it_huitems ASSIGNING FIELD-SYMBOL(<ls_huitm>).
*      TRY.
*          DATA(ls_huhdr) = REF #( it_huheaders[ guid_hu = <ls_huitm>-guid_parent ] ).
*        CATCH cx_sy_itab_line_not_found.
*          CONTINUE.
*      ENDTRY.
*
*      " get material for current HU
*      NEW /scwm/cl_ui_stock_fields( )->get_matkey_by_id(
*          EXPORTING iv_matid = <ls_huitm>-matid
*          IMPORTING ev_matnr = DATA(lv_matnr)
*                    ev_maktx = DATA(lv_mattxt) ).
*      INSERT VALUE #( lgnum = mv_lgnum
*                      trolley = ms_trolley_position-trolley
*                      position = mo_trolley->convert_logpos_to_string( ls_huhdr->logpos )
*                      hu-guid_hu = ls_huhdr->guid_hu
*                      hu-huident = ls_huhdr->huident
*                      hu-hutype = ls_huhdr->letyp
*                      product-matid = <ls_huitm>-matid
*                      product-matnr = lv_matnr
*                      product-MatTXT = lv_mattxt
*                      quantity-quan = <ls_huitm>-quan
*                      quantity-unit = <ls_huitm>-meins ) INTO TABLE mt_trolleycontent.
*    ENDLOOP.
  ENDMETHOD.


  METHOD update_warehouse_order.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Update WHO attributes
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/WHO_UPDATE'
      EXPORTING
        iv_lgnum      = iv_lgnum
        iv_db_update  = abap_true
        iv_who        = iv_who
        iv_queue      = iv_update_queue
        is_attributes = is_attributes
      EXCEPTIONS
        read_error    = 1
        attributes    = 2
        OTHERS        = 3.
    IF sy-subrc <> 0.
      CALL FUNCTION 'DB_ROLLBACK'.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg.
      raise_exception( ).
    ELSE.
      COMMIT WORK AND WAIT.
      MESSAGE i012(y20_ui5) WITH iv_who INTO mv_msg.
      mo_log->add_message( ).
    ENDIF.
  ENDMETHOD.


  METHOD validate_hu_vs_trolley_aarea.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Compare activity areas of last HU and trolley
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " if the trolley is empty no activity area will be filled
    " nothing to compare in this case
    IF ms_trolley-aarea IS INITIAL.
      RETURN.
    ENDIF.

    IF is_hu-aarea <> ms_trolley-aarea.
      MESSAGE e006(y20_ui5) WITH is_hu-huident ms_trolley_position-trolley INTO mv_msg.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD validate_set_scanned_trolley.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Validate stanned trolley number
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    ms_trolley_position = mo_trolley->split_scannum_to_trol_and_pos( iv_scantrolley ).

    DATA(ls_trolley) = mo_trolley->read_trolley_single( iv_lgnum   = mv_lgnum
                                                        iv_trolley = ms_trolley_position-trolley ).
    IF ls_trolley IS INITIAL.
      raise_exception( ).
    ENDIF.
  ENDMETHOD.


  METHOD validate_set_warehouse_number.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description :
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    CONSTANTS lc_tab_name TYPE string VALUE '/SCWM/T300'.
    DATA: ls_t300 TYPE /scwm/s_t300.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/T300_READ_SINGLE'
      EXPORTING
        iv_lgnum  = iv_lgnum
      IMPORTING
        es_t300   = ls_t300
      EXCEPTIONS
        not_found = 1
        OTHERS    = 2.
    IF sy-subrc <> 0 OR ls_t300-lgnum IS INITIAL.
      ##NEEDED
      MESSAGE e009(33) WITH iv_lgnum lc_tab_name INTO DATA(lv_msg).
      raise_exception( ).
    ENDIF.

    mv_lgnum = ls_t300-lgnum.
  ENDMETHOD.


  METHOD validate_trolley_extend_checks.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Do additional checks on trolley before moving the HU
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF is_scanned_hu-queue <> read_queue_from_parameter(
                          iv_context   = zswl_if_param_c=>mc_pick_prepare
                          iv_parameter = zswl_if_param_c=>mc_pp_hucre_queue_def )-queue.
      MESSAGE e077(y20_ui5) WITH ms_huhdr-huident INTO mv_msg.
      raise_exception( ).
    ENDIF.

    validate_hu_vs_trolley_aarea( is_scanned_hu ).

    check_trolley_is_open( ).

    check_trolley_posit_is_empty( ).
  ENDMETHOD.


  METHOD y20_if_odata_entity~create_deep_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHC
* Description : POST request for complex entities
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CASE mv_key_navigation.
      WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-scantrolley.
        ro_deep_entity = create_deep_scan_trolley( io_data_provider = io_data_provider ).

      WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-trolley_content.
        ro_deep_entity = create_deep_content_undo( io_data_provider = io_data_provider ).

      WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-trolley_close_list.
        ro_deep_entity = create_deep_close_trolley_mult( io_data_provider = io_data_provider ).

    ENDCASE.
  ENDMETHOD.


  METHOD y20_if_odata_entity~create_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHC
* Description : POST request for single entities
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CASE mv_key_navigation.
      WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-trolley_entity.
        ro_entity = create_ent_close_trolley_singl( io_data_provider = io_data_provider ).

    ENDCASE.
  ENDMETHOD.


  METHOD y20_if_odata_entity~get_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : GET request. Single entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CASE mv_key_navigation.
      WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-text_dlvitem.
        ro_entity = get_entity_dlvitem_text( ).
    ENDCASE.
  ENDMETHOD.


  METHOD y20_if_odata_entity~get_entityset.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHC
* Description : Get entity set
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CASE mv_key_navigation.
      WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-trolley_close_list.
        ro_entityset = get_entityset_close_alltrolley( ).

    ENDCASE.

  ENDMETHOD.


  METHOD y20_if_odata_entity~get_expanded_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHC
* Description : Get request, expanded entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CLEAR: et_expanded_tech_clauses, er_entity.

    CASE mv_key_navigation.
      WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-scanhu_huent.
        er_entity = get_expanded_hu( ).
        APPEND 'SCANHU_HU' TO et_expanded_tech_clauses.

      WHEN y20_cl_pick_prep_hutrl_mpc_ext=>ms_method_key-scantrolley.
        er_entity = get_expanded_trolley( ).
        APPEND 'SCANTRL_TROLLEY' TO et_expanded_tech_clauses.
        APPEND 'SCANTRL_TRLCONTENT' TO et_expanded_tech_clauses.
        APPEND 'SCANTRL_HU' TO et_expanded_tech_clauses.
    ENDCASE.
  ENDMETHOD.


  METHOD y20_if_odata_entity~set_execute_method.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHC
* Description : Set execution method
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    mv_key_navigation = iv_key.
  ENDMETHOD.


  METHOD read_whohu_by_huident.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PPHT
* Description : Read WHO - HUs
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    IF lines( it_huident ) = 0.
      RETURN.
    ENDIF.

    DATA(lt_selopt_hu) = VALUE rseloption( FOR <ls_hu> IN it_huident
      ( sign   = wmegc_sign_inclusive
        option = wmegc_option_eq
        low    = <ls_hu>-huident ) ).

    SELECT * FROM /scwm/whohu
      INTO TABLE @rt_whohu
     WHERE lgnum = @mv_lgnum
       AND huident IN @lt_selopt_hu.
    IF sy-subrc <> 0.
      LOG-POINT ID y20_odata_pick_prepare_hutrl FIELDS it_huident.
    ENDIF.
  ENDMETHOD.

ENDCLASS.