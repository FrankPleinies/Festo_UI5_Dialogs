CLASS y20_cl_ptwy_exec_pall_dpc_ext DEFINITION
  PUBLIC
  INHERITING FROM y20_cl_ptwy_exec_pall_dpc
  CREATE PUBLIC .

  PUBLIC SECTION.

    METHODS /iwbep/if_mgw_appl_srv_runtime~get_entity
        REDEFINITION .
    METHODS /iwbep/if_mgw_appl_srv_runtime~get_expanded_entity
        REDEFINITION .
    METHODS /iwbep/if_mgw_core_srv_runtime~create_entity
        REDEFINITION .
  PROTECTED SECTION.
  PRIVATE SECTION.

    DATA mv_msg_dummy TYPE string ##NEEDED.
    DATA mv_lgnum TYPE /scwm/lgnum .
    DATA mo_container TYPE REF TO /iwbep/if_message_container .
    CONSTANTS mc_user_logged_on TYPE /scwm/de_rsrc_sts VALUE 'A' ##NO_TEXT.
    CONSTANTS mc_userrsrc_rsrcpallet TYPE string VALUE 'USERRSRC_RSRCPALLET' ##NO_TEXT.
    DATA:
      BEGIN OF  ms_userrsrcpallet_exp_ent .
        INCLUDE TYPE y20_cl_ptwy_exec_pall_mpc=>ts_userrsrcscann.
    DATA: userrsrc_rsrcpallet TYPE y20_cl_ptwy_exec_pall_mpc=>tt_rsrcpalllet,
      END OF ms_userrsrcpallet_exp_ent .
    CONSTANTS mc_user_command_01 TYPE y20_de_user_comm_appl VALUE '01' ##NO_TEXT.
    CONSTANTS mc_user_command_02 TYPE y20_de_user_comm_appl VALUE '02' ##NO_TEXT.
    CONSTANTS mc_user_command_03 TYPE y20_de_user_comm_appl VALUE '03' ##NO_TEXT.

    METHODS load_key_data
      IMPORTING
        !it_key_tab TYPE /iwbep/t_mgw_name_value_pair
      EXPORTING
        !ev_lgnum   TYPE /scwm/lgnum
        !ev_rsrc    TYPE /scwm/de_rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS check_lgnum
      IMPORTING
        !iv_lgnum TYPE /scwm/lgnum OPTIONAL
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS add_message_odata_container .
    METHODS scan_resource
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(er_entity)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS read_resource_data
      IMPORTING
        !iv_rsrc     TYPE /scwm/de_rsrc
      EXPORTING
        !es_resource TYPE /scwm/rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS log_off_rsrc
      IMPORTING
        !iv_rsrc TYPE /scwm/de_rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS get_hus_rsrc
      IMPORTING
        !iv_rsrc TYPE /scwm/de_rsrc
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS get_scanned_rsrc_hus
      IMPORTING
        !it_key_tab               TYPE /iwbep/t_mgw_name_value_pair
      EXPORTING
        !er_entity                TYPE REF TO data
        !et_expanded_tech_clauses TYPE string_table
      RAISING
        /iwbep/cx_mgw_tech_exception
        ycx_ewm_general_exception_dyn .
    METHODS add_new_hu_rsrc
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(er_entity)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS assigne_who_to_rsrc
      IMPORTING
        !iv_rsrc TYPE /scwm/de_rsrc
        !it_who  TYPE /scwm/tt_who_int
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS clear_all_pallets
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(er_entity)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS get_resource_wt
      IMPORTING
        !it_key_tab      TYPE /iwbep/t_mgw_name_value_pair
      RETURNING
        VALUE(er_entity) TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS read_who_assigned
      IMPORTING
        !is_rsrc             TYPE /scwm/rsrc
      RETURNING
        VALUE(rt_wo_rsrc_ty) TYPE /scwm/tt_wo_rsrc_ty .
    METHODS check_dest_bin_allowed
      IMPORTING
        !iv_nltyp               TYPE /scwm/ltap_nltyp
      RETURNING
        VALUE(rv_allowe_change) TYPE abap_bool .
    METHODS conf_source_cr_wt_rsrc
      IMPORTING
        !is_ptwywtconfpall TYPE y20_cl_ptwy_exec_pall_mpc=>ts_ptwywtconfpall
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS wt_process_movement
      IMPORTING
        !io_data_provider TYPE REF TO /iwbep/if_mgw_entry_provider
      RETURNING
        VALUE(er_entity)  TYPE REF TO data
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS conf_final_dest_to_bin
      IMPORTING
        is_ptwywtconfpall TYPE y20_cl_ptwy_exec_pall_mpc=>ts_ptwywtconfpall
        iv_exc_flag       TYPE abap_bool
      RAISING
        /iwbep/cx_mgw_tech_exception .
    METHODS print_hu
      IMPORTING
        iv_guid_hu TYPE /scwm/ordim_o-dguid_hu.
ENDCLASS.



CLASS Y20_CL_PTWY_EXEC_PALL_DPC_EXT IMPLEMENTATION.


  METHOD /iwbep/if_mgw_appl_srv_runtime~get_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : ODATA: Get data entity - one line-structure info
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-28¦A.Yordanov    ¦Initial
*
************************************************************************

    TRY.
        CASE iv_entity_name.
          WHEN y20_cl_ptwy_exec_pall_mpc=>gc_ptwywtconfpall.
            " Load WT which we should confirm for current resource
            get_resource_wt( EXPORTING it_key_tab = it_key_tab
                             RECEIVING er_entity  = er_entity ).

        ENDCASE.
      CATCH /iwbep/cx_mgw_tech_exception.
        add_message_odata_container( ).
    ENDTRY.

  ENDMETHOD.


  METHOD /iwbep/if_mgw_appl_srv_runtime~get_expanded_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : ODATA: Get data of complex entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov    ¦Initial
*
************************************************************************

    CLEAR: er_entity,
           et_expanded_tech_clauses,
           es_response_context,
           et_expanded_clauses.

    TRY.
        CASE iv_entity_name.
          WHEN y20_cl_ptwy_exec_pall_mpc=>gc_userrsrcscann.
            " get all HUs which was scanned for the current rsrc
            get_scanned_rsrc_hus( EXPORTING
                                      it_key_tab               = it_key_tab
                                  IMPORTING
                                      er_entity                = er_entity
                                      et_expanded_tech_clauses = et_expanded_tech_clauses ).

        ENDCASE.
      CATCH /iwbep/cx_mgw_tech_exception.
        add_message_odata_container( ).
      CATCH ycx_ewm_general_exception_dyn INTO DATA(lx_ewm_gen_dyn).
        lx_ewm_gen_dyn->get_sy_msg( ).
        add_message_odata_container( ).
    ENDTRY.

  ENDMETHOD.


  METHOD /iwbep/if_mgw_core_srv_runtime~create_entity.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : ODATA: Create data of single entity
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    TRY.
        CASE iv_entity_name.

          WHEN y20_cl_ptwy_exec_pall_mpc=>gc_scannewresource.
            " scan resource
            scan_resource(  EXPORTING io_data_provider = io_data_provider
                            RECEIVING er_entity        = cr_entity ).
          WHEN y20_cl_ptwy_exec_pall_mpc=>gc_scannewpall.
            " add new HU to the Resource
            add_new_hu_rsrc( EXPORTING io_data_provider = io_data_provider
                             RECEIVING er_entity        = cr_entity ).
          WHEN y20_cl_ptwy_exec_pall_mpc=>gc_clearpallet.
            " clear all pallets which are assigned to the current resource
            clear_all_pallets( EXPORTING io_data_provider = io_data_provider
                               RECEIVING er_entity        = cr_entity ).
          WHEN y20_cl_ptwy_exec_pall_mpc=>gc_ptwywtconfpall.
            wt_process_movement( EXPORTING io_data_provider = io_data_provider
                                 RECEIVING er_entity        = cr_entity ).

        ENDCASE.
      CATCH /iwbep/cx_mgw_tech_exception.
        add_message_odata_container( ).
    ENDTRY.

  ENDMETHOD.


  METHOD add_message_odata_container.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Add messages log to service container
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    IF mo_container IS NOT BOUND.
      mo_container = mo_context->get_message_container( ).
    ENDIF.

    IF mo_container IS NOT BOUND.
      RETURN.
    ENDIF.

    mo_container->add_message(
      EXPORTING
        iv_msg_type    = sy-msgty
        iv_msg_id      = sy-msgid
        iv_msg_number  = sy-msgno
        iv_msg_v1      = sy-msgv1
        iv_msg_v2      = sy-msgv2
        iv_msg_v3      = sy-msgv3
        iv_msg_v4      = sy-msgv4
        iv_add_to_response_header = abap_true ).

  ENDMETHOD.


  METHOD add_new_hu_rsrc.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Scan New HU and assigne it to the resource
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-27¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_huident       TYPE /scwm/huident,
          ls_huhdr         TYPE /scwm/s_huhdr_int,
          ls_odata_hu_scan TYPE y20_cl_ptwy_exec_pall_mpc=>ts_scannewpall,
          lt_whoid         TYPE /scwm/tt_whoid,
          lt_ordim_o       TYPE /scwm/tt_ordim_o,
          lt_who           TYPE /scwm/tt_who_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " get data for POST request
    io_data_provider->read_entry_data( IMPORTING es_data = ls_odata_hu_scan ).

    IF ls_odata_hu_scan-rsrc IS INITIAL OR
       ls_odata_hu_scan-lgnum IS INITIAL.
      MESSAGE e102(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check lgnum
    check_lgnum( EXPORTING iv_lgnum = ls_odata_hu_scan-lgnum ).

    IF ls_odata_hu_scan-huident IS INITIAL.
      MESSAGE e076(/scwm/rf_en) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSE.
      lv_huident = |{ ls_odata_hu_scan-huident ALPHA = IN }|.
    ENDIF.

    CALL FUNCTION '/SCWM/HU_READ'
      EXPORTING
        iv_appl    = wmegc_huappl_wme
        iv_huident = lv_huident
        iv_lgnum   = mv_lgnum
      IMPORTING
        es_huhdr   = ls_huhdr
      EXCEPTIONS
        deleted    = 1
        not_found  = 2
        error      = 3
        OTHERS     = 4.

    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
           WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    CALL FUNCTION '/SCWM/TO_READ_SRC'
      EXPORTING
        iv_lgnum     = mv_lgnum
        iv_huident   = ls_huhdr-huident
      IMPORTING
        et_ordim_o   = lt_ordim_o
      EXCEPTIONS
        wrong_input  = 3
        not_found    = 1
        foreign_lock = 2
        OTHERS       = 4.

    IF sy-subrc = 1.
      MESSAGE e106(y20_ui5) WITH ls_odata_hu_scan-huident
                            INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSEIF  sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
           WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    lt_whoid = VALUE #( FOR <ls_who_ord> IN lt_ordim_o
                              (  who   = <ls_who_ord>-who ) ).

    " Select WO to check if already locked
    TRY.
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_to       = abap_true
            iv_lgnum    = mv_lgnum
            iv_lock_who = abap_true
            it_who      = lt_whoid
          IMPORTING
            et_who      = lt_who
            et_ordim_o  = lt_ordim_o.

      CATCH /scwm/cx_core.
        CALL FUNCTION 'DEQUEUE_ALL'.

        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
           WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDTRY.

    " check if HUs which are on the resource are putaway relevant
    DATA(lt_queue_pall) =  zswl_cl_param=>read_const_list( iv_lgnum     = mv_lgnum
                                                           iv_context   = zswl_if_param_c=>mc_ptwy_appl_pal
                                                           iv_parameter = zswl_if_param_c=>mc_ptwy_queue_pal ).

    LOOP AT lt_who ASSIGNING FIELD-SYMBOL(<ls_who_q>).

      CHECK NOT line_exists( lt_queue_pall[ table_line = <ls_who_q>-queue ] ).
      MESSAGE e077(y20_ui5) WITH ls_odata_hu_scan-huident INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDLOOP.

    SORT lt_ordim_o BY lgnum who tanum.

    LOOP AT lt_ordim_o ASSIGNING FIELD-SYMBOL(<ls_ordim_o>).
      ASSIGN lt_who[ who = <ls_ordim_o>-who ] TO  FIELD-SYMBOL(<ls_who>) .
      CHECK  sy-subrc = 0.

      IF <ls_ordim_o>-srsrc IS NOT INITIAL AND
        ( <ls_who>-status = wmegc_wo_in_process AND
          ls_odata_hu_scan-rsrc <> <ls_ordim_o>-srsrc ).
        CALL FUNCTION 'DEQUEUE_ALL'.

        MESSAGE e140(/scwm/rf_en) WITH <ls_ordim_o>-srsrc
                                       <ls_ordim_o>-who INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.
    ENDLOOP.

    " do the assigment and do the commit
    assigne_who_to_rsrc( EXPORTING
                                iv_rsrc = ls_odata_hu_scan-rsrc
                                it_who  = lt_who ).

    copy_data_to_ref( EXPORTING is_data = ls_odata_hu_scan
                      CHANGING  cr_data = er_entity ).

  ENDMETHOD.


  METHOD assigne_who_to_rsrc.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : assign WHO to the RSRC
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-27¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA:
      ls_wo_rsrc_ty TYPE /scwm/wo_rsrc_ty,
      ls_rsrc       TYPE /scwm/rsrc,
      ls_t346       TYPE /scwm/t346,
      lt_wo_rsrc_ty TYPE /scwm/tt_wo_rsrc_ty.

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    CALL FUNCTION '/SCWM/RSRC_READ_SINGLE'
      EXPORTING
        iv_lgnum    = mv_lgnum
        iv_rsrc     = iv_rsrc
      IMPORTING
        es_rsrc     = ls_rsrc
      EXCEPTIONS
        wrong_input = 1
        not_found   = 2
        OTHERS      = 3.

    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    LOOP AT  it_who ASSIGNING FIELD-SYMBOL(<ls_who_assgn>).
      CLEAR: lt_wo_rsrc_ty,
             ls_wo_rsrc_ty.

      MOVE-CORRESPONDING <ls_who_assgn> TO ls_wo_rsrc_ty  ##ENH_OK.
      APPEND ls_wo_rsrc_ty TO lt_wo_rsrc_ty.

      "   Read queue to get operating environment
      CALL FUNCTION '/SCWM/T346_READ_SINGLE'
        EXPORTING
          iv_lgnum  = ls_wo_rsrc_ty-lgnum
          iv_queue  = ls_wo_rsrc_ty-queue
        IMPORTING
          es_t346   = ls_t346
        EXCEPTIONS
          not_found = 1
          OTHERS    = 2.

      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

      "   Select/assign warehouse order for a resource
      CALL FUNCTION '/SCWM/RSRC_WHO_SELECT'
        EXPORTING
          iv_rfrsrc         = ls_t346-rfrsrc
          iv_norsrc_upd     = abap_true
          iv_man_wo_sel     = abap_true
          iv_no_rec_call    = abap_true "NO REC-Control by call from NON RF-Environment
        CHANGING
          cs_rsrc           = ls_rsrc
          ct_wo_rsrc_ty     = lt_wo_rsrc_ty
        EXCEPTIONS
          no_rstyp_attached = 1
          OTHERS            = 2.

      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

    ENDLOOP.

    " commit work and wait to display new data after auto-refresh
    COMMIT WORK AND WAIT.

    CALL FUNCTION 'DEQUEUE_ALL'.



  ENDMETHOD.


  METHOD check_dest_bin_allowed.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Check if change destination bin is allowed
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-29¦A.Yordanov    ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " get storage type which allowed destination storage type
    DATA(lt_change_dest_allow) =  zswl_cl_param=>read_const_list( iv_lgnum     = mv_lgnum
                                                                  iv_context   = zswl_if_param_c=>mc_ptwy_appl_pal
                                                                  iv_parameter = zswl_if_param_c=>mc_change_dest_allowed ).

    IF line_exists( lt_change_dest_allow[ table_line = iv_nltyp ] ).
      rv_allowe_change = abap_true.
    ENDIF.

  ENDMETHOD.


  METHOD check_lgnum.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Get LGNUM from key and set it to the object
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_t300  TYPE /scwm/s_t300.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " check lgnum
    CALL FUNCTION '/SCWM/T300_READ_SINGLE'
      EXPORTING
        iv_lgnum  = iv_lgnum
      IMPORTING
        es_t300   = ls_t300
      EXCEPTIONS
        not_found = 1
        OTHERS    = 2.

    IF sy-subrc <> 0 OR
       ls_t300-lgnum IS INITIAL.
      MESSAGE e011(/scwm/ccheck) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSE.
      mv_lgnum  = ls_t300-lgnum.
    ENDIF.

  ENDMETHOD.


  METHOD clear_all_pallets.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : This logic should unassigne WHO from the resource
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov     ¦Initial
*
************************************************************************

    DATA:
      ls_rsrc          TYPE /scwm/s_rsrc,
      lt_whoid         TYPE  /scwm/tt_whoid,
      lt_who           TYPE /scwm/tt_who_int,
      lt_ordim_o       TYPE /scwm/tt_ordim_o,
      ls_odata_hu_scan TYPE y20_cl_ptwy_exec_pall_mpc=>ts_clearpallet.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    " get data for POST request
    io_data_provider->read_entry_data( IMPORTING es_data = ls_odata_hu_scan ).

    IF ls_odata_hu_scan-rsrc  IS INITIAL OR
       ls_odata_hu_scan-lgnum IS INITIAL.
      MESSAGE e102(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check lgnum
    check_lgnum( EXPORTING iv_lgnum =  ls_odata_hu_scan-lgnum ).

    SELECT FROM /scwm/wo_rsrc_ty
       FIELDS who
      WHERE lgnum = @mv_lgnum
        AND rsrc  = @ls_odata_hu_scan-rsrc
        INTO TABLE @DATA(lt_who_sel).

    IF sy-subrc <> 0.
      " nothing to do here
      copy_data_to_ref( EXPORTING is_data = ls_odata_hu_scan
                        CHANGING  cr_data = er_entity ).
      RETURN.
    ELSE.
      lt_whoid = lt_who_sel.
    ENDIF.

    " Select WO to check if already locked
    TRY.
        " Get WHO for current scanned HU
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_to       = abap_true
            iv_lgnum    = mv_lgnum
            iv_lock_who = abap_true
            iv_rsrc     = ls_odata_hu_scan-rsrc
            it_who      = lt_whoid
          IMPORTING
            et_who      = lt_who
            et_ordim_o  = lt_ordim_o.

      CATCH /scwm/cx_core.
        CALL FUNCTION 'DEQUEUE_ALL'.

        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
           WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDTRY.

    SORT lt_ordim_o BY lgnum who tanum.

    LOOP AT lt_ordim_o ASSIGNING FIELD-SYMBOL(<ls_ordim_o>).
      READ TABLE lt_who ASSIGNING FIELD-SYMBOL(<ls_who>)
                       WITH KEY who = <ls_ordim_o>-who.
      IF sy-subrc NE 0.
        EXIT.
      ENDIF.
      IF <ls_ordim_o>-srsrc = ls_odata_hu_scan-rsrc AND
         <ls_who>-status = wmegc_wo_in_process.
        CALL FUNCTION 'DEQUEUE_ALL'.

        MESSAGE s140(/scwm/rf_en)
          WITH <ls_ordim_o>-srsrc <ls_ordim_o>-who INTO mv_msg_dummy.
        RETURN.
      ENDIF.
    ENDLOOP.

    ls_rsrc-lgnum = mv_lgnum.

    " Unassign warehouse order from a resource
    CALL FUNCTION '/SCWM/RSRC_WHO_UNASSIGN'
      EXPORTING
        it_who        = lt_who
        is_rsrc       = ls_rsrc
      EXCEPTIONS
        error_message = 1
        OTHERS        = 2.

    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE wmegc_severity_suc NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.
    "commit work and wait to display new data after auto-refresh
    COMMIT WORK AND WAIT.

    CALL FUNCTION 'DEQUEUE_ALL'.

    copy_data_to_ref( EXPORTING is_data = ls_odata_hu_scan
                      CHANGING  cr_data = er_entity ).

  ENDMETHOD.


  METHOD conf_final_dest_to_bin.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Confirm The WT to the final putaway Bin.
* Basically this is the movement from the Resource To the Bin
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-29¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_exc_code     TYPE abap_bool,
          lv_severity     TYPE bapi_mtype,
          lv_dest_bin     TYPE /scwm/ltap_nlpla,
          lv_val_dest_bin TYPE /scwm/ltap_nlpla,
          lv_lgpla        TYPE /scwm/lgpla,
          ls_lagp         TYPE /scwm/lagp,
          ls_ordim_o      TYPE /scwm/ordim_o,
          lt_bapiret      TYPE bapirettab,
          lt_to_conf      TYPE /scwm/to_conf_tt,
          lt_conf_exc     TYPE /scwm/tt_conf_exc.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    " get and lock the current task
    CALL FUNCTION '/SCWM/TO_READ_SINGLE'
      EXPORTING
        iv_lgnum     = mv_lgnum
        iv_tanum     = is_ptwywtconfpall-tanum
        iv_flglock   = abap_true
      IMPORTING
        es_ordim_o   = ls_ordim_o
      EXCEPTIONS
        wrong_input  = 1
        not_found    = 2
        foreign_lock = 3
        error        = 4
        OTHERS       = 5.

    IF sy-subrc <> 0.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check where is our HU right now
    IF ls_ordim_o-srsrc IS INITIAL AND
       ls_ordim_o-sloc_type <> wmegc_rsrc.
      MESSAGE e108(y20_ui5) WITH |{ ls_ordim_o-vlenr ALPHA = OUT }|
                                  is_ptwywtconfpall-rsrc
                            INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    IF is_ptwywtconfpall-lgpla_val IS INITIAL.
      " Enter a destination storage bin and proceed.
      MESSAGE e049(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    lv_dest_bin     = COND #( WHEN iv_exc_flag IS INITIAL
                              THEN  |{ is_ptwywtconfpall-nlpla ALPHA = OUT }|
                              ELSE space ).

    lv_val_dest_bin = |{ is_ptwywtconfpall-lgpla_val ALPHA = OUT }|.

    " if destination is not general storage type, then validate
    " destination bin
    IF lv_dest_bin      IS NOT INITIAL AND
       ls_ordim_o-nlpla IS NOT INITIAL AND
       lv_dest_bin <> lv_val_dest_bin.

      " Scanned destination storage bin &1 does not match WT data.
      MESSAGE e048(y20_ui5) WITH lv_val_dest_bin INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.

    ENDIF.

    IF ls_ordim_o-nlpla        IS INITIAL AND
       is_ptwywtconfpall-nltyp IS NOT INITIAL AND
       iv_exc_flag = abap_false.

      " get destination bin in a case of general storage type
      lv_lgpla = is_ptwywtconfpall-lgpla_val.

      CALL FUNCTION '/SCWM/LAGP_READ_SINGLE'
        EXPORTING
          iv_lgnum      = mv_lgnum
          iv_lgpla      = lv_lgpla
        IMPORTING
          es_lagp       = ls_lagp
        EXCEPTIONS
          wrong_input   = 1
          not_found     = 2
          enqueue_error = 3
          OTHERS        = 4.

      IF sy-subrc <> 0.
        MESSAGE e045(/scwm/rf_en) WITH lv_lgpla INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

      IF ls_lagp-lgtyp <> is_ptwywtconfpall-nltyp AND
         lv_exc_code = abap_false.
        " check if the scanned Bin is in the correct storage type
        " Scanned dest. bin &1 is not in correct storage type &2.
        MESSAGE e050(y20_ui5) WITH lv_val_dest_bin
                                   is_ptwywtconfpall-nltyp INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ELSE.
        ls_ordim_o-nltyp = ls_lagp-lgtyp.
      ENDIF.

      ls_ordim_o-nlpla = ls_lagp-lgpla.

    ENDIF.

    IF  ls_ordim_o-nlpla IS INITIAL.
      " if we have general warehouse type exception code is not need
      lv_exc_code = abap_false.
    ENDIF.

    lt_to_conf =  VALUE #( ( tanum = ls_ordim_o-tanum
                             papos = 2
                             nista = ls_ordim_o-vsolm
                             altme = ls_ordim_o-altme
                             wdatu = ls_ordim_o-wdatu
                             coo   = ls_ordim_o-coo
                             nlpla =  COND #( WHEN iv_exc_flag = abap_true
                                              THEN lv_val_dest_bin
                                              ELSE ls_ordim_o-nlpla )
                             guid_to = ls_ordim_o-guid_to ) ).

    IF iv_exc_flag = abap_true.
      " get exception code
      DATA(lv_exc_code_param) =  zswl_cl_param=>read_const( iv_lgnum     = mv_lgnum
                                                            iv_context   = zswl_if_param_c=>mc_ptwy_appl_pal
                                                            iv_parameter = zswl_if_param_c=>mc_exccode_chbd ).

      IF lv_exc_code_param IS INITIAL.
        MESSAGE e051(y20_ui5) INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

      lt_conf_exc = VALUE #( ( tanum     = ls_ordim_o-tanum
                               papos     = 2
                               buscon    = wmegc_buscon_tpt
                               exec_step = wmegc_execstep_04
                               exccode   = lv_exc_code_param ) ).

    ENDIF.

    " do real WT confirmation
    CALL FUNCTION '/SCWM/TO_CONFIRM'
      EXPORTING
        iv_lgnum       = mv_lgnum
        iv_update_task = abap_true
        iv_commit_work = abap_false
        it_conf        = lt_to_conf
        it_conf_exc    = lt_conf_exc
      IMPORTING
        et_bapiret     = lt_bapiret
        ev_severity    = lv_severity.

    IF lv_severity CA wmegc_severity_ea.
      LOOP AT lt_bapiret ASSIGNING FIELD-SYMBOL(<ls_bapiret>) WHERE type = wmegc_severity_err.
        MESSAGE ID <ls_bapiret>-id TYPE <ls_bapiret>-type NUMBER <ls_bapiret>-number
           WITH <ls_bapiret>-message_v1
                <ls_bapiret>-message_v2
                <ls_bapiret>-message_v3
                <ls_bapiret>-message_v4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDLOOP.
    ELSE.
      COMMIT WORK AND WAIT.
      print_hu( ls_ordim_o-dguid_hu ).
    ENDIF.

  ENDMETHOD.


  METHOD conf_source_cr_wt_rsrc.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Validate Source HU and create and confirm WT to the RSRC
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-29¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_val_hu   TYPE /scwm/ltap_vlenr,
          lv_severity TYPE bapi_mtype,
          lv_guid_hu  TYPE /scwm/guid_hu,
          ls_ordim_o  TYPE /scwm/ordim_o,
          ls_huhdr    TYPE /scwm/s_huhdr_int,
          lt_bapiret  TYPE bapirettab,
          lt_to_conf  TYPE /scwm/to_conf_tt.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    IF is_ptwywtconfpall-huident_val IS INITIAL.
      MESSAGE e053(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ELSE.
      lv_val_hu = |{ is_ptwywtconfpall-huident_val ALPHA = OUT }|.
    ENDIF.

    " validate the source HU
    IF is_ptwywtconfpall-vlenr <> lv_val_hu.
      MESSAGE e047(y20_ui5) WITH lv_val_hu INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " get and lock the current task
    CALL FUNCTION '/SCWM/TO_READ_SINGLE'
      EXPORTING
        iv_lgnum     = mv_lgnum
        iv_tanum     = is_ptwywtconfpall-tanum
        iv_flglock   = abap_true
      IMPORTING
        es_ordim_o   = ls_ordim_o
      EXCEPTIONS
        wrong_input  = 1
        not_found    = 2
        foreign_lock = 3
        error        = 4
        OTHERS       = 5.

    IF sy-subrc <> 0.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    IF ls_ordim_o-srsrc IS NOT INITIAL AND
       ls_ordim_o-sloc_type <> wmegc_bin.

      lv_guid_hu = ls_ordim_o-sguid_hu.

      CALL FUNCTION '/SCWM/HU_READ'
        EXPORTING
          iv_appl    = wmegc_huappl_wme
          iv_lgnum   = mv_lgnum
          iv_guid_hu = lv_guid_hu
        IMPORTING
          es_huhdr   = ls_huhdr
        EXCEPTIONS
          deleted    = 1
          not_found  = 2
          error      = 3
          OTHERS     = 4.

      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

      IF ls_huhdr-rsrc IS NOT INITIAL.
        MESSAGE i237(/scwm/rf_en) WITH is_ptwywtconfpall-huident_val
                                       ls_huhdr-rsrc INTO mv_msg_dummy.
        add_message_odata_container( ).
        RETURN.
      ENDIF.
    ENDIF.

    " prepare data for the confirmation
    APPEND VALUE #( tanum   = ls_ordim_o-tanum
                    papos   =  1
                    nista   = ls_ordim_o-vsolm
                    altme   = ls_ordim_o-altme
                    wdatu   = ls_ordim_o-wdatu
                    coo     = ls_ordim_o-coo
                    vlenr   = ls_ordim_o-vlenr
                    nlenr   = ls_ordim_o-nlenr
                    drsrc   = is_ptwywtconfpall-rsrc
                    huent   = abap_true   " move whole HU
                    guid_to = ls_ordim_o-guid_to ) TO lt_to_conf.

    " do confirmation and commit of the WT to the resource
    CALL FUNCTION '/SCWM/TO_CONFIRM'
      EXPORTING
        iv_lgnum       = mv_lgnum
        iv_update_task = abap_true
        iv_commit_work = abap_false
        it_conf        = lt_to_conf
        iv_wtcode      = wmegc_wtcode_rsrc
      IMPORTING
        et_bapiret     = lt_bapiret
        ev_severity    = lv_severity.

    IF lv_severity CA wmegc_severity_ea.
      LOOP AT lt_bapiret ASSIGNING FIELD-SYMBOL(<ls_bapiret>) WHERE type = wmegc_severity_err.
        MESSAGE ID <ls_bapiret>-id TYPE <ls_bapiret>-type NUMBER <ls_bapiret>-number
           WITH <ls_bapiret>-message_v1
                <ls_bapiret>-message_v2
                <ls_bapiret>-message_v3
                <ls_bapiret>-message_v4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDLOOP.
    ELSE.
      COMMIT WORK AND WAIT.
    ENDIF.

  ENDMETHOD.


  METHOD get_hus_rsrc.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Read HUs assign to a resource and check if they
* are relevant for Putaway
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov    ¦Initial
*
************************************************************************

    DATA: lt_huheaders   TYPE /scwm/tt_huhdr_int,
          lt_huident     TYPE /scwm/tt_huident,
          lt_ordim_o_src TYPE /scwm/tt_ordim_o.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/HU_SELECT_GEN'
      EXPORTING
        iv_lgnum     = mv_lgnum
        ir_rsrc      = VALUE rseloption( ( sign   = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low    = iv_rsrc ) )
        ir_vhi       = VALUE rseloption( ( sign   = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low    = wmegc_vhi_plan )
                                         ( sign   = wmegc_sign_inclusive
                                           option = wmegc_option_eq
                                           low    = wmegc_vhi_real ) )
      IMPORTING
        et_huhdr     = lt_huheaders
      EXCEPTIONS
        wrong_input  = 1
        not_possible = 2
        OTHERS       = 3.

    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    IF lines( lt_huheaders ) = 0.
      RETURN.
    ENDIF.

    lt_huident = VALUE #( FOR <ls_huheader> IN lt_huheaders
                             ( huident = <ls_huheader>-huident ) ).

    " get all open WTs for this HUs
    CALL FUNCTION '/SCWM/TO_READ_HU'
      EXPORTING
        iv_lgnum       = mv_lgnum
        it_huident     = lt_huident
      IMPORTING
        et_ordim_o_src = lt_ordim_o_src
      EXCEPTIONS
        error          = 1.

    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.


    IF lt_ordim_o_src IS INITIAL.
      MESSAGE e105(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check if HUs which are on the resource are putaway relevant
    DATA(lt_queue_pall) =  zswl_cl_param=>read_const_list( iv_lgnum     = mv_lgnum
                                                           iv_context   = zswl_if_param_c=>mc_ptwy_appl_pal
                                                           iv_parameter = zswl_if_param_c=>mc_ptwy_queue_pal ).

    LOOP AT lt_ordim_o_src ASSIGNING FIELD-SYMBOL(<ls_ordim_o>).

      CHECK NOT line_exists( lt_queue_pall[ table_line = <ls_ordim_o>-queue ]  ).
      MESSAGE e105(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDLOOP.

  ENDMETHOD.


  METHOD get_resource_wt.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Load WT which we should confirm for current resource
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-28¦A.Yordanov    ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lv_lgpla               TYPE /scwm/lgpla,
          lv_guid_hu             TYPE /scwm/guid_hu,
          ls_rsrc                TYPE /scwm/rsrc,
          ls_odata_ptwy_resource TYPE y20_cl_ptwy_exec_pall_mpc=>ts_ptwywtconfpall,
          ls_lagp                TYPE /scwm/lagp,
          ls_huhdr               TYPE /scwm/s_huhdr_int,
          ls_mat_global          TYPE /scwm/s_material_global,
          lt_whoid               TYPE /scwm/tt_whoid,
          lt_who_int             TYPE /scwm/tt_who_int,
          lt_ordim_o             TYPE /scwm/tt_ordim_o,
          lt_rsrc_ty             TYPE /scwm/tt_wo_rsrc_ty.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " get relevant key from the call
    load_key_data( EXPORTING it_key_tab = it_key_tab
                   IMPORTING ev_lgnum   = DATA(lv_lgnum)
                             ev_rsrc    = DATA(lv_rsrc) ).

    " check lgnum
    check_lgnum( iv_lgnum = lv_lgnum ).

    " read and check resource
    read_resource_data( EXPORTING iv_rsrc     = lv_rsrc
                        IMPORTING es_resource = ls_rsrc ).

    " get all who assigned to this resource
    lt_rsrc_ty = read_who_assigned( EXPORTING is_rsrc = ls_rsrc ).

    " check if WHO assigned to the resource are putaway relevant
    DATA(lt_queue_pall) =  zswl_cl_param=>read_const_list( iv_lgnum     = mv_lgnum
                                                           iv_context   = zswl_if_param_c=>mc_ptwy_appl_pal
                                                           iv_parameter = zswl_if_param_c=>mc_ptwy_queue_pal ).

    LOOP AT lt_rsrc_ty ASSIGNING FIELD-SYMBOL(<ls_rsrc_ty_who>).
      DATA(lv_indx) = sy-tabix.
      CHECK NOT line_exists( lt_queue_pall[ table_line = <ls_rsrc_ty_who>-queue ] ).
      DELETE lt_rsrc_ty INDEX lv_indx.
    ENDLOOP.

    IF lt_rsrc_ty IS INITIAL.
      " something is wrong there are no assigned WT for Putaway for this resource
      MESSAGE e107(y20_ui5) WITH lv_rsrc INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    lt_whoid = VALUE #( FOR <ls_who_rsrc> IN lt_rsrc_ty
                         ( who = <ls_who_rsrc>-who ) ).

    " get all open WTs
    TRY.
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_to       = abap_true
            iv_lgnum    = mv_lgnum
            iv_lock_who = abap_true
            it_who      = lt_whoid
          IMPORTING
            et_who      = lt_who_int
            et_ordim_o  = lt_ordim_o.

      CATCH /scwm/cx_core.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDTRY.

    SORT lt_ordim_o BY pathseq.

    ASSIGN lt_ordim_o[ 1 ] TO FIELD-SYMBOL(<ls_ordim_o>).

    IF sy-subrc <> 0.
      MESSAGE e321(/scwm/l3) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    IF <ls_ordim_o>-nlpla IS NOT INITIAL.
      lv_lgpla = <ls_ordim_o>-nlpla.

      CALL FUNCTION '/SCWM/LAGP_READ_SINGLE'
        EXPORTING
          iv_lgnum      = mv_lgnum
          iv_lgpla      = lv_lgpla
        IMPORTING
          es_lagp       = ls_lagp
        EXCEPTIONS
          wrong_input   = 1
          not_found     = 2
          enqueue_error = 3
          OTHERS        = 4.

      IF sy-subrc <> 0.
        MESSAGE e045(/scwm/rf_en) WITH lv_lgpla.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.

    ENDIF.

    IF <ls_ordim_o>-sguid_hu IS NOT INITIAL.

      lv_guid_hu = <ls_ordim_o>-sguid_hu.

      CALL FUNCTION '/SCWM/HU_READ'
        EXPORTING
          iv_appl    = wmegc_huappl_wme
          iv_lgnum   = mv_lgnum
          iv_guid_hu = lv_guid_hu
        IMPORTING
          es_huhdr   = ls_huhdr
        EXCEPTIONS
          deleted    = 1
          not_found  = 2
          error      = 3
          OTHERS     = 4.

      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
      ENDIF.
    ENDIF.

    NEW /scwm/cl_ui_stock_fields( )->get_matkey_by_id( EXPORTING
                                          iv_matid = <ls_ordim_o>-matid
                                       IMPORTING
                                          ev_matnr = DATA(lv_matnr)
                                          ev_maktx = DATA(lv_maktx) ).

    TRY.
        CALL FUNCTION '/SCWM/MATERIAL_READ_SINGLE'
          EXPORTING
            iv_matid      = <ls_ordim_o>-matid
          IMPORTING
            es_mat_global = ls_mat_global.
      CATCH /scwm/cx_md.                                "#EC NO_HANDLER
    ENDTRY.

    " build odata information to the structure
    ls_odata_ptwy_resource = VALUE #( lgnum   = mv_lgnum
                                      rsrc    = ls_rsrc-rsrc
                                      tanum   = <ls_ordim_o>-tanum
                                      who     = <ls_ordim_o>-who
                                      procty  = <ls_ordim_o>-procty
                                      aarea   = <ls_ordim_o>-aarea
                                      nltyp   = <ls_ordim_o>-nltyp
                                      nlber   = <ls_ordim_o>-nlber
                                      nlpla   =  COND #( WHEN  <ls_ordim_o>-nlpla IS INITIAL
                                                          THEN <ls_ordim_o>-nltyp
                                                          ELSE <ls_ordim_o>-nlpla )
                                     aisle   = ls_lagp-aisle
                                     stack   = ls_lagp-stack
                                     lvl_v   = ls_lagp-lvl_v
                                     vlenr   = |{ <ls_ordim_o>-vlenr ALPHA = OUT }|
                                     hutype  = ls_huhdr-letyp
                                     hndlcode = ls_mat_global-hndlcode
                                     chn_dest_bin_allow = check_dest_bin_allowed( <ls_ordim_o>-nltyp )
                                     matnr   = lv_matnr
                                     maktx   = lv_maktx
                                     vsolm   = <ls_ordim_o>-vsolm
                                     meins   = <ls_ordim_o>-meins ).

    " prepare data to ref
    copy_data_to_ref( EXPORTING is_data = ls_odata_ptwy_resource
                      CHANGING  cr_data = er_entity ).


  ENDMETHOD.


  METHOD get_scanned_rsrc_hus.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Load HUs which was scanned for the current resource
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov    ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_rsrc    TYPE /scwm/rsrc,
          lt_huhdr   TYPE /scwm/tt_huhdr_int,
          lt_whoid   TYPE /scwm/tt_whoid,
          lt_ordim_o TYPE /scwm/tt_ordim_o,
          lt_who_int TYPE /scwm/tt_who_int ##NEEDED,
          lt_rsrc_ty TYPE /scwm/tt_wo_rsrc_ty.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    load_key_data( EXPORTING it_key_tab = it_key_tab
                   IMPORTING ev_lgnum   = DATA(lv_lgnum)
                             ev_rsrc    = DATA(lv_rsrc) ).

    " check lgnum
    check_lgnum( iv_lgnum = lv_lgnum ).

    ls_rsrc-rsrc = lv_rsrc.

    ms_userrsrcpallet_exp_ent-lgnum = mv_lgnum.
    ms_userrsrcpallet_exp_ent-rsrc  = lv_rsrc.

    " read and check resource
    read_resource_data( EXPORTING iv_rsrc     = lv_rsrc
                        IMPORTING es_resource = ls_rsrc ).

    ls_rsrc-rfind = abap_true.

    " get all who assigned to this resource
    lt_rsrc_ty = read_who_assigned( EXPORTING is_rsrc = ls_rsrc ).

    " check if WHO assigned to the resource are putaway relevant
    DATA(lt_queue_pall) =  zswl_cl_param=>read_const_list( iv_lgnum     = mv_lgnum
                                                           iv_context   = zswl_if_param_c=>mc_ptwy_appl_pal
                                                           iv_parameter = zswl_if_param_c=>mc_ptwy_queue_pal ).

    LOOP AT lt_rsrc_ty ASSIGNING FIELD-SYMBOL(<ls_rsrc_ty_who>).
      DATA(lv_indx) = sy-tabix.
      CHECK NOT line_exists( lt_queue_pall[ table_line = <ls_rsrc_ty_who>-queue ] ).
      DELETE lt_rsrc_ty INDEX lv_indx.
    ENDLOOP.

    IF lt_rsrc_ty IS INITIAL.
      LOG-POINT ID y20_ui5_putaway_pall
        FIELDS lv_lgnum
               lv_rsrc.

      " there are no assigned WHO to this resource we go out in this case
      et_expanded_tech_clauses = VALUE #( (  mc_userrsrc_rsrcpallet ) ).

      " prepare data to ref
      copy_data_to_ref( EXPORTING is_data = ms_userrsrcpallet_exp_ent
                        CHANGING  cr_data = er_entity ).
      RETURN.
    ENDIF.

    lt_whoid = VALUE #( FOR <ls_who_rsrc> IN lt_rsrc_ty
                             ( who = <ls_who_rsrc>-who ) ).
    " get all open WTs
    TRY.
        CALL FUNCTION '/SCWM/WHO_SELECT'
          EXPORTING
            iv_to       = abap_true
            iv_lgnum    = mv_lgnum
            iv_lock_who = abap_true
            it_who      = lt_whoid
          IMPORTING
            et_who      = lt_who_int
            et_ordim_o  = lt_ordim_o.

      CATCH /scwm/cx_core.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
        RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDTRY.

    LOG-POINT ID y20_ui5_putaway_pall
           FIELDS lt_who_int
                  lt_ordim_o.

    DATA(lt_src_hu) =  VALUE /scwm/tt_guid_hu( FOR <ls_hu> IN lt_ordim_o
                                                         ( guid_hu = <ls_hu>-sguid_hu ) ).

    IF lt_src_hu IS INITIAL.
      " there are no open WHO to this resource we go out in this case
      ms_userrsrcpallet_exp_ent-lgnum = mv_lgnum.
      ms_userrsrcpallet_exp_ent-rsrc  = lv_rsrc.

      et_expanded_tech_clauses = VALUE #( (  mc_userrsrc_rsrcpallet ) ).

      " prepare data to ref
      copy_data_to_ref( EXPORTING is_data = ms_userrsrcpallet_exp_ent
                        CHANGING  cr_data = er_entity ).
      RETURN.
    ENDIF.

    " read relevant stock for HU
    CALL FUNCTION '/SCWM/HU_READ_MULT'
      EXPORTING
        it_guid_hu   = lt_src_hu
      IMPORTING
        et_huhdr     = lt_huhdr
      EXCEPTIONS
        wrong_input  = 1
        not_possible = 2
        OTHERS       = 3.

    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " populate the data for the pallets to the odata structure
    ms_userrsrcpallet_exp_ent-userrsrc_rsrcpallet = VALUE #( FOR <ls_hu_disp> IN lt_huhdr
                                                                  INDEX INTO cust_index
                                                                  LET  lv_aarea   = VALUE #( lt_ordim_o[ vlenr = <ls_hu_disp>-huident ]-aarea OPTIONAL )
                                                                  IN aarea = lv_aarea
                                                                  (  count_hu = cust_index
                                                                     huident  = <ls_hu_disp>-huident
                                                                     hutype   = <ls_hu_disp>-letyp
                                                                     rsrc     = lv_rsrc
                                                                     lgnum    = lv_lgnum ) ).

    et_expanded_tech_clauses = VALUE #( ( mc_userrsrc_rsrcpallet ) ).

    " prepare data to ref
    copy_data_to_ref( EXPORTING is_data = ms_userrsrcpallet_exp_ent
                      CHANGING  cr_data = er_entity ).

  ENDMETHOD.


  METHOD load_key_data.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Load Lgnum from Odata table key
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    CLEAR: ev_lgnum,
           ev_rsrc.


    LOOP AT it_key_tab ASSIGNING FIELD-SYMBOL(<ls_key_odata_appl>).

      " warehause number
      IF  <ls_key_odata_appl>-name = y20_cl_ptwy_exec_pall_mpc_ext=>mc_property_name-lgnum.
        ev_lgnum = <ls_key_odata_appl>-value.
      ENDIF.

      " resource
      IF  <ls_key_odata_appl>-name = y20_cl_ptwy_exec_pall_mpc_ext=>mc_property_name-rsrc.
        ev_rsrc = <ls_key_odata_appl>-value.
      ENDIF.

    ENDLOOP.

  ENDMETHOD.


  METHOD log_off_rsrc.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Remove all trolleys from the user. Clear button
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov    ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: ls_resource   TYPE /scwm/rsrc,
          ls_usr_st_log TYPE /scwm/usr_st_log.

    CONSTANTS lc_user_logged_off TYPE /scwm/de_rsrc_sts VALUE 'B'.
    " ------------------------------------------ "
    "  Processing logic..
    " ------------------------------------------ "


    CALL FUNCTION '/SCWM/RSRC_READ_SINGLE'
      EXPORTING
        iv_lgnum    = mv_lgnum
        iv_rsrc     = iv_rsrc
      IMPORTING
        es_rsrc     = ls_resource
      EXCEPTIONS
        wrong_input = 1
        not_found   = 2
        OTHERS      = 3.

    IF sy-subrc <> 0 OR ls_resource IS INITIAL.
      " this will not happen
      RETURN.
    ENDIF.

    " Clear user name from resource
    CLEAR ls_resource-uname.

    " Update resource record in DB
    CALL FUNCTION '/SCWM/RSRC_RESOURCE_SET'
      EXPORTING
        iv_action = wmegc_update
        is_rsrc   = ls_resource.

    " Set log with status, timestamp, reason
    MOVE-CORRESPONDING ls_resource TO ls_usr_st_log  ##ENH_OK.
    ls_usr_st_log-uname = sy-uname.
    GET TIME STAMP FIELD ls_usr_st_log-tstmp.
    ls_usr_st_log-status = lc_user_logged_off.
    CLEAR ls_usr_st_log-reason.
    " Update user status log table
    CALL FUNCTION '/SCWM/RSRC_USER_LOG_SET'
      EXPORTING
        iv_action   = wmegc_insert
        is_user_log = ls_usr_st_log.

    COMMIT WORK AND WAIT.

  ENDMETHOD.


  METHOD print_hu.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Print Handling Unit
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-04-08¦B.Sugarev     ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA ls_huhdr    TYPE /scwm/s_huhdr_int.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    CALL FUNCTION '/SCWM/HU_READ'
      EXPORTING
        iv_appl    = wmegc_huappl_wme
        iv_guid_hu = iv_guid_hu
        iv_lgnum   = mv_lgnum
      IMPORTING
        es_huhdr   = ls_huhdr
      EXCEPTIONS
        deleted    = 1
        not_found  = 2
        error      = 3
        OTHERS     = 4.
    IF sy-subrc <> 0.
      RETURN.
    ENDIF.

    y20_cl_odata_facade=>print_hu( it_huhdr = VALUE #( ( ls_huhdr ) )
                                   iv_fcode = y20_if_printing_c=>ms_odata_fcode-ptwy02 ).

  ENDMETHOD.


  METHOD read_resource_data.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTTR
* Description : Check if the scanned value is a trolley
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-05-11¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    DATA: ls_resource   TYPE /scwm/rsrc.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    IF iv_rsrc IS INITIAL OR mv_lgnum IS INITIAL.
      MESSAGE e102(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    CALL FUNCTION '/SCWM/RSRC_READ_SINGLE'
      EXPORTING
        iv_lgnum    = mv_lgnum
        iv_rsrc     = iv_rsrc
      IMPORTING
        es_rsrc     = ls_resource
      EXCEPTIONS
        wrong_input = 1
        not_found   = 2
        OTHERS      = 3.

    IF sy-subrc <> 0 OR ls_resource IS INITIAL.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4 INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check if scanned resource is pallet movement
    DATA(lt_rsrc_type_pall) =  zswl_cl_param=>read_const_list( iv_lgnum     = mv_lgnum
                                                             iv_context   = zswl_if_param_c=>mc_ptwy_appl_pal
                                                             iv_parameter = zswl_if_param_c=>mc_resource_type_pal ).

    IF NOT line_exists( lt_rsrc_type_pall[ table_line = ls_resource-rsrc_type ] ).
      MESSAGE e103(y20_ui5) WITH iv_rsrc  INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    es_resource = ls_resource.

  ENDMETHOD.


  METHOD read_who_assigned.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Get all WHO which are assigned to this resource
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov    ¦Initial
*
************************************************************************
    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "
    DATA: lt_rsrc_ty TYPE /scwm/tt_wo_rsrc_ty.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "

    " get all who assigned to this resource
    SELECT * FROM
       /scwm/wo_rsrc_ty
      WHERE lgnum     = @mv_lgnum AND
            rsrc_type = @is_rsrc-rsrc_type AND
            rsrc      = @is_rsrc-rsrc
      INTO TABLE @lt_rsrc_ty.

    IF sy-subrc <> 0.
      LOG-POINT ID y20_ui5_putaway_pall
            FIELDS is_rsrc.
    ELSE.
      rt_wo_rsrc_ty = lt_rsrc_ty.
    ENDIF.

  ENDMETHOD.


  METHOD scan_resource.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Scan Resource - validate the scanned data and assigne it
* to user
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-24¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    DATA: ls_rsrc_curr           TYPE /scwm/rsrc,
          ls_usr_st_log          TYPE /scwm/usr_st_log,
          ls_odata_resource_scan TYPE y20_cl_ptwy_exec_pall_mpc=>ts_scannewresource.
    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " get data for POST request
    io_data_provider->read_entry_data( IMPORTING es_data = ls_odata_resource_scan ).

    IF ls_odata_resource_scan-rsrc IS INITIAL OR
       ls_odata_resource_scan-lgnum IS INITIAL.
      MESSAGE e102(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check lgnum
    check_lgnum( EXPORTING iv_lgnum = ls_odata_resource_scan-lgnum ).

    " read and check resource
    read_resource_data( EXPORTING iv_rsrc     = ls_odata_resource_scan-rsrc
                        IMPORTING es_resource = ls_rsrc_curr ).

    " check what we have on the scanned resource
    get_hus_rsrc( EXPORTING iv_rsrc = ls_rsrc_curr-rsrc ).

    " check if user was log on to another resource
    SELECT FROM /scwm/rsrc
       FIELDS rsrc
        WHERE lgnum      = @mv_lgnum  AND
              uname      = @sy-uname
      INTO TABLE @DATA(lt_rsrc).

    IF sy-subrc = 0.
      IF  NOT line_exists( lt_rsrc[ rsrc = ls_odata_resource_scan-rsrc ] ).

        DATA(lt_rsrc_type_pall) =  zswl_cl_param=>read_const_list( iv_lgnum     = mv_lgnum
                                                                 iv_context   = zswl_if_param_c=>mc_ptwy_appl_pal
                                                                 iv_parameter = zswl_if_param_c=>mc_resource_type_pal ).

        LOOP AT lt_rsrc ASSIGNING FIELD-SYMBOL(<ls_loged_curr_rsrc>)
                                                 WHERE rsrc <> ls_odata_resource_scan-rsrc.
          " we have assigned resources which are not the current scanned one
          " what we should do ???
          IF NOT line_exists( lt_rsrc_type_pall[ table_line = <ls_loged_curr_rsrc>-rsrc ] ).
            " check if scanned resource is pallet movement
            MESSAGE e104(y20_ui5) WITH <ls_loged_curr_rsrc>-rsrc  INTO mv_msg_dummy.
            RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
          ELSE.
            " log of the resource which is pallet type and is not the current scanned
            log_off_rsrc( EXPORTING iv_rsrc = ls_odata_resource_scan-rsrc ).
          ENDIF.
        ENDLOOP.

      ELSE.
        " the scanned resource was scanned for current user before
        " so do nothing
        copy_data_to_ref( EXPORTING is_data = ls_odata_resource_scan
                          CHANGING  cr_data = er_entity ).
        RETURN.
      ENDIF.
    ENDIF.

    " final step log in to the current scanned resource
    ls_rsrc_curr-uname = sy-uname.

    " Update Resource record in DB
    CALL FUNCTION '/SCWM/RSRC_RESOURCE_SET'
      EXPORTING
        iv_action = wmegc_update
        is_rsrc   = ls_rsrc_curr.

    " Set log with status, timestamp, reason
    MOVE-CORRESPONDING ls_rsrc_curr TO ls_usr_st_log  ##ENH_OK.
    GET TIME STAMP FIELD ls_usr_st_log-tstmp.
    ls_usr_st_log-status = mc_user_logged_on.
    CLEAR ls_usr_st_log-reason.

    " Update user status log table
    CALL FUNCTION '/SCWM/RSRC_USER_LOG_SET'
      EXPORTING
        iv_action   = wmegc_insert
        is_user_log = ls_usr_st_log.

    COMMIT WORK AND WAIT.

    copy_data_to_ref( EXPORTING is_data = ls_odata_resource_scan
                      CHANGING  cr_data = er_entity ).

  ENDMETHOD.


  METHOD wt_process_movement.
************************************************************************
* Company     : Swisslog AG
* Package     : Y20_UI_UI5_PTPA
* Description : Main Screen WT Processes
************************************************************************
* REVISIONS:
* ---------
*
* Ver. ¦Date      ¦Author        ¦Description
* -----¦----------¦--------------¦--------------------------------------
* 1.0  ¦2020-07-29¦A.Yordanov    ¦Initial
*
************************************************************************

    " ------------------------------------------ "
    " Local Declarations..
    " ------------------------------------------ "

    DATA: ls_odata_wt_proc TYPE y20_cl_ptwy_exec_pall_mpc=>ts_ptwywtconfpall.

    " ------------------------------------------ "
    "  Processing Logic..
    " ------------------------------------------ "
    " get data for POST request
    io_data_provider->read_entry_data( IMPORTING es_data = ls_odata_wt_proc ).

    IF ls_odata_wt_proc-rsrc IS INITIAL OR
       ls_odata_wt_proc-lgnum IS INITIAL OR
       ls_odata_wt_proc-tanum IS INITIAL.
      MESSAGE e102(y20_ui5) INTO mv_msg_dummy.
      RAISE EXCEPTION TYPE /iwbep/cx_mgw_tech_exception.
    ENDIF.

    " check lgnum
    check_lgnum( EXPORTING iv_lgnum = ls_odata_wt_proc-lgnum ).

    CASE ls_odata_wt_proc-user_comm.
      WHEN mc_user_command_01.
        " validate source HU and move it to the resource
        conf_source_cr_wt_rsrc( ls_odata_wt_proc ).
      WHEN mc_user_command_02.
        " validate dest bin and move HU from rsrc to the final bin
        " do not change dest. bin
        conf_final_dest_to_bin( EXPORTING is_ptwywtconfpall = ls_odata_wt_proc
                                          iv_exc_flag       = abap_false ).
      WHEN mc_user_command_03.
        " validate dest bin and move HU from rsrc to final bin
        " change destination bin via excetpion code
        conf_final_dest_to_bin( EXPORTING is_ptwywtconfpall = ls_odata_wt_proc
                                          iv_exc_flag       = abap_true ).
      WHEN OTHERS.
    ENDCASE.

    " prepare data to ref
    copy_data_to_ref( EXPORTING is_data = ls_odata_wt_proc
                      CHANGING  cr_data = er_entity ).


  ENDMETHOD.
ENDCLASS.